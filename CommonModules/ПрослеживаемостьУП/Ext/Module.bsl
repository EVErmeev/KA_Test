#Область ПрограммныйИнтерфейс

//++ НЕ УТ

// Метод возвращает ссылку на элемент справочника КодыОперацийПрослеживаемости по коду
// 
// Параметры:
//  КодОперации - Строка - Код операции
// 
// Возвращаемое значение:
//  СправочникСсылка.КодыОперацийПрослеживаемости - Операция прослеживаемости
//
Функция ОперацияПрослеживаемости(КодОперации = "") Экспорт
	
	КОП = Справочники.КодыОперацийПрослеживаемости;
	
	Если КодОперации = "01" Тогда
		Операция = КОП.ПередачаВПроизводство;
	ИначеЕсли КодОперации = "02" Тогда
		Операция = КОП.ЗахоронениеОбезвреживаниеУтилизацияТовара;
	ИначеЕсли КодОперации = "03" Тогда
		Операция = КОП.УничтожениеУтратаВследствиеДействияНепреодолимойСилы;
	ИначеЕсли КодОперации = "04" Тогда
		Операция = КОП.РеализацияРозничная;
	ИначеЕсли КодОперации = "05" Тогда
		Операция = КОП.ВывозТовараЗаПределыРФ;
	ИначеЕсли КодОперации = "06" Тогда
		Операция = КОП.БезвозмезднаяПередачаТовараФизическимЛицам;
	ИначеЕсли КодОперации = "07" Тогда
		Операция = КОП.КонфискацияГосударством;
	ИначеЕсли КодОперации = "08" Тогда
		Операция = КОП.РеализацияТовараДипломатическимИКонсульскимУчрежденям;
	ИначеЕсли КодОперации = "09" Тогда
		Операция = КОП.НедостачаВыявленнаяВХодеИнвентаризации;
	ИначеЕсли КодОперации = "10" Тогда
		Операция = КОП.ВозвратРанееУтраченногоТовара;
	ИначеЕсли КодОперации = "11" Тогда
		Операция = КОП.ВозвратОтРозничногоПокупателя;
	ИначеЕсли КодОперации = "12" Тогда
		Операция = КОП.ВозвратИзПроизводства;
	ИначеЕсли КодОперации = "13" Тогда
		Операция = КОП.ПередачаНеСвязаннаяСРеализацией;
	ИначеЕсли КодОперации = "14" Тогда
		Операция = КОП.ПолучениеНеСвязанноеСРеализацией;
	ИначеЕсли КодОперации = "15" Тогда
		Операция = КОП.Реализация;
	ИначеЕсли КодОперации = "16" Тогда
		Операция = КОП.БезвозмезднаяПередача;
	ИначеЕсли КодОперации = "17" Тогда
		Операция = КОП.Приобретение;
	ИначеЕсли КодОперации = "18" Тогда
		Операция = КОП.БезвозмездноеПолучение;
	ИначеЕсли КодОперации = "19" Тогда
		Операция = КОП.УКДНаУменьшениеВыданный;
	ИначеЕсли КодОперации = "20" Тогда
		Операция = КОП.УКДНаУвеличениеВыданный;
	ИначеЕсли КодОперации = "21" Тогда
		Операция = КОП.УКДНаУменьшениеПолученный;
	ИначеЕсли КодОперации = "22" Тогда
		Операция = КОП.УКДНаУвеличениеПолученный;
	ИначеЕсли КодОперации = "23" Тогда
		Операция = КОП.РеализацияТовараКомитента;
	ИначеЕсли КодОперации = "24" Тогда
		Операция = КОП.ПередачаСведенийОРеализацииКомитенту;
	ИначеЕсли КодОперации = "25" Тогда
		Операция = КОП.ПолучениеКомитентомСведенийОРеализацииТовараКомиссионером;
	ИначеЕсли КодОперации = "26" Тогда
		Операция = КОП.ПриобретениеТовараДляКомитента;
	ИначеЕсли КодОперации = "27" Тогда
		Операция = КОП.ПередачаСведенийОЗакупкеКомитенту;
	ИначеЕсли КодОперации = "28" Тогда
		Операция = КОП.ПолучениеКомитентомСведенийОПриобретенииТовараКомиссионером;
	ИначеЕсли КодОперации = "29" Тогда
		Операция = КОП.СоставлениеКомиссионеромКорректировкиНаУменьшение;
	ИначеЕсли КодОперации = "30" Тогда
		Операция = КОП.ПередачаКомиссионеромКомитентуСведенийОВозвратеТовара;
	ИначеЕсли КодОперации = "31" Тогда
		Операция = КОП.ПолучениеКомитентомСведенийОВозвратеТовараКомиссионеру;
	ИначеЕсли КодОперации = "32" Тогда
		Операция = КОП.ПолучениеКомитентомКорректировкиНаУменьшение;
	ИначеЕсли КодОперации = "33" Тогда
		Операция = КОП.ОтражениеКомиссионеромПоЗакупкеВозвратаКомитентомТоваров;
	ИначеЕсли КодОперации = "34" Тогда
		Операция = КОП.ПолучениеКомиссионеромКорректировкиВСторонуУменьшения;
	ИначеЕсли КодОперации = "35" Тогда
		Операция = КОП.ОтражениеКомитентомВозвратаТовараПриобретенногоКомиссионером;
	ИначеЕсли КодОперации = "36" Тогда
		Операция = КОП.ПередачаСведенийОРозничнойРеализацииКомитенту;
	ИначеЕсли КодОперации = "37" Тогда
		Операция = КОП.ПолучениеКомитентомСведенийОРозничнойРеализацииТовараКомиссионером;
	ИначеЕсли КодОперации = "38" Тогда
		Операция = КОП.ПередачаКомиссионеромКомитентуСведенийОРозничномВозвратеТовара;
	ИначеЕсли КодОперации = "39" Тогда
		Операция = КОП.ПолучениеКомитентомСведенийОРозничномВозвратеТовараКомиссионеру;
	Иначе
		Операция = КОП.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Операция;
	
КонецФункции

// Метод подписки на событие, отслеживающий изменение регистров товары организаций и товары переданных на комиссию.
// 
// Параметры:
//  Источник - РегистрНакопленияНаборЗаписей.ТоварыОрганизаций, РегистрНакопленияНаборЗаписей.ТоварыПереданныеНаКомиссию - набор записей регистра накоплений
//  Отказ - Булево - Отказ
//  Замещение - Булево - Замещение
//
Процедура ОтразитьОперацииСПрослеживаемымиТоварами(Источник, Отказ, Замещение) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("НеОтражатьОперацииСПрослеживаемымиТоварами") И 
			Источник.ДополнительныеСвойства.НеОтражатьОперацииСПрослеживаемымиТоварами Тогда
			Возврат;
	КонецЕсли;
	
	Регистратор = Источник.Отбор.Регистратор.Значение;
	Если Не ДокументТребуетсяОтразитьВЖурнале(Регистратор) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоПроведениеДокумента =
		Источник.ДополнительныеСвойства.Свойство("СвойстваДокумента")
		И Источник.ДополнительныеСвойства.СвойстваДокумента.Свойство("РежимЗаписи")
		И Источник.ДополнительныеСвойства.СвойстваДокумента.РежимЗаписи = РежимЗаписиДокумента.Проведение;
	ЭтоОтменаРучногоРедактирования =
		Источник.ДополнительныеСвойства.Свойство("ОтменаРучногоРедактирования")
			И Источник.ДополнительныеСвойства.ОтменаРучногоРедактирования;
	
	Если ЭтоПроведениеДокумента ИЛИ ЭтоОтменаРучногоРедактирования Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Регистратор", Регистратор);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Операции.Период КАК Период,
		|	Операции.Организация КАК Организация,
		|	Операции.ТипЗапасов КАК ТипЗапасов,
		|	Операции.КодОперации КАК КодОперации,
		|	Операции.Контрагент КАК Контрагент,
		|	Операции.ТипДокументаВПрослеживаемости КАК ТипДокументаВПрослеживаемости,
		|	Операции.ОтражениеВОтчетности КАК ОтражениеВОтчетности
		|ИЗ
		|	РегистрСведений.ОперацииСПрослеживаемымиТоварами КАК Операции
		|ГДЕ
		|	Операции.Регистратор = &Регистратор
		|	И Операции.РучноеРедактирование";
		
		Блокировка = Новый БлокировкаДанных;

		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОперацииСПрослеживаемымиТоварами.НаборЗаписей");
		ЭлементБлокировки.УстановитьЗначение("Регистратор", Регистратор);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		Блокировка.Заблокировать();
		
		ТаблицаОпераций = Запрос.Выполнить().Выгрузить();
		Если ЭтоОтменаРучногоРедактирования Тогда
			ТаблицаОпераций.Очистить();
		ИначеЕсли ЗначениеЗаполнено(ТаблицаОпераций) Тогда
			// Если таблица операций не пустая, значит есть ручные корректировки
			// в этом случае операцию отражать не нужно
			Возврат;
		КонецЕсли;
		
		ОперацииПрослеживаемости = ОперацииПрослеживаемости(Регистратор, Источник.Выгрузить());
		Для Каждого СвойстваОперации Из ОперацииПрослеживаемости Цикл
			
			НоваяОперация = ТаблицаОпераций.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяОперация, СвойстваОперации, "Организация, ТипЗапасов, Контрагент");
			
			НоваяОперация.КодОперации = КодПрослеживаемойОперации(СвойстваОперации);
			НоваяОперация.ОтражениеВОтчетности = ОтчетПрослеживаемойОперации(СвойстваОперации);
			НоваяОперация.ТипДокументаВПрослеживаемости = ТипДокументаПрослеживаемойОперации(СвойстваОперации);
			
		КонецЦикла;
		
		ТаблицаОпераций.Свернуть(
			"Период, Организация, ТипЗапасов, Контрагент,
			|КодОперации, ТипДокументаВПрослеживаемости, ОтражениеВОтчетности");
		ТаблицаОпераций.ЗаполнитьЗначения(Регистратор.Дата, "Период");
		
		ОтменитьОтражениеИсправляемогоДокумента(Регистратор);
		
	Иначе
		
		ТаблицаОпераций = Новый ТаблицаЗначений;
		
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ОперацииСПрослеживаемымиТоварами.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Загрузить(ТаблицаОпераций);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Выполняет отмену ручных корректировок в регистре операций с прослеживаемыми товарами.
// 
// Параметры:
//  СписокДокументов - Массив - Список документов
//
Процедура ОтменитьРучныеКорректировкиОпераций(СписокДокументов) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Регистраторы", СписокДокументов);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Операции.Регистратор КАК Документ,
		|	""ТоварыОрганизаций"" КАК ИмяРегистра
		|ИЗ
		|	РегистрСведений.ОперацииСПрослеживаемымиТоварами КАК Операции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|		ПО Операции.Регистратор = ТоварыОрганизаций.Регистратор
		|ГДЕ
		|	Операции.Регистратор В (&Регистраторы)
		|	И Операции.РучноеРедактирование
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Операции.Регистратор КАК Документ,
		|	""ТоварыПереданныеНаКомиссию"" КАК ИмяРегистра
		|ИЗ
		|	РегистрСведений.ОперацииСПрослеживаемымиТоварами КАК Операции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ТоварыПереданныеНаКомиссию
		|		ПО Операции.Регистратор = ТоварыПереданныеНаКомиссию.Регистратор
		|ГДЕ
		|	Операции.Регистратор В (&Регистраторы)
		|	И Операции.РучноеРедактирование";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НаборЗаписей = РегистрыНакопления[Выборка.ИмяРегистра].СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Документ);
			НаборЗаписей.Прочитать();
			
			НаборЗаписей.ДополнительныеСвойства.Вставить("ОтменаРучногоРедактирования", Истина);
			
			ОтразитьОперацииСПрослеживаемымиТоварами(НаборЗаписей, Ложь, Истина);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Проверяет, есть ли необходимость в расширенных движениях прослеживаемых товаров. Записываем в расширенный регистр данные,
// если есть строки с различным отражением в отчетности, или если заполнены идентификаторы строк - в этом случае пишем все данные в расширенный регистр
// 
// Параметры:
//  ТаблицаДвижений - ТаблицаЗначений - Движения в Регистр Накопления Операции с ПТ расширенный
//
// Возвращаемое значение:
//  Булево
//
Функция ПроверитьНеобходимостьРасширенныхДвиженийПрослеживаемыхТоваров(ТаблицаДвижений) Экспорт
	
	Если ТаблицаДвижений.Количество() = 0 Тогда
		Возврат Ложь;
	ИначеЕсли ТаблицаДвижений[0].ИдентификаторСтроки <> "" Тогда
		Возврат Истина;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ВЫРАЗИТЬ(ТаблицаДвижений.ВидЗапасов КАК Справочник.ВидыЗапасов) КАК ВидЗапасов,
	|	ТаблицаДвижений.ОтражениеВОтчетности КАК ОтражениеВОтчетности
	|ПОМЕСТИТЬ ВтПроверочнаяТаблица
	|ИЗ
	|	&ТаблицаДвижений КАК ТаблицаДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтПроверочнаяТаблица.Организация КАК Организация,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтПроверочнаяТаблица.ОтражениеВОтчетности) КАК ОтражениеВОтчетности,
	|	ВидыЗапасов.ТипЗапасов КАК ТипЗапасов
	|ИЗ
	|	ВтПроверочнаяТаблица КАК ВтПроверочнаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|		ПО ВтПроверочнаяТаблица.ВидЗапасов = ВидыЗапасов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтПроверочнаяТаблица.Организация,
	|	ВидыЗапасов.ТипЗапасов";
	Запрос.УстановитьПараметр("ТаблицаДвижений", ТаблицаДвижений);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ОтражениеВОтчетности > 1 Тогда
			Возврат Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;

КонецФункции // ПроверитьНеобходимостьРасширенныхДвиженийПрослеживаемыхТоваров()

// Выполняет подготовку и заполнение данных для регламентированного отчета.
// 
// Параметры:
//  ПараметрыОтчета - Структура - Параметры отчета
//  Контейнер - Структура - Контейнер
//
Процедура ЗаполнитьОтчетОбОперациях(ПараметрыОтчета, Контейнер) Экспорт
	
	Если ПараметрыОтчета.Свойство("ДатаНачалаРасширенногоПериодаОтчета") Тогда
		НачалоПериодаОтчета = ПараметрыОтчета.ДатаНачалаРасширенногоПериодаОтчета;
	Иначе
		НачалоПериодаОтчета = ПараметрыОтчета.мДатаНачалаПериодаОтчета;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	МассивОрганизаций = Новый ФиксированныйМассив(БухгалтерскийУчетПереопределяемый.ВсяОрганизация(ПараметрыОтчета.Организация));
	Запрос.УстановитьПараметр("Организация", МассивОрганизаций);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(НачалоПериодаОтчета));
	Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(ПараметрыОтчета.мДатаКонцаПериодаОтчета));
	Запрос.УстановитьПараметр("ДатаПодписи", КонецДня(Макс(ПараметрыОтчета.ДатаПодписи, ПараметрыОтчета.мДатаКонцаПериодаОтчета)));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Операции.Период КАК ДатаОперации,
	|	Операции.Организация КАК Организация,
	|	Операции.Регистратор КАК ДокументОперации,
	|	Операции.ТипЗапасов КАК ТипЗапасов,
	|	Операции.КодОперации КАК КодОперации,
	|	Операции.Контрагент КАК Контрагент,
	|	Операции.ТипДокументаВПрослеживаемости КАК ТипДокумента
	|ПОМЕСТИТЬ ПрослеживаемыеОперации
	|ИЗ
	|	РегистрСведений.ОперацииСПрослеживаемымиТоварами КАК Операции
	|ГДЕ
	|	Операции.ОтражениеВОтчетности = ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|	И Операции.Период >= &НачалоПериода
	|	И Операции.Период <= &КонецПериода
	|	И Операции.Организация В(&Организация)
	|	И Операции.Активность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Операции.Период,
	|	Операции.Организация,
	|	Операции.Регистратор,
	|	Операции.ВидЗапасов.ТипЗапасов,
	|	Операции.КодОперации,
	|	Операции.Контрагент,
	|	Операции.ТипДокументаВПрослеживаемости
	|ИЗ
	|	РегистрСведений.ОперацииСПрослеживаемымиТоварамиРасширенный КАК Операции
	|ГДЕ
	|	Операции.ОтражениеВОтчетности = ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|	И Операции.Период >= &НачалоПериода
	|	И Операции.Период <= &КонецПериода
	|	И Операции.Организация В(&Организация)
	|	И Операции.Активность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Операции.ДокументОперации КАК Документ
	|ПОМЕСТИТЬ ВТ_СписокДокументов
	|ИЗ
	|	ПрослеживаемыеОперации КАК Операции
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ";
	
	Запрос.Выполнить();
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	СтруктураПараметров.Вставить("СписокОрганизаций", МассивОрганизаций);
	СтруктураПараметров.Вставить("ПорядокОтраженияВОтчетностиПоПрослеживаемости", Перечисления.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях);
	
	ПолучитьДанныеРНПТ(СтруктураПараметров);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрослеживаемыеОперации.ДокументОперации КАК ДокументОперации,
	|	ПрослеживаемыеОперации.ТипЗапасов КАК ТипЗапасов,
	|	ЕСТЬNULL(ДанныеРНПТ.КодОперации, ПрослеживаемыеОперации.КодОперации) КАК КодОперации,
	|	ПрослеживаемыеОперации.ДатаОперации КАК ДатаОперации,
	|	ВЫБОР ПрослеживаемыеОперации.ТипДокумента
	|		КОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.СчетФактура)
	|			ТОГДА 1
	|		КОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.КорректировочныйСчетФактура)
	|			ТОГДА 2
	|		КОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|			ТОГДА 3
	|		КОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УКД)
	|			ТОГДА 4
	|		ИНАЧЕ 5
	|	КОНЕЦ КАК КодТипаДокумента,
	|	ДанныеПервичныхДокументов.Номер КАК НомерДокумента,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаДокумента,
	|	ПрослеживаемыеОперации.Контрагент КАК Контрагент,
	|	ПрослеживаемыеОперации.Контрагент.ИНН КАК ИНН,
	|	ПрослеживаемыеОперации.Контрагент.КПП КАК КПП,
	|	ПрослеживаемыеОперации.Контрагент.НаименованиеПолное КАК НаименованиеКонтрагента,
	|	ДанныеРНПТ.РНПТ КАК РНПТ,
	|	ДанныеРНПТ.ОКЕИ КАК ЕдиницаИзмерения,
	|	СУММА(ДанныеРНПТ.КоличествоРНПТ) КАК Количество,
	|	СУММА(ДанныеРНПТ.СуммаБезНДС) КАК СуммаБезНДС
	|ИЗ
	|	ПрослеживаемыеОперации КАК ПрослеживаемыеОперации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ПрослеживаемыеОперации.Организация = ДанныеПервичныхДокументов.Организация
	|			И ПрослеживаемыеОперации.ДокументОперации = ДанныеПервичныхДокументов.Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРНПТ КАК ДанныеРНПТ
	|		ПО ПрослеживаемыеОперации.ДокументОперации = ДанныеРНПТ.Документ
	|			И ПрослеживаемыеОперации.Организация = ДанныеРНПТ.Организация
	|			И ПрослеживаемыеОперации.ТипЗапасов = ДанныеРНПТ.ТипЗапасов
	|			И (ПрослеживаемыеОперации.КодОперации = ДанныеРНПТ.КодОперации
	|				ИЛИ ДанныеРНПТ.КодОперации ЕСТЬ NULL)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрослеживаемыеОперации.ДокументОперации,
	|	ПрослеживаемыеОперации.ТипЗапасов,
	|	ЕСТЬNULL(ДанныеРНПТ.КодОперации, ПрослеживаемыеОперации.КодОперации),
	|	ПрослеживаемыеОперации.ДатаОперации,
	|	ВЫБОР ПрослеживаемыеОперации.ТипДокумента
	|		КОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.СчетФактура)
	|			ТОГДА 1
	|		КОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.КорректировочныйСчетФактура)
	|			ТОГДА 2
	|		КОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|			ТОГДА 3
	|		КОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УКД)
	|			ТОГДА 4
	|		ИНАЧЕ 5
	|	КОНЕЦ,
	|	ДанныеПервичныхДокументов.Номер,
	|	ДанныеПервичныхДокументов.Дата,
	|	ПрослеживаемыеОперации.Контрагент,
	|	ПрослеживаемыеОперации.Контрагент.ИНН,
	|	ПрослеживаемыеОперации.Контрагент.КПП,
	|	ПрослеживаемыеОперации.Контрагент.НаименованиеПолное,
	|	ДанныеРНПТ.РНПТ,
	|	ДанныеРНПТ.ОКЕИ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПрослеживаемыеОперации.ДокументОперации,
	|	КодОперации";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Реестр = Контейнер.Реестр;
		Реестр.Строки.Очистить();
		НоваяСтраница = Реестр.Строки.Добавить();
		
		НоваяСтраница.Данные = Новый Структура("СтоимостьИтого", 0);
		НоваяСтраница.ДанныеМногострочныхЧастей = Новый Структура("П10000", ДеревоЗначенийОтчетОбОперациях());
		Сч = 1;
		
		Выборка = Результат.Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("ДокументОперации") Цикл
			Пока Выборка.СледующийПоЗначениюПоля("КодОперации") Цикл
				
				Если Сч > ПараметрыОтчета.МаксКолСтрокНаСтранице Тогда
					
					НоваяСтраница = Реестр.Строки.Добавить();
					НоваяСтраница.Данные = Новый Структура("СтоимостьИтого", 0);
					НоваяСтраница.ДанныеМногострочныхЧастей = Новый Структура("П10000", ДеревоЗначенийОтчетОбОперациях());
					
					Сч = 1;
					
				КонецЕсли;
				
				НоваяСтрокаОперации = НоваяСтраница.ДанныеМногострочныхЧастей.П10000.Строки.Добавить();
				СтруктураДанныхОперации = Новый Структура;
				СтруктураДанныхОперации.Вставить("П1000001", 0);
				СтруктураДанныхОперации.Вставить("П1000002", Выборка.ДатаОперации);
				СтруктураДанныхОперации.Вставить("П1000003", Выборка.КодОперации);
				СтруктураДанныхОперации.Вставить("П1000004", Выборка.КодТипаДокумента);
				СтруктураДанныхОперации.Вставить("П1000005", Выборка.НомерДокумента);
				СтруктураДанныхОперации.Вставить("П1000006", Выборка.ДатаДокумента);
				СтруктураДанныхОперации.Вставить("П1000007", Выборка.НаименованиеКонтрагента);
				СтруктураДанныхОперации.Вставить("П1000008", Выборка.ИНН);
				СтруктураДанныхОперации.Вставить("П1000009", Выборка.КПП);
				
				НоваяСтрокаОперации.Данные = СтруктураДанныхОперации;
				НоваяСтрокаОперации.ДанныеМногострочныхЧастей = Новый Структура("П11000", ДеревоЗначенийОтчетОбОперациях());
				
				Пока Выборка.СледующийПоЗначениюПоля("РНПТ") Цикл
					НовыйРНПТ = НоваяСтрокаОперации.ДанныеМногострочныхЧастей.П11000.Строки.Добавить();
					
					НовыйРНПТ.Данные = Новый Структура;
					НовыйРНПТ.Данные.Вставить("П1100010", Выборка.РНПТ);
					НовыйРНПТ.Данные.Вставить("П1100011", Выборка.ЕдиницаИзмерения);
					НовыйРНПТ.Данные.Вставить("П1100012", Выборка.Количество);
					НовыйРНПТ.Данные.Вставить("П1100013", Выборка.СуммаБезНДС);
				КонецЦикла;
				
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДанныеРНПТ(СтруктураПараметров) Экспорт

	ЗаполнитьДополнительныеПараметрыЗапросаРНПТ(СтруктураПараметров);
	
	ДополнитьЗапросИнформациейОРозничныхПродажах(СтруктураПараметров);
	
	ПолучитьОсновныеДанныеРНПТ(СтруктураПараметров);
	
	ПолучитьДанныеРНПТКорректировкиПриобретения(СтруктураПараметров);
	
	ПолучитьДанныеРНПТКорректировкиРеализации(СтруктураПараметров);
	
	ПолучитьДанныеРНПТвОС(СтруктураПараметров);
	
	ПолучитьИтоговыеДанныеРНПТ(СтруктураПараметров);
	
	УдалитьВременныеТаблицы(СтруктураПараметров);

КонецПроцедуры // ()


//-- НЕ УТ

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет реквизиты объекта значениями, полученными в структуре ДанныхЗаполнения.
//	Заполнение возможно только в случае, когда в качестве ДанныхЗаполнения получена структура.
//	Заполнены будут только те реквизиты, для которых в метаданных установлен флаг "ЗаполнятьИзДанныхЗаполнения".
//
//
// Параметры:
//  <Объект>  - <СправочникОбъект>, <ДокументОбъект> и т.д. - Заполняемый объект.
//  <ДанныеЗаполнения>  - <произвольный тип> - параметр, полученный в ОбработкеЗаполнения объекта.
//  <МетаданныеОбъекта>  - <Метаданные>.
//
//
Процедура ЗаполнитьПоСтруктуре(Объект, ДанныеЗаполнения, МетаданныеОбъекта = Неопределено)	Экспорт

	Если Объект = Неопределено
		ИЛИ ТипЗнч(ДанныеЗаполнения) <> Тип("Структура")
		ИЛИ ДанныеЗаполнения.Количество() = 0 Тогда

		Возврат;

	КонецЕсли;

	Если МетаданныеОбъекта = Неопределено Тогда
		МетаданныеОбъекта = Объект.Метаданные();
	КонецЕсли;
	Если  Метаданные.Справочники.Содержит(МетаданныеОбъекта)
		ИЛИ Метаданные.ПланыВидовХарактеристик.Содержит(МетаданныеОбъекта) Тогда
		РазличатьГруппыИЭлементы = МетаданныеОбъекта.Иерархический;
	Иначе
		РазличатьГруппыИЭлементы = Ложь;
	КонецЕсли;

	СтруктураЗаполнения = Новый Структура;
	Для Каждого Реквизит Из МетаданныеОбъекта.СтандартныеРеквизиты Цикл
		Если Реквизит.ЗаполнятьИзДанныхЗаполнения
			И ДанныеЗаполнения.Свойство(Реквизит.Имя) Тогда
			СтруктураЗаполнения.Вставить(Реквизит.Имя);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Реквизит Из МетаданныеОбъекта.Реквизиты Цикл
		Если Реквизит.ЗаполнятьИзДанныхЗаполнения
			И ДанныеЗаполнения.Свойство(Реквизит.Имя) Тогда
			Если РазличатьГруппыИЭлементы Тогда
				Если (Объект.ЭтоГруппа И Реквизит.Использование <> Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента)
					ИЛИ (НЕ Объект.ЭтоГруппа И Реквизит.Использование <> Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы) Тогда
					СтруктураЗаполнения.Вставить(Реквизит.Имя);
				КонецЕсли;
			Иначе
				СтруктураЗаполнения.Вставить(Реквизит.Имя);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ЗаполняемыйЭлемент Из СтруктураЗаполнения Цикл
		ЗначениеЗаполнения = ДанныеЗаполнения[ЗаполняемыйЭлемент.Ключ];
		Если ТипЗнч(ЗначениеЗаполнения) = Тип("Массив")
			ИЛИ ТипЗнч(ЗначениеЗаполнения) = Тип("ФиксированныйМассив") Тогда
			СтруктураЗаполнения[ЗаполняемыйЭлемент.Ключ] = ЗначениеЗаполнения[0];
		Иначе
			СтруктураЗаполнения[ЗаполняемыйЭлемент.Ключ] = ЗначениеЗаполнения;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(Объект, СтруктураЗаполнения);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ НЕ УТ
Функция ДеревоЗначенийОтчетОбОперациях()
	
	НовоеДеревоЗначений = Новый ДеревоЗначений();
	НовоеДеревоЗначений.Колонки.Добавить("Данные");
	НовоеДеревоЗначений.Колонки.Добавить("ДанныеМногострочныхЧастей");
	НовоеДеревоЗначений.Колонки.Добавить("АдресТабличногоДокумента");
	
	Возврат НовоеДеревоЗначений;
	
КонецФункции

Функция ДокументТребуетсяОтразитьВЖурнале(Регистратор)
	
	МетаданныеДокумента = Регистратор.Метаданные();
	
	Если МетаданныеДокумента = Метаданные.Документы.ВводОстатков
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ВводОстатковДенежныхСредств
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ВводОстатковНДСПредъявленного
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ВводОстатковОПродажахЗаПрошлыеПериоды
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ВводОстатковПоФинансовымИнструментам
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ВводОстатковПрочиеРасходы
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ВводОстатковПрочихАктивовПассивов
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ВводОстатковРасходовПриУСН
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ВводОстатковРасчетовПоЭквайрингу
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ВводОстатковСПодотчетниками
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ВводОстатковТМЦВЭксплуатации
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ВводОстатковТоваров
		ИЛИ МетаданныеДокумента = Метаданные.Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров
		ИЛИ МетаданныеДокумента = Метаданные.Документы.УведомлениеОВвозеПрослеживаемыхТоваров
		ИЛИ МетаданныеДокумента = Метаданные.Документы.УведомлениеОПеремещенииПрослеживаемыхТоваров
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ВводОстатковВнеоборотныхАктивов2_4
		ИЛИ МетаданныеДокумента = Метаданные.Документы.ОбъединениеОС
		ИЛИ МетаданныеДокумента = Метаданные.Документы.РазукомплектацияОС
		ИЛИ МетаданныеДокумента = Метаданные.Документы.СборкаТоваров
		ИЛИ МетаданныеДокумента = Метаданные.Документы.КорректировкаРегистров Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Если МетаданныеДокумента = Метаданные.Документы.ПрочееОприходованиеТоваров Тогда
		
		ХозОперация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "ХозяйственнаяОперация");
		Если ХозОперация = Перечисления.ХозяйственныеОперации.ВозвратИзЭксплуатации
			ИЛИ ХозОперация = Перечисления.ХозяйственныеОперации.СторноСписанияНаРасходы Тогда
		Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ МетаданныеДокумента.Движения.Содержит(
		Метаданные.РегистрыСведений.ОперацииСПрослеживаемымиТоварами) Тогда
		ВызватьИсключение НСтр("ru='Документ не является регистратором для регистра сведений ""Операции с прослеживаемыми товарами""'");
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура СформироватьТаблицуКонтрагентовДокумента(МенеджерВременныхТаблиц, Регистратор)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеестрДокументов.Организация КАК Организация,
	|	КлючиРеестра.Ключ КАК Контрагент
	|ПОМЕСТИТЬ КонтрагентыДокумента
	|ИЗ
	|	РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиРеестраДокументов КАК КлючиРеестра
	|	ПО РеестрДокументов.Контрагент = КлючиРеестра.Ссылка
	|		И (КлючиРеестра.Ключ ССЫЛКА Справочник.Контрагенты
	|			ИЛИ КлючиРеестра.Ключ ССЫЛКА Справочник.Организации)
	|
	|ГДЕ
	|	РеестрДокументов.Ссылка = &Регистратор
	|	И НЕ РеестрДокументов.Ссылка ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|	И НЕ РеестрДокументов.Ссылка ССЫЛКА Документ.ВозвратТоваровМеждуОрганизациями
	|	И НЕ КлючиРеестра.Ключ В (
	|		ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|	И НЕ(РеестрДокументов.Ссылка ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ВЫРАЗИТЬ(РеестрДокументов.Ссылка КАК Документ.РеализацияТоваровУслуг).ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионера))
	|	И НЕ(РеестрДокументов.Ссылка ССЫЛКА Документ.ВозвратТоваровОтКлиента
	|				И ВЫРАЗИТЬ(РеестрДокументов.Ссылка КАК Документ.ВозвратТоваровОтКлиента).ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровЧерезКомиссионера))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.КлиентКонтрагент
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Регистратор
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионера)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.КлиентКонтрагент
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Регистратор
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровЧерезКомиссионера)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ОрганизацияПолучатель
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.ОрганизацияПолучатель,
	|	ДанныеДокумента.Организация
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ОрганизацияПолучатель
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.ОрганизацияПолучатель,
	|	ДанныеДокумента.Организация
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Регистратор";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ТекстЗапросаДвиженияПрослеживаемогоТовара(ЕстьКолонкаРНПТ = Ложь)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Движения.ВидДвижения КАК ВидДвижения,
	|	Движения.Организация КАК Организация,
	|	Движения.ВидЗапасов КАК ВидЗапасов,
	|	Движения.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Движения.НомерГТД КАК НомерГТД,
	|	Движения.КоличествоПоРНПТ
	|ПОМЕСТИТЬ ДвиженияДокумента
	|ИЗ
	|	&ТаблицаДвижений КАК Движения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Движения.Организация,
	|	ЕСТЬNULL(Контрагенты.Контрагент, НЕОПРЕДЕЛЕНО) КАК Контрагент,
	|	ЕСТЬNULL(ВидыЗапасов.ТипЗапасов, ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)) КАК ТипЗапасов,
	|	Движения.ХозяйственнаяОперация,
	|	Движения.НомерГТД КАК РНПТ,
	|	ВЫБОР Движения.ВидДвижения
	|		КОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Движения.КоличествоПоРНПТ
	|		ИНАЧЕ -Движения.КоличествоПоРНПТ
	|	КОНЕЦ КАК КоличествоПоРНПТ,
	|	Движения.КоличествоПоРНПТ < 0  КАК СторноКоличества
	|ПОМЕСТИТЬ ДвижениеПрослеживаемыхТоваров
	|ИЗ
	|	ДвиженияДокумента КАК Движения
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|		ПО Движения.НомерГТД = НомераГТД.Ссылка
	|			И НомерГТД.ТипНомераГТД В(ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ),
	|										ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТКомплекта))
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|		ПО Движения.ВидЗапасов = ВидыЗапасов.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ КонтрагентыДокумента КАК Контрагенты
	|		ПО Движения.Организация = Контрагенты.Организация
	|;
	|
	|ВЫБРАТЬ
	|	ДвижениеТоваров.Организация КАК Организация,
	|	ДвижениеТоваров.Контрагент КАК Контрагент,
	|	ДвижениеТоваров.ТипЗапасов КАК ТипЗапасов,
	|	ДвижениеТоваров.ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА СУММА(ДвижениеТоваров.КоличествоПоРНПТ) > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Поступление,
	|	ВЫБОР
	|		КОГДА СУММА(ДвижениеТоваров.КоличествоПоРНПТ) < 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Выбытие,
	|	МАКСИМУМ(ДвижениеТоваров.СторноКоличества) КАК Сторно
	|ИЗ
	|	ДвижениеПрослеживаемыхТоваров КАК ДвижениеТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвижениеТоваров.Организация,
	|	ДвижениеТоваров.Контрагент,
	|	ДвижениеТоваров.ТипЗапасов,
	|	ДвижениеТоваров.ХозяйственнаяОперация
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДвижениеТоваров.КоличествоПоРНПТ) <> 0";
	
	Если ЕстьКолонкаРНПТ Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Движения.НомерГТД КАК НомерГТД", "Движения.РНПТ КАК НомерГТД");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СобственныйТовар(ТипЗапасов)
	Возврат ТипЗапасов = Перечисления.ТипыЗапасов.Товар
		ИЛИ ТипЗапасов = Перечисления.ТипыЗапасов.УдалитьТоварПереданный
		ИЛИ ТипЗапасов = Перечисления.ТипыЗапасов.СобственныйТоварВПути
		ИЛИ ТипЗапасов = Перечисления.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке;
КонецФункции

Функция ОперацииПрослеживаемости(Регистратор, ТаблицаДвижений)
	
	Операции = Новый Массив;
	
	СвойстваРегистратора = ПолучитьСвойстваРегистратора(Регистратор, ТаблицаДвижений);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СформироватьТаблицуКонтрагентовДокумента(МенеджерВременныхТаблиц, Регистратор);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаДвижений", ТаблицаДвижений);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ТаблицаДвижений.Колонки.Найти("ВидЗапасов") = Неопределено Тогда
		ТаблицаДвижений.Колонки.Добавить("ВидЗапасов", Новый ОписаниеТипов("СправочникСсылка.ВидыЗапасов"));
	КонецЕсли;
	Если ТаблицаДвижений.Колонки.Найти("ХозяйственнаяОперация") = Неопределено Тогда
		ТаблицаДвижений.Колонки.Добавить("ХозяйственнаяОперация", Новый ОписаниеТипов("ПеречислениеСсылка.ХозяйственныеОперации"));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапросаДвиженияПрослеживаемогоТовара(ТаблицаДвижений.Колонки.Найти("РНПТ") <> Неопределено);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("Организация") Цикл
		
		СвойстваОрганизации = ПолучитьСвойстваОрганизации(Выборка.Организация, Регистратор.Дата);
		
		Пока Выборка.Следующий() Цикл
			
			СвойстваОперации = НоваяСтруктураСвойствОперации();
			ЗаполнитьЗначенияСвойств(СвойстваОперации, Выборка);
			ЗаполнитьЗначенияСвойств(СвойстваОперации, СвойстваОрганизации);
			ЗаполнитьЗначенияСвойств(СвойстваОперации, СвойстваРегистратора);
			
			Если СвойстваОрганизации.ПлательщикНДС Тогда
				СвойстваОперации.ОблагаетсяНДС = НЕ СвойстваРегистратора.Свойство("НалогообложениеНДС")
					ИЛИ СвойстваРегистратора.НалогообложениеНДС <> Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
			КонецЕсли;
			
			Если ОперациюТребуетсяОтразитьВЖурнале(СвойстваОперации) Тогда
				Операции.Добавить(СвойстваОперации);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Операции;
	
КонецФункции

Функция НоваяСтруктураСвойствОперации()
	
	Свойства = Новый Структура;
	
	Свойства.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Свойства.Вставить("Контрагент", Неопределено);
	
	Свойства.Вставить("ТипРегистратора", Неопределено);
	Свойства.Вставить("ТипЗапасов", Перечисления.ТипыЗапасов.ПустаяСсылка());
	Свойства.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПустаяСсылка());
	Свойства.Вставить("ХозОперацияВнутреннееПотребление", Перечисления.ХозяйственныеОперации.ПустаяСсылка());
	Свойства.Вставить("ВнутреннееПотреблениеТипРасходовВНА", Ложь);
	Свойства.Вставить("КодОперацииВыбытия", Справочники.КодыОперацийПрослеживаемости.ПустаяСсылка());
	Свойства.Вставить("ОблагаетсяНДС", Ложь);
	Свойства.Вставить("Поступление", Ложь);
	Свойства.Вставить("Выбытие", Ложь);
	Свойства.Вставить("Сторно", Ложь);
	
	Возврат Свойства;
	
КонецФункции

Функция ПолучитьСвойстваРегистратора(Регистратор, ТаблицаДвижений)
	
	Свойства = Новый Структура;
	Свойства.Вставить("ТипРегистратора", ТипЗнч(Регистратор));
	
	Если Свойства.ТипРегистратора = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		
		Свойства.Вставить("НалогообложениеНДС", Регистратор.НалогообложениеНДС);
		
	ИначеЕсли Свойства.ТипРегистратора = Тип("ДокументСсылка.ВнутреннееПотребление") Тогда
		
		Свойства.Вставить("ХозОперацияВнутреннееПотребление", Регистратор.ХозяйственнаяОперация);
		Свойства.Вставить("ВнутреннееПотреблениеТипРасходовВНА", ПолучитьТипРасходовВНА(ТаблицаДвижений));
		
	ИначеЕсли Свойства.ТипРегистратора = Тип("ДокументСсылка.СписаниеОС2_4") И Регистратор.ПрослеживаемыеТовары.Количество() > 0 Тогда
		
		 Свойства.Вставить("КодОперацииВыбытия", Регистратор.ПрослеживаемыеТовары[0].КодОперацииВыбытия);
		
	//Если исправительная корректировка реализации, то отражаем в книге продаж
	//Если исправительная корректировка приобретения - в книге покупок. Не смотрим на
	//поступление-выбытие товаров
	ИначеЕсли Свойства.ТипРегистратора = Тип("ДокументСсылка.КорректировкаРеализации") 
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "ВидКорректировки") = Перечисления.ХозяйственныеОперации.ИсправлениеОшибок Тогда
		
		Свойства.Вставить("Выбытие", Истина);
		Свойства.Вставить("Поступление", Ложь);
		
	ИначеЕсли Свойства.ТипРегистратора = Тип("ДокументСсылка.КорректировкаПриобретения") 
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "ВидКорректировки") = Перечисления.ХозяйственныеОперации.ИсправлениеОшибок Тогда
		
		Свойства.Вставить("Выбытие", Ложь);
		Свойства.Вставить("Поступление", Истина);
	
	КонецЕсли;
	
	Возврат Свойства;
	
КонецФункции

Функция ПолучитьТипРасходовВНА(ТаблицаДвижений)
	
	Если ТаблицаДвижений.Колонки.Найти("СтатьяРасходов") = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДвижений.НомерГТД КАК НомерГТД,
	|	ТаблицаДвижений.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаДвижений.АналитикаРасходов КАК АналитикаРасходов
	|ПОМЕСТИТЬ ВТ_ТаблицаДвижений
	|ИЗ
	|	&ТаблицаДвижений КАК ТаблицаДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДвижений.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	ВТ_ТаблицаДвижений КАК ВТ_ТаблицаДвижений
	|ГДЕ
	|	(ВТ_ТаблицаДвижений.НомерГТД.ТипНомераГТД = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ)
	|				ИЛИ ВТ_ТаблицаДвижений.НомерГТД.ТипНомераГТД = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТКомплекта))
	|	И ВТ_ТаблицаДвижений.СтатьяРасходов.ТипРасходов = ЗНАЧЕНИЕ(Перечисление.ТипыРасходов.ФормированиеСтоимостиВНА)
	|	И ТИПЗНАЧЕНИЯ(ВТ_ТаблицаДвижений.АналитикаРасходов) = ТИП(Справочник.ОбъектыЭксплуатации)";
	Запрос.УстановитьПараметр("ТаблицаДвижений", ТаблицаДвижений);
	
	Возврат НЕ Запрос.Выполнить().Пустой();

КонецФункции

Функция ПолучитьСвойстваОрганизации(Организация, Дата)
	
	Свойства = Новый Структура;
	Свойства.Вставить("Организация", Организация);
	Свойства.Вставить("ПлательщикНДС", Ложь);
	
	НастройкиУчетаНДС = НастройкиНалоговУчетныхПолитикПовтИсп.ДействующиеПараметрыНалоговУчетныхПолитик(
		"НастройкиУчетаНДС", Организация, Дата);
	
	Если НЕ НастройкиУчетаНДС = Неопределено И ТипЗнч(НастройкиУчетаНДС) = Тип("Структура") Тогда
		Свойства.ПлательщикНДС =
			НастройкиУчетаНДС.Свойство("ПлательщикНДС") И НастройкиУчетаНДС.ПлательщикНДС
			И НЕ (НастройкиУчетаНДС.Свойство("ПрименяетсяОсвобождениеОтУплатыНДС") И НастройкиУчетаНДС.ПрименяетсяОсвобождениеОтУплатыНДС);
	КонецЕсли;
	
	Возврат Свойства;
	
КонецФункции

Функция ОперациюТребуетсяОтразитьВЖурнале(СвойстваОперации)
	
	Если СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		И СвойстваОперации.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ОтчетПрослеживаемойОперации(СвойстваОперации)
	
	ОтражатьВОтчете = Перечисления.ПорядокОтраженияВОтчетностиПоПрослеживаемости;
	
	Если НЕ ОтражатьВОтчетахПрослеживаемости(СвойстваОперации.ТипРегистратора) Тогда
		
		Возврат ОтражатьВОтчете.ПустаяСсылка();
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВнутреннееПотребление") 
		И (СвойстваОперации.ВнутреннееПотреблениеТипРасходовВНА 
			ИЛИ СвойстваОперации.ХозОперацияВнутреннееПотребление = Перечисления.ХозяйственныеОперации.ПередачаВЭксплуатацию) Тогда
		
		Возврат ОтражатьВОтчете.НеОтражаетсяВОтчетности;
		
	ИначеЕсли НЕ ЭтоТорговыйДокумент(СвойстваОперации.ТипРегистратора) Тогда
		
		Возврат ОтражатьВОтчете.ОтчетОбОперациях;
		
	ИначеЕсли НЕ СобственныйТовар(СвойстваОперации.ТипЗапасов) Тогда
		
		Возврат ОтражатьВОтчете.ЖурналСФ;
		
	ИначеЕсли НЕ СвойстваОперации.ОблагаетсяНДС
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.СписаниеОС2_4") Тогда
		
		Возврат ОтражатьВОтчете.ОтчетОбОперациях;
		
	ИначеЕсли СвойстваОперации.Поступление Тогда
		
		Возврат ОтражатьВОтчете.КнигаПокупок;
		
	ИначеЕсли СвойстваОперации.Выбытие Тогда
		
		Возврат ОтражатьВОтчете.КнигаПродаж;
		
	КонецЕсли;
	
КонецФункции

Функция КодПрослеживаемойОперации(СвойстваОперации)
	
	КодОперации = ОперацияПрослеживаемости();
	
	Если НЕ ОтражатьВОтчетахПрослеживаемости(СвойстваОперации.ТипРегистратора) Тогда
		Возврат КодОперации;
	КонецЕсли;
	
	ХозОперации = Перечисления.ХозяйственныеОперации;
	
	Если СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВнутреннееПотребление")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВыпускПродукции")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ДвижениеПродукцииИМатериалов")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОтчетКомиссионераОСписании")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациямиОСписании")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ПередачаМатериаловВПроизводство")
		//++ Устарело_Переработка24
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ПередачаСырьяПереработчику")
		//-- Устарело_Переработка24
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ПроизводствоБезЗаказа")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.РаспределениеВозвратныхОтходов")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.РаспределениеПроизводственныхЗатрат")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.СборкаТоваров")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОтчетОСписанииТоваровУХранителя") Тогда
		
		КодОперации = ОперацияПрослеживаемости("01");
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		
		КодОперации = ОперацияПрослеживаемости("04");
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.СписаниеНедостачТоваров") Тогда
		
		КодОперации = ОперацияПрослеживаемости("09");
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ПрочееОприходованиеТоваров")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОприходованиеИзлишковТоваров") Тогда
			
		КодОперации = ОперацияПрослеживаемости("10");
		
	//++ Устарело_Переработка24
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВозвратСырьяОтПереработчика")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВозвратМатериаловИзПроизводства") Тогда
		
		КодОперации = ОперацияПрослеживаемости("12");
	//-- Устарело_Переработка24
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		
		Если СвойстваОперации.ХозяйственнаяОперация = ХозОперации.ВозвратОтРозничногоПокупателя Тогда
			КодОперации = ОперацияПрослеживаемости("11");
		Иначе
			КодОперации = ОперацияПрослеживаемости("19");
		КонецЕсли;
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВыкупТоваровХранителем")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОтгрузкаТоваровСХранения")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		
		КодОперации = ОперацияПрослеживаемости("15");
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВыкупПринятыхНаХранениеТоваров")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт") Тогда
		
		КодОперации = ОперацияПрослеживаемости("17");
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		
		Если СвойстваОперации.ХозяйственнаяОперация = ХозОперации.ПриемНаКомиссию Тогда
		Иначе 
			КодОперации = ОперацияПрослеживаемости("17");
		КонецЕсли;
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		Если СвойстваОперации.ХозяйственнаяОперация = ХозОперации.ПередачаНаКомиссию Тогда
			
		ИначеЕсли СобственныйТовар(СвойстваОперации.ТипЗапасов) Тогда
			КодОперации = ОперацияПрослеживаемости("15");
		КонецЕсли;
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
		
		Если СвойстваОперации.ХозяйственнаяОперация = ХозОперации.РеализацияТоваровВДругуюОрганизацию Тогда
			Если СвойстваОперации.Поступление Тогда
				КодОперации = ОперацияПрослеживаемости("17");
			ИначеЕсли СобственныйТовар(СвойстваОперации.ТипЗапасов) Тогда
				КодОперации = ОперацияПрослеживаемости("15");
			Иначе
				// Реализация комиссионных товаров
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		
		Если СвойстваОперации.ХозяйственнаяОперация = ХозОперации.ВозвратТоваровМеждуОрганизациями Тогда
			Если СвойстваОперации.Сторно Тогда
				КодОперации = ОперацияПрослеживаемости("19");
			Иначе
				КодОперации = ОперацияПрослеживаемости("21");
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		КодОперации = ОперацияПрослеживаемости("21");
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		
		Если СвойстваОперации.Выбытие Тогда
			КодОперации = ОперацияПрослеживаемости("20");
		Иначе
			КодОперации = ОперацияПрослеживаемости("19");
		КонецЕсли;
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.СписаниеОС2_4") Тогда
		
		КодОперации = СвойстваОперации.КодОперацииВыбытия;
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ИсправлениеРазвернутогоСальдоТоваровОрганизаций") Тогда
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.КорректировкаВидаДеятельностиНДС") Тогда
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.КорректировкаНазначенияТоваров") Тогда
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.КорректировкаНалогообложенияНДСПартийТоваров") Тогда
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.КорректировкаОбособленногоУчетаЗапасов") Тогда
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.МаркировкаТоваровГИСМ") Тогда
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОтчетКомиссионера") Тогда
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") Тогда
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ПеремаркировкаТоваровГИСМ") Тогда
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.Сторно") Тогда
	КонецЕсли;
	
	Возврат КодОперации;
	
КонецФункции

Функция ТипДокументаПрослеживаемойОперации(СвойстваОперации)
	
	// Согласно правилам заполнения отчета об операциях требуется выделять следующие типы документов: 
	//   1 – счет-фактура
	//   2 – корректировочный счет-фактура
	//   3 – универсальный передаточный документ
	//   4 – универсальный корректировочный документ
	//
	// Во всех остальных случаях указывается код:
	//   5 – иной первичный учетный документ
	
	ТипДокумента = Справочники.ТипыДокументов.ПустаяСсылка();
	
	Если СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		
		ТипДокумента = Справочники.ТипыДокументов.ОтчетОРозничныхПродажах
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОтчетКомитенту")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОприходованиеИзлишковТоваров")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		
		ТипДокумента = Справочники.ТипыДокументов.Акт;
		
	ИначеЕсли СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОтчетКомиссионера")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.КорректировкаРеализации")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
		ИЛИ СвойстваОперации.ТипРегистратора = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		
		ТипДокумента = Справочники.ТипыДокументов.Накладная;
		
	КонецЕсли;
	
	Возврат ТипДокумента;
	
КонецФункции

Функция ЭтоТорговыйДокумент(ТипДокумента)
	
	Возврат ТипДокумента = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.КорректировкаПриобретения")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.КорректировкаРеализации")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОтчетКомиссионера")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОтчетОРозничныхПродажах")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОтгрузкаТоваровСХранения")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВыкупПринятыхНаХранениеТоваров")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВыкупТоваровХранителем")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВыкупТоваровХранителем")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВыкупАрендованныхОС")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.СписаниеОС2_4")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов");
	
КонецФункции

Функция ОтражатьВОтчетахПрослеживаемости(ТипДокумента)
	
	Возврат НЕ (ТипДокумента = Тип("ДокументСсылка.ПорчаТоваров")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПеремещениеТоваров")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПересортицаТоваров")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПередачаТоваровХранителю")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПриемкаТоваровНаХранение")
		//++ Устарело_Переработка24
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПоступлениеОтПереработчика")
		//-- Устарело_Переработка24
		
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваровНаСклад")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваровОтХранителя")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ОтчетОСписанииТоваровСХранения"));
	
КонецФункции

//Выполняет отмену отражения в отчетности ПТ документа внутреннее потребление товаров
//при проведении исправительного документа
Процедура ОтменитьОтражениеИсправляемогоДокумента(Регистратор)
	
	Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.ВнутреннееПотребление") И Регистратор.Исправление
		И ЗначениеЗаполнено(Регистратор.ИсправляемыйДокумент) Тогда
		НаборЗаписей = РегистрыСведений.ОперацииСПрослеживаемымиТоварами.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Регистратор.ИсправляемыйДокумент);
		НаборЗаписей.Прочитать();
		
		Для каждого СтрокаНЗ Из НаборЗаписей Цикл
			СтрокаНЗ.ОтражениеВОтчетности = Перечисления.ПорядокОтраженияВОтчетностиПоПрослеживаемости.НеОтражаетсяВОтчетности;
		КонецЦикла;
	
		НаборЗаписей.Записать();
	КонецЕсли;

КонецПроцедуры

Процедура ДополнитьЗапросИнформациейОРозничныхПродажах(СтруктураПараметров)

	Если НЕ СтруктураПараметров.ЕстьРозничныеПродажи Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос = СтруктураПараметров.Запрос;
	
	Запрос.УстановитьПараметр("ТипыЗапасовСобственные", Перечисления.ТипыЗапасов.ТипыЗапасовСобственные());
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СводнаяСправкаНДС.Организация КАК Организация,
	|	СводнаяСправкаНДС.Дата КАК КонецПериода,
	|	ВЫБОР
	|		КОГДА СводнаяСправкаНДС.ПериодичностьОформления = ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(СводнаяСправкаНДС.Дата, МЕСЯЦ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(СводнаяСправкаНДС.Дата, КВАРТАЛ)
	|	КОНЕЦ КАК НачалоПериода
	|ПОМЕСТИТЬ ПериодыРозничныхПродаж
	|ИЗ
	|	ВТ_СписокДокументов КАК ВТ_СписокДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СводнаяСправкаНДС КАК СводнаяСправкаНДС
	|		ПО ВТ_СписокДокументов.Документ = СводнаяСправкаНДС.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетОРозничныхПродажах.КассоваяСмена КАК КассоваяСмена
	|ПОМЕСТИТЬ КассовыеСменыСОтчетами
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетОРозничныхПродажах.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетОРозничныхПродажах.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Проведен
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетОРозничныхВозвратах.КассоваяСмена
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах КАК ОтчетОРозничныхВозвратах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетОРозничныхВозвратах.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетОРозничныхВозвратах.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетОРозничныхВозвратах.Проведен
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КассоваяСмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЧекККМ.Ссылка КАК Документ
	|ПОМЕСТИТЬ ВТ_СписокДокументовСРозничнымиПродажами
	|ИЗ
	|	Документ.ЧекККМ КАК ЧекККМ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ЧекККМ.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ЧекККМ.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ЧекККМ.Проведен
	|	И ЧекККМ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|	И НЕ ЧекККМ.КассоваяСмена В
	|				(ВЫБРАТЬ
	|					Т.КассоваяСмена
	|				ИЗ
	|					КассовыеСменыСОтчетами КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЧекККМВозврат.Ссылка
	|ИЗ
	|	Документ.ЧекККМВозврат КАК ЧекККМВозврат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ЧекККМВозврат.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ЧекККМВозврат.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ЧекККМВозврат.Проведен
	|	И ЧекККМВозврат.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|	И НЕ ЧекККМВозврат.КассоваяСмена В
	|				(ВЫБРАТЬ
	|					Т.КассоваяСмена
	|				ИЗ
	|					КассовыеСменыСОтчетами КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЧекККМКоррекции.Ссылка
	|ИЗ
	|	Документ.ЧекККМКоррекции КАК ЧекККМКоррекции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ЧекККМКоррекции.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ЧекККМКоррекции.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|			И (ЧекККМКоррекции.ЧекККМ.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ЧекККМКоррекции.Проведен
	|	И ЧекККМКоррекции.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|	И НЕ ЧекККМКоррекции.КассоваяСмена В
	|				(ВЫБРАТЬ
	|					Т.КассоваяСмена
	|				ИЗ
	|					КассовыеСменыСОтчетами КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетОРозничныхПродажахТовары.Ссылка
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Товары КАК ОтчетОРозничныхПродажахТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КассовыеСменыСОтчетами КАК КассовыеСменыСОтчетами
	|		ПО (КассовыеСменыСОтчетами.КассоваяСмена = ОтчетОРозничныхПродажахТовары.Ссылка.КассоваяСмена)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетОРозничныхПродажахТовары.Ссылка.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетОРозничныхПродажахТовары.Ссылка.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетОРозничныхПродажахТовары.Ссылка.Проведен
	|	И ОтчетОРозничныхПродажахТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетОРозничныхПродажахТовары.Ссылка
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.ВидыЗапасов КАК ОтчетОРозничныхПродажахТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КассовыеСменыСОтчетами КАК КассовыеСменыСОтчетами
	|		ПО (КассовыеСменыСОтчетами.КассоваяСмена = ОтчетОРозничныхПродажахТовары.Ссылка.КассоваяСмена)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетОРозничныхПродажахТовары.Ссылка.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетОРозничныхПродажахТовары.Ссылка.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетОРозничныхПродажахТовары.Ссылка.Проведен
	|	И ОтчетОРозничныхПродажахТовары.ВидЗапасов.ТипЗапасов В(&ТипыЗапасовСобственные)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетКомиссионераТовары.Ссылка
	|ИЗ
	|	Документ.ОтчетКомиссионера.ВидыЗапасов КАК ОтчетКомиссионераТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетКомиссионераТовары.Ссылка.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетКомиссионераТовары.Ссылка.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетКомиссионераТовары.Ссылка.Проведен
	|	И ОтчетКомиссионераТовары.ВидЗапасов.ТипЗапасов В(&ТипыЗапасовСобственные)
	|	И (НЕ ОтчетКомиссионераТовары.Покупатель.ЮрФизЛицо В (ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо), ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель))
	|			ИЛИ ОтчетКомиссионераТовары.Покупатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|	И ОтчетКомиссионераТовары.НомерСчетаФактурыКомиссионера = """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетКомиссионераТовары.Ссылка
	|ИЗ
	|	Документ.ОтчетКомиссионера.Товары КАК ОтчетКомиссионераТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетКомиссионераТовары.Ссылка.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетКомиссионераТовары.Ссылка.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетКомиссионераТовары.Ссылка.Проведен
	|	И ОтчетКомиссионераТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга))
	|	И (НЕ ОтчетКомиссионераТовары.Покупатель.ЮрФизЛицо В (ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо), ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель))
	|			ИЛИ ОтчетКомиссионераТовары.Покупатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|	И ОтчетКомиссионераТовары.НомерСчетаФактурыКомиссионера = """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетОРозничныхВозвратахТовары.Ссылка
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах.Товары КАК ОтчетОРозничныхВозвратахТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КассовыеСменыСОтчетами КАК КассовыеСменыСОтчетами
	|		ПО (КассовыеСменыСОтчетами.КассоваяСмена = ОтчетОРозничныхВозвратахТовары.Ссылка.КассоваяСмена)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетОРозничныхВозвратахТовары.Ссылка.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетОРозничныхВозвратахТовары.Ссылка.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|			И (ОтчетОРозничныхВозвратахТовары.ДокументРеализации.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетОРозничныхВозвратахТовары.Ссылка.Проведен
	|	И ОтчетОРозничныхВозвратахТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга))
	|	И ОтчетОРозничныхВозвратахТовары.ПоЧекуКоррекции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетОРозничныхВозвратахТовары.Ссылка
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах.ВидыЗапасов КАК ОтчетОРозничныхВозвратахТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КассовыеСменыСОтчетами КАК КассовыеСменыСОтчетами
	|		ПО (КассовыеСменыСОтчетами.КассоваяСмена = ОтчетОРозничныхВозвратахТовары.Ссылка.КассоваяСмена)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетОРозничныхВозвратахТовары.Ссылка.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетОРозничныхВозвратахТовары.Ссылка.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|			И (ОтчетОРозничныхВозвратахТовары.ДокументРеализации.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетОРозничныхВозвратахТовары.Ссылка.Проведен
	|	И ОтчетОРозничныхВозвратахТовары.ВидЗапасов.ТипЗапасов В(&ТипыЗапасовСобственные)
	|	И ОтчетОРозничныхВозвратахТовары.ПоЧекуКоррекции
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПериодыРозничныхПродаж
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ КассовыеСменыСОтчетами";
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();

КонецПроцедуры // ДополнитьЗапросИнформациейОРозничныхПродажах()

Процедура ПолучитьОсновныеДанныеРНПТ(СтруктураПараметров)
	
	Запрос = СтруктураПараметров.Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписокДокументов.Документ КАК Документ,
	|	Операции.ТипЗапасов КАК ТипЗапасов,
	|	Операции.Организация КАК Организация,
	|	ТоварыОрганизаций.НомерГТД КАК РегНомПросл,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД.ЕдиницаИзмерения.Код КАК ОКЕИ,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|	СУММА(ТоварыОрганизаций.КоличествоПоРНПТ) КАК Количество,
	|	NULL КАК КодОперации
	|ПОМЕСТИТЬ ВТ_ПрослеживаемыеОперации
	|ИЗ
	|	РегистрСведений.ОперацииСПрослеживаемымиТоварами КАК Операции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #СписокДокументов КАК СписокДокументов
	|		ПО Операции.Регистратор = СписокДокументов.Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|		ПО Операции.Регистратор = ТоварыОрганизаций.Регистратор
	|			И Операции.Организация = ТоварыОрганизаций.Организация
	|			И Операции.ТипЗапасов = ТоварыОрганизаций.ВидЗапасов.ТипЗапасов
	|			И ТоварыОрганизаций.НомерГТД.ТипНомераГТД В(ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ),
	|														ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТКомплекта))
	|
	|ГДЕ
	|	Операции.ОтражениеВОтчетности = &ОтражениеВОтчетности
	|	И НЕ ТИПЗНАЧЕНИЯ(СписокДокументов.Документ) = ТИП(Документ.КорректировкаПриобретения)
	|	И НЕ ТИПЗНАЧЕНИЯ(СписокДокументов.Документ) = ТИП(Документ.КорректировкаРеализации)
	|	И Операции.Организация В(&Организация)
	|	И Операции.Активность
	|	И ТоварыОрганизаций.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокДокументов.Документ,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
	|	ТоварыОрганизаций.ВидЗапасов,
	|	Операции.Организация,
	|	ТоварыОрганизаций.НомерГТД,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД.ЕдиницаИзмерения.Код,
	|	Операции.ТипЗапасов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписокДокументов.Документ,
	|	Операции.ТипЗапасов,
	|	Операции.Организация,
	|	ТоварыПереданныеНаКомиссию.НомерГТД,
	|	ТоварыПереданныеНаКомиссию.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД.ЕдиницаИзмерения.Код,
	|	ТоварыПереданныеНаКомиссию.АналитикаУчетаНоменклатуры,
	|	ТоварыПереданныеНаКомиссию.ВидЗапасов,
	|	СУММА(ТоварыПереданныеНаКомиссию.КоличествоПоРНПТ),
	|	NULL
	|ИЗ
	|	РегистрСведений.ОперацииСПрослеживаемымиТоварами КАК Операции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #СписокДокументов КАК СписокДокументов
	|		ПО Операции.Регистратор = СписокДокументов.Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ТоварыПереданныеНаКомиссию
	|		ПО Операции.Регистратор = ТоварыПереданныеНаКомиссию.Регистратор
	|			И Операции.Организация = ТоварыПереданныеНаКомиссию.Организация
	|			И Операции.ТипЗапасов = ТоварыПереданныеНаКомиссию.ВидЗапасов.ТипЗапасов
	|			И (ТоварыПереданныеНаКомиссию.НомерГТД.ТипНомераГТД В(ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ),
	|														ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТКомплекта)))
	|ГДЕ
	|	Операции.ОтражениеВОтчетности = &ОтражениеВОтчетности
	|	И НЕ ТИПЗНАЧЕНИЯ(СписокДокументов.Документ) = ТИП(Документ.КорректировкаПриобретения)
	|	И НЕ ТИПЗНАЧЕНИЯ(СписокДокументов.Документ) = ТИП(Документ.КорректировкаРеализации)
	|	И Операции.Организация В(&Организация)
	|	И Операции.Активность
	|	И ТоварыПереданныеНаКомиссию.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокДокументов.Документ,
	|	ТоварыПереданныеНаКомиссию.АналитикаУчетаНоменклатуры,
	|	ТоварыПереданныеНаКомиссию.ВидЗапасов,
	|	Операции.Организация,
	|	ТоварыПереданныеНаКомиссию.НомерГТД,
	|	ТоварыПереданныеНаКомиссию.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД.ЕдиницаИзмерения.Код,
	|	Операции.ТипЗапасов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписокДокументов.Документ,
	|	Операции.ВидЗапасов.ТипЗапасов,
	|	Операции.Организация,
	|	Операции.РНПТ,
	|	Операции.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД.ЕдиницаИзмерения.Код,
	|	Операции.АналитикаУчетаНоменклатуры,
	|	Операции.ВидЗапасов,
	|	СУММА(Операции.КоличествоПоРНПТ),
	|	Операции.КодОперации
	|ИЗ
	|	РегистрСведений.ОперацииСПрослеживаемымиТоварамиРасширенный КАК Операции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #СписокДокументов КАК СписокДокументов
	|		ПО Операции.Регистратор = СписокДокументов.Документ
	|ГДЕ
	|	Операции.ОтражениеВОтчетности = &ОтражениеВОтчетности
	|	И НЕ ТИПЗНАЧЕНИЯ(СписокДокументов.Документ) = ТИП(Документ.КорректировкаПриобретения)
	|	И НЕ ТИПЗНАЧЕНИЯ(СписокДокументов.Документ) = ТИП(Документ.КорректировкаРеализации)
	|	И Операции.Организация В(&Организация)
	|	И Операции.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокДокументов.Документ,
	|	Операции.АналитикаУчетаНоменклатуры,
	|	Операции.ВидЗапасов,
	|	Операции.Организация,
	|	Операции.РНПТ,
	|	Операции.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД.ЕдиницаИзмерения.Код,
	|	Операции.ВидЗапасов.ТипЗапасов,
	|	Операции.КодОперации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СебестоимостьТоваров.Регистратор КАК Регистратор,
	|	СебестоимостьТоваров.Организация КАК Организация,
	|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	СебестоимостьТоваров.ВидЗапасов КАК ВидЗапасов,
	|	СУММА(СебестоимостьТоваров.СтоимостьРегл) КАК СтоимостьРегл
	|ПОМЕСТИТЬ ВТ_СебестоимостьТоваров
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|ГДЕ
	|	СебестоимостьТоваров.Активность
	|	И (СебестоимостьТоваров.Регистратор, СебестоимостьТоваров.Организация, СебестоимостьТоваров.АналитикаУчетаНоменклатуры, СебестоимостьТоваров.ВидЗапасов) В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВТ_ПрослеживаемыеОперации.Документ,
	|				ВТ_ПрослеживаемыеОперации.Организация,
	|				ВТ_ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры,
	|				ВТ_ПрослеживаемыеОперации.ВидЗапасов
	|			ИЗ
	|				ВТ_ПрослеживаемыеОперации)
	|
	|СГРУППИРОВАТЬ ПО
	|	СебестоимостьТоваров.Регистратор,
	|	СебестоимостьТоваров.Организация,
	|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры,
	|	СебестоимостьТоваров.ВидЗапасов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СебестоимостьТоваров.Регистратор,
	|	СебестоимостьТоваров.Организация,
	|	ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры,
	|	ПрослеживаемыеОперации.ВидЗапасов,
	|	СУММА(СебестоимостьТоваров.СтоимостьРегл)
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПрослеживаемыеОперации КАК ПрослеживаемыеОперации
	|		ПО СебестоимостьТоваров.Регистратор = ПрослеживаемыеОперации.Документ
	|			И СебестоимостьТоваров.Организация = ПрослеживаемыеОперации.Организация
	|			И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура = ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры.Номенклатура
	|			И (СебестоимостьТоваров.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|				ИЛИ СебестоимостьТоваров.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке))
	|
	|СГРУППИРОВАТЬ ПО
	|	СебестоимостьТоваров.Регистратор,
	|	СебестоимостьТоваров.Организация,
	|	ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры,
	|	ПрослеживаемыеОперации.ВидЗапасов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыручкаИСебестоимостьПродаж.Регистратор КАК Регистратор,
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВыручкаИСебестоимостьПродаж.ВидЗапасов КАК ВидЗапасов,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл) КАК СуммаВыручкиРегл
	|ПОМЕСТИТЬ ВТ_ВыручкаИСебестоимостьПродаж
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|ГДЕ
	|	ВыручкаИСебестоимостьПродаж.Активность
	|	И (ВыручкаИСебестоимостьПродаж.Регистратор, ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры, ВыручкаИСебестоимостьПродаж.ВидЗапасов) В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВТ_ПрослеживаемыеОперации.Документ,
	|				ВТ_ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры,
	|				ВТ_ПрослеживаемыеОперации.ВидЗапасов
	|			ИЗ
	|				ВТ_ПрослеживаемыеОперации)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыручкаИСебестоимостьПродаж.Регистратор,
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры,
	|	ВыручкаИСебестоимостьПродаж.ВидЗапасов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлениюДокументовИмпорта.Регистратор КАК Регистратор,
	|	ТоварыКОформлениюДокументовИмпорта.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТоварыКОформлениюДокументовИмпорта.ВидЗапасов КАК ВидЗапасов,
	|	СУММА(ТоварыКОформлениюДокументовИмпорта.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТ_ТоварыКОформлениюДокументовИмпорта
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюДокументовИмпорта КАК ТоварыКОформлениюДокументовИмпорта
	|ГДЕ
	|	ТоварыКОформлениюДокументовИмпорта.Активность
	|	И (ТоварыКОформлениюДокументовИмпорта.Регистратор, ТоварыКОформлениюДокументовИмпорта.АналитикаУчетаНоменклатуры, ТоварыКОформлениюДокументовИмпорта.ВидЗапасов) В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВТ_ПрослеживаемыеОперации.Документ,
	|				ВТ_ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры,
	|				ВТ_ПрослеживаемыеОперации.ВидЗапасов
	|			ИЗ
	|				ВТ_ПрослеживаемыеОперации)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлениюДокументовИмпорта.Регистратор,
	|	ТоварыКОформлениюДокументовИмпорта.АналитикаУчетаНоменклатуры,
	|	ТоварыКОформлениюДокументовИмпорта.ВидЗапасов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПрослеживаемыеОперации.Документ КАК Документ,
	|	ВТ_ПрослеживаемыеОперации.Организация КАК Организация,
	|	ВТ_ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВТ_ПрослеживаемыеОперации.ВидЗапасов КАК ВидЗапасов,
	|	СУММА(ВТ_ПрослеживаемыеОперации.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_КоличествоТоваров
	|ИЗ
	|	ВТ_ПрослеживаемыеОперации КАК ВТ_ПрослеживаемыеОперации
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПрослеживаемыеОперации.Документ,
	|	ВТ_ПрослеживаемыеОперации.Организация,
	|	ВТ_ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры,
	|	ВТ_ПрослеживаемыеОперации.ВидЗапасов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрослеживаемыеОперации.Документ КАК Документ,
	|	ПрослеживаемыеОперации.ТипЗапасов КАК ТипЗапасов,
	|	ПрослеживаемыеОперации.Организация КАК Организация,
	|	ПрослеживаемыеОперации.РегНомПросл КАК РегНомПросл,
	|	ПрослеживаемыеОперации.ОКЕИ КАК ОКЕИ,
	|	ВЫБОР
	|		КОГДА ПрослеживаемыеОперации.Количество >= 0
	|			ТОГДА ПрослеживаемыеОперации.Количество
	|		ИНАЧЕ -ПрослеживаемыеОперации.Количество
	|	КОНЕЦ КАК КолТовПросл,
	|	ВЫБОР
	|		КОГДА ВТ_КоличествоТоваров.Количество = 0
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл, ЕСТЬNULL(СебестоимостьТоваров.СтоимостьРегл, 0)) >= 0
	|			ТОГДА ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл, ЕСТЬNULL(СебестоимостьТоваров.СтоимостьРегл, 0)) / ВТ_КоличествоТоваров.Количество * ПрослеживаемыеОперации.Количество
	|		ИНАЧЕ -ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл, ЕСТЬNULL(СебестоимостьТоваров.СтоимостьРегл, 0)) / ВТ_КоличествоТоваров.Количество * ПрослеживаемыеОперации.Количество
	|	КОНЕЦ КАК СтоимТовПросл,
	|	ПрослеживаемыеОперации.КодОперации КАК КодОперации
	|ПОМЕСТИТЬ вт_ПокупкиПродажи
	|ИЗ
	|	ВТ_ПрослеживаемыеОперации КАК ПрослеживаемыеОперации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СебестоимостьТоваров КАК СебестоимостьТоваров
	|		ПО ПрослеживаемыеОперации.Документ = СебестоимостьТоваров.Регистратор
	|			И ПрослеживаемыеОперации.Организация = СебестоимостьТоваров.Организация
	|			И ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры = СебестоимостьТоваров.АналитикаУчетаНоменклатуры
	|			И ПрослеживаемыеОперации.ВидЗапасов = СебестоимостьТоваров.ВидЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|		ПО ПрослеживаемыеОперации.Документ = ВыручкаИСебестоимостьПродаж.Регистратор
	|			И ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры = ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры
	|			И ПрослеживаемыеОперации.ВидЗапасов = ВыручкаИСебестоимостьПродаж.ВидЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоТоваров КАК ВТ_КоличествоТоваров
	|		ПО ПрослеживаемыеОперации.Документ = ВТ_КоличествоТоваров.Документ
	|			И ПрослеживаемыеОперации.Организация = ВТ_КоличествоТоваров.Организация
	|			И ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры = ВТ_КоличествоТоваров.АналитикаУчетаНоменклатуры
	|			И ПрослеживаемыеОперации.ВидЗапасов = ВТ_КоличествоТоваров.ВидЗапасов
	|ГДЕ
	|	НЕ ТИПЗНАЧЕНИЯ(ПрослеживаемыеОперации.Документ) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрослеживаемыеОперации.Документ,
	|	ПрослеживаемыеОперации.ТипЗапасов,
	|	ПрослеживаемыеОперации.Организация,
	|	ПрослеживаемыеОперации.РегНомПросл,
	|	ПрослеживаемыеОперации.ОКЕИ,
	|	ВЫБОР
	|		КОГДА ПрослеживаемыеОперации.Количество >= 0
	|			ТОГДА ПрослеживаемыеОперации.Количество
	|		ИНАЧЕ -ПрослеживаемыеОперации.Количество
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТ_КоличествоТоваров.Количество = 0
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(ВТ_ТоварыКОформлениюДокументовИмпорта.Сумма, 0) >= 0
	|			ТОГДА ЕСТЬNULL(ВТ_ТоварыКОформлениюДокументовИмпорта.Сумма, 0) / ВТ_КоличествоТоваров.Количество * ПрослеживаемыеОперации.Количество
	|		ИНАЧЕ -ВТ_ТоварыКОформлениюДокументовИмпорта.Сумма / ВТ_КоличествоТоваров.Количество * ПрослеживаемыеОперации.Количество
	|	КОНЕЦ,
	|	NULL
	|ИЗ
	|	ВТ_ПрослеживаемыеОперации КАК ПрослеживаемыеОперации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТоварыКОформлениюДокументовИмпорта КАК ВТ_ТоварыКОформлениюДокументовИмпорта
	|		ПО ПрослеживаемыеОперации.Документ = ВТ_ТоварыКОформлениюДокументовИмпорта.Регистратор
	|			И ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры = ВТ_ТоварыКОформлениюДокументовИмпорта.АналитикаУчетаНоменклатуры
	|			И ПрослеживаемыеОперации.ВидЗапасов = ВТ_ТоварыКОформлениюДокументовИмпорта.ВидЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоТоваров КАК ВТ_КоличествоТоваров
	|		ПО ПрослеживаемыеОперации.Документ = ВТ_КоличествоТоваров.Документ
	|			И ПрослеживаемыеОперации.Организация = ВТ_КоличествоТоваров.Организация
	|			И ПрослеживаемыеОперации.АналитикаУчетаНоменклатуры = ВТ_КоличествоТоваров.АналитикаУчетаНоменклатуры
	|			И ПрослеживаемыеОперации.ВидЗапасов = ВТ_КоличествоТоваров.ВидЗапасов
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ПрослеживаемыеОперации.Документ) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ПрослеживаемыеОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_СебестоимостьТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ВыручкаИСебестоимостьПродаж
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТоварыКОформлениюДокументовИмпорта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_КоличествоТоваров";
	
	Если СтруктураПараметров.ЕстьРозничныеПродажи Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#СписокДокументов", "ВТ_СписокДокументовСРозничнымиПродажами");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#СписокДокументов", "ВТ_СписокДокументов");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();

КонецПроцедуры // ПолучитьОсновныеДанныеРНПТ()

Процедура ПолучитьДанныеРНПТКорректировкиПриобретения(СтруктураПараметров)
	
	Если НЕ СтруктураПараметров.ЕстьКорректировкиПриобретения Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = СтруктураПараметров.Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаБазовыхВалют.Организация КАК Организация,
	|	ТаблицаБазовыхВалют.БазоваяВалюта КАК БазоваяВалюта
	|ПОМЕСТИТЬ ВТ_БазовыеВалюты
	|ИЗ
	|	&ТаблицаБазовыхВалют КАК ТаблицаБазовыхВалют
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СписокДокументов.Документ КАК Документ,
	|	Операции.ТипЗапасов КАК ТипЗапасов,
	|	Операции.Организация КАК Организация,
	|	МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|	КорректировкаПриобретения.Валюта КАК Валюта,
	|	ВТ_БазовыеВалюты.БазоваяВалюта КАК БазоваяВалюта,
	|	КурсыВалют.БазоваяВалюта КАК БазоваяВалютаРегистра,
	|	NULL КАК КодОперации
	|ПОМЕСТИТЬ ВТ_КорректировкиПриобретения_Предварительная
	|ИЗ
	|	РегистрСведений.ОперацииСПрослеживаемымиТоварами КАК Операции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокДокументов КАК ВТ_СписокДокументов
	|		ПО Операции.Регистратор = ВТ_СписокДокументов.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_БазовыеВалюты КАК ВТ_БазовыеВалюты
	|			ПО КорректировкаПриобретения.Организация = ВТ_БазовыеВалюты.Организация
	|		ПО (ВТ_СписокДокументов.Документ = КорректировкаПриобретения.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО (КурсыВалют.Валюта = КорректировкаПриобретения.Валюта)
	|			И (КурсыВалют.Период < КорректировкаПриобретения.ДокументОснование.Дата)
	|ГДЕ
	|	Операции.ОтражениеВОтчетности = &ОтражениеВОтчетности
	|	И Операции.Организация В(&Организация)
	|	И Операции.Активность
	|	И ТИПЗНАЧЕНИЯ(ВТ_СписокДокументов.Документ) = ТИП(Документ.КорректировкаПриобретения)
	|	И (КурсыВалют.БазоваяВалюта = ВТ_БазовыеВалюты.БазоваяВалюта
	|			ИЛИ КурсыВалют.БазоваяВалюта ЕСТЬ NULL)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СписокДокументов.Документ,
	|	Операции.ТипЗапасов,
	|	Операции.Организация,
	|	КорректировкаПриобретения.Валюта,
	|	ВТ_БазовыеВалюты.БазоваяВалюта,
	|	КурсыВалют.БазоваяВалюта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_СписокДокументов.Документ,
	|	Операции.ВидЗапасов.ТипЗапасов,
	|	Операции.Организация,
	|	МАКСИМУМ(КурсыВалют.Период),
	|	КорректировкаПриобретения.Валюта,
	|	ВТ_БазовыеВалюты.БазоваяВалюта,
	|	КурсыВалют.БазоваяВалюта,
	|	Операции.КодОперации
	|ИЗ
	|	РегистрСведений.ОперацииСПрослеживаемымиТоварамиРасширенный КАК Операции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокДокументов КАК ВТ_СписокДокументов
	|		ПО Операции.Регистратор = ВТ_СписокДокументов.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_БазовыеВалюты КАК ВТ_БазовыеВалюты
	|			ПО КорректировкаПриобретения.Организация = ВТ_БазовыеВалюты.Организация
	|		ПО (ВТ_СписокДокументов.Документ = КорректировкаПриобретения.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО (КурсыВалют.Валюта = КорректировкаПриобретения.Валюта)
	|			И (КурсыВалют.Период < КорректировкаПриобретения.ДокументОснование.Дата)
	|ГДЕ
	|	Операции.ОтражениеВОтчетности = &ОтражениеВОтчетности
	|	И Операции.Организация В(&Организация)
	|	И Операции.Активность
	|	И ТИПЗНАЧЕНИЯ(ВТ_СписокДокументов.Документ) = ТИП(Документ.КорректировкаПриобретения)
	|	И (КурсыВалют.БазоваяВалюта = ВТ_БазовыеВалюты.БазоваяВалюта
	|			ИЛИ КурсыВалют.БазоваяВалюта ЕСТЬ NULL)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СписокДокументов.Документ,
	|	Операции.ВидЗапасов.ТипЗапасов,
	|	Операции.Организация,
	|	КорректировкаПриобретения.Валюта,
	|	ВТ_БазовыеВалюты.БазоваяВалюта,
	|	КурсыВалют.БазоваяВалюта,
	|	Операции.КодОперации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КорректировкиПриобретения_Предварительная.Документ КАК Документ,
	|	ВТ_КорректировкиПриобретения_Предварительная.ТипЗапасов КАК ТипЗапасов,
	|	ВТ_КорректировкиПриобретения_Предварительная.Организация КАК Организация,
	|	КорректировкаПриобретенияТовары.НомерГТД КАК РегНомПросл,
	|	КорректировкаПриобретенияТовары.Номенклатура.КодТНВЭД.ЕдиницаИзмерения.Код КАК ОКЕИ,
	|	КорректировкаПриобретенияТовары.КоличествоПоРНПТ КАК КолТовПросл,
	|	КорректировкаПриобретенияТовары.Цена * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 0) * ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 0) * КорректировкаПриобретенияТовары.КоличествоПоРНПТ КАК СтоимТовПросл,
	|	ВТ_КорректировкиПриобретения_Предварительная.КодОперации
	|ПОМЕСТИТЬ ВТ_КорректировкиПриобретения
	|ИЗ
	|	ВТ_КорректировкиПриобретения_Предварительная КАК ВТ_КорректировкиПриобретения_Предварительная
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения.Товары КАК КорректировкаПриобретенияТовары
	|		ПО ВТ_КорректировкиПриобретения_Предварительная.Документ = КорректировкаПриобретенияТовары.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО ВТ_КорректировкиПриобретения_Предварительная.Период = КурсыВалют.Период
	|			И ВТ_КорректировкиПриобретения_Предварительная.Валюта = КурсыВалют.Валюта
	|			И ВТ_КорректировкиПриобретения_Предварительная.БазоваяВалютаРегистра = КурсыВалют.БазоваяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_КорректировкиПриобретения_Предварительная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_БазовыеВалюты";
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	
КонецПроцедуры 

Процедура ПолучитьДанныеРНПТКорректировкиРеализации(СтруктураПараметров)
	
	Если НЕ СтруктураПараметров.ЕстьКорректировкиРеализации Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = СтруктураПараметров.Запрос;
	
	Если СтруктураПараметров.МенеджерВременныхТаблиц.Таблицы.Найти("ВТ_СопоставлениеСФОснований") = Неопределено Тогда
	
		Запрос.Текст = 
		
		"ВЫБРАТЬ
		|	ВТ_СписокДокументов.Документ КАК Документ,
		|	Операции.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
		|	Операции.Организация КАК Организация,
		|	Операции.РНПТ КАК РегНомПросл,
		|	Операции.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД.ЕдиницаИзмерения.Код КАК ОКЕИ,
		|	СУММА(Операции.КоличествоПоРНПТ) КАК КолТовПросл,
		|	СУММА(-СуммыДокументовВВалютахУчета.СуммаБезНДС) КАК СтоимТовПросл,
		|	NULL КАК КодОперации
		|ПОМЕСТИТЬ ВТ_КорректировкиРеализации
		|ИЗ
		|	РегистрСведений.ОперацииСПрослеживаемымиТоварамиРасширенный КАК Операции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокДокументов КАК ВТ_СписокДокументов
		|		ПО Операции.Регистратор = ВТ_СписокДокументов.Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
		|		ПО (СуммыДокументовВВалютахУчета.Регистратор = Операции.Регистратор)
		|			И (СуммыДокументовВВалютахУчета.ИдентификаторСтроки = Операции.ИдентификаторСтроки)
		|ГДЕ
		|	Операции.ОтражениеВОтчетности = &ОтражениеВОтчетности
		|	И Операции.Организация В(&Организация)
		|	И Операции.Активность
		|	И ТИПЗНАЧЕНИЯ(ВТ_СписокДокументов.Документ) = ТИП(Документ.КорректировкаРеализации)
		|
		|СГРУППИРОВАТЬ ПО


		|	ВТ_СписокДокументов.Документ,
		|	Операции.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД.ЕдиницаИзмерения.Код,
		|	Операции.Организация,
		|	Операции.ВидЗапасов.ТипЗапасов,
		|	Операции.РНПТ";
	
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТ_СопоставлениеСФОснований.Документ КАК Документ,
		|	Операции.ТипЗапасов КАК ТипЗапасов,
		|	Операции.Организация КАК Организация,
		|	СчетФактураВыданныйТовары.НомерГТД КАК РегНомПросл,
		|	СчетФактураВыданныйТовары.Номенклатура.КодТНВЭД.ЕдиницаИзмерения.Код КАК ОКЕИ,
		|	СчетФактураВыданныйТовары.КоличествоПоРНПТ КАК КолТовПросл,
		|	СчетФактураВыданныйТовары.Сумма - СчетФактураВыданныйТовары.СуммаНДС КАК СтоимТовПросл,
		|	NULL КАК КодОперации
		|ПОМЕСТИТЬ ВТ_КорректировкиРеализации
		|ИЗ
		|	РегистрСведений.ОперацииСПрослеживаемымиТоварами КАК Операции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СопоставлениеСФОснований КАК ВТ_СопоставлениеСФОснований
		|		ПО Операции.Регистратор = ВТ_СопоставлениеСФОснований.Документ
		|			И (ВТ_СопоставлениеСФОснований.Документ ССЫЛКА Документ.КорректировкаРеализации)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.Товары КАК СчетФактураВыданныйТовары
		|		ПО (ВТ_СопоставлениеСФОснований.СчетФактура = СчетФактураВыданныйТовары.Ссылка)
		|ГДЕ
		|	Операции.ОтражениеВОтчетности = &ОтражениеВОтчетности
		|	И Операции.Организация В(&Организация)
		|	И Операции.Активность
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_СопоставлениеСФОснований.Документ,
		|	Операции.ВидЗапасов.ТипЗапасов,
		|	Операции.Организация,
		|	СчетФактураВыданныйТовары.НомерГТД,
		|	СчетФактураВыданныйТовары.Номенклатура.КодТНВЭД.ЕдиницаИзмерения.Код,
		|	СчетФактураВыданныйТовары.КоличествоПоРНПТ,
		|	СчетФактураВыданныйТовары.Сумма - СчетФактураВыданныйТовары.СуммаНДС,
		|	Операции.КодОперации
		|ИЗ
		|	РегистрСведений.ОперацииСПрослеживаемымиТоварамиРасширенный КАК Операции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СопоставлениеСФОснований КАК ВТ_СопоставлениеСФОснований
		|		ПО Операции.Регистратор = ВТ_СопоставлениеСФОснований.Документ
		|			И (ВТ_СопоставлениеСФОснований.Документ ССЫЛКА Документ.КорректировкаРеализации)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.Товары КАК СчетФактураВыданныйТовары
		|		ПО (ВТ_СопоставлениеСФОснований.СчетФактура = СчетФактураВыданныйТовары.Ссылка)
		|ГДЕ
		|	Операции.ОтражениеВОтчетности = &ОтражениеВОтчетности
		|	И Операции.Организация В(&Организация)
		|	И Операции.Активность";
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПолучитьДанныеРНПТвОС(СтруктураПараметров)
	
	Если НЕ СтруктураПараметров.ЕстьРУиПА
		И НЕ СтруктураПараметров.ЕстьПУиПА
		И НЕ СтруктураПараметров.ЕстьВыкупАрендованныхОС
		И НЕ СтруктураПараметров.ЕстьСписанияОС Тогда
		Возврат;
	КонецЕсли;
	
	Разделитель = "
	|;
	|/////////////////////////////////////////////////////////////
	|";
	
	Запрос = СтруктураПараметров.Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТ_СписокДокументов.Документ КАК Документ,
	|	Операции.ТипЗапасов КАК ТипЗапасов,
	|	Операции.Организация КАК Организация,
	|	ПрослеживаемыеТоварыВСоставеОС.РНПТ КАК РегНомПросл,
	|	ПрослеживаемыеТоварыВСоставеОС.КодТНВЭД.ЕдиницаИзмерения.Код КАК ОКЕИ,
	|	ПрослеживаемыеТоварыВСоставеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(ПрослеживаемыеТоварыВСоставеОС.КоличествоПоРНПТ) КАК Количество
	|ПОМЕСТИТЬ ВТ_РНПТвОС
	|ИЗ
	|	РегистрСведений.ОперацииСПрослеживаемымиТоварами КАК Операции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокДокументов КАК ВТ_СписокДокументов
	|		ПО Операции.Регистратор = ВТ_СписокДокументов.Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрослеживаемыеТоварыВСоставеОС КАК ПрослеживаемыеТоварыВСоставеОС
	|		ПО Операции.Регистратор = ПрослеживаемыеТоварыВСоставеОС.Регистратор
	|			И Операции.Организация = ПрослеживаемыеТоварыВСоставеОС.Организация
	|ГДЕ
	|	Операции.ОтражениеВОтчетности = &ОтражениеВОтчетности
	|	И Операции.Организация В(&Организация)
	|	И Операции.Активность
	|	И ПрослеживаемыеТоварыВСоставеОС.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СписокДокументов.Документ,
	|	Операции.ТипЗапасов,
	|	Операции.Организация,
	|	ПрослеживаемыеТоварыВСоставеОС.РНПТ,
	|	ПрослеживаемыеТоварыВСоставеОС.КодТНВЭД.ЕдиницаИзмерения.Код,
	|	ПрослеживаемыеТоварыВСоставеОС.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	РНПТ";
	
	Если СтруктураПараметров.ЕстьРУиПА Тогда
		
		Запрос.Текст = Запрос.Текст + Разделитель + 
		"ВЫБРАТЬ
		|	ВТ_РНПТвОС.Документ КАК Документ,
		|	ВТ_РНПТвОС.Организация КАК Организация,
		|	ВТ_РНПТвОС.ТипЗапасов КАК ТипЗапасов,
		|	ВТ_РНПТвОС.РегНомПросл КАК РегНомПросл,
		|	ВТ_РНПТвОС.ОКЕИ КАК ОКЕИ,
		|	ВТ_РНПТвОС.Количество КАК Количество,
		|	РеализацияУслугПрочихАктивовПрослеживаемыеТовары.ОсновноеСредство КАК ОсновноеСредство,
		|	РеализацияУслугПрочихАктивовДоходы.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТ_РУиПА_Предварительная
		|ИЗ
		|	ВТ_РНПТвОС КАК ВТ_РНПТвОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.ПрослеживаемыеТовары КАК РеализацияУслугПрочихАктивовПрослеживаемыеТовары
		|		ПО ВТ_РНПТвОС.Документ = РеализацияУслугПрочихАктивовПрослеживаемыеТовары.Ссылка
		|			И ВТ_РНПТвОС.РегНомПросл = РеализацияУслугПрочихАктивовПрослеживаемыеТовары.НомерГТД
		|			И ВТ_РНПТвОС.ОсновноеСредство = РеализацияУслугПрочихАктивовПрослеживаемыеТовары.ОсновноеСредство
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов.Доходы КАК РеализацияУслугПрочихАктивовДоходы
		|		ПО (РеализацияУслугПрочихАктивовПрослеживаемыеТовары.Ссылка = РеализацияУслугПрочихАктивовДоходы.Ссылка)
		|			И (РеализацияУслугПрочихАктивовПрослеживаемыеТовары.ОсновноеСредство = РеализацияУслугПрочихАктивовДоходы.АналитикаДоходов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_РУиПА_Предварительная.Документ КАК Документ,
		|	ВТ_РУиПА_Предварительная.ОсновноеСредство КАК ОсновноеСредство,
		|	СУММА(ВТ_РУиПА_Предварительная.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТ_РУиПА_БазаРаспределения
		|ИЗ
		|	ВТ_РУиПА_Предварительная КАК ВТ_РУиПА_Предварительная
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_РУиПА_Предварительная.Документ,
		|	ВТ_РУиПА_Предварительная.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_РУиПА_Предварительная.Документ КАК Документ,
		|	ВТ_РУиПА_Предварительная.Организация КАК Организация,
		|	ВТ_РУиПА_Предварительная.ТипЗапасов КАК ТипЗапасов,
		|	ВТ_РУиПА_Предварительная.РегНомПросл КАК РегНомПросл,
		|	ВТ_РУиПА_Предварительная.ОКЕИ КАК ОКЕИ,
		|	ВТ_РУиПА_Предварительная.Количество КАК КолТовПросл,
		|	ВЫБОР
		|		КОГДА ВТ_РУиПА_БазаРаспределения.Количество = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВТ_РУиПА_Предварительная.Сумма / ВТ_РУиПА_БазаРаспределения.Количество * ВТ_РУиПА_Предварительная.Количество
		|	КОНЕЦ КАК СтоимТовПросл
		|ПОМЕСТИТЬ ВТ_РУиПА
		|ИЗ
		|	ВТ_РУиПА_Предварительная КАК ВТ_РУиПА_Предварительная
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РУиПА_БазаРаспределения КАК ВТ_РУиПА_БазаРаспределения
		|		ПО ВТ_РУиПА_Предварительная.Документ = ВТ_РУиПА_БазаРаспределения.Документ
		|			И ВТ_РУиПА_Предварительная.ОсновноеСредство = ВТ_РУиПА_БазаРаспределения.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_РУиПА_Предварительная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_РУиПА_БазаРаспределения";
			
	КонецЕсли;
	
	Если СтруктураПараметров.ЕстьПУиПА Тогда
	
		Запрос.Текст = Запрос.Текст + Разделитель +
		"ВЫБРАТЬ
		|	ВТ_РНПТвОС.Документ КАК Документ,
		|	ВТ_РНПТвОС.Организация КАК Организация, 
		|	ВТ_РНПТвОС.ТипЗапасов КАК ТипЗапасов,
		|	ВТ_РНПТвОС.РегНомПросл КАК РегНомПросл,
		|	ВТ_РНПТвОС.ОКЕИ КАК ОКЕИ,
		|	ВТ_РНПТвОС.Количество КАК КолТовПросл,
		|	СуммыДокументовВВалютахУчета.СуммаБезНДСРегл КАК СтоимТовПросл
		|ПОМЕСТИТЬ ВТ_ПУиПА
		|ИЗ
		|	ВТ_РНПТвОС КАК ВТ_РНПТвОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеУслугПрочихАктивов.Расходы КАК ПриобретениеУслугПрочихАктивовРасходы
		|		ПО ВТ_РНПТвОС.Документ = ПриобретениеУслугПрочихАктивовРасходы.Ссылка
		|			И ВТ_РНПТвОС.РегНомПросл = ПриобретениеУслугПрочихАктивовРасходы.НомерГТД
		|			И ВТ_РНПТвОС.ОсновноеСредство = ПриобретениеУслугПрочихАктивовРасходы.АналитикаРасходов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
		|		ПО (ПриобретениеУслугПрочихАктивовРасходы.Ссылка = СуммыДокументовВВалютахУчета.Регистратор)
		|			И (ПриобретениеУслугПрочихАктивовРасходы.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки)";
	
	КонецЕсли;
	
	Если СтруктураПараметров.ЕстьВыкупАрендованныхОС Тогда
		
		Запрос.Текст = Запрос.Текст + Разделитель + 
		"ВЫБРАТЬ
		|	ВТ_РНПТвОС.Документ КАК Документ,
		|	ВТ_РНПТвОС.Организация КАК Организация,
		|	ВТ_РНПТвОС.ТипЗапасов КАК ТипЗапасов,
		|	ВТ_РНПТвОС.РегНомПросл КАК РегНомПросл,
		|	ВТ_РНПТвОС.ОКЕИ КАК ОКЕИ,
		|	ВТ_РНПТвОС.Количество КАК Количество,
		|	ВыкупАрендованныхОСПрослеживаемыеТовары.ОсновноеСредство КАК ОсновноеСредство,
		|	ВыкупАрендованныхОСОС.СуммаСНДС - ВыкупАрендованныхОСОС.СуммаНДС КАК Сумма
		|ПОМЕСТИТЬ ВТ_ВыкупАрендованныхОС_Предварительная
		|ИЗ
		|	ВТ_РНПТвОС КАК ВТ_РНПТвОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыкупАрендованныхОС.ПрослеживаемыеТовары КАК ВыкупАрендованныхОСПрослеживаемыеТовары
		|		ПО ВТ_РНПТвОС.Документ = ВыкупАрендованныхОСПрослеживаемыеТовары.Ссылка
		|			И ВТ_РНПТвОС.РегНомПросл = ВыкупАрендованныхОСПрослеживаемыеТовары.НомерГТД
		|			И ВТ_РНПТвОС.ОсновноеСредство = ВыкупАрендованныхОСПрослеживаемыеТовары.ОсновноеСредство
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыкупАрендованныхОС.ОС КАК ВыкупАрендованныхОСОС
		|		ПО (ВыкупАрендованныхОСПрослеживаемыеТовары.Ссылка = ВыкупАрендованныхОСОС.Ссылка)
		|			И (ВыкупАрендованныхОСПрослеживаемыеТовары.ОсновноеСредство = ВыкупАрендованныхОСОС.ОсновноеСредство)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ВыкупАрендованныхОС_Предварительная.Документ КАК Документ,
		|	ВТ_ВыкупАрендованныхОС_Предварительная.ОсновноеСредство КАК ОсновноеСредство,
		|	СУММА(ВТ_ВыкупАрендованныхОС_Предварительная.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТ_ВыкупАрендованныхОС_БазаРаспределения
		|ИЗ
		|	ВТ_ВыкупАрендованныхОС_Предварительная КАК ВТ_ВыкупАрендованныхОС_Предварительная
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ВыкупАрендованныхОС_Предварительная.Документ,
		|	ВТ_ВыкупАрендованныхОС_Предварительная.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ВыкупАрендованныхОС_Предварительная.Документ КАК Документ,
		|	ВТ_ВыкупАрендованныхОС_Предварительная.Организация КАК Организация,
		|	ВТ_ВыкупАрендованныхОС_Предварительная.ТипЗапасов КАК ТипЗапасов,
		|	ВТ_ВыкупАрендованныхОС_Предварительная.РегНомПросл КАК РегНомПросл,
		|	ВТ_ВыкупАрендованныхОС_Предварительная.ОКЕИ КАК ОКЕИ,
		|	ВТ_ВыкупАрендованныхОС_Предварительная.Количество КАК КолТовПросл,
		|	ВЫБОР
		|		КОГДА ВТ_ВыкупАрендованныхОС_БазаРаспределения.Количество = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВТ_ВыкупАрендованныхОС_Предварительная.Сумма / ВТ_ВыкупАрендованныхОС_БазаРаспределения.Количество * ВТ_ВыкупАрендованныхОС_Предварительная.Количество
		|	КОНЕЦ КАК СтоимТовПросл
		|ПОМЕСТИТЬ ВТ_ВыкупАрендованныхОС
		|ИЗ
		|	ВТ_ВыкупАрендованныхОС_Предварительная КАК ВТ_ВыкупАрендованныхОС_Предварительная
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыкупАрендованныхОС_БазаРаспределения КАК ВТ_ВыкупАрендованныхОС_БазаРаспределения
		|		ПО ВТ_ВыкупАрендованныхОС_Предварительная.Документ = ВТ_ВыкупАрендованныхОС_БазаРаспределения.Документ
		|			И ВТ_ВыкупАрендованныхОС_Предварительная.ОсновноеСредство = ВТ_ВыкупАрендованныхОС_БазаРаспределения.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ВыкупАрендованныхОС_Предварительная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ВыкупАрендованныхОС_БазаРаспределения";
	КонецЕсли; 
	
	Если СтруктураПараметров.ЕстьСписанияОС Тогда
		
		Запрос.Текст = Запрос.Текст + Разделитель + 
		"ВЫБРАТЬ
		|	ВТ_РНПТвОС.Документ КАК Документ,
		|	ВТ_РНПТвОС.Организация КАК Организация,
		|	ВТ_РНПТвОС.ТипЗапасов КАК ТипЗапасов,
		|	ВТ_РНПТвОС.РегНомПросл КАК РегНомПросл,
		|	ВТ_РНПТвОС.ОКЕИ КАК ОКЕИ,
		|	ВТ_РНПТвОС.Количество КАК Количество,
		|	СписаниеОСПрослеживаемыеТовары.ОсновноеСредство КАК ОсновноеСредство,
		|	ЕСТЬNULL(ПрочиеРасходы.СуммаРегл, 0) КАК Сумма
		|ПОМЕСТИТЬ ВТ_СписаниеОС_Предварительная
		|ИЗ
		|	ВТ_РНПТвОС КАК ВТ_РНПТвОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС2_4.ПрослеживаемыеТовары КАК СписаниеОСПрослеживаемыеТовары
		|		ПО ВТ_РНПТвОС.Документ = СписаниеОСПрослеживаемыеТовары.Ссылка
		|			И ВТ_РНПТвОС.РегНомПросл = СписаниеОСПрослеживаемыеТовары.НомерГТД
		|			И ВТ_РНПТвОС.ОсновноеСредство = СписаниеОСПрослеживаемыеТовары.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
		|		ПО (СписаниеОСПрослеживаемыеТовары.Ссылка = ПрочиеРасходы.Регистратор)
		|			И (СписаниеОСПрослеживаемыеТовары.ОсновноеСредство = ПрочиеРасходы.АналитикаРасходов)
		|			И (ПрочиеРасходы.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыПриСписанииОС))
		|			И (ПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
		|			И (ПрочиеРасходы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакрытиеРасходовОтСписанияОС)
		|				ИЛИ ПрочиеРасходы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакрытиеРасходовОтРеализацииОС)
		|				ИЛИ ПрочиеРасходы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакрытиеРасходовОтРеализацииОССОтложеннымПереходомПрав)
		|				ИЛИ ПрочиеРасходы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакрытиеРасходовОтРеализацииОСПослеПереходаПрав))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_СписаниеОС_Предварительная.Документ КАК Документ,
		|	ВТ_СписаниеОС_Предварительная.ОсновноеСредство КАК ОсновноеСредство,
		|	СУММА(ВТ_СписаниеОС_Предварительная.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТ_СписаниеОС_БазаРаспределения
		|ИЗ
		|	ВТ_СписаниеОС_Предварительная КАК ВТ_СписаниеОС_Предварительная
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_СписаниеОС_Предварительная.Документ,
		|	ВТ_СписаниеОС_Предварительная.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_СписаниеОС_Предварительная.Документ КАК Документ,
		|	ВТ_СписаниеОС_Предварительная.Организация КАК Организация, 
		|	ВТ_СписаниеОС_Предварительная.ТипЗапасов КАК ТипЗапасов,
		|	ВТ_СписаниеОС_Предварительная.РегНомПросл КАК РегНомПросл,
		|	ВТ_СписаниеОС_Предварительная.ОКЕИ КАК ОКЕИ,
		|	ВТ_СписаниеОС_Предварительная.Количество КАК КолТовПросл,
		|	ВЫБОР
		|		КОГДА ВТ_СписаниеОС_БазаРаспределения.Количество = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВТ_СписаниеОС_Предварительная.Сумма / ВТ_СписаниеОС_БазаРаспределения.Количество * ВТ_СписаниеОС_Предварительная.Количество
		|	КОНЕЦ КАК СтоимТовПросл
		|ПОМЕСТИТЬ ВТ_СписаниеОС
		|ИЗ
		|	ВТ_СписаниеОС_Предварительная КАК ВТ_СписаниеОС_Предварительная
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписаниеОС_БазаРаспределения КАК ВТ_СписаниеОС_БазаРаспределения
		|		ПО ВТ_СписаниеОС_Предварительная.Документ = ВТ_СписаниеОС_БазаРаспределения.Документ
		|			И ВТ_СписаниеОС_Предварительная.ОсновноеСредство = ВТ_СписаниеОС_БазаРаспределения.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_СписаниеОС_Предварительная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_СписаниеОС_БазаРаспределения";
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПолучитьИтоговыеДанныеРНПТ(СтруктураПараметров)
	
	Запрос = СтруктураПараметров.Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	вт_ПокупкиПродажи.Документ КАК Документ,
	|	вт_ПокупкиПродажи.ТипЗапасов КАК ТипЗапасов,
	|	вт_ПокупкиПродажи.Организация КАК Организация,
	|	вт_ПокупкиПродажи.РегНомПросл КАК РНПТ,
	|	вт_ПокупкиПродажи.ОКЕИ КАК ОКЕИ,
	|	вт_ПокупкиПродажи.КолТовПросл КАК КоличествоРНПТ,
	|	ВЫРАЗИТЬ(вт_ПокупкиПродажи.СтоимТовПросл КАК ЧИСЛО(15, 0)) КАК СуммаБезНДС,
	|	вт_ПокупкиПродажи.КодОперации
	|ПОМЕСТИТЬ ДанныеРНПТ_Предварительная
	|ИЗ
	|	вт_ПокупкиПродажи КАК вт_ПокупкиПродажи";
	
	Если СтруктураПараметров.ЕстьКорректировкиПриобретения Тогда
		Запрос.Текст = Запрос.Текст + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_КорректировкиПриобретения.Документ,
		|	ВТ_КорректировкиПриобретения.ТипЗапасов,
		|	ВТ_КорректировкиПриобретения.Организация,
		|	ВТ_КорректировкиПриобретения.РегНомПросл,
		|	ВТ_КорректировкиПриобретения.ОКЕИ,
		|	ВТ_КорректировкиПриобретения.КолТовПросл,
		|	ВЫРАЗИТЬ(ВТ_КорректировкиПриобретения.СтоимТовПросл КАК ЧИСЛО(15, 0)),
		|	ВТ_КорректировкиПриобретения.КодОперации
		|ИЗ
		|	ВТ_КорректировкиПриобретения КАК ВТ_КорректировкиПриобретения";
	КонецЕсли;
	
	Если СтруктураПараметров.ЕстьКорректировкиРеализации Тогда
		Запрос.Текст = Запрос.Текст + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_КорректировкиРеализации.Документ,
		|	ВТ_КорректировкиРеализации.ТипЗапасов,
		|	ВТ_КорректировкиРеализации.Организация,
		|	ВТ_КорректировкиРеализации.РегНомПросл,
		|	ВТ_КорректировкиРеализации.ОКЕИ,
		|	ВТ_КорректировкиРеализации.КолТовПросл,
		|	ВЫРАЗИТЬ(ВТ_КорректировкиРеализации.СтоимТовПросл КАК ЧИСЛО(15, 0)),
		|	ВТ_КорректировкиРеализации.КодОперации
		|ИЗ
		|	ВТ_КорректировкиРеализации КАК ВТ_КорректировкиРеализации";
	КонецЕсли;
	
	Если СтруктураПараметров.ЕстьРУиПА Тогда
		Запрос.Текст = Запрос.Текст + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_РУиПА.Документ,
		|	ВТ_РУиПА.ТипЗапасов,
		|	ВТ_РУиПА.Организация,
		|	ВТ_РУиПА.РегНомПросл,
		|	ВТ_РУиПА.ОКЕИ,
		|	ВТ_РУиПА.КолТовПросл,
		|	ВЫРАЗИТЬ(ВТ_РУиПА.СтоимТовПросл КАК ЧИСЛО(15, 0)),
		|	NULL
		|ИЗ
		|	ВТ_РУиПА КАК ВТ_РУиПА";
	КонецЕсли;
	
	Если СтруктураПараметров.ЕстьПУиПА Тогда
		Запрос.Текст = Запрос.Текст + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ПУиПА.Документ, 
		|	ВТ_ПУиПА.ТипЗапасов,
		|	ВТ_ПУиПА.Организация,
		|	ВТ_ПУиПА.РегНомПросл,
		|	ВТ_ПУиПА.ОКЕИ,
		|	ВТ_ПУиПА.КолТовПросл,
		|	ВЫРАЗИТЬ(ВТ_ПУиПА.СтоимТовПросл КАК ЧИСЛО(15, 0)),
		|	NULL
		|ИЗ
		|	ВТ_ПУиПА КАК ВТ_ПУиПА";
	КонецЕсли;
	
	Если СтруктураПараметров.ЕстьВыкупАрендованныхОС Тогда
		Запрос.Текст = Запрос.Текст + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ВыкупАрендованныхОС.Документ,
		|	ВТ_ВыкупАрендованныхОС.ТипЗапасов,
		|	ВТ_ВыкупАрендованныхОС.Организация,
		|	ВТ_ВыкупАрендованныхОС.РегНомПросл,
		|	ВТ_ВыкупАрендованныхОС.ОКЕИ,
		|	ВТ_ВыкупАрендованныхОС.КолТовПросл,
		|	ВЫРАЗИТЬ(ВТ_ВыкупАрендованныхОС.СтоимТовПросл КАК ЧИСЛО(15, 0)),
		|	NULL
		|ИЗ
		|	ВТ_ВыкупАрендованныхОС КАК ВТ_ВыкупАрендованныхОС";
	КонецЕсли; 
	
	Если СтруктураПараметров.ЕстьСписанияОС Тогда
		Запрос.Текст = Запрос.Текст + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_СписаниеОС.Документ,
		|	ВТ_СписаниеОС.ТипЗапасов,
		|	ВТ_СписаниеОС.Организация,
		|	ВТ_СписаниеОС.РегНомПросл,
		|	ВТ_СписаниеОС.ОКЕИ,
		|	ВТ_СписаниеОС.КолТовПросл,
		|	ВЫРАЗИТЬ(ВТ_СписаниеОС.СтоимТовПросл КАК ЧИСЛО(15, 0)),
		|	NULL
		|ИЗ
		|	ВТ_СписаниеОС КАК ВТ_СписаниеОС";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РНПТ;
	|
	|/////////////////////////////////////////////////////////////
	|";
	
	Запрос.Текст = Запрос.Текст + "ВЫБРАТЬ
	|	ДанныеРНПТ_Предварительная.Документ КАК Документ,
	|	ДанныеРНПТ_Предварительная.ТипЗапасов КАК ТипЗапасов,
	|	ДанныеРНПТ_Предварительная.Организация КАК Организация,
	|	ДанныеРНПТ_Предварительная.РНПТ КАК РНПТ,
	|	ДанныеРНПТ_Предварительная.КодОперации КАК КодОперации,
	|	СУММА(НомераГТДПрослеживаемыеКомплектующие.КоличествоПоРНПТ) КАК КоличествоРНПТ
	|ПОМЕСТИТЬ СгруппированныеДанныеРНПТ
	|ИЗ
	|	ДанныеРНПТ_Предварительная КАК ДанныеРНПТ_Предварительная
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД.ПрослеживаемыеКомплектующие КАК НомераГТДПрослеживаемыеКомплектующие
	|		ПО ДанныеРНПТ_Предварительная.РНПТ = НомераГТДПрослеживаемыеКомплектующие.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРНПТ_Предварительная.Документ,
	|	ДанныеРНПТ_Предварительная.ТипЗапасов,
	|	ДанныеРНПТ_Предварительная.Организация,
	|	ДанныеРНПТ_Предварительная.РНПТ,
	|	ДанныеРНПТ_Предварительная.КодОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРНПТ_Предварительная.Документ КАК Документ,
	|	ДанныеРНПТ_Предварительная.ТипЗапасов КАК ТипЗапасов,
	|	ДанныеРНПТ_Предварительная.Организация КАК Организация,
	|	ДанныеРНПТ_Предварительная.РНПТ КАК РНПТ,
	|	ДанныеРНПТ_Предварительная.КодОперации КАК КодОперации,
	|	ДанныеРНПТ_Предварительная.ОКЕИ КАК ОКЕИ,
	|	ДанныеРНПТ_Предварительная.КоличествоРНПТ КАК КоличествоРНПТ,
	|	ДанныеРНПТ_Предварительная.СуммаБезНДС КАК СуммаБезНДС
	|ПОМЕСТИТЬ ДанныеРНПТ
	|ИЗ
	|	ДанныеРНПТ_Предварительная КАК ДанныеРНПТ_Предварительная
	|ГДЕ
	|	ДанныеРНПТ_Предварительная.РНПТ.ТипНомераГТД = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРНПТ_Предварительная.Документ,
	|	ДанныеРНПТ_Предварительная.ТипЗапасов,
	|	ДанныеРНПТ_Предварительная.Организация,
	|	НомераГТДПрослеживаемыеКомплектующие.НомерРНПТ,
	|	ДанныеРНПТ_Предварительная.КодОперации,
	|	НомераГТДПрослеживаемыеКомплектующие.ЕдиницаИзмеренияТНВЭД.Код,
	|	ДанныеРНПТ_Предварительная.КоличествоРНПТ * НомераГТДПрослеживаемыеКомплектующие.КоличествоПоРНПТ,
	|	ВЫБОР
	|		КОГДА СгруппированныеДанныеРНПТ.КоличествоРНПТ = 0
	|			ТОГДА 0
	|		ИНАЧЕ ДанныеРНПТ_Предварительная.СуммаБезНДС / СгруппированныеДанныеРНПТ.КоличествоРНПТ * НомераГТДПрослеживаемыеКомплектующие.КоличествоПоРНПТ
	|	КОНЕЦ
	|ИЗ
	|	ДанныеРНПТ_Предварительная КАК ДанныеРНПТ_Предварительная
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД.ПрослеживаемыеКомплектующие КАК НомераГТДПрослеживаемыеКомплектующие
	|		ПО ДанныеРНПТ_Предварительная.РНПТ = НомераГТДПрослеживаемыеКомплектующие.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СгруппированныеДанныеРНПТ КАК СгруппированныеДанныеРНПТ
	|		ПО ДанныеРНПТ_Предварительная.РНПТ = СгруппированныеДанныеРНПТ.РНПТ
	|			И ДанныеРНПТ_Предварительная.Документ = СгруппированныеДанныеРНПТ.Документ
	|			И ДанныеРНПТ_Предварительная.ТипЗапасов = СгруппированныеДанныеРНПТ.ТипЗапасов
	|			И ДанныеРНПТ_Предварительная.Организация = СгруппированныеДанныеРНПТ.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеРНПТ_Предварительная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СгруппированныеДанныеРНПТ"; 
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ЗаполнитьДополнительныеПараметрыЗапросаРНПТ(СтруктураПараметров)

	ТаблицаБазовыхВалют = Новый ТаблицаЗначений;
	ТаблицаБазовыхВалют.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаБазовыхВалют.Колонки.Добавить("БазоваяВалюта", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	
	Для каждого ЭлементОрганизация Из СтруктураПараметров.СписокОрганизаций Цикл
		Организация = ЭлементОрганизация;
		Если ТипЗнч(ЭлементОрганизация) = Тип("ЭлементСпискаЗначений") Тогда
			Организация = ЭлементОрганизация.Значение;
		КонецЕсли;
		СтрокаТаблицы = ТаблицаБазовыхВалют.Добавить();
		СтрокаТаблицы.Организация = Организация;
		СтрокаТаблицы.БазоваяВалюта = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	КонецЦикла;
	
	СтруктураПараметров.Вставить("ТаблицаБазовыхВалют", ТаблицаБазовыхВалют);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураПараметров.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТаблицаБазовыхВалют",  СтруктураПараметров.ТаблицаБазовыхВалют);
	Запрос.УстановитьПараметр("ОтражениеВОтчетности", СтруктураПараметров.ПорядокОтраженияВОтчетностиПоПрослеживаемости);
	Запрос.УстановитьПараметр("Организация",          СтруктураПараметров.СписокОрганизаций);
	
	СтруктураПараметров.Вставить("Запрос", Запрос);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТипыДокументов.ТипДокумента КАК ТипДокумента
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТИПЗНАЧЕНИЯ(ВТ_СписокДокументов.Документ) КАК ТипДокумента
	|	ИЗ
	|		ВТ_СписокДокументов КАК ВТ_СписокДокументов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОперацииСПрослеживаемымиТоварами КАК Операции
	|			ПО ВТ_СписокДокументов.Документ = Операции.Регистратор
	|	ГДЕ
	|		Операции.ОтражениеВОтчетности = &ОтражениеВОтчетности
	|		И Операции.Организация В(&Организация)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТИПЗНАЧЕНИЯ(ВТ_СписокДокументов.Документ)
	|	ИЗ
	|		ВТ_СписокДокументов КАК ВТ_СписокДокументов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОперацииСПрослеживаемымиТоварамиРасширенный КАК Операции
	|			ПО ВТ_СписокДокументов.Документ = Операции.Регистратор
	|	ГДЕ
	|		Операции.ОтражениеВОтчетности = &ОтражениеВОтчетности
	|		И Операции.Организация В(&Организация)) КАК ТипыДокументов";
	
	МассивТипов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ТипДокумента");
	
	СтруктураПараметров.Вставить("ЕстьРозничныеПродажи", НЕ МассивТипов.Найти(Тип("ДокументСсылка.СводнаяСправкаНДС")) 					= Неопределено);
	СтруктураПараметров.Вставить("ЕстьКорректировкиПриобретения", НЕ МассивТипов.Найти(Тип("ДокументСсылка.КорректировкаПриобретения")) = Неопределено);
	СтруктураПараметров.Вставить("ЕстьКорректировкиРеализации", НЕ МассивТипов.Найти(Тип("ДокументСсылка.КорректировкаРеализации")) 	= Неопределено);
	СтруктураПараметров.Вставить("ЕстьРУиПА", НЕ МассивТипов.Найти(Тип("ДокументСсылка.РеализацияУслугПрочихАктивов")) 					= Неопределено);
	СтруктураПараметров.Вставить("ЕстьПУиПА", НЕ МассивТипов.Найти(Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов")) 				= Неопределено);
	СтруктураПараметров.Вставить("ЕстьВыкупАрендованныхОС", НЕ МассивТипов.Найти(Тип("ДокументСсылка.ВыкупАрендованныхОС")) 			= Неопределено);
	СтруктураПараметров.Вставить("ЕстьСписанияОС", НЕ МассивТипов.Найти(Тип("ДокументСсылка.СписаниеОС2_4")) 							= Неопределено);
	
КонецПроцедуры

Процедура УдалитьВременныеТаблицы(СтруктураПараметров)

	Запрос = СтруктураПараметров.Запрос;
	Запрос.Текст = 
	"УНИЧТОЖИТЬ вт_ПокупкиПродажи";
	
	Разделитель = "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	
	Если СтруктураПараметров.ЕстьРозничныеПродажи Тогда
		Запрос.Текст = Запрос.Текст + Разделитель + "
		|УНИЧТОЖИТЬ ВТ_СписокДокументовСРозничнымиПродажами";
	КонецЕсли;
	
	Если СтруктураПараметров.ЕстьКорректировкиПриобретения Тогда
		Запрос.Текст = Запрос.Текст + Разделитель + "
		|УНИЧТОЖИТЬ ВТ_КорректировкиПриобретения";
	КонецЕсли;
	
	Если СтруктураПараметров.ЕстьКорректировкиРеализации Тогда
		Запрос.Текст = Запрос.Текст + Разделитель + "
		|УНИЧТОЖИТЬ ВТ_КорректировкиРеализации";
	КонецЕсли;
	
	ЕстьРНПТвОС = Ложь;
	
	Если СтруктураПараметров.ЕстьРУиПА Тогда
		Запрос.Текст = Запрос.Текст + Разделитель + "
		|УНИЧТОЖИТЬ ВТ_РУиПА"; 
		ЕстьРНПТвОС = Истина;
	КонецЕсли; 
	
	Если СтруктураПараметров.ЕстьПУиПА Тогда
		Запрос.Текст = Запрос.Текст + Разделитель + "
		|УНИЧТОЖИТЬ ВТ_ПУиПА";
		ЕстьРНПТвОС = Истина;
	КонецЕсли;
	
	Если СтруктураПараметров.ЕстьВыкупАрендованныхОС Тогда
		Запрос.Текст = Запрос.Текст + Разделитель + "
		|УНИЧТОЖИТЬ ВТ_ВыкупАрендованныхОС"; 
		ЕстьРНПТвОС = Истина;
	КонецЕсли;
	
	Если СтруктураПараметров.ЕстьСписанияОС Тогда
		Запрос.Текст = Запрос.Текст + Разделитель + "
		|УНИЧТОЖИТЬ ВТ_СписаниеОС"; 
		ЕстьРНПТвОС = Истина;
	КонецЕсли;
	
	Если ЕстьРНПТвОС Тогда
		Запрос.Текст = Запрос.Текст + Разделитель + "
		|УНИЧТОЖИТЬ ВТ_РНПТвОС"; 
	КонецЕсли; 
	
	Запрос.Выполнить();
	
	СтруктураПараметров.Удалить("Запрос");

КонецПроцедуры



//-- НЕ УТ

#КонецОбласти
