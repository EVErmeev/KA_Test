#Область ПрограммныйИнтерфейс

// Открыть форму настройки отборов.
// 
// Параметры:
//  ПараметрыОткрытия - Структура:
//  * НеНастраиватьПараметры - Булево
//  * УникальныйИдентификатор - УникальныйИдентификатор
//  * СхемаКомпоновкиДанных - СхемаКомпоновкиДанных
//  * НастройкиКомпоновкиДанных - НастройкиКомпоновкиДанных
//  Форма - ФормаКлиентскогоПриложения
//  ЗаголовокФормы - Строка.
Процедура ОткрытьФормуНастройкиОтборов(ПараметрыОткрытия, Форма, ЗаголовокФормы = "") Экспорт

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НеПомещатьНастройкиВСхемуКомпоновкиДанных", Истина);
	ПараметрыФормы.Вставить("НеЗагружатьСхемуКомпоновкиДанныхИзФайла", Истина);
	ПараметрыФормы.Вставить("НеНастраиватьПараметры", ПараметрыОткрытия.НеНастраиватьПараметры);
	ПараметрыФормы.Вставить("НеРедактироватьСхемуКомпоновкиДанных", Истина);
	ПараметрыФормы.Вставить("НеНастраиватьУсловноеОформление", Истина);
	ПараметрыФормы.Вставить("НеНастраиватьВыбор", Истина);
	ПараметрыФормы.Вставить("НеНастраиватьПорядок", Истина);
	ПараметрыФормы.Вставить("УникальныйИдентификатор", ПараметрыОткрытия.УникальныйИдентификатор);
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", ПараметрыОткрытия.СхемаКомпоновкиДанных);
	ПараметрыФормы.Вставить("АдресНастроекКомпоновкиДанных", ПараметрыОткрытия.НастройкиКомпоновкиДанных);
	ПараметрыФормы.Вставить("Заголовок", ЗаголовокФормы);
	
	ОткрытьФорму("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных",
		ПараметрыФормы,,,,,
		Новый ОписаниеОповещения("РедактироватьСхемуКомпоновкиДанныхЗавершение",
			Форма,
			ПараметрыОткрытия),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

// см. СправочникМенеджер.НастройкиЗаполненияБухОтчетности.СгенерироватьНастройкиПоУмолчанию
// 
Процедура СгенерироватьНастройкиПоУмолчанию(Настройка, Форма) Экспорт
	
	ФормыОтчетностиКЗагрузке = Новый СписокЗначений;
	
	ФормыОтчетностиКЗагрузке.Добавить(ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.Баланс"), 	"Бухгалтерский баланс",										Истина);
	ФормыОтчетностиКЗагрузке.Добавить(ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.ОФР"),		"Отчет о финансовых результатах",							Истина);
	ФормыОтчетностиКЗагрузке.Добавить(ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.ОИК"),		"Отчет об изменении капитала",								Истина);
	ФормыОтчетностиКЗагрузке.Добавить(ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.ОДДС"),		"Отчет о движении денежных средств",						Истина);
	ФормыОтчетностиКЗагрузке.Добавить(ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.Пояснения1"), "Пояснения 1. НМА и НИОКР",									Истина);
	ФормыОтчетностиКЗагрузке.Добавить(ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.Пояснения2"), "Пояснения 2. Основные средства",							Истина);
	ФормыОтчетностиКЗагрузке.Добавить(ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.Пояснения4"), "Пояснения 4. Запасы", 										Истина);
	ФормыОтчетностиКЗагрузке.Добавить(ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.Пояснения5"), "Пояснения 5. Дебиторская и кредиторская задолженность",	Истина);
	ФормыОтчетностиКЗагрузке.Добавить(ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.Пояснения6"), "Пояснения 6. Затраты на производство",						Истина);
	ФормыОтчетностиКЗагрузке.Добавить(ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.Пояснения7"), "Пояснения 7. Оценочные обязательства",						Истина);
	ФормыОтчетностиКЗагрузке.Добавить(ПредопределенноеЗначение("Перечисление.ФормыБухгалтерскойОтчетности.Пояснения8"), "Пояснения 8. Обеспечения обязательств",					Истина);
	
	ЗагрузитьНастройкиФормыОтчетности(Форма, Настройка, ФормыОтчетностиКЗагрузке);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Асинх Процедура ЗагрузитьНастройкиФормыОтчетности(Форма, Настройка, ФормыОтчетностиКЗагрузке)
	
	ВыбратьФормы = ФормыОтчетностиКЗагрузке.ОтметитьЭлементыАсинх("Выберите формы к загрузке");
	
	ВыбранныеФормы = Ждать ВыбратьФормы;
		
	Если ВыбранныеФормы <> Неопределено Тогда
		
		ФормыКзагрузке = Новый Массив;
		Для Каждого ФормаОтчетности Из ВыбранныеФормы Цикл
			Если ФормаОтчетности.Пометка Тогда
				ФормыКзагрузке.Добавить(ФормаОтчетности.Значение);
			КонецЕсли;
		КонецЦикла;
		
		ТекущиеЭлементыНастройки = НастраиваемаяБухгалтерскаяОтчетностьВызовСервера.ТекущиеЭлементыНастройки(Настройка, ФормыКзагрузке);
		Если ТекущиеЭлементыНастройки.Количество()>0 Тогда
			Режим = РежимДиалогаВопрос.ДаНет;
			Ответ =  Ждать ВопросАсинх(НСтр("ru = 'Обнаружены существующие настройки по указанным формам. Они будут помечены на удаление и созданы новые.
													|Продолжить?'"), Режим, 0);
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ДлительнаяОперация = НастраиваемаяБухгалтерскаяОтчетностьВызовСервера.СгенерироватьНастройкиПоУмолчанию(Настройка, ФормыКзагрузке, ТекущиеЭлементыНастройки);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("СгенерироватьНастройкиПоУмолчаниюЗавершение", НастраиваемаяБухгалтерскаяОтчетностьКлиент);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

	КонецЕсли;
	
КонецПроцедуры

Процедура СгенерироватьНастройкиПоУмолчаниюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Формирование настроек заполнения бухгалтерской отчетности завершено.'"));
	
КонецПроцедуры

#КонецОбласти
