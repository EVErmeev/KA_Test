////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обработка заявлений абонента на подключение".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция СледующееЗаявлениеТребующееРеакцииПользователя() Экспорт
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.Документы.ЗаявлениеАбонентаСпецоператораСвязи) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЗаявлениеАбонентаСпецоператораСвязи.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаявлениеАбонентаСпецоператораСвязи КАК ЗаявлениеАбонентаСпецоператораСвязи
	|ГДЕ
	|	ЗаявлениеАбонентаСпецоператораСвязи.Статус В (&Одобрено, &Отклонено)
	|	И (НЕ ЗаявлениеАбонентаСпецоператораСвязи.НастройкаЗавершена
	|			ИЛИ ЗаявлениеАбонентаСпецоператораСвязи.НастройкаЗавершена
	|				И НЕ ЗаявлениеАбонентаСпецоператораСвязи.НастройкаЭДОЗавершена
	|				И ПОДСТРОКА(ЗаявлениеАбонентаСпецоператораСвязи.НастройкиЭДО, 0, 1000) <> """"
	|				И (ЗаявлениеАбонентаСпецоператораСвязи.ПереиздатьСертификатЭДО
	|					ИЛИ ЗаявлениеАбонентаСпецоператораСвязи.ПодключитьЭДО))
	|	И ЗаявлениеАбонентаСпецоператораСвязи.Ответственный = &Ответственный
	|	И НЕ ЗаявлениеАбонентаСпецоператораСвязи.ПометкаУдаления
	|	И НЕ ЗаявлениеАбонентаСпецоператораСвязи.Ссылка В (&ЗаявленияТребующиеНапоминанияПозже)";
	
	Запрос.УстановитьПараметр("Ответственный", 	Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("Одобрено", 		Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Одобрено);
	Запрос.УстановитьПараметр("Отклонено", 		Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отклонено);
	
	Запрос.УстановитьПараметр("ЗаявленияТребующиеНапоминанияПозже", ЗаявленияТребующиеНапоминанияПозже());
	
	РезультатЗапроса 	= Запрос.Выполнить();
	Выборка 			= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ссылка;
	КонецЦикла;

	Возврат Неопределено;
		
КонецФункции

Процедура ВключитьОтслеживаниеИзмененияСтатусаЗаявления(ДокументЗаявление) Экспорт
	
	Если ДокументЗаявление.Статус = Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отправлено Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		// Повторный запуск задания по одному заявлению вызывает ошибку.
		Задания = ЗаданияПоЗаявлению(ДокументЗаявление);
		Если Задания.Количество() > 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если ДокументооборотСКОВызовСервера.ИспользуетсяРежимТестирования() Тогда
			Интервал = 30;
		Иначе
			Интервал = 600;
		КонецЕсли;
		
		// Запускаем отслеживание изменения состояния
		Расписание = Новый РасписаниеРегламентногоЗадания;
		Расписание.ПериодПовтораВТечениеДня  	= Интервал;
		Расписание.ПериодПовтораДней 			= 1;
		
		Параметры = Новый Массив;
		Параметры.Добавить(ДокументЗаявление);
		
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Метаданные", 	Метаданные.РегламентныеЗадания.ОбработкаЗаявленийАбонента);
		ПараметрыЗадания.Вставить("Ключ", 			КлючЗадания(ДокументЗаявление));
		ПараметрыЗадания.Вставить("Параметры", 		Параметры);
		ПараметрыЗадания.Вставить("Расписание", 	Расписание);
		ПараметрыЗадания.Вставить("Использование", 	Истина);
		ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 		10);
		ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 	3);
		ПараметрыЗадания.Вставить("Наименование", 	НСтр("ru = 'Отслеживание заявления '") + ДокументЗаявление.Номер + НСтр("ru = ' по 1С-Отчетности'"));
		
		РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
		
	КонецЕсли;

КонецПроцедуры

Процедура ОтключитьОтслеживаниеИзменениеСтатусаЗаявления(ДокументЗаявление) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Задания = ЗаданияПоЗаявлению(ДокументЗаявление);
	Для каждого Задание Из Задания Цикл
		РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
	КонецЦикла;

КонецПроцедуры
 
Функция ПолучитьИРазобратьОтветНаЗаявление(
		Знач ДокументЗаявление,
		ЭтоВызовИзМастера = Ложь,
		ОбновитьЗаявление = Ложь) Экспорт
		
	Результат = Новый Структура;
	Результат.Вставить("Выполнено", 					Ложь);
	Результат.Вставить("Статус", 						ДокументЗаявление.Статус);
	Результат.Вставить("СтатусКомментарий", 			ДокументЗаявление.СтатусКомментарий);
	Результат.Вставить("ОтпечатокСертификатаИзОтвета", 	"");
	Результат.Вставить("ИдентификаторАбонента", 		"");
	Результат.Вставить("ПовторятьСоединение", 			Истина);
	Результат.Вставить("УдалосьСоединиться", 			Ложь);
	Результат.Вставить("СтатусИзменился", 				Ложь);
	
	РезультатОбмена = ОбменССерверомПолучитьОтвет(
		ДокументЗаявление,
		ЭтоВызовИзМастера); 
		
	Результат.Вставить("ПовторятьСоединение", 	РезультатОбмена.ПовторятьСоединение);
	Результат.Вставить("УдалосьСоединиться",	РезультатОбмена.УдалосьСоединиться);
	Результат.Вставить("СтатусКомментарий", 	РезультатОбмена.СтатусКомментарий);
	
	ОтветПолучен = РезультатОбмена.Выполнено;
		
	Если ОтветПолучен Тогда
		
		РезультатРазбораОтвета = РегистрацияРазобратьОтвет(ДокументЗаявление, РезультатОбмена);
			
		ОтветРазобран = РезультатРазбораОтвета.Выполнено;
		
		Результат.Вставить("Статус", РезультатРазбораОтвета.Статус);
		
		
		Если ОтветРазобран Тогда
			
			Результат.Вставить("Выполнено", 					Истина);
			Результат.Вставить("ОтпечатокСертификатаИзОтвета",	РезультатРазбораОтвета.ОтпечатокСертификатаИзОтвета);
			Результат.Вставить("ИдентификаторАбонента", 		РезультатРазбораОтвета.ИдентификаторАбонента);
			Результат.Вставить("СтатусИзменился", 				РезультатРазбораОтвета.Статус <> ДокументЗаявление.Статус);
			
			Если ОбновитьЗаявление Тогда
				РеквизитыДляЗаписи = Новый Структура();
				РеквизитыДляЗаписи.Вставить("СтатусКомментарий", 	РезультатРазбораОтвета.СтатусКомментарий);
				РеквизитыДляЗаписи.Вставить("ДатаПолученияОтвета", 	РезультатОбмена.ДатаОтвета);
				РеквизитыДляЗаписи.Вставить("Статус", 				РезультатРазбораОтвета.Статус);
				
				ОбновитьРеквизитыЗаявления(ДокументЗаявление, РеквизитыДляЗаписи);
			КонецЕсли;
			
		КонецЕсли;
		
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(РезультатОбмена.ИмяКаталогаСОтветом);
		
	ИначеЕсли РезультатОбмена.УдалосьСоединиться И НЕ ОтветПолучен Тогда 
		
		Если ОбновитьЗаявление Тогда
			РеквизитыДляЗаписи = Новый Структура();
			РеквизитыДляЗаписи.Вставить("СтатусКомментарий", 	РезультатОбмена.СтатусКомментарий);
			РеквизитыДляЗаписи.Вставить("ДатаПолученияОтвета", 	РезультатОбмена.ДатаОтвета);
			РеквизитыДляЗаписи.Вставить("Статус", 				РезультатОбмена.Статус);
			
			ОбновитьРеквизитыЗаявления(ДокументЗаявление, РеквизитыДляЗаписи);
		КонецЕсли;
		
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(РезультатОбмена.ИмяКаталогаСОтветом);
		
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

Функция ОтправленныеЗаявленияАбонентов(
		ДокументЗаявление 	 = Неопределено,
		Организация 		 = Неопределено,
		ТипЗаявления 		 = Неопределено,
		ИзмененныеРеквизиты  = Неопределено,
		ВключаяЭДО           = Ложь) Экспорт
		
	Если НЕ ПравоДоступа("Чтение", Метаданные.Документы.ЗаявлениеАбонентаСпецоператораСвязи) Тогда
		Возврат Новый Массив;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	
	Если ИзмененныеРеквизиты = Неопределено Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаявлениеАбонентаСпецоператораСвязи.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаявлениеАбонентаСпецоператораСвязи КАК ЗаявлениеАбонентаСпецоператораСвязи
		|ГДЕ
		|	НЕ ЗаявлениеАбонентаСпецоператораСвязи.ПометкаУдаления
		|	И (ЗаявлениеАбонентаСпецоператораСвязи.Статус В (&Отклонено, &Одобрено)
		|				И НЕ ЗаявлениеАбонентаСпецоператораСвязи.НастройкаЗавершена
		|			ИЛИ ЗаявлениеАбонентаСпецоператораСвязи.Статус = &Отправлено
		|			ИЛИ &ВключаяЭДО
		|				И НЕ ЗаявлениеАбонентаСпецоператораСвязи.НастройкаЭДОЗавершена
		|				И ЗаявлениеАбонентаСпецоператораСвязи.НастройкаЗавершена
		|				И НЕ ЗаявлениеАбонентаСпецоператораСвязи.Статус = &Отклонено
		|				И ПОДСТРОКА(ЗаявлениеАбонентаСпецоператораСвязи.НастройкиЭДО, 0, 1000) <> """"
		|				И (ЗаявлениеАбонентаСпецоператораСвязи.ПодключитьЭДО
		|					ИЛИ ЗаявлениеАбонентаСпецоператораСвязи.ПереиздатьСертификатЭДО))
		|	И (ЗаявлениеАбонентаСпецоператораСвязи.Организация = &Организация
		|				И &ЕстьОтборПоОрганизации
		|			ИЛИ НЕ &ЕстьОтборПоОрганизации)
		|	И (ЗаявлениеАбонентаСпецоператораСвязи.Ссылка В (&Заявления)
		|				И &ЕстьОтборПоЗаявлению
		|			ИЛИ НЕ &ЕстьОтборПоЗаявлению)
		|	И (ЗаявлениеАбонентаСпецоператораСвязи.ТипЗаявления = &ТипЗаявления
		|				И &ЕстьОтборПоТипу
		|			ИЛИ НЕ &ЕстьОтборПоТипу)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗаявлениеАбонентаСпецоператораСвязи.ДатаОтправкиЗаявления УБЫВ";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаявлениеАбонентаСпецоператораСвязи.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаявлениеАбонентаСпецоператораСвязи.ИзменившиесяРеквизитыВторичногоЗаявления КАК ЗаявлениеАбонентаСпецоператораСвязи
		|ГДЕ
		|	НЕ ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.ПометкаУдаления
		|	И (ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.Статус В (&Отклонено, &Одобрено)
		|				И НЕ ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.НастройкаЗавершена
		|			ИЛИ ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.Статус = &Отправлено
		|			ИЛИ &ВключаяЭДО
		|				И НЕ ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.НастройкаЭДОЗавершена
		|				И ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.НастройкаЗавершена
		|				И НЕ ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.Статус = &Отклонено
		|				И ПОДСТРОКА(ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.НастройкиЭДО, 0, 1000) <> """"
		|				И (ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.ПодключитьЭДО
		|					ИЛИ ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.ПереиздатьСертификатЭДО))
		|	И (ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.Организация = &Организация
		|				И &ЕстьОтборПоОрганизации
		|			ИЛИ НЕ &ЕстьОтборПоОрганизации)
		|	И (ЗаявлениеАбонентаСпецоператораСвязи.Ссылка В (&Заявления)
		|				И &ЕстьОтборПоЗаявлению
		|			ИЛИ НЕ &ЕстьОтборПоЗаявлению)
		|	И (ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.ТипЗаявления = &ТипЗаявления
		|				И &ЕстьОтборПоТипу
		|			ИЛИ НЕ &ЕстьОтборПоТипу)
		|	И ЗаявлениеАбонентаСпецоператораСвязи.ИзмененныйРеквизит В(&ИзмененныеРеквизиты)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗаявлениеАбонентаСпецоператораСвязи.Ссылка.Дата УБЫВ";

		Запрос.УстановитьПараметр("ИзмененныеРеквизиты", ИзмененныеРеквизиты);
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Отправлено", Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отправлено);
	Запрос.УстановитьПараметр("Одобрено", 	Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Одобрено);
	Запрос.УстановитьПараметр("Отклонено", 	Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отклонено);
	
	Запрос.УстановитьПараметр("Организация", 			Организация);
	Запрос.УстановитьПараметр("ЕстьОтборПоОрганизации", ЗначениеЗаполнено(Организация));
	
	Если ТипЗнч(ДокументЗаявление) = Тип("ДокументСсылка.ЗаявлениеАбонентаСпецоператораСвязи") Тогда
		
		Заявления = Новый Массив;
		Заявления.Добавить(ДокументЗаявление);
		
		Запрос.УстановитьПараметр("Заявления", Заявления);
		
	Иначе
		
		Заявления = ДокументЗаявление;
		Запрос.УстановитьПараметр("Заявления", Заявления);
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ЕстьОтборПоЗаявлению", 	ЗначениеЗаполнено(ДокументЗаявление));
	
	Запрос.УстановитьПараметр("ТипЗаявления", 		ТипЗаявления);
	Запрос.УстановитьПараметр("ЕстьОтборПоТипу", 	ЗначениеЗаполнено(ТипЗаявления));
	Запрос.УстановитьПараметр("ВключаяЭДО", 	    ВключаяЭДО);
	
	Результат = Запрос.Выполнить();
	Возврат Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
		
КонецФункции

// Проверяет наличие отправленных или одобренных заявления по 1С-Отчетности
// Внимание! Не менять без согласования с БП3. Вызывается из БП3.
// 
// Возвращаемое значение:
//  Булево - есть ли отправленные или одобренные заявлений
//
Функция ЕстьОтправленныеИлиОдобренныеЗаявления() Экспорт
		
	Если НЕ ПравоДоступа("Чтение", Метаданные.Документы.ЗаявлениеАбонентаСпецоператораСвязи) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	
	Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаявлениеАбонентаСпецоператораСвязи.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаявлениеАбонентаСпецоператораСвязи КАК ЗаявлениеАбонентаСпецоператораСвязи
	|ГДЕ
	|	НЕ ЗаявлениеАбонентаСпецоператораСвязи.ПометкаУдаления
	|	И (ЗаявлениеАбонентаСпецоператораСвязи.Статус = &Отправлено
	|				И ЗаявлениеАбонентаСпецоператораСвязи.ДатаОтправкиЗаявления > &ДатаОтправкиЗаявления
	|			ИЛИ ЗаявлениеАбонентаСпецоператораСвязи.Статус = &Одобрено
	|				И НЕ ЗаявлениеАбонентаСпецоператораСвязи.НастройкаЗавершена)";
	
	ДвеНеделиНазад = ТекущаяДатаСеанса() - 14 * 24 * 60 * 60;
	
	Запрос.УстановитьПараметр("Отправлено", Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отправлено);
	Запрос.УстановитьПараметр("Одобрено", 	Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Одобрено);
	Запрос.УстановитьПараметр("ДатаОтправкиЗаявления", ДвеНеделиНазад);
	
	Запрос.Текст = Текст;
	
	Результат 		= Запрос.Выполнить();
	ЕстьЗаявления 	= Результат.Выгрузить().ВыгрузитьКолонку("Ссылка").Количество() > 0;
	
	Возврат ЕстьЗаявления;
		
КонецФункции

Функция ОбновитьРеквизитыЗаявления(Документ, РеквизитыДокумента) Экспорт
	
	Если Документ = Неопределено Тогда
		Возврат Ложь;
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаявлениеАбонентаСпецоператораСвязи") Тогда
		ДокументОбъект = Документ.ПолучитьОбъект();
	Иначе
		ДокументОбъект = Документ;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		ДокументОбъект.Прочитать();
		
		// Копируем в заявление все новые свойства.
		ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
		// ИдентификаторКлючевогоКонтейнера
		Если РеквизитыДокумента.Свойство("ИдентификаторКлючевогоКонтейнера") Тогда
			
			Если ЗначениеЗаполнено(ДокументОбъект.УчетнаяЗапись) Тогда 
				ЗаблокироватьДанныеДляРедактирования(ДокументОбъект.УчетнаяЗапись);
				УчетнаяЗапись = ДокументОбъект.УчетнаяЗапись.ПолучитьОбъект();
				УчетнаяЗапись.ИдентификаторДокументооборота = РеквизитыДокумента.ИдентификаторКлючевогоКонтейнера;
				УчетнаяЗапись.Записать();
			КонецЕсли;
			
		КонецЕсли;
		
		ДокументОбъект.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Обработка заявлений.Обновление статуса'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьСтруктуруРеквизитовЗаявления(Документ) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("СпецОператорСвязи", 					Документ.СпецОператорСвязи);
	ДополнительныеПараметры.Вставить("ПутьКонтейнерЗакрытогоКлюча", 		Документ.ПутьКонтейнерЗакрытогоКлюча);
	ДополнительныеПараметры.Вставить("СтатусКомментарий", 					Документ.СтатусКомментарий);
	ДополнительныеПараметры.Вставить("ИдентификаторДокументооборота", 		Документ.ИдентификаторДокументооборота);
	ДополнительныеПараметры.Вставить("ДатаПолученияОтвета", 				Документ.ДатаПолученияОтвета);
	ДополнительныеПараметры.Вставить("Статус", 								Документ.Статус);
	ДополнительныеПараметры.Вставить("Организация", 						Документ.Организация);
	ДополнительныеПараметры.Вставить("НастройкаЗавершена", 					Документ.НастройкаЗавершена);
	ДополнительныеПараметры.Вставить("Дата", 								Документ.Дата);
	ДополнительныеПараметры.Вставить("Номер", 								Документ.Номер);
	ДополнительныеПараметры.Вставить("ЭлектроннаяПодписьВМоделиСервиса", 	Документ.ЭлектроннаяПодписьВМоделиСервиса);
	ДополнительныеПараметры.Вставить("ТелефонМобильныйДляАвторизации", 		Документ.ТелефонМобильныйДляАвторизации);
	ДополнительныеПараметры.Вставить("ЭтоУпрощенноеЗаявление", 				Документ.ЭтоУпрощенноеЗаявление);
	ДополнительныеПараметры.Вставить("ПодписатьЭП", 						Документ.ПодписатьЭП);
	ДополнительныеПараметры.Вставить("ТипЗаявления", 						Документ.ТипЗаявления);
	ДополнительныеПараметры.Вставить("ПометкаУдаления", 					Документ.ПометкаУдаления);
	ДополнительныеПараметры.Вставить("НастройкиЭДО", 						Документ.НастройкиЭДО);
	ДополнительныеПараметры.Вставить("НастройкаЭДОЗавершена", 				Документ.НастройкаЭДОЗавершена);
	ДополнительныеПараметры.Вставить("ПодключитьЭДО", 						Документ.ПодключитьЭДО);
	ДополнительныеПараметры.Вставить("ПереиздатьСертификатЭДО", 			Документ.ПереиздатьСертификатЭДО);
	ДополнительныеПараметры.Вставить("НомерОсновнойПоставки1с", 			Документ.НомерОсновнойПоставки1с);
	ДополнительныеПараметры.Вставить("СпособПолученияСертификата", 			Документ.СпособПолученияСертификата);
	ДополнительныеПараметры.Вставить("МодельХраненияЗакрытогоКлюча",		Документ.МодельХраненияЗакрытогоКлюча);
	ДополнительныеПараметры.Вставить("УчетнаяЗаписьОблачнойПодписи", 		ОбработкаЗаявленийАбонентаКлиентСервер.ПолучитьПараметрПодключения(Документ, "УчетнаяЗапись"));

	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция СформироватьИОтправитьЗаявлениеВМоделиСервиса(ДокументЗаявление, Алгоритм) Экспорт
	
	РезультатВыгрузки 		  = ОбработкаЗаявленийАбонента.ВыгрузитьЗаявлениеАбонентаВМоделиСервиса(ДокументЗаявление, Алгоритм);
	УдалосьВыгрузитьЗаявление = РезультатВыгрузки.Выполнено;
	
	Если УдалосьВыгрузитьЗаявление Тогда
		
		Статус 					= ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отправлено");
		ДатаОтправкиЗаявления 	= ТекущаяДатаСеанса();

		РеквизитыДокументаДляЗаписи = Новый Структура;
		РеквизитыДокументаДляЗаписи.Вставить("ПутьКонтейнерЗакрытогоКлюча", Неопределено);
		РеквизитыДокументаДляЗаписи.Вставить("Статус",						Статус);
		РеквизитыДокументаДляЗаписи.Вставить("ДатаОтправкиЗаявления",		ДатаОтправкиЗаявления);
		РеквизитыДокументаДляЗаписи.Вставить("СтатусКомментарий", 			"");
		
		ОбновитьРеквизитыЗаявления(ДокументЗаявление.Ссылка, РеквизитыДокументаДляЗаписи);
			
	Иначе
		
		Статус 					= ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Подготовлено");
		ДатаОтправкиЗаявления 	= ТекущаяДатаСеанса();
		
	КонецЕсли;
	
	РезультатВыгрузки.Вставить("Статус", 				Статус);
	РезультатВыгрузки.Вставить("ДатаОтправкиЗаявления", ДатаОтправкиЗаявления);
		
	Возврат РезультатВыгрузки;
		
КонецФункции

Функция ОбработатьИзменениеСтатусаЗаявленияАбонентаВМоделиСервиса(ДокументЗаявление) Экспорт
	
	Результат = МенеджерСервисаКриптографии.ПолучитьСтатусЗаявленияНаПодключение(ДокументЗаявление.ИдентификаторДокументооборота);
	
	Если Не Результат.Выполнено Тогда
		ОбщегоНазначения.СообщитьПользователю(Результат.ОписаниеОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	Если Результат.Статус = "Исполняется" Тогда
		Статус = Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отправлено;
	ИначеЕсли Результат.Статус = "Отклонено" Тогда
		Статус = Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отклонено;
	ИначеЕсли Результат.Статус = "Исполнено" Тогда
		Статус = Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Одобрено;
	КонецЕсли;
	
	Если Статус = Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отправлено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Пояснение = Результат.Пояснение;

	Если ЗначениеЗаполнено(Результат.Токен) 
		И ЗначениеЗаполнено(Результат.ИдентификаторСертификата) Тогда
		СпособПодтвержденияКриптоопераций = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументЗаявление, "СпособПодтвержденияКриптоопераций");
		ОбновитьДанныеДолговременногоТокена(Результат.Токен, Результат.ИдентификаторСертификата, СпособПодтвержденияКриптоопераций);
	КонецЕсли;
	
	РеквизитыДляЗаписи = Новый Структура;
	РеквизитыДляЗаписи.Вставить("Статус", Статус);
	РеквизитыДляЗаписи.Вставить("ДатаПолученияОтвета", ТекущаяДатаСеанса());
	РеквизитыДляЗаписи.Вставить("СтатусКомментарий", Пояснение);
	
	Возврат ОбработкаЗаявленийАбонентаВызовСервера.ОбновитьРеквизитыЗаявления(ДокументЗаявление, РеквизитыДляЗаписи);
	
КонецФункции

Функция ПолучитьОтветСервераНаЗаявлениеАбонента(ДокументЗаявление) Экспорт
	
	РезультатОтветаСервера = Новый Структура;
	РезультатОтветаСервера.Вставить("СтатусИзменился", Ложь);
	
	РеквизитыДокумента	= ПолучитьСтруктуруРеквизитовЗаявления(ДокументЗаявление);
	
	Ответ = Новый Структура;
	Ответ.Вставить("РезультатОтветаСервера", РезультатОтветаСервера);
	Ответ.Вставить("РеквизитыДокумента", 	 РеквизитыДокумента);
	
	// Для заявления с ЭП в модели сервиса обязательно нужно сначала получить идентификатор ключевого контейнера.
	МестоХраненияКлюча = КриптографияЭДКОКлиентСервер.КонтекстМоделиХраненияКлюча(РеквизитыДокумента);
	Если КриптографияЭДКОКлиентСервер.ЭтоПодписьСервиса(МестоХраненияКлюча) Тогда
		
		ЗаявлениеОбработано = ОбработатьИзменениеСтатусаЗаявленияАбонентаВМоделиСервиса(ДокументЗаявление);
		
		Если ЗаявлениеОбработано Тогда
			
			// Обновляем структуру реквизитов в доп. параметрах после обращения к серверу.
			РеквизитыДокумента	= ПолучитьСтруктуруРеквизитовЗаявления(ДокументЗаявление);
			
		Иначе
			
			Возврат Ответ;
			
		КонецЕсли;
		
	КонецЕсли;
		
	// Получаем ответ от сервера.
	Если КриптографияЭДКОКлиентСервер.ЭтоПодписьСервиса(МестоХраненияКлюча)
		И РеквизитыДокумента.Статус = Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отклонено Тогда
		
		РезультатОтветаСервера = Новый Структура;
		РезультатОтветаСервера.Вставить("ОтпечатокСертификатаИзОтвета", "");
		РезультатОтветаСервера.Вставить("ИдентификаторАбонента", 		"");
		РезультатОтветаСервера.Вставить("СтатусИзменился", 				Истина);
		РезультатОтветаСервера.Вставить("Статус", 						РеквизитыДокумента.Статус);
		РезультатОтветаСервера.Вставить("Выполнено", 					Истина);
		РезультатОтветаСервера.Вставить("ПовторятьСоединение", 			Ложь);
		
	Иначе
		
		РезультатОтветаСервера = ПолучитьИРазобратьОтветНаЗаявление(ДокументЗаявление,,Истина);
		
		ПолученОтветНаЗаявление = РезультатОтветаСервера.Выполнено;
		ПовторятьСоединение 	= РезультатОтветаСервера.ПовторятьСоединение;
		
		Если НЕ ПолученОтветНаЗаявление И ПовторятьСоединение Тогда
			РезультатОтветаСервера.Вставить("СообщитьОбОтсутствииИнтернета", Истина);
		Иначе
			// Обновляем структуру реквизитов в доп. параметрах после обращения к серверу.
			РеквизитыДокумента = ПолучитьСтруктуруРеквизитовЗаявления(ДокументЗаявление);
		КонецЕсли;
		
	КонецЕсли;
	
	Ответ.Вставить("РезультатОтветаСервера", РезультатОтветаСервера);
	Ответ.Вставить("РеквизитыДокумента", 	 РеквизитыДокумента);
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьРеквизитОблачнойПодписи(ДокументЗаявление, ВидПараметра) Экспорт
	
	Результат = Новый Массив;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаявлениеАбонентаСпецоператораСвязиПараметрыПодключенияОблачнойПодписи.ЗначениеБулево КАК ЗначениеБулево,
	|	ЗаявлениеАбонентаСпецоператораСвязиПараметрыПодключенияОблачнойПодписи.ЗначениеСтрока КАК ЗначениеСтрока,
	|	ЗаявлениеАбонентаСпецоператораСвязиПараметрыПодключенияОблачнойПодписи.ЗначениеСсылка КАК ЗначениеСсылка,
	|	ЗаявлениеАбонентаСпецоператораСвязиПараметрыПодключенияОблачнойПодписи.Параметр КАК Параметр
	|ИЗ
	|	Документ.ЗаявлениеАбонентаСпецоператораСвязи.ПараметрыПодключенияОблачнойПодписи КАК ЗаявлениеАбонентаСпецоператораСвязиПараметрыПодключенияОблачнойПодписи
	|ГДЕ
	|	ЗаявлениеАбонентаСпецоператораСвязиПараметрыПодключенияОблачнойПодписи.Ссылка = &Ссылка
	|	И ЗаявлениеАбонентаСпецоператораСвязиПараметрыПодключенияОблачнойПодписи.Параметр = &Параметр";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДокументЗаявление);
	Запрос.УстановитьПараметр("Параметр", ВидПараметра);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Новый Структура("Параметр, ЗначениеСсылка, ЗначениеБулево, ЗначениеСтрока");
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Результат.Добавить(НоваяСтрока);
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗаданияПоЗаявлению(ДокументЗаявление)
	
	// Задание ищется по ключу - ссылка на заявление абонента
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Ключ", КлючЗадания(ДокументЗаявление));
	ДополнительныеПараметры.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ОбработкаЗаявленийАбонента);
	
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(ДополнительныеПараметры);
	
	Возврат Задания;
	
КонецФункции

Функция КлючЗадания(ДокументЗаявление)
	
	Возврат ДокументЗаявление.ИдентификаторДокументооборота;
	
КонецФункции

Функция ОбменССерверомПолучитьОтвет(ДокументЗаявление, ЭтоВызовИзМастера) Экспорт
	
	ИдентификаторДокументооборота 	= ДокументЗаявление.ИдентификаторДокументооборота;
	СпецоператорСвязи 				= ДокументЗаявление.СпецоператорСвязи;
	
	ИмяКаталогаСОтветомНаСервере 	= ОперацииСФайламиЭДКО.СоздатьВременныйКаталог();
	ИмяФайлаОтвета 					= ИмяКаталогаСОтветомНаСервере + "ответ.zip";
	
	Результат = Новый Структура;
	Результат.Вставить("Выполнено", 			Ложь);
	Результат.Вставить("ДатаОтвета", 			Неопределено);
	Результат.Вставить("СтатусКомментарий", 	ДокументЗаявление.СтатусКомментарий);
	Результат.Вставить("ИмяКаталогаСОтветом", 	ИмяКаталогаСОтветомНаСервере);
	Результат.Вставить("ИмяФайлаОтвета", 		ИмяФайлаОтвета);
	Результат.Вставить("Статус", 				ДокументЗаявление.Статус);
	Результат.Вставить("УдалосьСоединиться", 	Ложь);
	Результат.Вставить("ПовторятьСоединение", 	Истина);
	
	Сервис = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ОбменССерверомСоздатьСервис(
		СпецоператорСвязи, 
		ЭтоВызовИзМастера, 
		,
		, 
		Ложь);
		
	Если Сервис = Неопределено Тогда
		Результат.Вставить("СтатусКомментарий", НСтр("ru = 'Не удалось соединиться с сервером'"));
		Возврат Результат;
	КонецЕсли;
	
	Попытка
		XDTOРезультат = Сервис.ReceivePacket(Строка(идентификаторДокументооборота));
	Исключение
		
		ТекстОшибки = НСтр("ru = 'Не удалось соединиться с сервером. '") + ИнформацияОбОшибке().Описание;
		ТекстОшибки = ДокументооборотСКОВызовСервера.ДобавитьТекстОшибкиСертификатаКА(ТекстОшибки);
		
		Результат.Вставить("СтатусКомментарий", ТекстОшибки);
		
		Возврат Результат;
	КонецПопытки;
	
	Результат.Вставить("УдалосьСоединиться", 	Истина);
	Результат.Вставить("ПовторятьСоединение", 	Ложь);
	
	ЧтениеXML 		= Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(XDTOРезультат);
	
	ПостроительДОМ 	= Новый ПостроительDOM();
	ДОМ 			= ПостроительДОМ.Прочитать(ЧтениеXML);
	УзелDOM 		= ДОМ.ПолучитьЭлементыПоИмени("code");
	кодРезультата 	= УзелDOM[0].ТекстовоеСодержимое;
	
	Результат.Вставить("ДатаОтвета", ТекущаяДатаСеанса());
	
	Если кодРезультата = "0" Тогда
		УзелDOM = ДОМ.ПолучитьЭлементыПоИмени("packet");
		
		Если УзелDOM.Количество() > 0 Тогда
			
			ДвоичныеДанные = Base64Значение(УзелDOM[0].ТекстовоеСодержимое);
			ДвоичныеДанные.Записать(ИмяФайлаОтвета);
			ЧтениеXML.Закрыть();
			
			Результат.Вставить("Выполнено", Истина);
			
		Иначе
			
			СтатусКомментарий = НСтр("ru = 'При получении ответа возникла ошибка: ответ нечитаем'");
			Результат.Вставить("СтатусКомментарий", СтатусКомментарий);
			
		КонецЕсли;
		
	ИначеЕсли Кодрезультата = "1" Тогда
		
		СтатусКомментарий = НСтр("ru = 'Заявление еще не обработано сервером, попробуйте позже.'");
		Результат.Вставить("СтатусКомментарий", СтатусКомментарий);
		
	Иначе
		
		УзелDOM = ДОМ.ПолучитьЭлементыПоИмени("errorMessage");
		Если УзелDOM.Количество() > 0 Тогда
			сообщениеОшибки = УзелDOM[0].ТекстовоеСодержимое;
			СтатусКомментарий = НСтр("ru = 'При получении ответа сервер Калуги Астрал вернул ошибку: '") + сообщениеОшибки;
		Иначе
			СтатусКомментарий = НСтр("ru = 'При получении ответа сервер Калуги Астрал вернул ошибку: '") + кодРезультата;
		КонецЕсли;
		
		Результат.Вставить("СтатусКомментарий", СтатусКомментарий);
		
	КонецЕсли;
	
	ЧтениеXML.Закрыть();
	
	Результат.Вставить("XDTOРезультат", XDTOРезультат);
	
	Возврат Результат;
	
КонецФункции

Функция РегистрацияРазобратьОтвет(ДокументЗаявление, РезультатОбмена) Экспорт
		
	Результат = Новый Структура;
	Результат.Вставить("Выполнено", 					Ложь);
	Результат.Вставить("СтатусКомментарий", 			"");
	Результат.Вставить("Статус", 						ДокументЗаявление.Статус);
	Результат.Вставить("ИдентификаторАбонента", 		"");
	Результат.Вставить("ОтпечатокСертификатаИзОтвета", 	"");
	
	ИмяКаталогаСОтветом 	= РезультатОбмена.ИмяКаталогаСОтветом;
	ИмяФайлаОтвета 			= РезультатОбмена.ИмяФайлаОтвета; 
		
	Попытка
		Архив1 = Новый ЧтениеZipФайла(ИмяФайлаОтвета);
		
		Для Счетчик = 0 по Архив1.Элементы.Количество() - 1 Цикл
			
			Если Архив1.Элементы[Счетчик].расширение = "bin" Тогда
				
				Архив1.Извлечь(Архив1.Элементы[Счетчик], ИмяКаталогаСОтветом);
				Архив2 = Новый ЧтениеZipФайла(ИмяКаталогаСОтветом + Архив1.Элементы[Счетчик].имя);
				Архив2.Извлечьвсе(ИмяКаталогаСОтветом);
				
				ДокументDOM = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ЗагрузитьФайлXML(ИмяКаталогаСОтветом + "file");
				
				// Отпечаток
				ОтпечатокСертификатаИзОтвета = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ПолучитьЗначениеУзлаXML(ДокументDOM,"ОтпечатокСертификата");
				Результат.Вставить("ОтпечатокСертификатаИзОтвета", ОтпечатокСертификатаИзОтвета);
				
				// Комментарий
				Результат.Вставить("СтатусКомментарий", ЭлектронныйДокументооборотСКонтролирующимиОрганами.ПолучитьЗначениеУзлаXML(ДокументDOM,"Результат"));
				
				// Успешность регистрации заявления
				РезультатРегистрации = Булево(ЭлектронныйДокументооборотСКонтролирующимиОрганами.ПолучитьЗначениеУзлаXML(ДокументDOM,"РегистрацияУспешна"));
				Если РезультатРегистрации Тогда
					
					ИдентификаторАбонента = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ПолучитьЗначениеУзлаXML(ДокументDOM,"ИдентификаторАбонента");
					
					Результат.Вставить("Статус", 				Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Одобрено);
					Результат.Вставить("ИдентификаторАбонента",	ИдентификаторАбонента);
					Результат.Вставить("Выполнено", 			Истина);
					
					Возврат Результат;
					
				Иначе
					
					Результат.Вставить("Статус", 	Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отклонено);
					Результат.Вставить("Выполнено", Истина);
									
					Возврат Результат;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Исключение
		
		СтатусКомментарий = НСтр("ru = 'Ошибка. Не удалось разобрать ответ сервера.'");
		
		Результат.Вставить("Статус", 			Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отклонено);
		Результат.Вставить("СтатусКомментарий", СтатусКомментарий);
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Процедура ОчиститьЗаявленияТребующиеНапоминанияПозжеСПрошедшимСроком()
	
	Заявления = ХранилищеОбщихНастроек.Загрузить(ОбработкаЗаявленийАбонента.КлючЗаявленийТребующихНапоминанияПозже());
	
	Если Заявления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Количество 		= Заявления.Количество();
	ТекущаяСтрока 	= 0;

	Пока ТекущаяСтрока <= Количество - 1 Цикл
		
		Если Заявления[ТекущаяСтрока].Дата < ТекущаяДатаСеанса() Тогда
			Заявления.Удалить(ТекущаяСтрока);
			Количество = Количество - 1;
		Иначе
			ТекущаяСтрока = ТекущаяСтрока + 1;
		КонецЕсли;
		
	Конеццикла; 
	
	ХранилищеОбщихНастроек.Сохранить(ОбработкаЗаявленийАбонента.КлючЗаявленийТребующихНапоминанияПозже(), , Заявления);

КонецПроцедуры

Функция ЗаявленияТребующиеНапоминанияПозже() Экспорт
	
	ОчиститьЗаявленияТребующиеНапоминанияПозжеСПрошедшимСроком();
	Заявления = ХранилищеОбщихНастроек.Загрузить(ОбработкаЗаявленийАбонента.КлючЗаявленийТребующихНапоминанияПозже());
	
	Если Заявления = Неопределено Тогда
		Возврат Новый Массив;
	Иначе
		Возврат Заявления.ВыгрузитьКолонку("Заявление");
	КонецЕсли;

КонецФункции

Функция ОбновитьДанныеДолговременногоТокена(ДолговременныйТокен, ИдентификаторСертификата, Знач СпособПодтверждения = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если СпособПодтверждения = Неопределено Тогда
		СпособПодтверждения = Перечисления.СпособыПодтвержденияКриптоопераций.СессионныйТокен;
	КонецЕсли;
	
	Если СпособПодтверждения = Перечисления.СпособыПодтвержденияКриптоопераций.ДолговременныйТокен Тогда
		ИмяВеткиХранения				= СервисКриптографииСлужебный.ИмяНастройкиДлительногоМаркерБезопасностиСертификата(ИдентификаторСертификата);
		ДлительныйМаркерБезопасности 	= ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ИмяВеткиХранения, "ДлительныйМаркерБезопасности", Ложь);
		
		Если ДлительныйМаркерБезопасности = Неопределено Тогда
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ИмяВеткиХранения, ДолговременныйТокен, "ДлительныйМаркерБезопасности"); 
		КонецЕсли;
		
		ЭлектроннаяПодписьВМоделиСервиса.УстановитьСвойстваРасшифрованияПодписания(ИдентификаторСертификата);
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецФункции	

Функция ЭтоРуководитель(Заявление) Экспорт
	
	Возврат ОбработкаЗаявленийАбонентаКлиентСервер.ЭтоРуководитель(Заявление);
	
КонецФункции

Функция ЭтоБюджетополучатель(Заявление) Экспорт
	
	Возврат Заявление.ЭтоБюджетополучатель;
	
КонецФункции

Функция СертификатИзЗаявления(ДокументЗаявление) Экспорт
	
	Если ТипЗнч(ДокументЗаявление) = Тип("ДанныеФормыСтруктура") Тогда
		ЗаявлениеСсылка = ДокументЗаявление.Ссылка;
	Иначе
		ЗаявлениеСсылка = ДокументЗаявление;
	КонецЕсли;
	
	Если ДокументЗаявление.ТипЗаявления = Перечисления.ТипыЗаявленияАбонентаСпецоператораСвязи.Первичное Тогда
		Сертификат = ЗаявлениеСсылка.РеквизитыСертификата.Получить();
	Иначе
		Сертификат = ЗаявлениеСсылка.Сертификат.Получить();
	КонецЕсли;
	
	Возврат Сертификат;
		
КонецФункции

Функция ЭтоПоставляемыйСерверОблачнойПодписи(УчетнаяЗаписьОблачнойПодписи) Экспорт
	
	Результат = Ложь;
	
	Если ЗначениеЗаполнено(УчетнаяЗаписьОблачнойПодписи) Тогда
		МодульСервисКриптографииDSS = ОбщегоНазначения.ОбщийМодуль("СервисКриптографииDSS");
		ДанныеУчетнойЗаписи = МодульСервисКриптографииDSS.ПолучитьВсеУчетныеЗаписи(УчетнаяЗаписьОблачнойПодписи);
		Если ДанныеУчетнойЗаписи.Количество() > 0 Тогда
			Результат = ЗначениеЗаполнено(ДанныеУчетнойЗаписи[0].ВнутреннийИдентификатор);
		КонецЕсли;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

Функция ЭтоИнтеграцияСБанком() Экспорт

	Возврат ОбработкаЗаявленийАбонента.ЭтоИнтеграцияСБанком();
	
КонецФункции

Функция МЧДЗагружена(МЧД) Экспорт
	
	Загружена = 
		МЧД.ФайлВырузки.Получить() <> Неопределено
		И МЧД.ЭлектроннаяПодпись.Получить() <> Неопределено;
		
	Возврат Загружена;
		
КонецФункции

Функция ДополнитьОшибкуМЧД(МЧД, ТекстОшибки) Экспорт
	
	Возврат Строка(МЧД) + НСтр("ru = ' не может быть выбрана, поскольку '") + ТекстОшибки;
	
КонецФункции

Функция ПолучитьФайлыМЧД(МЧД, ВладелецЭЦПИНН) Экспорт
	
	Если ДокументооборотСКОКлиентСервер.ПодсистемаЦБСуществует() Тогда
		ИмяТипаСправочникаМашиночитаемыеДоверенностиЦБ = "МашиночитаемыеДоверенностиЦБ";
		ТипМЧДЦБ = Тип("СправочникСсылка." + ИмяТипаСправочникаМашиночитаемыеДоверенностиЦБ);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Выполнено", Ложь);
	Результат.Вставить("ТекстОшибки", "");
	Результат.Вставить("Доверенность", Неопределено);
	
	Доверенность = Новый Структура;
	Доверенность.Вставить("МЧД", Неопределено);
	Доверенность.Вставить("ИмяМЧД", "");
	Доверенность.Вставить("АдресМЧД", "");
	Доверенность.Вставить("РазмерМЧД", 0);
	Доверенность.Вставить("ИмяПодписи", "");
	Доверенность.Вставить("АдресПодписи", "");
	Доверенность.Вставить("РазмерПодписи", 0);
	
	Если МЧД  = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	ПредставительЭтоОрганизация = ТипЗнч(МЧД.Представитель) = Тип("СправочникСсылка.Организации");
	СовпадаетИННФЛ = 
		НЕ ПредставительЭтоОрганизация И МЧД.ПредставительФЛ_ИНН = ВладелецЭЦПИНН
		ИЛИ ПредставительЭтоОрганизация; 
	
	Если НЕ СовпадаетИННФЛ Тогда
		ТекстОшибки = НСтр("ru = 'ИНН представителя %1 в МЧД отличается от ИНН владельца эл. подписи %2 в заявлении'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, МЧД.ПредставительФЛ_ИНН, ВладелецЭЦПИНН);
		Результат.ТекстОшибки = ДополнитьОшибкуМЧД(МЧД, ТекстОшибки);
		Возврат Результат;
	КонецЕсли;
	
	Если ТипЗнч(МЧД) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФНС")
		ИЛИ ТипЗнч(МЧД) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиРаспределенныйРеестр") Тогда
		
		Отправлена = ОбработкаЗаявленийАбонента.МЧДОтправлена(МЧД);
		Загружена  = МЧДЗагружена(МЧД);
		Если НЕ Отправлена И НЕ Загружена Тогда
			Результат.ТекстОшибки = ДополнитьОшибкуМЧД(МЧД, НСтр("ru = 'она не отправлена'"));
			Возврат Результат;
		КонецЕсли;
		
		Отозвана = ОбработкаЗаявленийАбонента.МЧДОтозвана(МЧД);
		Если Отозвана Тогда
			Результат.ТекстОшибки = ДополнитьОшибкуМЧД(МЧД, НСтр("ru = 'она отозвана'"));
			Возврат Результат;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(МЧД) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФСС") Тогда
		
		Зарегистрирована = ОбработкаЗаявленийАбонента.МЧДФССЗарегистрирована(МЧД);
		Если НЕ Зарегистрирована Тогда
			Результат.ТекстОшибки = ДополнитьОшибкуМЧД(МЧД, НСтр("ru = 'она не зарегистрирована'"));
			Возврат Результат;
		КонецЕсли;
		
	КонецЕсли;
	
	Доверенность.МЧД = МЧД;
	
	Файл = ФайлВыгрузкиМЧД(МЧД);
	
	Если Файл = Неопределено Тогда
		Результат.ТекстОшибки = ДополнитьОшибкуМЧД(МЧД, НСтр("ru = 'не удалось получить файл выгрузки доверенности'"));
		Возврат Результат;
	Иначе
		Доверенность.ИмяМЧД    = Файл.Имя;
		Доверенность.АдресМЧД  = Файл.Адрес;
		Доверенность.РазмерМЧД = Файл.Размер;
	КонецЕсли;
	
	Если ТипЗнч(МЧД) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФНС")
		ИЛИ ТипЗнч(МЧД) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиРаспределенныйРеестр")
		ИЛИ ТипЗнч(МЧД) = ТипМЧДЦБ Тогда
		
		Файл = ФайлыПодписиМЧД(МЧД);
		
		Если Файл = Неопределено Тогда
			Результат.ТекстОшибки = ДополнитьОшибкуМЧД(МЧД, НСтр("ru = 'не удалось получить файл подписи доверенности'"));
			Возврат Результат;
		Иначе
			Доверенность.ИмяПодписи    = Файл.Имя;
			Доверенность.АдресПодписи  = Файл.Адрес;
			Доверенность.РазмерПодписи = Файл.Размер;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат.Выполнено = Истина;
	Результат.Доверенность = Доверенность;
	
	Возврат Результат;
	
КонецФункции

Функция ФайлыПодписиМЧД(Доверенность) Экспорт
	
	Если ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФСС") Тогда
		// Нет отсоединенной подписи
		Возврат Неопределено;
	КонецЕсли;
	
	МЧДЗагружена = МЧДЗагружена(Доверенность);
	Если МЧДЗагружена Тогда
		
		ДвДанные = Доверенность.ЭлектроннаяПодпись.Получить();
		Адрес    = ПоместитьВоВременноеХранилище(ДвДанные, Новый УникальныйИдентификатор);
		Имя      = ИмяПодписиМЧД(Доверенность);
		
		Файл = Новый Структура;
		Файл.Вставить("Адрес",  Адрес);
		Файл.Вставить("Размер", ДвДанные.Размер());
		Файл.Вставить("Имя",    Имя);
		
	ИначеЕсли ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФНС") Тогда
		
		Файл = ФайлыОтправленнойМЧДФНС(Доверенность, , Истина);
		
	ИначеЕсли ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиРаспределенныйРеестр") Тогда
		
		Файл = ФайлыОтправленнойМЧДРР(Доверенность, , Истина);
		
	КонецЕсли;
	
	Возврат Файл;

КонецФункции

Функция ФайлВыгрузкиМЧД(Доверенность) Экспорт
	
	МЧДЗагружена = МЧДЗагружена(Доверенность);
	Если МЧДЗагружена Тогда
		
		ДвДанные = Доверенность.ФайлВырузки.Получить();
		Адрес    = ПоместитьВоВременноеХранилище(ДвДанные, Новый УникальныйИдентификатор);
		
		Файл = Новый Структура;
		Файл.Вставить("Адрес",  Адрес);
		Файл.Вставить("Имя",    Доверенность.ИмяФайлаВыгрузки);
		Файл.Вставить("Размер", ДвДанные.Размер());
		
	ИначеЕсли ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФНС") Тогда
		
		Файл = ФайлыОтправленнойМЧДФНС(Доверенность, Истина);
		
	ИначеЕсли ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиРаспределенныйРеестр") Тогда
		
		Файл = ФайлыОтправленнойМЧДРР(Доверенность, Истина);
		
	ИначеЕсли ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФСС") Тогда
		
		Файл = ФайлыОтправленнойМЧДФСС(Доверенность);
		
	КонецЕсли;
	
	Возврат Файл;

КонецФункции

Функция ФайлыОтправленнойМЧДРР(МЧД, НужнаМЧД = Ложь, НужнаПодпись = Ложь) Экспорт
	
	Если НужнаМЧД Тогда
		ДвДанные = МЧД.ФайлВырузки.Получить();
		ИмяФайла = МЧД.ИмяФайлаВыгрузки;
	ИначеЕсли НужнаПодпись Тогда
		ДвДанные = МЧД.ЭлектроннаяПодпись.Получить();
		ИмяФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(
			МЧД.ИмяФайлаВыгрузки).ИмяБезРасширения + "_SGN_"
			+ нрег(Строка(Новый УникальныйИдентификатор())) + ".sgn";
	КонецЕсли;
	
	Если ДвДанные = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		
		Адрес = ПоместитьВоВременноеХранилище(ДвДанные, Новый УникальныйИдентификатор);
		
		Файл = Новый Структура;
		Файл.Вставить("Адрес",  Адрес);
		Файл.Вставить("Имя",    ИмяФайла);
		Файл.Вставить("Размер", ДвДанные.Размер());
		
		Возврат Файл;
		
	КонецЕсли;
	
КонецФункции

Функция ФайлыОтправленнойМЧДФНС(МЧД, НужнаМЧД = Ложь, НужнаПодпись = Ложь) Экспорт
	
	ЦиклОбмена = ДокументооборотСКОВызовСервера.ПолучитьПоследнийЦиклОбмена(МЧД);
	Если ЦиклОбмена = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	ТипСообщения = Перечисления.ТипыТранспортныхСообщений.ПредставлениеНП;
	Сообщения = ДокументооборотСКОВызовСервера.ПолучитьСообщенияЦиклаОбмена(ЦиклОбмена, ТипСообщения);
	Если Сообщения.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТипСообщения = Перечисления.ТипыТранспортныхСообщений.ПредставлениеНП;
	Строки       = Сообщения.НайтиСтроки(Новый Структура("Тип", ТипСообщения));
	
	Если Строки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НужнаМЧД Тогда
		Тип = Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Представление;
	ИначеЕсли НужнаПодпись Тогда
		Тип = Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Приложение;
	КонецЕсли;
	
	ТранспортноеСообщение = Строки[0];
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Вложения = КонтекстЭДОСервер.ПолучитьВложенияТранспортногоСообщения(
		ТранспортноеСообщение, 
		Истина, 
		, 
		, 
		Истина);
		
	Если Вложения.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для каждого Вложение Из Вложения Цикл
		
		ЭтоПодпись = Вложение.Тип = Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Приложение;
		ЭтоМЧД     = Вложение.Тип = Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Представление;
		
		ЭтоНужныйФайл =
			 ЭтоПодпись И НужнаПодпись
			 ИЛИ ЭтоМЧД И НужнаМЧД;
		
		Если ЭтоНужныйФайл Тогда
			
			Файл = Новый Структура;
			Файл.Вставить("Адрес",  Вложение.Адрес);
			Файл.Вставить("Имя",    Вложение.ИмяФайла);
			Файл.Вставить("Размер", Вложение.Размер);
			
			Возврат Файл;
			
		КонецЕсли;
			
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция ФайлыОтправленнойМЧДФСС(МЧД) Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Отправка = КонтекстЭДОСервер.ПолучитьПоследнююОтправкуОтчетаВФСС(МЧД);
	
	Если ЗначениеЗаполнено(Отправка) Тогда
		
		ДвДанные = Отправка.ПодписанныйПакет.Получить();
		Если ДвДанные = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
			
		Адрес = ПоместитьВоВременноеХранилище(ДвДанные, Новый УникальныйИдентификатор);
		
		ИмяФайла = Отправка.ИмяФайлаПакета;
		
		Файл = Новый Структура;
		Файл.Вставить("Адрес",  Адрес);
		Файл.Вставить("Имя",    ИмяФайла);
		Файл.Вставить("Размер", ДвДанные.Размер());
		
		Возврат Файл;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ИмяПодписиМЧД(МЧД) Экспорт
	
	Расширение = "sgn";
	Части = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(МЧД.ИмяФайлаВыгрузки);
	GUID = ВРЕГ(Строка(Новый УникальныйИдентификатор));
	Имя  = Части.ИмяБезРасширения + "_SGN_" + GUID + "." + Расширение;
	
	Возврат Имя;

КонецФункции

#КонецОбласти
