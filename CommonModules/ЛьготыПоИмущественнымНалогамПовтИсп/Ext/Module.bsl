
#Область ПрограммныйИнтерфейс

Функция ДанныеОЛьготахПоИмущественнымНалогам(ВключатьНеактуальные = Истина) Экспорт 

	Результат = Новый Структура;
	
	ФедеральныеЛьготы = Новый Соответствие;
	МакетЛьгот = Справочники.ОснованияЛьготПоИмущественнымНалогам.ПолучитьМакет("КлассификаторЛьгот_ru");
	МакетЛьгот.КодЯзыка = "ru";
	ОбластьМакета = МакетЛьгот.ПолучитьОбласть("ФедеральныеЛьготы");	
	КоличествоЛьгот = ОбластьМакета.ВысотаТаблицы;
	
	// Актуальные льготы
	Для НомерСтроки = 1 По КоличествоЛьгот Цикл
		
		КодЛьготы = СокрЛП(ОбластьМакета.Область(НомерСтроки, 1).Текст);
		
		ОписаниеЛьготы = Новый Структура;
		
		ОписаниеЛьготы.Вставить("Наименование", ОбластьМакета.Область(НомерСтроки, 2).Текст);
		ОписаниеЛьготы.Вставить("Категория", ОбластьМакета.Область(НомерСтроки, 3).Текст);
		ОписаниеЛьготы.Вставить("Основание", ОбластьМакета.Область(НомерСтроки, 4).Текст);
		
		ВидЛьготыСтрокой = ОбластьМакета.Область(НомерСтроки, 5).Текст;
		ОписаниеЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам[ВидЛьготыСтрокой]);
		ПроцентЛьготы = ОбластьМакета.Область(НомерСтроки, 6).Текст;
		ОписаниеЛьготы.Вставить("ПроцентЛьготы", ?(ЗначениеЗаполнено(ПроцентЛьготы), Число(ПроцентЛьготы), 0));
		ОписаниеЛьготы.Вставить("Актуальная", Истина);
		
		ФедеральныеЛьготы.Вставить(КодЛьготы, ОписаниеЛьготы);
		
	КонецЦикла;
	
	// Неактуальные льготы - для обратной совместимости
	Если ВключатьНеактуальные Тогда
		ОбластьМакета = МакетЛьгот.ПолучитьОбласть("ФедеральныеЛьготы_Неактуальные");	
		КоличествоЛьгот = ОбластьМакета.ВысотаТаблицы;
		Для НомерСтроки = 1 По КоличествоЛьгот Цикл
			
			КодЛьготы = СокрЛП(ОбластьМакета.Область(НомерСтроки, 1).Текст);
			
			ОписаниеЛьготы = Новый Структура;
			
			ОписаниеЛьготы.Вставить("Наименование", ОбластьМакета.Область(НомерСтроки, 2).Текст);
			ОписаниеЛьготы.Вставить("Категория", ОбластьМакета.Область(НомерСтроки, 3).Текст);
			ОписаниеЛьготы.Вставить("Основание", ОбластьМакета.Область(НомерСтроки, 4).Текст);
			
			ВидЛьготыСтрокой = ОбластьМакета.Область(НомерСтроки, 5).Текст;
			ОписаниеЛьготы.Вставить("ВидЛьготы", Перечисления.ВидыЛьготПоИмущественнымНалогам[ВидЛьготыСтрокой]);
			ПроцентЛьготы = ОбластьМакета.Область(НомерСтроки, 6).Текст;
			ОписаниеЛьготы.Вставить("ПроцентЛьготы", ?(ЗначениеЗаполнено(ПроцентЛьготы), Число(ПроцентЛьготы), 0));
			ОписаниеЛьготы.Вставить("Актуальная", Ложь);
			
			ФедеральныеЛьготы.Вставить(КодЛьготы, ОписаниеЛьготы);
			
		КонецЦикла;
	КонецЕсли;	
	
	Результат.Вставить("ФедеральныеЛьготы", ФедеральныеЛьготы);
	
	РегиональныеЛьготы = Новый Соответствие;
	ОбластьМакета = МакетЛьгот.ПолучитьОбласть("РегиональныеЛьготы");	
	КоличествоЛьгот = ОбластьМакета.ВысотаТаблицы;
	
	// Для региональных льгот больше информации несет вид льготы - по нему можно однозначно определить код льготы
	Для НомерСтроки = 1 По КоличествоЛьгот Цикл
		
		ВидЛьготыСтрокой = ОбластьМакета.Область(НомерСтроки, 4).Текст;
		ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам[ВидЛьготыСтрокой];
				
		ОписаниеЛьготы = Новый Структура;
		
		ОписаниеЛьготы.Вставить("КодЛьготы", ОбластьМакета.Область(НомерСтроки, 1).Текст);
		ОписаниеЛьготы.Вставить("Наименование", ОбластьМакета.Область(НомерСтроки, 2).Текст);
		ОписаниеЛьготы.Вставить("Категория", ОбластьМакета.Область(НомерСтроки, 3).Текст);
		
		РегиональныеЛьготы.Вставить(ВидЛьготы, ОписаниеЛьготы);
		
	КонецЦикла;
	
	Результат.Вставить("РегиональныеЛьготы", РегиональныеЛьготы);
	
	Возврат Результат;
	
КонецФункции
 
#КонецОбласти
