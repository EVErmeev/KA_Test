
#Область ПрограммныйИнтерфейс

// Выполняет проверку используется ли для выбранных организаций настраиваемая отчетность.
// 
// Параметры:
//  ПараметрыОтчета - Структура - набор именованных параметров для определения вариантов заполнения отчета.
// 
// Возвращаемое значение:
//  Булево
Функция ИспользуетсяНастраиваемаяОтчетность(ПараметрыОтчета) Экспорт
	
	ПолныйСписокОрганизаций = Новый Массив;
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	
	ОсновнаяОрганизация = ПараметрыОтчета.Организация;
	
	Если ДоступныеОрганизации.Найти(ОсновнаяОрганизация) <> Неопределено Тогда
		ПолныйСписокОрганизаций.Добавить(ОсновнаяОрганизация);
	КонецЕсли;
	
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитикПовтИсп.ДействующиеПараметрыНалоговУчетныхПолитик("УчетнаяПолитикаБухУчета",
		ОсновнаяОрганизация,
		ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	
	Если ЗначениеЗаполнено(ПараметрыУчетнойПолитики.НастройкаЗаполненияБухОтчетности)
		И ПараметрыУчетнойПолитики.НастройкаЗаполненияБухОтчетности <> Справочники.НастройкиЗаполненияБухОтчетности.НеНастраивается Тогда
		
		Если ТипЗнч(ПараметрыОтчета) = Тип("Структура") Тогда
			ПараметрыОтчета.Вставить("НастройкаЗаполненияБухОтчетности", ПараметрыУчетнойПолитики.НастройкаЗаполненияБухОтчетности);
		КонецЕсли;
		
		// Для списка организаций с разными ИНН заполнение отчетности с использованием настроек недоступно
		Если ПараметрыОтчета.Свойство("ГруппаОрганизаций") И ПараметрыОтчета.ГруппаОрганизаций.Количество()>0 Тогда
			Для Каждого Организация Из ПараметрыОтчета.ГруппаОрганизаций Цикл
				Если ДоступныеОрганизации.Найти(Организация.Значение) <> Неопределено Тогда
					ПолныйСписокОрганизаций.Добавить(Организация.Значение);
				КонецЕсли;
			КонецЦикла;
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Организации.ИНН) КАК КоличествоИНН
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	Организации.Ссылка В (&Ссылки)";
			Запрос.УстановитьПараметр("Ссылки", ПолныйСписокОрганизаций);
	
			РезультатЗапроса = Запрос.Выполнить();
			
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			ВыборкаДетальныеЗаписи.Следующий();
			Если ВыборкаДетальныеЗаписи.КоличествоИНН>1 Тогда
				Возврат Ложь;
			Иначе 
				Возврат Истина;
			КонецЕсли;
		Иначе 
			Возврат Истина;
		КонецЕсли;
	Иначе 
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Заполняет РегламентированныйОтчетБухОтчетность по форме ФормаОтчета2019Кв1.
//
// Параметры:
//  ПараметрыОтчета - Структура - набор именованных параметров для определения вариантов заполнения отчета.
//  Контейнер - Структура - Показателей отчета.
//
Процедура ЗаполнитьОтчетБухОтчетностьФормаОтчета2019Кв1(ПараметрыОтчета, Контейнер) Экспорт
	
	Перем РассчитанныеПоказатели; // Таблица для сбора сумм показателей по организациям
	
	Если ПараметрыОтчета.Свойство("ТаблицаРасшифровки") И ЗначениеЗаполнено(ПараметрыОтчета.ТаблицаРасшифровки) Тогда
		ТаблицаРасшифровки = ПараметрыОтчета.ТаблицаРасшифровки;
	Иначе
		ТаблицаРасшифровки = Новый ТаблицаЗначений;
		ТаблицаРасшифровки.Колонки.Добавить("ИмяПоказателя");
		ТаблицаРасшифровки.Колонки.Добавить("НаименованиеПоказателя");
		ТаблицаРасшифровки.Колонки.Добавить("ЗнакОперации");
		ТаблицаРасшифровки.Колонки.Добавить("НаименованиеСлагаемого");
		ТаблицаРасшифровки.Колонки.Добавить("Сумма");
		ТаблицаРасшифровки.Колонки.Добавить("ИмяРаздела");
		ТаблицаРасшифровки.Колонки.Добавить("ДополнительныеПараметры");
	КонецЕсли;
	
	ЗаполнениеФормОтчетности = ЗаполнениеФормОтчетности(ПараметрыОтчета);
	// Очистка таблицы расшифровки от сведений заполняемых отчетов.
	Для Каждого Элемент Из ЗаполнениеФормОтчетности Цикл
		Если Элемент.Значение = Истина Тогда
			ЗаполнениеБухгалтерскойОтчетности.ОчиститьРазделРасшифровки(ТаблицаРасшифровки, Элемент.Ключ);
		КонецЕсли;
	КонецЦикла;
		
	РассчитанныеПоказатели = Новый ТаблицаЗначений;
	РассчитанныеПоказатели.Колонки.Добавить("Раздел");
	РассчитанныеПоказатели.Колонки.Добавить("Показатель");
	РассчитанныеПоказатели.Колонки.Добавить("Сумма");
	
	МногострочныеПоказатели = Новый Структура;
	
	// Вспомогательные значения.
	ОсновнаяОрганизация = ПараметрыОтчета.Организация;
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	
	ПолныйСписокОрганизаций = Новый Массив;
	
	Если ДоступныеОрганизации.Найти(ОсновнаяОрганизация) <> Неопределено Тогда
		ПолныйСписокОрганизаций.Добавить(ОсновнаяОрганизация);
	КонецЕсли;
	
	Если ПараметрыОтчета.Свойство("ГруппаОрганизаций") Тогда
		Для Каждого Организация Из ПараметрыОтчета.ГруппаОрганизаций Цикл
			Если ДоступныеОрганизации.Найти(Организация.Значение) <> Неопределено Тогда
				ПолныйСписокОрганизаций.Добавить(Организация.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаполнятьПредыдущиеПериоды = НЕ ЗаполнениеБухгалтерскойОтчетности.РасширенныйПериодОтчета(ПараметрыОтчета.мДатаНачалаПериодаОтчета);
		
	Для Каждого Организация Из ПолныйСписокОрганизаций Цикл
		
		КонтекстВыполнения = Новый Структура;
		КонтекстВыполнения.Вставить("Организация", Организация);
		КонтекстВыполнения.Вставить("ЗаполнениеФормОтчетности", ЗаполнениеФормОтчетности);
		КонтекстВыполнения.Вставить("ТаблицаРасшифровки", ТаблицаРасшифровки);
		КонтекстВыполнения.Вставить("РассчитанныеПоказатели", РассчитанныеПоказатели);
		КонтекстВыполнения.Вставить("МногострочныеПоказатели", Новый Структура);
		КонтекстВыполнения.Вставить("СтруктураОтчета", ПараметрыОтчета.СтруктураОтчета);
		КонтекстВыполнения.Вставить("КонецОтчетногоПериода", ПараметрыОтчета.мДатаКонцаПериодаОтчета);
		КонтекстВыполнения.Вставить("НастройкаЗаполненияБухОтчетности", ПараметрыОтчета.НастройкаЗаполненияБухОтчетности);
		КонтекстВыполнения.Вставить("ЗаполнятьПредыдущиеПериоды", ЗаполнятьПредыдущиеПериоды); 
		КонтекстВыполнения.Вставить("ЭтоГодовойОтчет", Контейнер.Баланс.ДатаКонцаПериодаОтчетаМесяц = 12);
		КонтекстВыполнения.Вставить("Данные", ПолучитьОбщиеДанныеДляЗаполнения(ПараметрыОтчета, КонтекстВыполнения));
		
		// Бухгалтерский баланс.
		Если ЗаполнениеФормОтчетности.Баланс Тогда
			
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);
			
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.Баланс);
			КонтекстОтчета.Вставить("ИмяРаздела", "Баланс");
			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);

			ДобавитьРегулярныеГрафыФормы(КонтекстВыполнения);
			
			ЗаполнитьФормуОтчетаПоНастройке(КонтекстВыполнения);
			
		КонецЕсли;
		
		// Отчет о финансовых результатах.
		Если ЗаполнениеФормОтчетности.ОФР Тогда
			
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);
			
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.ОФР);
			КонтекстОтчета.Вставить("ИмяРаздела", "ОФР");
			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);

			ДобавитьРегулярныеГрафыФормы(КонтекстВыполнения);
			
			ЗаполнитьФормуОтчетаПоНастройке(КонтекстВыполнения);
			
		КонецЕсли;
		
		// Отчет об изменениях капитала.
		Если ЗаполнениеФормОтчетности.ОИК Тогда
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);
			
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.ОИК);
			КонтекстОтчета.Вставить("ИмяРаздела", "ОИК");
			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);
			
			ЗаполнитьФормуОтчетаПоНастройке(КонтекстВыполнения);
			
			КонтекстВыполнения.Удалить("КонтекстОтчета");
			
		КонецЕсли;
		
		// Отчет о движении денежных средств.
		Если ЗаполнениеФормОтчетности.ОДДС Тогда
			
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);
			
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.ОДДС);
			КонтекстОтчета.Вставить("ИмяРаздела", "ОДДС");
			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);

			ДобавитьРегулярныеГрафыФормы(КонтекстВыполнения);
			
			ДополнитьДаннымиЗаполненияПоОДДС(КонтекстВыполнения, ПараметрыОтчета);
			
			ЗаполнитьФормуОтчетаПоНастройке(КонтекстВыполнения);

		КонецЕсли;
		
		// Отчет о целевом использовании средств
		Если ЗаполнениеФормОтчетности.ОЦИПС Тогда
			
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);
			
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.ОЦИС);
			КонтекстОтчета.Вставить("ИмяРаздела", "ОЦИС");
			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);

			ДобавитьРегулярныеГрафыФормы(КонтекстВыполнения);
			
			ЗаполнитьФормуОтчетаПоНастройке(КонтекстВыполнения);

		КонецЕсли;
		
		// Пояснения 1.
		Если ЗаполнениеФормОтчетности.Пояснения1 Тогда
			
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);
			
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.Пояснения1);
			КонтекстОтчета.Вставить("ИмяРаздела", "Пояснения1");
			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);
			
			ЗаполнитьФормуОтчетаПоНастройке(КонтекстВыполнения);
		КонецЕсли;
		
		// Пояснения 2.
		Если ЗаполнениеФормОтчетности.Пояснения2 Тогда
			
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);
			
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.Пояснения2);
			КонтекстОтчета.Вставить("ИмяРаздела", "Пояснения2");
			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);
			
			ЗаполнитьФормуОтчетаПоНастройке(КонтекстВыполнения);
			
		КонецЕсли;
		
		// Пояснения 4.
		Если ЗаполнениеФормОтчетности.Пояснения4 Тогда
				
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);
			
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.Пояснения4);
			КонтекстОтчета.Вставить("ИмяРаздела", "Пояснения4");
			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);
			
			ЗаполнитьФормуОтчетаПоНастройке(КонтекстВыполнения);

		КонецЕсли;

		// Пояснения 5.
		Если ЗаполнениеФормОтчетности.Пояснения5 Тогда
			
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);

			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.Пояснения5);
			КонтекстОтчета.Вставить("ИмяРаздела", "Пояснения5");

			ЗаполнитьФормуПояснения5(КонтекстВыполнения, ЗаполнятьПредыдущиеПериоды);

		КонецЕсли;
		
		// Пояснения 6.
		Если ЗаполнениеФормОтчетности.Пояснения6 Тогда
									
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);
			
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.Пояснения6);
			КонтекстОтчета.Вставить("ИмяРаздела", "Пояснения6");
			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);

			ДобавитьРегулярныеГрафыФормы(КонтекстВыполнения);
			
			ЗаполнитьФормуОтчетаПоНастройке(КонтекстВыполнения);

		КонецЕсли;
		
		// Пояснения 7.
		Если ЗаполнениеФормОтчетности.Пояснения7 Тогда
						
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);
			
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.Пояснения7);
			КонтекстОтчета.Вставить("ИмяРаздела", "Пояснения7");
			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);
			
			ЗаполнитьФормуОтчетаПоНастройке(КонтекстВыполнения);

		КонецЕсли;
		
		// Пояснения 8.
		Если ЗаполнениеФормОтчетности.Пояснения8 Тогда
						
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);
			
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.Пояснения8);
			КонтекстОтчета.Вставить("ИмяРаздела", "Пояснения8");
			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);

			ДобавитьРегулярныеГрафыФормы(КонтекстВыполнения);
			
			ЗаполнитьФормуОтчетаПоНастройке(КонтекстВыполнения);

		КонецЕсли;
		
		// Пояснения 9.
		Если ЗаполнениеФормОтчетности.Пояснения8 Тогда
						
			КонтекстОтчета = Новый Структура;
			КонтекстВыполнения.Вставить("КонтекстОтчета", КонтекстОтчета);
			
			КонтекстОтчета.Вставить("ФормаОтчетности", Перечисления.ФормыБухгалтерскойОтчетности.Пояснения9);
			КонтекстОтчета.Вставить("ИмяРаздела", "Пояснения9");
			КонтекстОтчета.Вставить("НачалоПериодаОтчета", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
			КонтекстОтчета.Вставить("КонецПериодаОтчета", ПараметрыОтчета.мДатаКонцаПериодаОтчета);
			
			ЗаполнитьФормуОтчетаПоНастройке(КонтекстВыполнения);

		КонецЕсли;
		
		Для Каждого ЭлементРаздел Из КонтекстВыполнения.МногострочныеПоказатели Цикл
			Раздел = ЭлементРаздел.Ключ;
			МногострочныеЧасти = ЭлементРаздел.Значение;
			
			Если НЕ МногострочныеПоказатели.Свойство(Раздел) Тогда
				МногострочныеПоказатели.Вставить(Раздел, Новый Структура);
			КонецЕсли;
			
			Для Каждого ЭлементМСЧ Из МногострочныеЧасти Цикл
				ИдентификаторМСЧ = ЭлементМСЧ.Ключ;
				МСЧ = ЭлементМСЧ.Значение;
				
				Если МногострочныеПоказатели.Свойство(ИдентификаторМСЧ) Тогда
					МногострочныеПоказатели[Раздел].Вставить(ИдентификаторМСЧ, МСЧ);
				Иначе
					МногострочныеПоказатели[Раздел].Вставить(ИдентификаторМСЧ, МСЧ);
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	РассчитанныеПоказатели.Свернуть("Раздел, Показатель", "Сумма");
	Для Каждого СтрокаПоказателя Из РассчитанныеПоказатели Цикл
		Контейнер[СтрокаПоказателя.Раздел].Вставить(СтрокаПоказателя.Показатель, СтрокаПоказателя.Сумма);
	КонецЦикла;

	Для Каждого ЭлементРаздел Из МногострочныеПоказатели Цикл
		Раздел = ЭлементРаздел.Ключ;
		МногострочныеЧасти = ЭлементРаздел.Значение;
		Для Каждого ЭлементМСЧ Из МногострочныеЧасти Цикл
			ИдентификаторМСЧ = ЭлементМСЧ.Ключ;
			МСЧ = ЭлементМСЧ.Значение; //ТаблицаЗначений
			СвернутьТаблицуЗначенийДопСтрок(МСЧ);
			
			СтруктураСтроки = ПараметрыОтчета.СтруктураМногострочныхЧастей[ИдентификаторМСЧ].Состав[0];
			ПараметрыОтчета.СтруктураМногострочныхЧастей[ИдентификаторМСЧ].Состав.Очистить();
			ПараметрыОтчета.СтруктураМногострочныхЧастей[ИдентификаторМСЧ].Состав.Добавить(СтруктураСтроки);
			
			Для Инд = 0 По МСЧ.Количество() - 1 Цикл
				
				Если Инд >= 1 Тогда
					ПараметрыОтчета.СтруктураМногострочныхЧастей[ИдентификаторМСЧ].Состав.Добавить(СтруктураСтроки);
				КонецЕсли;
				
				Постфикс = "_" + Формат(Инд + 1, "ЧГ=0");
				Для Каждого Колонка Из МСЧ.Колонки Цикл
					Показатель = Колонка.Имя + Постфикс;
					Контейнер[Раздел].Вставить(Показатель, МСЧ[Инд][Колонка.Имя]);
				КонецЦикла;
			КонецЦикла;
			
		КонецЦикла;
	КонецЦикла;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Контейнер", Контейнер);
	СтруктураДанных.Вставить("ТаблицаРасшифровки", ТаблицаРасшифровки);
	СтруктураДанных.Вставить("СтруктураМногострочныхЧастей", ПараметрыОтчета.СтруктураМногострочныхЧастей);
	
	ПоместитьВоВременноеХранилище(СтруктураДанных, ПараметрыОтчета.АдресВоВременномХранилище);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДополнитьСтруктураМогострочныхЧастей(ПараметрыОтчета, СтруктураМогострочныхЧастей) Экспорт
	
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитикПовтИсп.ДействующиеПараметрыНалоговУчетныхПолитик("УчетнаяПолитикаБухУчета",
		ПараметрыОтчета.Организация,
		ПараметрыОтчета.мДатаКонцаПериодаОтчета);

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтрокиБухОтчетности.ФормаОтчетности КАК ФормаОтчетности,
		|	СтрокиБухОтчетности.Наименование КАК Наименование,
		|	СтрокиБухОтчетности.КодСтроки КАК КодСтроки,
		|	СтрокиБухОтчетности.КодСтрокиПрошлогоПериода КАК КодСтрокиПрошлогоПериода,
		|	СтрокиБухОтчетности.ОсновнаяСтрока.КодСтроки КАК ОсновнаяСтрокаКодСтроки,
		|	СтрокиБухОтчетности.ОсновнаяСтрока.КодЯчейки КАК ОсновнаяСтрокаКодЯчейки
		|ИЗ
		|	Справочник.СтрокиБухОтчетности КАК СтрокиБухОтчетности
		|ГДЕ
		|	СтрокиБухОтчетности.Владелец = &Настройка
		|	И НЕ СтрокиБухОтчетности.Регламентированная
		|	И НЕ СтрокиБухОтчетности.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	КодСтроки
		|ИТОГИ ПО
		|	ФормаОтчетности,
		|	ОсновнаяСтрокаКодСтроки";
	
	Запрос.Параметры.Вставить("Настройка", ПараметрыУчетнойПолитики.НастройкаЗаполненияБухОтчетности);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаФорма = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаФорма.Следующий() Цикл
		
		Если ВыборкаФорма.ФормаОтчетности = Перечисления.ФормыБухгалтерскойОтчетности.Баланс 
			Или ВыборкаФорма.ФормаОтчетности = Перечисления.ФормыБухгалтерскойОтчетности.ОФР Тогда
			Префикс = "П100100";
			Постфикс = "02";
			ПостфиксНомера = "03";
		Иначе
			Префикс = "ДопСтрока";
			Постфикс = "01";
			ПостфиксНомера = "02";
		КонецЕсли;
		
		ВыборкаОсновнаяСтрокаКодСтроки = ВыборкаФорма.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаОсновнаяСтрокаКодСтроки.Следующий() Цикл
			
			Ключ = Префикс + ВыборкаОсновнаяСтрокаКодСтроки.ОсновнаяСтрокаКодСтроки;
			
			Показатели = Неопределено;
			
			СтруктураМогострочныхЧастей.Свойство(Ключ, Показатели);
			
			Если Показатели <> Неопределено Тогда
				
				ВыборкаДетальныеЗаписи = ВыборкаОсновнаяСтрокаКодСтроки.Выбрать();
				
				Для Сч=0 По ВыборкаДетальныеЗаписи.Количество()-1 Цикл
					
					ВыборкаДетальныеЗаписи.Следующий();
					
					Если Показатели.Состав.Количество()-1 >= Сч Тогда
						Показатели.Состав[Сч]["П100100"+ВыборкаДетальныеЗаписи.ОсновнаяСтрокаКодСтроки + Постфикс] = ВыборкаДетальныеЗаписи.Наименование;
						Показатели.Состав[Сч]["П100100"+ВыборкаДетальныеЗаписи.ОсновнаяСтрокаКодСтроки + ПостфиксНомера] = ВыборкаДетальныеЗаписи.КодСтроки;
					Иначе                                                   
						СтруктураПоказателя = Показатели.Состав[0];
						
						НоваяСтруктураПоказателя = Новый Структура;
						
						Для каждого Элемент Из СтруктураПоказателя Цикл
							НоваяСтруктураПоказателя.Вставить(Элемент.Ключ, Неопределено);
						КонецЦикла;
						НоваяСтруктураПоказателя["П100100"+ВыборкаДетальныеЗаписи.ОсновнаяСтрокаКодСтроки + Постфикс] = ВыборкаДетальныеЗаписи.Наименование;
						НоваяСтруктураПоказателя["П100100"+ВыборкаДетальныеЗаписи.ОсновнаяСтрокаКодСтроки + ПостфиксНомера] = ВыборкаДетальныеЗаписи.КодСтроки;

						Показатели.Состав.Вставить(Сч, НоваяСтруктураПоказателя);
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбщиеСубконтоСчетов(Счета, МаксКоличествоСубконто) Экспорт

	ДанныеСчетов = Новый Структура;
	ДанныеСчетов.Вставить("УчетПоПризнакамУчета", Неопределено);
	
	Для ИндексСубконто = 1 По МаксКоличествоСубконто Цикл
		ДанныеСчетов.Вставить("ВидСубконто" + ИндексСубконто,                   Неопределено);
		ДанныеСчетов.Вставить("ВидСубконто" + ИндексСубконто + "Наименование",  Неопределено);
		ДанныеСчетов.Вставить("ВидСубконто" + ИндексСубконто + "ТипЗначения",   Неопределено);
	КонецЦикла;
	
	Если Счета.Количество()>0 Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто,
			|	ХозрасчетныйВидыСубконто.НомерСтроки КАК НомерСтроки
			|ПОМЕСТИТЬ НомераСубконто
			|ИЗ
			|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
			|ГДЕ
			|	ХозрасчетныйВидыСубконто.Ссылка = &Счет
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ВидСубконто
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ХозрасчетныйВидыСубконто.Ссылка) КАК Ссылка
			|ПОМЕСТИТЬ ОбщиеСубконто
			|ИЗ
			|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
			|ГДЕ
			|	ХозрасчетныйВидыСубконто.Ссылка В(&Счета)
			|
			|СГРУППИРОВАТЬ ПО
			|	ХозрасчетныйВидыСубконто.ВидСубконто
			|
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ХозрасчетныйВидыСубконто.Ссылка) = &ЧислоСчетов
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ВидСубконто
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ОбщиеСубконто.ВидСубконто КАК ВидСубконто,
			|	ЕСТЬNULL(ОбщиеСубконто.ВидСубконто.Наименование, """") КАК Наименование,
			|	ОбщиеСубконто.ВидСубконто.ТипЗначения КАК ТипЗначения
			|ИЗ
			|	ОбщиеСубконто КАК ОбщиеСубконто
			|		ЛЕВОЕ СОЕДИНЕНИЕ НомераСубконто КАК НомераСубконто
			|		ПО ОбщиеСубконто.ВидСубконто = НомераСубконто.ВидСубконто
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомераСубконто.НомерСтроки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МИНИМУМ(Хозрасчетный.УчетПоПодразделениям) КАК УчетПоПодразделениям,
			|	МИНИМУМ(Хозрасчетный.УчетПоНаправлениямДеятельности) КАК УчетПоНаправлениямДеятельности
			|ИЗ
			|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
			|ГДЕ
			|	Хозрасчетный.Ссылка В(&Счета)
			|
			|ИМЕЮЩИЕ
			|	НЕ МИНИМУМ(Хозрасчетный.УчетПоПодразделениям) ЕСТЬ NULL";
		
		Запрос.УстановитьПараметр("Счета", Счета);
		Запрос.УстановитьПараметр("Счет", Счета[0]);
		Запрос.УстановитьПараметр("ЧислоСчетов", Счета.Количество());
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ВыборкаВидыСубконто = РезультатЗапроса[2].Выбрать();
		
		ИндексСубконто = 0;
		Пока ВыборкаВидыСубконто.Следующий() Цикл
			ИндексСубконто = ИндексСубконто + 1;
			ТипСубконто = ?(ВыборкаВидыСубконто.ТипЗначения = NULL, Новый ОписаниеТипов("Неопределено"), ВыборкаВидыСубконто.ТипЗначения);
			
			ДанныеСчетов.Вставить("ВидСубконто" + ИндексСубконто,                   ВыборкаВидыСубконто.ВидСубконто);
			ДанныеСчетов.Вставить("ВидСубконто" + ИндексСубконто + "Наименование",  ВыборкаВидыСубконто.Наименование);
			ДанныеСчетов.Вставить("ВидСубконто" + ИндексСубконто + "ТипЗначения",   ТипСубконто);
		КонецЦикла;
		
		УчетПоНаправлениямПодразделениям = РезультатЗапроса[3].Выбрать();
		Если УчетПоНаправлениямПодразделениям.Следующий() Тогда
			УчетПоПризнакамУчета = Новый Структура;
			
			УчетПоПризнакамУчета.Вставить("Подразделение"           , УчетПоНаправлениямПодразделениям.УчетПоПодразделениям);
			УчетПоПризнакамУчета.Вставить("НаправлениеДеятельности" , УчетПоНаправлениямПодразделениям.УчетПоНаправлениямДеятельности);
			
			ДанныеСчетов.Вставить("УчетПоПризнакамУчета", УчетПоПризнакамУчета);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеСчетов;

КонецФункции

#Область ЗаполнениеПоНастройке

Процедура ЗаполнитьФормуОтчетаПоНастройке (КонтекстВыполнения)
	
	ПравилаЗаполнения = ПолучитьПакетыПравилЗаполнения(КонтекстВыполнения);
	
	Если ПравилаЗаполнения <> Неопределено Тогда
		
		СчетчикДопСтрок = Новый Структура;
		
		СтрокиОтчетности = ПравилаЗаполнения[2].Выгрузить();
		
		ОбщиеПравила = ПравилаЗаполнения[3].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
		Для Каждого СтрокаОтчетности Из СтрокиОтчетности Цикл
			
			ПравилаСтроки = ОбщиеПравила.Строки.Найти(СтрокаОтчетности.КодСтроки, "КодСтроки");
			
			Если ПравилаСтроки <> Неопределено Тогда
				
				ПараметрыПоказателя = ПолучитьПараметрыПоказателя();
				ПараметрыПоказателя.КодСтроки = СтрокаОтчетности.КодСтроки;
				ПараметрыПоказателя.ДопАлгоритмСтроки = СтрокаОтчетности.ДопАлгоритмСтроки;
				
				Если Не СтрокаОтчетности.Регламентированная Тогда
					КодОсновнойСтроки = "П"+СтрокаОтчетности.КодОсновнойСтроки;
					Если СчетчикДопСтрок.Свойство(КодОсновнойСтроки) Тогда
						СчетчикДопСтрок[КодОсновнойСтроки] = СчетчикДопСтрок[КодОсновнойСтроки] + 1;
					Иначе
						СчетчикДопСтрок.Вставить(КодОсновнойСтроки,1);
					КонецЕсли;
					
					ПараметрыПоказателя.НомерДопСтроки = СчетчикДопСтрок[КодОсновнойСтроки];
				КонецЕсли;
				
				ЗаполнитьГрафыПоСтроке(КонтекстВыполнения, 
					КонтекстВыполнения.Данные, 
					ПравилаСтроки.Строки,
					ПараметрыПоказателя);
					
				Если ЗначениеЗаполнено(СтрокаОтчетности.КодСтрокиПрошлогоПериода) Тогда
					
					ПараметрыПоказателя.ПрошлыйПериод = Истина;
					ПараметрыПоказателя.КодСтроки = СтрокаОтчетности.КодСтроки;
					ПараметрыПоказателя.КодСтрокиПрошлогоПериода = СтрокаОтчетности.КодСтрокиПрошлогоПериода;
					ЗаполнитьГрафыПоСтроке(КонтекстВыполнения,
						КонтекстВыполнения.Данные, 
						ПравилаСтроки.Строки,
						ПараметрыПоказателя);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПакетыПравилЗаполнения(КонтекстВыполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтрокиБухОтчетности.Ссылка КАК Строка,
		|	СтрокиБухОтчетности.КодСтроки КАК КодСтроки,
		|	СтрокиБухОтчетности.КодСтрокиПрошлогоПериода КАК КодСтрокиПрошлогоПериода,
		|	СтрокиБухОтчетности.КодЯчейки КАК КодЯчейки,
		|	СтрокиБухОтчетности.КодЯчейкиПрошлогоПериода КАК КодЯчейкиПрошлогоПериода,
		|	СтрокиБухОтчетности.НаименованиеРасшифровки КАК СтрокаНаименованиеРасшифровки,
		|	СтрокиБухОтчетности.ДопАлгоритм КАК ДопАлгоритмСтроки,
		|	СтрокиБухОтчетности.Регламентированная КАК Регламентированная,
		|	СтрокиБухОтчетности.ОсновнаяСтрока.КодСтроки КАК КодОсновнойСтроки,
		|	NULL КАК Графа,
		|	NULL КАК КодГрафы,
		|	NULL КАК ГрафаНаименование,
		|	0 КАК КодГода,
		|	NULL КАК ДопАлгоритмГрафы,
		|	АлгоритмыСбораДанныхБухОтчетности.Ссылка КАК АлгоритмСбораДанных,
		|	АлгоритмыСбораДанныхБухОтчетности.ОтборЗадан КАК ОтборЗадан,
		|	АлгоритмыСбораДанныхБухОтчетности.ТипПоказателя КАК ТипПоказателя,
		|	АлгоритмыСбораДанныхБухОтчетности.Вычитаемый КАК Вычитаемый,
		|	АлгоритмыСбораДанныхБухОтчетности.НастройкиОтбора КАК НастройкиОтбора,
		|	АлгоритмыСбораДанныхБухОтчетности.ВидДвиженияДенежныхСредств КАК ВидДвиженияДенежныхСредств,
		|	АлгоритмыСбораДанныхБухОтчетности.НаименованиеРасшифровки КАК АлгоритмНаименованиеРасшифровки
		|ПОМЕСТИТЬ Строки
		|ИЗ
		|	Справочник.АлгоритмыСбораДанныхБухОтчетности КАК АлгоритмыСбораДанныхБухОтчетности
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтрокиБухОтчетности КАК СтрокиБухОтчетности
		|		ПО АлгоритмыСбораДанныхБухОтчетности.Владелец = СтрокиБухОтчетности.Ссылка
		|ГДЕ
		|	СтрокиБухОтчетности.Владелец = &Настройка
		|	И СтрокиБухОтчетности.ФормаОтчетности = &ФормаОтчетности
		|	И НЕ АлгоритмыСбораДанныхБухОтчетности.ПометкаУдаления
		|	И НЕ СтрокиБухОтчетности.ПометкаУдаления
		|	И НЕ СтрокиБухОтчетности.ЕстьГрафы
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СтрокиБухОтчетности.Ссылка,
		|	СтрокиБухОтчетности.КодСтроки,
		|	СтрокиБухОтчетности.КодСтрокиПрошлогоПериода,
		|	СтрокиБухОтчетности.КодЯчейки,
		|	СтрокиБухОтчетности.КодЯчейкиПрошлогоПериода,
		|	СтрокиБухОтчетности.НаименованиеРасшифровки,
		|	СтрокиБухОтчетности.ДопАлгоритм,
		|	СтрокиБухОтчетности.Регламентированная,
		|	СтрокиБухОтчетности.ОсновнаяСтрока.КодСтроки,
		|	ГрафыБухОтчетности.Ссылка,
		|	ГрафыБухОтчетности.КодГрафы,
		|	ГрафыБухОтчетности.Наименование,
		|	ГрафыБухОтчетности.КодГода,
		|	ГрафыБухОтчетности.ДопАлгоритм,
		|	АлгоритмыСбораДанныхБухОтчетности.Ссылка,
		|	АлгоритмыСбораДанныхБухОтчетности.ОтборЗадан,
		|	АлгоритмыСбораДанныхБухОтчетности.ТипПоказателя,
		|	АлгоритмыСбораДанныхБухОтчетности.Вычитаемый,
		|	АлгоритмыСбораДанныхБухОтчетности.НастройкиОтбора,
		|	АлгоритмыСбораДанныхБухОтчетности.ВидДвиженияДенежныхСредств,
		|	АлгоритмыСбораДанныхБухОтчетности.НаименованиеРасшифровки
		|ИЗ
		|	Справочник.СтрокиБухОтчетности КАК СтрокиБухОтчетности
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГрафыБухОтчетности КАК ГрафыБухОтчетности
		|		ПО (ГрафыБухОтчетности.Владелец = СтрокиБухОтчетности.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.АлгоритмыСбораДанныхБухОтчетности КАК АлгоритмыСбораДанныхБухОтчетности
		|		ПО (АлгоритмыСбораДанныхБухОтчетности.Владелец = ГрафыБухОтчетности.Ссылка)
		|ГДЕ
		|	СтрокиБухОтчетности.Владелец = &Настройка
		|	И СтрокиБухОтчетности.ФормаОтчетности = &ФормаОтчетности
		|	И СтрокиБухОтчетности.ЕстьГрафы
		|	И НЕ ГрафыБухОтчетности.ПометкаУдаления
		|	И НЕ СтрокиБухОтчетности.ПометкаУдаления
		|	И НЕ АлгоритмыСбораДанныхБухОтчетности.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СтрокиБухОтчетности.Ссылка,
		|	СтрокиБухОтчетности.КодСтроки,
		|	СтрокиБухОтчетности.КодСтрокиПрошлогоПериода,
		|	СтрокиБухОтчетности.КодЯчейки,
		|	СтрокиБухОтчетности.КодЯчейкиПрошлогоПериода,
		|	СтрокиБухОтчетности.НаименованиеРасшифровки,
		|	СтрокиБухОтчетности.ДопАлгоритм,
		|	СтрокиБухОтчетности.Регламентированная,
		|	СтрокиБухОтчетности.ОсновнаяСтрока.КодСтроки,
		|	ГрафыБухОтчетности.Ссылка,
		|	ГрафыБухОтчетности.КодГрафы,
		|	ГрафыБухОтчетности.Наименование,
		|	ГрафыБухОтчетности.КодГода,
		|	ГрафыБухОтчетности.ДопАлгоритм,
		|	АлгоритмыСбораДанныхБухОтчетности.Ссылка,
		|	АлгоритмыСбораДанныхБухОтчетности.ОтборЗадан,
		|	АлгоритмыСбораДанныхБухОтчетности.ТипПоказателя,
		|	АлгоритмыСбораДанныхБухОтчетности.Вычитаемый,
		|	АлгоритмыСбораДанныхБухОтчетности.НастройкиОтбора,
		|	АлгоритмыСбораДанныхБухОтчетности.ВидДвиженияДенежныхСредств,
		|	АлгоритмыСбораДанныхБухОтчетности.НаименованиеРасшифровки
		|ИЗ
		|	Справочник.СтрокиБухОтчетности КАК СтрокиБухОтчетности
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГрафыБухОтчетности КАК ГрафыБухОтчетности
		|		ПО (ГрафыБухОтчетности.Владелец = СтрокиБухОтчетности.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.АлгоритмыСбораДанныхБухОтчетности КАК АлгоритмыСбораДанныхБухОтчетности
		|		ПО (АлгоритмыСбораДанныхБухОтчетности.Владелец = ГрафыБухОтчетности.Ссылка)
		|ГДЕ
		|	СтрокиБухОтчетности.Владелец = &Настройка
		|	И СтрокиБухОтчетности.ФормаОтчетности = &ФормаОтчетности
		|	И АлгоритмыСбораДанныхБухОтчетности.Ссылка ЕСТЬ NULL
		|	И НЕ ГрафыБухОтчетности.ПометкаУдаления
		|	И НЕ СтрокиБухОтчетности.ПометкаУдаления
		|	И СтрокиБухОтчетности.ДопАлгоритм <> """"
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АлгоритмСбораДанных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Строки.Строка КАК Строка,
		|	Строки.АлгоритмСбораДанных КАК АлгоритмСбораДанных,
		|	АлгоритмыСбораДанныхБухОтчетностиСчета.Счет КАК Счет,
		|	АлгоритмыСбораДанныхБухОтчетностиСчета.Счет.Код КАК СчетКод,
		|	Строки.Графа КАК Графа,
		|	Строки.КодСтроки КАК КодСтроки,
		|	Строки.КодГрафы КАК КодГрафы,
		|	Строки.СтрокаНаименованиеРасшифровки КАК СтрокаНаименованиеРасшифровки,
		|	Строки.ТипПоказателя КАК ТипПоказателя,
		|	Строки.Вычитаемый КАК Вычитаемый,
		|	Строки.ДопАлгоритмСтроки КАК ДопАлгоритмСтроки,
		|	Строки.ДопАлгоритмГрафы КАК ДопАлгоритмГрафы,
		|	АлгоритмыСбораДанныхБухОтчетностиКорСчета.Счет КАК КорСчет,
		|	АлгоритмыСбораДанныхБухОтчетностиКорСчета.Счет.Код КАК КорСчетКод,
		|	Строки.КодСтрокиПрошлогоПериода КАК КодСтрокиПрошлогоПериода,
		|	Строки.КодГода КАК КодГода,
		|	Строки.ОтборЗадан КАК ОтборЗадан,
		|	Строки.НастройкиОтбора КАК НастройкиОтбора,
		|	Строки.Регламентированная КАК Регламентированная,
		|	Строки.КодЯчейки КАК КодЯчейки,
		|	Строки.КодЯчейкиПрошлогоПериода КАК КодЯчейкиПрошлогоПериода,
		|	Строки.ВидДвиженияДенежныхСредств КАК ВидДвиженияДенежныхСредств,
		|	Строки.ГрафаНаименование КАК ГрафаНаименование,
		|	Строки.АлгоритмНаименованиеРасшифровки КАК АлгоритмНаименованиеРасшифровки,
		|	Строки.КодОсновнойСтроки КАК КодОсновнойСтроки
		|ПОМЕСТИТЬ ОбщиеПравила
		|ИЗ
		|	Строки КАК Строки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.АлгоритмыСбораДанныхБухОтчетности.Счета КАК АлгоритмыСбораДанныхБухОтчетностиСчета
		|		ПО (АлгоритмыСбораДанныхБухОтчетностиСчета.Ссылка = Строки.АлгоритмСбораДанных)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.АлгоритмыСбораДанныхБухОтчетности.КорСчета КАК АлгоритмыСбораДанныхБухОтчетностиКорСчета
		|		ПО (АлгоритмыСбораДанныхБухОтчетностиКорСчета.Ссылка = Строки.АлгоритмСбораДанных)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбщиеПравила.Строка КАК Строка,
		|	ОбщиеПравила.КодСтроки КАК КодСтроки,
		|	ОбщиеПравила.КодСтрокиПрошлогоПериода КАК КодСтрокиПрошлогоПериода,
		|	ОбщиеПравила.ДопАлгоритмСтроки КАК ДопАлгоритмСтроки,
		|	ОбщиеПравила.Регламентированная КАК Регламентированная,
		|	ОбщиеПравила.КодОсновнойСтроки КАК КодОсновнойСтроки
		|ИЗ
		|	ОбщиеПравила КАК ОбщиеПравила
		|
		|УПОРЯДОЧИТЬ ПО
		|	КодСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбщиеПравила.Строка КАК Строка,
		|	ОбщиеПравила.АлгоритмСбораДанных КАК АлгоритмСбораДанных,
		|	ОбщиеПравила.Счет КАК Счет,
		|	ОбщиеПравила.СчетКод КАК СчетКод,
		|	ОбщиеПравила.Графа КАК Графа,
		|	ОбщиеПравила.КодСтроки КАК КодСтроки,
		|	ОбщиеПравила.КодГрафы КАК КодГрафы,
		|	ОбщиеПравила.СтрокаНаименованиеРасшифровки КАК СтрокаНаименованиеРасшифровки,
		|	ОбщиеПравила.ТипПоказателя КАК ТипПоказателя,
		|	ОбщиеПравила.Вычитаемый КАК Вычитаемый,
		|	ОбщиеПравила.ДопАлгоритмСтроки КАК ДопАлгоритмСтроки,
		|	ОбщиеПравила.ДопАлгоритмГрафы КАК ДопАлгоритмГрафы,
		|	ОбщиеПравила.КорСчет КАК КорСчет,
		|	ОбщиеПравила.КорСчетКод КАК КорСчетКод,
		|	ОбщиеПравила.КодСтрокиПрошлогоПериода КАК КодСтрокиПрошлогоПериода,
		|	ОбщиеПравила.КодГода КАК КодГода,
		|	ОбщиеПравила.ОтборЗадан КАК ОтборЗадан,
		|	ОбщиеПравила.НастройкиОтбора КАК НастройкиОтбора,
		|	ОбщиеПравила.Регламентированная КАК Регламентированная,
		|	ОбщиеПравила.КодЯчейки КАК КодЯчейки,
		|	ОбщиеПравила.КодЯчейкиПрошлогоПериода КАК КодЯчейкиПрошлогоПериода,
		|	ОбщиеПравила.ВидДвиженияДенежныхСредств КАК ВидДвиженияДенежныхСредств,
		|	ОбщиеПравила.ГрафаНаименование КАК ГрафаНаименование,
		|	ОбщиеПравила.АлгоритмНаименованиеРасшифровки КАК АлгоритмНаименованиеРасшифровки
		|ИЗ
		|	ОбщиеПравила КАК ОбщиеПравила
		|
		|УПОРЯДОЧИТЬ ПО
		|	КодСтроки,
		|	КодГрафы,
		|	СчетКод
		|ИТОГИ ПО
		|	КодСтроки,
		|	КодГрафы,
		|	АлгоритмСбораДанных";
	
	Запрос.УстановитьПараметр("Настройка", КонтекстВыполнения.НастройкаЗаполненияБухОтчетности);
	Запрос.УстановитьПараметр("ФормаОтчетности", КонтекстВыполнения.КонтекстОтчета.ФормаОтчетности);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Возврат РезультатЗапроса;
	
КонецФункции

Процедура ЗаполнитьГрафыПоСтроке(КонтекстВыполнения, Данные, Графы, ПараметрыПоказателя)

	//Проверка предусмотрен ли для строки отдельный алгоритм заполнения
	Если СтрокаЗаполняетсяСтандартно(ПараметрыПоказателя.ДопАлгоритмСтроки) Тогда
		
		Для Каждого Графа Из Графы Цикл
			
			АлгоритмСбораДанных = Графа.Строки.Получить(0).АлгоритмСбораДанных;
			Если ЗначениеЗаполнено(АлгоритмСбораДанных) Тогда
				
				Если КонтекстВыполнения.КонтекстОтчета.Свойство("РегулярныеГрафы") Тогда
					
					РегулярныеГрафы = КонтекстВыполнения.КонтекстОтчета.РегулярныеГрафы;
					
					Для Каждого РегулярнаяГрафа Из РегулярныеГрафы Цикл
						
						ПараметрыПоказателя.РегулярнаяГрафа = РегулярнаяГрафа;
						
						ЗаполнитьПоказательПоАлгоритмам(КонтекстВыполнения, Данные, Графа.Строки,  ПараметрыПоказателя);
					КонецЦикла;
				Иначе
					ЗаполнитьПоказательПоАлгоритмам(КонтекстВыполнения, Данные, Графа.Строки, ПараметрыПоказателя);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Иначе 
		ПрименитьДопАлгоритм(КонтекстВыполнения, ПараметрыПоказателя);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоказательПоАлгоритмам(КонтекстВыполнения, Данные, Алгоритмы, ПараметрыПоказателя)
	
	ЗначениеПоказателя = 0;
	
	ТаблицаРасшифровки = КонтекстВыполнения.ТаблицаРасшифровки;
	РассчитанныеПоказатели = КонтекстВыполнения.РассчитанныеПоказатели;
	КонтекстОтчета = КонтекстВыполнения.КонтекстОтчета;
	
	ПараметрыРасшифровки = Новый Структура;
	ПараметрыРасшифровки.Вставить("Организация", КонтекстВыполнения.Организация);
	
	Для Каждого Алгоритм Из Алгоритмы Цикл
		
		АлгоритмСтрока = Алгоритм.Строки.Получить(0);
		Если ПараметрыПоказателя.ПрошлыйПериод = Истина Тогда
			КодГода = -1;
		Иначе
			Если ПараметрыПоказателя.РегулярнаяГрафа <> Неопределено Тогда
				Для Каждого Период Из ПараметрыПоказателя.РегулярнаяГрафа.Значение Цикл
					КодГода = Период.Значение;
				КонецЦикла;
			Иначе	
				КодГода = АлгоритмСтрока.КодГода;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(АлгоритмСтрока.КодГрафы) Тогда
			КодГрафы = АлгоритмСтрока.КодГрафы;
			ГрафаНаименование = ЗаполнитьПараметризируемыеНаименования(АлгоритмСтрока.ГрафаНаименование, КонтекстОтчета);
		Иначе
			КодГрафы = ПараметрыПоказателя.РегулярнаяГрафа.Ключ;
			Для Каждого РегулярнаяГрафа Из ПараметрыПоказателя.РегулярнаяГрафа.Значение Цикл
				ГрафаНаименование = РегулярнаяГрафа.Ключ; 
			КонецЦикла;
		КонецЕсли;
		
		Если ПараметрыПоказателя.КодСтрокиПрошлогоПериода <> "" Тогда
			ИмяОбластиПоказателя = АлгоритмСтрока.КодЯчейкиПрошлогоПериода + КодГрафы;
		Иначе
			ИмяОбластиПоказателя = АлгоритмСтрока.КодЯчейки + КодГрафы;
		КонецЕсли;
		
		Если ПараметрыПоказателя.НомерДопСтроки <> 0 Тогда
			ИмяОбластиПоказателя = ИмяОбластиПоказателя + "_" + ПараметрыПоказателя.НомерДопСтроки;
		КонецЕсли;
		
		ПараметрыПоказателя.КодГрафы = КодГрафы;
		ПараметрыПоказателя.ИмяОбластиПоказателя = ИмяОбластиПоказателя;
		
		ПараметрыПоказателя.ДопАлгоритмГрафы = АлгоритмСтрока.ДопАлгоритмГрафы;
		ПараметрыПоказателя.КодГода = КодГода;
		Если ПараметрыПоказателя.НаименованиеСтроки = "" Тогда
			ПараметрыПоказателя.НаименованиеСтроки = АлгоритмСтрока.СтрокаНаименованиеРасшифровки;
		КонецЕсли;
		
		Если НЕ КонтекстВыполнения.ЭтоГодовойОтчет 
			И КодГода < 0
			И НЕ (КонтекстОтчета.ФормаОтчетности = Перечисления.ФормыБухгалтерскойОтчетности.ОФР
				ИЛИ КонтекстОтчета.ФормаОтчетности = Перечисления.ФормыБухгалтерскойОтчетности.ОДДС
				ИЛИ КонтекстОтчета.ФормаОтчетности = Перечисления.ФормыБухгалтерскойОтчетности.ОЦИС) Тогда
			ПараметрыПоказателя.НачалоПериода = НачалоГода(ДобавитьМесяц(КонтекстОтчета.НачалоПериодаОтчета, 12*КодГода));
			ПараметрыПоказателя.КонецПериода = КонецГода(ДобавитьМесяц(КонтекстОтчета.КонецПериодаОтчета, 12*КодГода));
		Иначе
			ПараметрыПоказателя.НачалоПериода = ДобавитьМесяц(КонтекстОтчета.НачалоПериодаОтчета, 12*КодГода);
			ПараметрыПоказателя.КонецПериода = ДобавитьМесяц(КонтекстОтчета.КонецПериодаОтчета, 12*КодГода);
		КонецЕсли;
		
		//Проверка заполняется ли показатель через типовой механизм
		Если ПоказательЗаполняетсяСтандартно(ПараметрыПоказателя.ДопАлгоритмСтроки) Тогда
			
			ТекДанные = ДанныеДляРасчетаПоказателя(КонтекстВыполнения,
				Данные, 
				АлгоритмСтрока,
				ОбщегоНазначенияКлиентСервер.СвернутьМассив(Алгоритм.Строки.ВыгрузитьКолонку("Счет")),
				ОбщегоНазначенияКлиентСервер.СвернутьМассив(Алгоритм.Строки.ВыгрузитьКолонку("КорСчет")),
				ПараметрыПоказателя);
				
			Если ТипЗнч(ТекДанные) = Тип("Структура") Тогда
				ТаблицаДанных = ТекДанные.ТаблицаДанных;
				ОтборПоАналитике = ТекДанные.ОтборПоАналитике;
			Иначе
				ТаблицаДанных = ТекДанные;
				ОтборПоАналитике = Неопределено;
			КонецЕсли;

			ЗначениеСлагаемогоАлгоритм = 0;
			Счета = Новый Массив;
			КорСчета = Новый Массив;
			
			Для Каждого СтрокаСчет Из Алгоритм.Строки Цикл
				Счета.Добавить(СтрокаСчет.Счет);
				КорСчета.Добавить(СтрокаСчет.КорСчет);
				
				ЗначениеСлагаемого = ?(СтрокаСчет.Вычитаемый, -1, 1) * ВычислитьПоказательПоТипу(СтрокаСчет.ТипПоказателя, ТаблицаДанных, СтрокаСчет.Счет, СтрокаСчет.КорСчет);
				ЗначениеПоказателя = ЗначениеПоказателя + ЗначениеСлагаемого;
				ЗначениеСлагаемогоАлгоритм = ЗначениеСлагаемогоАлгоритм + ЗначениеСлагаемого;
			КонецЦикла;
			
			Счета = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Счета);
			КорСчета = ОбщегоНазначенияКлиентСервер.СвернутьМассив(КорСчета);
								
			ПараметрыРасшифровки.Вставить("НачалоПериода", ПараметрыПоказателя.НачалоПериода);
			ПараметрыРасшифровки.Вставить("КонецПериода", ПараметрыПоказателя.КонецПериода);
			Если АлгоритмСтрока.ТипПоказателя = "СКДР" ИЛИ АлгоритмСтрока.ТипПоказателя = "СККР" Тогда
				ПараметрыРасшифровки.Вставить("РазвернутоеСальдо", Истина);
			КонецЕсли;
			
			РасшифровкаЗаголовок = НСтр("ru = 'Строка %1, графа ""%2""'");
			РасшифровкаЗаголовок = СтрШаблон(РасшифровкаЗаголовок, 
				ЗаполнитьПараметризируемыеНаименования(СтрокаСчет.СтрокаНаименованиеРасшифровки, КонтекстОтчета),
				ГрафаНаименование);
				
			НастройкиРасшифровки = Неопределено;
			Если АлгоритмСтрока.ТипПоказателя = "ОБ" Тогда 
				Если Счета.Количество() = 1 Тогда
					НастройкиРасшифровки = НастройкиАнализаСчета(ПараметрыРасшифровки, Счета[0], КорСчета, ОтборПоАналитике);
				ИначеЕсли КорСчета.Количество() = 1 Тогда
					НастройкиРасшифровки = НастройкиАнализаСчета(ПараметрыРасшифровки, КорСчета[0], Счета, ОтборПоАналитике);
				КонецЕсли;	
			Иначе
				Если Счета.Количество() = 1 Тогда
					НастройкиРасшифровки = НастройкиОСВПоСчету(ПараметрыРасшифровки, Счета[0], ОтборПоАналитике);
				ИначеЕсли ОтборПоАналитике = Неопределено Тогда
					НастройкиРасшифровки = НастройкиОСВ(ПараметрыРасшифровки, Счета);
				КонецЕсли;
			КонецЕсли;
			
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
				ИмяОбластиПоказателя,
				РасшифровкаЗаголовок,
				?(АлгоритмСтрока.Вычитаемый, "-", "+"),
				АлгоритмСтрока.АлгоритмНаименованиеРасшифровки,
				ЗначениеСлагаемогоАлгоритм,
				КонтекстОтчета.ИмяРаздела,
				НастройкиРасшифровки);

		Иначе
			ВычислитьПоказательСДопАлгоритмом(КонтекстВыполнения, ПараметрыПоказателя, Алгоритм, ЗначениеПоказателя);
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыПоказателя.ЗначениеПоказателя = ЗначениеПоказателя;

	ПрименитьДопАлгоритм(КонтекстВыполнения, ПараметрыПоказателя);
	
	УстановитьЗначениеПоказателя(РассчитанныеПоказатели,
		КонтекстОтчета.ИмяРаздела,
		ПараметрыПоказателя.ИмяОбластиПоказателя,
		ПараметрыПоказателя.ЗначениеПоказателя);
	
КонецПроцедуры

Функция ДанныеДляРасчетаПоказателя(КонтекстВыполнения, Данные, Алгоритм, Счета, КорСчета, ПараметрыПоказателя)
	
	ДанныеГод = Данные.Получить(ПараметрыПоказателя.КонецПериода);
	
	ВидМакета = ПолучитьВидМакета(Алгоритм.ТипПоказателя, ПараметрыПоказателя);
	ИмяПакетаДанных = ВидМакета;
	
	Если ДопАлгоритмОтбораПоДокументам(ПараметрыПоказателя.ДопАлгоритмСтроки) Тогда
		
		ИмяПакетаДанных = ВидМакета + ПараметрыПоказателя.ДопАлгоритмСтроки; 
		
		Если Не ДанныеГод.Свойство(ИмяПакетаДанных) И Не Алгоритм.ОтборЗадан Тогда
			
			Параметры = Новый Структура;
			Параметры.Вставить("Организация", КонтекстВыполнения.Организация);
			Параметры.Вставить("НачалоПериода", ПараметрыПоказателя.НачалоПериода);
			Параметры.Вставить("КонецПериода", ПараметрыПоказателя.КонецПериода);
			Параметры.Вставить("ДопАлгоритм", ПараметрыПоказателя.ДопАлгоритмСтроки);
		
			ТекДанные = ПолучитьДанныеЗаполнения(ВидМакета, Параметры);
			
			ДанныеГод.Вставить(ИмяПакетаДанных, ТекДанные.ТаблицаДанных);
			
		КонецЕсли;
	КонецЕсли;

	Если Алгоритм.ОтборЗадан Тогда
		Параметры = Новый Структура;
		Параметры.Вставить("Организация", КонтекстВыполнения.Организация);
		Параметры.Вставить("НачалоПериода", ПараметрыПоказателя.НачалоПериода);
		Параметры.Вставить("КонецПериода", ПараметрыПоказателя.КонецПериода);
		Параметры.Вставить("Счета", Счета);
		Параметры.Вставить("КорСчета", КорСчета);
		Параметры.Вставить("НастройкиОтбора", Алгоритм.НастройкиОтбора);
		Параметры.Вставить("ДопАлгоритм", ПараметрыПоказателя.ДопАлгоритмСтроки);
		
		Возврат ПолучитьДанныеЗаполнения(ВидМакета, Параметры);
		
	КонецЕсли;
	
	Возврат ДанныеГод[ИмяПакетаДанных];

КонецФункции

Процедура ВычислитьПоказательСДопАлгоритмом(КонтекстВыполнения, ПараметрыПоказателя, Алгоритм, ЗначениеПоказателя)
	
	Если ПараметрыПоказателя.ДопАлгоритмСтроки = "ОДДС" Тогда
		ЗаполнитьПоказателиОДДС(КонтекстВыполнения, ПараметрыПоказателя, Алгоритм, ЗначениеПоказателя);
	ИначеЕсли ПараметрыПоказателя.ДопАлгоритмСтроки = "ОДДС_Агентские" Тогда
		ЗаполнитьПоказателиОДДС_Агентские(КонтекстВыполнения, ПараметрыПоказателя, Алгоритм, ЗначениеПоказателя)
	ИначеЕсли ПараметрыПоказателя.ДопАлгоритмСтроки = "ОДДС_НДС" Тогда
		ЗаполнитьПоказателиОДДС_НДС(КонтекстВыполнения, ПараметрыПоказателя, Алгоритм, ЗначениеПоказателя)
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрименитьДопАлгоритм(КонтекстВыполнения, ПараметрыПоказателя)

	Если ПараметрыПоказателя.ДопАлгоритмГрафы = ">0" Тогда
		ПараметрыПоказателя.ЗначениеПоказателя = Макс(ПараметрыПоказателя.ЗначениеПоказателя,0);	
	ИначеЕсли ПараметрыПоказателя.ДопАлгоритмГрафы = "<0" Тогда
		ПараметрыПоказателя.ЗначениеПоказателя = Мин(ПараметрыПоказателя.ЗначениеПоказателя,0);
	ИначеЕсли ПараметрыПоказателя.ДопАлгоритмГрафы = "-" Тогда
		ПараметрыПоказателя.ЗначениеПоказателя = -ПараметрыПоказателя.ЗначениеПоказателя;
	КонецЕсли;

	Если ПараметрыПоказателя.ДопАлгоритмСтроки = "НМАСПогашеннойСтоимостью" Тогда
		ЗаполнитьКоличествоНМАССальдо04_05(КонтекстВыполнения, ПараметрыПоказателя);
	ИначеЕсли ПараметрыПоказателя.ДопАлгоритмСтроки = "ПервоначальнаяСтоимостьНМА" Тогда
		ЗаполнитьПоказателиСПервоначальнойСтоимостьюНМА(КонтекстВыполнения, ПараметрыПоказателя);
	ИначеЕсли ПараметрыПоказателя.ДопАлгоритмСтроки = "Прирост[-]_Уменьшение[+]" Тогда
		ЗаполнитьПоказательПриростаУменьшения(КонтекстВыполнения, ПараметрыПоказателя);
	ИначеЕсли ПараметрыПоказателя.ДопАлгоритмСтроки = "-" Тогда
		ПараметрыПоказателя.ЗначениеПоказателя =  -ПараметрыПоказателя.ЗначениеПоказателя;
	ИначеЕсли ПараметрыПоказателя.ДопАлгоритмСтроки = "ОДДС_Агентские" Тогда
		ДополнитьПоказателиОДДС_Агентские(КонтекстВыполнения, ПараметрыПоказателя)
	ИначеЕсли ПараметрыПоказателя.ДопАлгоритмСтроки = "ОДДС_НДС" Тогда
		ДополнитьПоказателиОДДС_НДС(КонтекстВыполнения, ПараметрыПоказателя)
	КонецЕсли;

КонецПроцедуры 

#Область ПолучениеДанныхДляЗаполнения

Функция ПолучитьДанныеЗаполнения(Макет, Параметры)
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ТаблицаДанных", Неопределено);
	ДанныеЗаполнения.Вставить("ОтборПоАналитике", Неопределено);
	
	СхемаКомпоновки = Справочники.АлгоритмыСбораДанныхБухОтчетности.ПолучитьМакет(Макет);
	
	Если Параметры.Свойство("ДопАлгоритм") И ДопАлгоритмОтбораПоДокументам(Параметры.ДопАлгоритм) Тогда 
		ДобавитьВЗапросОтборПоДокументам(СхемаКомпоновки, Параметры.ДопАлгоритм);
	КонецЕсли;
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновки));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновки.НастройкиПоУмолчанию);
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Настройки, "Организация", Параметры.Организация);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Настройки, "НачалоПериода", Параметры.НачалоПериода);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Настройки, "КонецПериода", Параметры.КонецПериода);
	
	Если Параметры.Свойство("НастройкиОтбора") Тогда
		
		НастройкиОтбора = Параметры.НастройкиОтбора.Получить();
		КомпоновкаДанныхКлиентСервер.СкопироватьОтборКомпоновкиДанных(СхемаКомпоновки, Настройки, НастройкиОтбора);
		
		Если Параметры.Свойство("Счета") Тогда
			ОтборПоВидамСубконто = ПолучитьОтборПоВидамСубконто(Параметры.Счета);
			Если ОтборПоВидамСубконто.Количество() > 0 Тогда
				КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Настройки, "ВидыСубконто", ОтборПоВидамСубконто);
			КонецЕсли;
		КонецЕсли;
		
		Если Параметры.Свойство("КорСчета") Тогда
			ОтборПоВидамКорСубконто = ПолучитьОтборПоВидамСубконто(Параметры.КорСчета);
			Если ОтборПоВидамКорСубконто.Количество() > 0 Тогда
				КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Настройки, "ВидыКорСубконто", ОтборПоВидамКорСубконто);
			КонецЕсли;
		КонецЕсли;
		
		ОтборПоАналитике = Новый Соответствие;
		Для Каждого ЭлементОтбора Из НастройкиОтбора.Отбор.Элементы Цикл
			
			Если ЭлементОтбора.Использование Тогда
				
				ЗначениеОтбора = Новый Структура;
				ЗначениеОтбора.Вставить("ВидСравненияКомпоновкиДанных", ЭлементОтбора.ВидСравнения);
				ЗначениеОтбора.Вставить("Значение", ЭлементОтбора.ПравоеЗначение);

				ОтборПоАналитике.Вставить(ЭлементОтбора.ЛевоеЗначение, ЗначениеОтбора); 
			КонецЕсли;
		КонецЦикла;
		
		Если ОтборПоАналитике.Количество() <> 0 Тогда
			ДанныеЗаполнения.ОтборПоАналитике = ОтборПоАналитике;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Счета") Тогда
		СписокСчетов = Новый СписокЗначений;
		СписокСчетов.ЗагрузитьЗначения(Параметры.Счета);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Настройки, "Счета", СписокСчетов);
	КонецЕсли;
	
	Если Параметры.Свойство("КорСчета") И (Макет = "ОборотыМеждуСчетами" ИЛИ Макет = "ОборотыМеждуСчетамиПоТипуДокумента")  Тогда
		СписокСчетов = Новый СписокЗначений;
		СписокСчетов.ЗагрузитьЗначения(Параметры.КорСчета);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Настройки, "КорСчета", СписокСчетов);
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновки, Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ТаблицаРезультатов = Новый ТаблицаЗначений();
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,,,Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаРезультатов);
	ТаблицаРезультатов = ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	Если Макет = "СальдоИОборотыПоСчетам" Тогда
		ТаблицаРезультатов.Индексы.Добавить("Счет");
	ИначеЕсли Макет = "ОборотыМеждуСчетами" Тогда
		ТаблицаРезультатов.Индексы.Добавить("Счет0, КорСчет0");
		ТаблицаРезультатов.Индексы.Добавить("Счет0, КорСчет1");
		ТаблицаРезультатов.Индексы.Добавить("Счет0, КорСчет2");
		
		ТаблицаРезультатов.Индексы.Добавить("Счет1, КорСчет0");
		ТаблицаРезультатов.Индексы.Добавить("Счет1, КорСчет1");
		ТаблицаРезультатов.Индексы.Добавить("Счет1, КорСчет2");
		
		ТаблицаРезультатов.Индексы.Добавить("Счет2, КорСчет0");
		ТаблицаРезультатов.Индексы.Добавить("Счет2, КорСчет1");
		ТаблицаРезультатов.Индексы.Добавить("Счет2, КорСчет2");
	КонецЕсли;
	
	ДанныеЗаполнения.ТаблицаДанных = ТаблицаРезультатов;
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

Функция ПолучитьОбщиеДанныеДляЗаполнения(ПараметрыОтчета, КонтекстВыполнения)
	
	Данные = Новый Соответствие; 
	
	//Текущий период
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", ПараметрыОтчета.Организация);
	Параметры.Вставить("НачалоПериода", ПараметрыОтчета.мДатаНачалаПериодаОтчета);
	Параметры.Вставить("КонецПериода", ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	
	ДанныеЗаПериод = Новый Структура;
	ДанныеЗаПериод.Вставить("СальдоИОборотыПоСчетам", ПолучитьДанныеЗаполнения("СальдоИОборотыПоСчетам", Параметры));
	ДанныеЗаПериод.Вставить("ОборотыМеждуСчетами", ПолучитьДанныеЗаполнения("ОборотыМеждуСчетами", Параметры));
	ДанныеЗаПериод.Вставить("СальдоИОборотыПоСчетамИСубконто", ПолучитьДанныеЗаполнения("СальдоИОборотыПоСчетамИСубконто", Параметры));
	
	Данные.Вставить(Параметры.КонецПериода, ДанныеЗаПериод);
	
	//Прошлый год
	Параметры.Вставить("НачалоПериода", ДобавитьМесяц(ПараметрыОтчета.мДатаНачалаПериодаОтчета, -12));
	Параметры.Вставить("КонецПериода", КонецГода(ДобавитьМесяц(ПараметрыОтчета.мДатаКонцаПериодаОтчета, -12)));
	
	ДанныеЗаПериод = Новый Структура;
	ДанныеЗаПериод.Вставить("СальдоИОборотыПоСчетам", ПолучитьДанныеЗаполнения("СальдоИОборотыПоСчетам", Параметры));
	ДанныеЗаПериод.Вставить("ОборотыМеждуСчетами", ПолучитьДанныеЗаполнения("ОборотыМеждуСчетами", Параметры));
	ДанныеЗаПериод.Вставить("СальдоИОборотыПоСчетамИСубконто", ПолучитьДанныеЗаполнения("СальдоИОборотыПоСчетамИСубконто", Параметры));
	
	Данные.Вставить(Параметры.КонецПериода, ДанныеЗаПериод);
	
	//Позапрошлый год
	Параметры.Вставить("НачалоПериода", ДобавитьМесяц(ПараметрыОтчета.мДатаНачалаПериодаОтчета, -24));
	Параметры.Вставить("КонецПериода", КонецГода(ДобавитьМесяц(ПараметрыОтчета.мДатаКонцаПериодаОтчета, -24)));
	
	ДанныеЗаПериод = Новый Структура;
	ДанныеЗаПериод.Вставить("СальдоИОборотыПоСчетам", ПолучитьДанныеЗаполнения("СальдоИОборотыПоСчетам", Параметры));
	ДанныеЗаПериод.Вставить("ОборотыМеждуСчетами", ПолучитьДанныеЗаполнения("ОборотыМеждуСчетами", Параметры));
	ДанныеЗаПериод.Вставить("СальдоИОборотыПоСчетамИСубконто", ПолучитьДанныеЗаполнения("СальдоИОборотыПоСчетамИСубконто", Параметры));
	
	Данные.Вставить(Параметры.КонецПериода, ДанныеЗаПериод);
	
	Если НЕ КонтекстВыполнения.ЭтоГодовойОтчет 
		И (КонтекстВыполнения.ЗаполнениеФормОтчетности.ОФР
			ИЛИ КонтекстВыполнения.ЗаполнениеФормОтчетности.ОДДС
			ИЛИ КонтекстВыполнения.ЗаполнениеФормОтчетности.ОЦИПС)Тогда
			
		// Аналогичный период прошлого года
		Параметры.Вставить("НачалоПериода", ДобавитьМесяц(ПараметрыОтчета.мДатаНачалаПериодаОтчета, -12));
		Параметры.Вставить("КонецПериода", ДобавитьМесяц(ПараметрыОтчета.мДатаКонцаПериодаОтчета, -12));
	
		ДанныеЗаПериод = Новый Структура;
		ДанныеЗаПериод.Вставить("СальдоИОборотыПоСчетам", ПолучитьДанныеЗаполнения("СальдоИОборотыПоСчетам", Параметры));
		ДанныеЗаПериод.Вставить("ОборотыМеждуСчетами", ПолучитьДанныеЗаполнения("ОборотыМеждуСчетами", Параметры));
		ДанныеЗаПериод.Вставить("СальдоИОборотыПоСчетамИСубконто", ПолучитьДанныеЗаполнения("СальдоИОборотыПоСчетамИСубконто", Параметры));
	
		Данные.Вставить(Параметры.КонецПериода, ДанныеЗаПериод);
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

Процедура ДополнитьДаннымиЗаполненияПоОДДС(КонтекстВыполнения, ПараметрыОтчета)
	
	Данные = КонтекстВыполнения.Данные;
	ЗаполнятьПредыдущиеПериоды = КонтекстВыполнения.ЗаполнятьПредыдущиеПериоды;
	ЭтоГодовойОтчет = КонтекстВыполнения.ЭтоГодовойОтчет;
	
	ВидДДСПолучения    = Перечисления.ВидыДвиженийДенежныхСредств.ПрочиеПоступленияПоТекущейДеятельности;
	ВидДДСПеречисления = Перечисления.ВидыДвиженийДенежныхСредств.ПрочиеРасходыПоТекущейДеятельности;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =  
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АлгоритмыСбораДанныхБухОтчетностиСчета.Счет КАК Счет
		|ИЗ
		|	Справочник.АлгоритмыСбораДанныхБухОтчетности.Счета КАК АлгоритмыСбораДанныхБухОтчетностиСчета
		|ГДЕ
		|	ВЫРАЗИТЬ(АлгоритмыСбораДанныхБухОтчетностиСчета.Ссылка.Владелец КАК Справочник.СтрокиБухОтчетности).ФормаОтчетности = &ОДДС
		|	И АлгоритмыСбораДанныхБухОтчетностиСчета.Ссылка.ВидДвиженияДенежныхСредств <> &ВидДДСПустая";
	
	Запрос.УстановитьПараметр("ВидДДСПустая", Перечисления.ВидыДвиженийДенежныхСредств.ПустаяСсылка());
	Запрос.УстановитьПараметр("ОДДС", Перечисления.ФормыБухгалтерскойОтчетности.ОДДС);
	
	РезультатЗапроса = Запрос.Выполнить();
	СчетаДДС = РезультатЗапроса.Выгрузить();
	
	НачалоПериода = ПараметрыОтчета.мДатаНачалаПериодаОтчета;
	КонецПериода = ПараметрыОтчета.мДатаКонцаПериодаОтчета;
	
	Данные[КонецПериода].Вставить("СтруктураДДС", ПолучитьДанныеДДС(
		ПараметрыОтчета.Организация, НачалоПериода, КонецПериода, ВидДДСПолучения, ВидДДСПеречисления, СчетаДДС));
		
	Данные[КонецПериода].Вставить("РасчетыПоВалютноОбменнымОперациям", ПолучитьДанныеПоВалютноОбменнымОперациям(
		ПараметрыОтчета.Организация, НачалоПериода, КонецПериода, ВидДДСПолучения, ВидДДСПеречисления));
		
	Данные[КонецПериода].Вставить("РасчетыПоНДС", ПолучитьРасчетыПоНДС(
		ПараметрыОтчета.Организация, НачалоПериода, КонецПериода, ВидДДСПолучения, ВидДДСПеречисления));
		
	Данные[КонецПериода].Вставить("СтруктураНДСвДДС", ПолучитьДанныеНДСвДДС(
		ПараметрыОтчета.Организация, НачалоПериода, КонецПериода, СчетаДДС, Данные[КонецПериода].СтруктураДДС));
		
	Если ЗаполнятьПредыдущиеПериоды Тогда
		
		НачалоПериода = ДобавитьМесяц(ПараметрыОтчета.мДатаНачалаПериодаОтчета, -12);
		КонецПериода = ДобавитьМесяц(ПараметрыОтчета.мДатаКонцаПериодаОтчета, -12);
		
		Данные[КонецПериода].Вставить("СтруктураДДС", ПолучитьДанныеДДС(
			ПараметрыОтчета.Организация, НачалоПериода, КонецПериода, ВидДДСПолучения, ВидДДСПеречисления, СчетаДДС));
			
		Данные[КонецПериода].Вставить("РасчетыПоВалютноОбменнымОперациям", ПолучитьДанныеПоВалютноОбменнымОперациям(
			ПараметрыОтчета.Организация, НачалоПериода, КонецПериода, ВидДДСПолучения, ВидДДСПеречисления));
			
		Данные[КонецПериода].Вставить("РасчетыПоНДС", ПолучитьРасчетыПоНДС(
			ПараметрыОтчета.Организация, НачалоПериода, КонецПериода, ВидДДСПолучения, ВидДДСПеречисления));
			
		Данные[КонецПериода].Вставить("СтруктураНДСвДДС", ПолучитьДанныеНДСвДДС(
			ПараметрыОтчета.Организация, НачалоПериода, КонецПериода, СчетаДДС, Данные[КонецПериода].СтруктураДДС));
	КонецЕсли;
		
КонецПроцедуры

Функция ПолучитьДанныеДДС(Организация, НачалоПериодаОтчета, КонецПериодаОтчета, ВидДДСПолучения, ВидДДСПеречисления, СчетаДДС)

	ОписаниеТиповЧисло15_2 = ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля();

	ТаблицаОборотовПоВидамДДС = Новый ТаблицаЗначений;
	ТаблицаОборотовПоВидамДДС.Колонки.Добавить("СчетДДС",   Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаОборотовПоВидамДДС.Колонки.Добавить("ВидДДС",    Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДвиженийДенежныхСредств"));
	ТаблицаОборотовПоВидамДДС.Колонки.Добавить("ДО",        ОписаниеТиповЧисло15_2);
	ТаблицаОборотовПоВидамДДС.Колонки.Добавить("ДО_НДС",    ОписаниеТиповЧисло15_2);
	ТаблицаОборотовПоВидамДДС.Колонки.Добавить("КО",        ОписаниеТиповЧисло15_2);
	ТаблицаОборотовПоВидамДДС.Колонки.Добавить("КО_НДС",    ОписаниеТиповЧисло15_2);
	
	КоллекцияПеречисления = Перечисления.ВидыДвиженийДенежныхСредств;
	
	Для Каждого ВидДДС Из КоллекцияПеречисления Цикл
		Для Каждого СчетДДС Из СчетаДДС Цикл
			НовыйСтрокаСчетаДДС = ТаблицаОборотовПоВидамДДС.Добавить();
			НовыйСтрокаСчетаДДС.СчетДДС = СчетДДС.Счет;
			НовыйСтрокаСчетаДДС.ВидДДС  = ВидДДС;
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаОборотовПоВидамДДС.Индексы.Добавить("ВидДДС, СчетДДС");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Счет КАК СчетДДС,
	|	ХозрасчетныйОбороты.Субконто1 КАК СтатьяДДС,
	|	ХозрасчетныйОбороты.СуммаОборотДт КАК СуммаОборотДт,
	|	ХозрасчетныйОбороты.СуммаОборотКт КАК СуммаОборотКт
	|ПОМЕСТИТЬ ВТ_Хозрасчетный
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачПериода,
	|			&КонПериода,
	|			Период,
	|			Счет В
	|				(&СчетаДДС),
	|			&СубконтоСтатьиДДС,
	|			Организация = &Организация,
	|			,
	|			) КАК ХозрасчетныйОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Счет,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПоступлениеОплатыОтКлиента),
	|	ХозрасчетныйОбороты.СуммаОборотДт,
	|	ХозрасчетныйОбороты.СуммаОборотКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачПериода, &КонПериода, Период, Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПродажиПоПлатежнымКартам)), &СубконтоДоговорыЭквайринга, Организация = &Организация, , ) КАК ХозрасчетныйОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Хозрасчетный.СчетДДС КАК СчетДДС,
	|	СтатьиДвиженияДенежныхСредств.ВидДвиженияДенежныхСредств КАК ВидДДС,
	|	СУММА(ВТ_Хозрасчетный.СуммаОборотДт) КАК СуммаОборотДт,
	|	СУММА(ВТ_Хозрасчетный.СуммаОборотКт) КАК СуммаОборотКт
	|ИЗ
	|	ВТ_Хозрасчетный КАК ВТ_Хозрасчетный
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиДвиженияДенежныхСредств КАК СтатьиДвиженияДенежныхСредств
	|		ПО ВТ_Хозрасчетный.СтатьяДДС = СтатьиДвиженияДенежныхСредств.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Хозрасчетный.СчетДДС,
	|	СтатьиДвиженияДенежныхСредств.ВидДвиженияДенежныхСредств";
	
	Запрос.УстановитьПараметр("СубконтоДоговорыЭквайринга", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДоговорыЭквайринга);
	Запрос.УстановитьПараметр("НачПериода", НачалоПериодаОтчета);
	Запрос.УстановитьПараметр("КонПериода", КонецПериодаОтчета);
	Запрос.УстановитьПараметр("СчетаДДС", СчетаДДС);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СубконтоСтатьиДДС", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств);
	
	СтруктураПоиска = Новый Структура("ВидДДС, СчетДДС");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Выборка);
		НайденныеСтроки = ТаблицаОборотовПоВидамДДС.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтрокаТаблицы = НайденныеСтроки[0];
			СтрокаТаблицы.ДО = СтрокаТаблицы.ДО + Выборка.СуммаОборотДт;
			СтрокаТаблицы.КО = СтрокаТаблицы.КО + Выборка.СуммаОборотКт;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаОборотовПоВидамДДС;
	
КонецФункции 

Функция ПолучитьРасчетыПоНДС(Организация, НачалоПериодаОтчета, КонецПериодаОтчета, ВидДДСПолучения, ВидДДСПеречисления)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.СуммаОборотДт КАК ДО,
	               |	ХозрасчетныйОбороты.СуммаОборотКт КАК КО
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачПериода,
	               |			&КонПериода,
	               |			Период,
	               |			Счет = &Счет_51,
	               |			&СубконтоСтатьиДДС,
	               |			Организация = &Организация
	               |				И ВЫРАЗИТЬ(Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств).ВидДвиженияДенежныхСредств В (&Поступление, &Перечисление),
	               |			КорСчет = &Счет_68_02,
	               |			) КАК ХозрасчетныйОбороты";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачПериода", НачалоПериодаОтчета);
	Запрос.УстановитьПараметр("КонПериода", КонецПериодаОтчета);
	Запрос.УстановитьПараметр("Счет_51", Счет("51"));
	Запрос.УстановитьПараметр("Счет_68_02", Счет("68.02"));
	Запрос.УстановитьПараметр("СубконтоСтатьиДДС", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств);
	Запрос.УстановитьПараметр("Перечисление", ВидДДСПолучения);
	Запрос.УстановитьПараметр("Поступление",  ВидДДСПеречисления);
	Выборка = Запрос.Выполнить().Выбрать();
	
	РасчетыПоНДС = Новый Структура;
	РасчетыПоНДС.Вставить("ПеречисленныйНДС", 0);
	РасчетыПоНДС.Вставить("ПолученныйНДС", 0);
	Если Выборка.Следующий() Тогда
		РасчетыПоНДС.ПеречисленныйНДС = Выборка.КО;
		РасчетыПоНДС.ПолученныйНДС    = Выборка.ДО;
	КонецЕсли;

	Возврат РасчетыПоНДС;
	
КонецФункции 

Функция ПолучитьДанныеНДСвДДС(Организация, НачалоПериодаОтчета, КонецПериодаОтчета, СчетаДДС, СтруктураДДС)
	
	ОрганизацияПерешлаНаУСН = УчетнаяПолитика.ПрименяетсяУСН(Организация, КонецПериодаОтчета);
	
	Если ОрганизацияПерешлаНаУСН Тогда
		ТаблицаОборотовПоВидамДДС = СтруктураДДС.СкопироватьКолонки();
	Иначе
		
		Запрос = Новый Запрос;
		ЗаполнениеБухгалтерскойОтчетностиПереопределяемый.ПереопределитьЗапросСтруктураНДСвДДС(Запрос);
		
		Запрос.УстановитьПараметр("НачПериода",     НачалоПериодаОтчета);
		Запрос.УстановитьПараметр("КонПериода",     КонецПериодаОтчета);
		Запрос.УстановитьПараметр("Организация",    Организация);
		Запрос.УстановитьПараметр("СчетаДДС",       СчетаДДС);
		Запрос.УстановитьПараметр("ВалютаРегУчета", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация));
		
		ТаблицаОборотовПоВидамДДС = Запрос.Выполнить().Выгрузить();
		ТаблицаОборотовПоВидамДДС.Индексы.Добавить("ВидДДС");
	КонецЕсли;
	
	Возврат ТаблицаОборотовПоВидамДДС;
	
КонецФункции

Функция ПолучитьДанныеПоВалютноОбменнымОперациям(Организация, НачалоПериодаОтчета, КонецПериодаОтчета, ВидДДСПолучения, ВидДДСПеречисления)
	
	СчетаБанка = Новый Массив;
	СчетаБанка.Добавить(Счет("51"));
	СчетаБанка.Добавить(Счет("52"));
	
	СчетаПриобретениеРеализацииВалюты = Новый Массив;
	СчетаПриобретениеРеализацииВалюты.Добавить(Счет("57.02"));
	СчетаПриобретениеРеализацииВалюты.Добавить(Счет("57.22"));
	
	ДоходыРасходыПриПродажеПокупкеВалюты = Новый Массив;
	ДоходыРасходыПриПродажеПокупкеВалюты.Добавить(ПланыВидовХарактеристик.СтатьиДоходов.ДоходыПриКонвертацииВалюты);
	ДоходыРасходыПриПродажеПокупкеВалюты.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.РасходыПриКонвертацииВалюты);

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(ВложенныйЗапрос.ВалютоОбменДО, 0)) КАК ВалютоОбменДО,
	|	СУММА(ЕСТЬNULL(ВложенныйЗапрос.ВалютоОбменКО, 0)) КАК ВалютоОбменКО,
	|	0 КАК ВалютоОбменДельта
	|ИЗ
	|	(ВЫБРАТЬ
	|		ХозрасчетныйОборотыДтКт.СуммаОборот КАК ВалютоОбменДО,
	|		0 КАК ВалютоОбменКО
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|				&НачПериода,
	|				&КонПериода,
	|				,
	|				СчетДт В (&СчетаБанка),
	|				&СубконтоСтатьиДДС,
	|				СчетКт В (&СчетаПриобретениеРеализацииВалюты),
	|				,
	|				Организация = &Организация
	|					И ВЫРАЗИТЬ(СубконтоДт1 КАК Справочник.СтатьиДвиженияДенежныхСредств).ВидДвиженияДенежныхСредств = &ВидДДСПрочиеПоступления) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		ХозрасчетныйОборотыДтКт.СуммаОборот
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|				&НачПериода,
	|				&КонПериода,
	|				,
	|				СчетДт В (&СчетаПриобретениеРеализацииВалюты),
	|				,
	|				СчетКт В (&СчетаБанка),
	|				&СубконтоСтатьиДДС,
	|				Организация = &Организация
	|					И ВЫРАЗИТЬ(СубконтоКт1 КАК Справочник.СтатьиДвиженияДенежныхСредств).ВидДвиженияДенежныхСредств = &ВидДДСПрочиеПлатежи) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ХозрасчетныйОборотыДтКт.СуммаОборот,
	|		0
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|				&НачПериода,
	|				&КонПериода,
	|				,
	|				СчетДт = &СчетРасчетныеСчета,
	|				&СубконтоСтатьиДДС,
	|				СчетКт = &СчетПрочиеДоходы,
	|				&СубконтоПрочиеДоходыРасходы,
	|				Организация = &Организация
	|					И ВЫРАЗИТЬ(СубконтоДт1 КАК Справочник.СтатьиДвиженияДенежныхСредств).ВидДвиженияДенежныхСредств = &ВидДДСПрочиеПоступления
	|					И СубконтоКт1 В (&ПокупкаПродажаВалюты)) КАК ХозрасчетныйОборотыДтКт) КАК ВложенныйЗапрос
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЕСТЬNULL(ВложенныйЗапрос.ВалютоОбменДО, 0)) > 0 И
	|	СУММА(ЕСТЬNULL(ВложенныйЗапрос.ВалютоОбменКО, 0)) > 0";


	Запрос.УстановитьПараметр("НачПериода",                        НачалоПериодаОтчета);
	Запрос.УстановитьПараметр("КонПериода",                        КонецПериодаОтчета);
	Запрос.УстановитьПараметр("Организация",                       Организация);
	Запрос.УстановитьПараметр("СчетаБанка",                        СчетаБанка);
	Запрос.УстановитьПараметр("СчетаПриобретениеРеализацииВалюты", СчетаПриобретениеРеализацииВалюты);
	Запрос.УстановитьПараметр("СчетРасчетныеСчета",                Счет("51"));
	Запрос.УстановитьПараметр("СчетПрочиеДоходы",                  ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ПокупкаПродажаВалюты",              ДоходыРасходыПриПродажеПокупкеВалюты);
	Запрос.УстановитьПараметр("ВидДДСПрочиеПоступления",           ВидДДСПолучения);
	Запрос.УстановитьПараметр("ВидДДСПрочиеПлатежи",               ВидДДСПеречисления);
	Запрос.УстановитьПараметр("СубконтоСтатьиДДС",                 ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств);
	Запрос.УстановитьПараметр("СубконтоПрочиеДоходыРасходы",       ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы);
	
	ТаблицаПоВалютноОбменнымОперациям = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаПоВалютноОбменнымОперациям;
	
КонецФункции

Процедура ДобавитьВЗапросОтборПоДокументам(СхемаКомпоновки, ДопАлгоритм);
	
	Если ДопАлгоритм = "МодернизацияОС" Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	МодернизацияОС.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ТаблицаДокументы
		|ИЗ
		|	Документ.МодернизацияОС КАК МодернизацияОС
		|ГДЕ
		|	МодернизацияОС.СобытиеОС.ВидСобытияОС = ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Модернизация)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МодернизацияОС.Ссылка
		|ИЗ
		|	Документ.МодернизацияОС2_4 КАК МодернизацияОС";
		
		
		СхемаКомпоновки.НаборыДанных.НаборДанных1.Запрос = СтрЗаменить(СхемаКомпоновки.НаборыДанных.НаборДанных1.Запрос, "//ТекстЗапросаТаблицаДокументы", ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов()); 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДополнительныеАлгоритмы

Процедура ЗаполнитьПоказательПриростаУменьшения(КонтекстВыполнения, ПараметрыПоказателя);
	
	Если ПараметрыПоказателя.КодСтроки = "5670" Тогда
		
		СтрокиРасшифровки = КонтекстВыполнения.ТаблицаРасшифровки.НайтиСтроки(Новый Структура("ИмяПоказателя", "П0001005670" + ПараметрыПоказателя.КодГрафы));
		
		Если ПараметрыПоказателя.ЗначениеПоказателя > 0 Тогда
			ПараметрыПоказателя.НаименованиеСтроки = СтрЗаменить(ПараметрыПоказателя.НаименованиеСтроки, "()", НСтр("ru = '(прирост [-])'"));
		Иначе
			ПараметрыПоказателя.ИмяОбластиПоказателя = СтрЗаменить(ПараметрыПоказателя.ИмяОбластиПоказателя, "5670", "5680");
			ПараметрыПоказателя.НаименованиеСтроки = СтрЗаменить(ПараметрыПоказателя.НаименованиеСтроки, "()", НСтр("ru = '(уменьшение [+)'"));
			ПараметрыПоказателя.НаименованиеСтроки = СтрЗаменить(ПараметрыПоказателя.НаименованиеСтроки, "5670", "5680");
			Для Каждого Строка Из СтрокиРасшифровки Цикл 
				Строка.ИмяПоказателя = СтрЗаменить(Строка.ИмяПоказателя, "5670", "5680");
			КонецЦикла;
		КонецЕсли;
		Для Каждого Строка Из СтрокиРасшифровки Цикл 
				Строка.НаименованиеПоказателя = ПараметрыПоказателя.НаименованиеСтроки; 
		КонецЦикла;
		ПараметрыПоказателя.ЗначениеПоказателя = -ПараметрыПоказателя.ЗначениеПоказателя;
	Иначе
		Сообщение = НСтр("ru = 'Для строки %1 не поддерживается дополнительный алгоритм.'"); 
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(Сообщение, ПараметрыПоказателя.КодСтроки));
	КонецЕсли;
		
КонецПроцедуры

Функция ДопАлгоритмОтбораПоДокументам(ДопАлгоритм)
	
	Если ДопАлгоритм = "МодернизацияОС" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#Область ДополнительныеАлгоритмыПояснения1

Процедура ЗаполнитьКоличествоНМАССальдо04_05(КонтекстВыполнения, ПараметрыПоказателя)
	
	ИмяРаздела = КонтекстВыполнения.КонтекстОтчета.ИмяРаздела;
	Организация = КонтекстВыполнения.Организация;
	ТаблицаРасшифровки = КонтекстВыполнения.ТаблицаРасшифровки;
	РассчитанныеПоказатели = КонтекстВыполнения.РассчитанныеПоказатели;
	
	КодСтроки = ПараметрыПоказателя.КодСтроки;
	
	ИмяГруппы = "ДопСтрока" + КодСтроки;
	
	МногострочнаяЧасть = НоваяТаблицаМногострочнойЧасти(КодСтроки); 	
	
	ГрафыКЗаполнению = Новый Соответствие;
	ГрафыКЗаполнению.Вставить("03", КонтекстВыполнения.КонтекстОтчета.КонецПериодаОтчета);
	ГрафыКЗаполнению.Вставить("04", ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.КонецПериодаОтчета, 12));
	ГрафыКЗаполнению.Вставить("05", ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.КонецПериодаОтчета, 24));
	
	Для Каждого Графа Из ГрафыКЗаполнению Цикл
		
		ДополнительныеПараметрыРасшифровки = Новый Структура("Организация", Организация);
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ХозрасчетныйОстатки.Субконто1 КАК НМА,
		               |	СУММА(ВЫБОР
		               |			КОГДА ХозрасчетныйОстатки.Счет В (&Счет_04_01)
		               |				ТОГДА ХозрасчетныйОстатки.СуммаОстатокДт
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК СКД_04,
		               |	СУММА(ВЫБОР
		               |			КОГДА ХозрасчетныйОстатки.Счет В (&Счет_05)
		               |				ТОГДА ХозрасчетныйОстатки.СуммаОстатокКт
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК СКК_05
		               |ПОМЕСТИТЬ ВТ_СальдоПоНМА
		               |ИЗ
		               |	РегистрБухгалтерии.Хозрасчетный.Остатки(
		               |			&КонецПериода,
		               |			Счет В (&Счет_04_01)
		               |				ИЛИ Счет В (&Счет_05),
		               |			&СубконтоНематериальныеАктивы,
		               |			Организация = &Организация) КАК ХозрасчетныйОстатки
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ХозрасчетныйОстатки.Субконто1
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВЫРАЗИТЬ(ВТ_СальдоПоНМА.НМА КАК Справочник.НематериальныеАктивы).ВидНМА КАК ВидАктива,
		               |	1 КАК Количество
		               |ИЗ
		               |	ВТ_СальдоПоНМА КАК ВТ_СальдоПоНМА
		               |ГДЕ
		               |	ВТ_СальдоПоНМА.СКД_04 - ВТ_СальдоПоНМА.СКК_05 <= 0
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ВидАктива
		               |ИТОГИ
		               |	СУММА(Количество)
		               |ПО
		               |	ОБЩИЕ,
		               |	ВидАктива";
		
		Запрос.УстановитьПараметр("Организация",  Организация);
		Запрос.УстановитьПараметр("КонецПериода", Новый Граница(Графа.Значение, ВидГраницы.Включая));
		
		Запрос.УстановитьПараметр("Счет_04_01", БухгалтерскийУчетПовтИсп.СчетаВИерархии(Счет("04.01")));
		Запрос.УстановитьПараметр("Счет_05",    БухгалтерскийУчетПовтИсп.СчетаВИерархии(Счет("05")));
		
		Запрос.УстановитьПараметр("СубконтоНематериальныеАктивы", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы);
		
		КоличествоНМАСПолностьюПогашеннойСтоимостью = 0;
		
		ОбщийИтог = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ОбщийИтог.Следующий() Тогда
			КоличествоНМАСПолностьюПогашеннойСтоимостью = ОбщийИтог.Количество;
			
			ВыборкаПоВидамНМА = ОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоВидамНМА.Следующий() Цикл
				Если ЗначениеЗаполнено(ВыборкаПоВидамНМА.ВидАктива) Тогда
					СтрокаМногострочнаяЧасть = МногострочнаяЧасть.Добавить();
					СтрокаМногострочнаяЧасть["П100100" + КодСтроки + "01"] = ВыборкаПоВидамНМА.ВидАктива;
					СтрокаМногострочнаяЧасть["П100100" + КодСтроки + Графа.Ключ] = ВыборкаПоВидамНМА.Количество;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
				
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа.Ключ, КоличествоНМАСПолностьюПогашеннойСтоимостью);
		
		ШаблонНаименованиеСтрокиРасшифровки = НСтр("ru = 'Строка %1 """"Всего"""", графа ""На %2 г.""'");
		НаименованиеСтрокиРасшифровки = СтрШаблон(ШаблонНаименованиеСтрокиРасшифровки, КодСтроки, Формат(Графа.Значение, "ДФ = 'дд ММММ гггг'"));

		НаименованиеСлагаемого = НСтр("ru = 'Количество объектов, для которых сальдо на конец периода по дебету счета 04.01 меньше или равно сальдо на конец периода по кредиту счета 05'");
		
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
			"П000100" + КодСтроки + Графа, 
			НаименованиеСтрокиРасшифровки,
			"+", 
			НаименованиеСлагаемого,
			КоличествоНМАСПолностьюПогашеннойСтоимостью,
			ИмяРаздела,
			ДополнительныеПараметрыРасшифровки);
		
	КонецЦикла;
	
		
	Если НЕ КонтекстВыполнения.МногострочныеПоказатели.Свойство(ИмяРаздела) Тогда
		КонтекстВыполнения.МногострочныеПоказатели.Вставить(ИмяРаздела, Новый Структура);
	КонецЕсли;
	
	КонтекстВыполнения.МногострочныеПоказатели[ИмяРаздела].Вставить(ИмяГруппы, МногострочнаяЧасть);

КонецПроцедуры

Процедура ЗаполнитьПоказателиСПервоначальнойСтоимостьюНМА(КонтекстВыполнения, ПараметрыПоказателя)

	ИмяРаздела = КонтекстВыполнения.КонтекстОтчета.ИмяРаздела; 
	Организация = КонтекстВыполнения.Организация;
	ТаблицаРасшифровки = КонтекстВыполнения.ТаблицаРасшифровки;
	РассчитанныеПоказатели = КонтекстВыполнения.РассчитанныеПоказатели;
	
	КодСтроки = ?(ПараметрыПоказателя.КодСтрокиПрошлогоПериода<>"", ПараметрыПоказателя.КодСтрокиПрошлогоПериода, ПараметрыПоказателя.КодСтроки);
	
	ИмяГруппы = "ДопСтрока5140";
	Если Не КонтекстВыполнения.МногострочныеПоказатели.Свойство(ИмяРаздела) Тогда	
		КонтекстВыполнения.МногострочныеПоказатели.Вставить(ИмяРаздела, Новый Структура);
	КонецЕсли;
	
	Если Не КонтекстВыполнения.МногострочныеПоказатели[ИмяРаздела].Свойство(ИмяГруппы) Тогда		
		МногострочнаяЧасть = НоваяТаблицаМногострочнойЧасти("5140"); 
	Иначе 
		МногострочнаяЧасть = КонтекстВыполнения.МногострочныеПоказатели[ИмяРаздела][ИмяГруппы];
	КонецЕсли;
	
	КорректировкаПериода = ?(ПараметрыПоказателя.ПрошлыйПериод, -12, 0); 
	
	ДатаНачалаПериодаОтчета = ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.НачалоПериодаОтчета, КорректировкаПериода);
	ДатаКонцаПериодаОтчета = ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.КонецПериодаОтчета, КорректировкаПериода);
		
	ПараметрыРасшифровки = Новый Структура;
	ПараметрыРасшифровки.Вставить("Организация",   КонтекстВыполнения.Организация);
	ПараметрыРасшифровки.Вставить("НачалоПериода", ДатаНачалаПериодаОтчета);
	ПараметрыРасшифровки.Вставить("КонецПериода",  ДатаКонцаПериодаОтчета);
	
	КонтекстВыполнения.Вставить("ПараметрыРасшифровки", ПараметрыРасшифровки);
	
	ДополнительныеПараметрыРасшифровки = Новый Структура("Организация", Организация);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РасходыНаНИОКР.Организация,
	               |	РасходыНаНИОКР.НИОКР,
	               |	СУММА(РасходыНаНИОКР.СНД_04_02) КАК СНД_04_02,
	               |	СУММА(РасходыНаНИОКР.КО_04_02) КАК КО_04_02,
	               |	СУММА(РасходыНаНИОКР.СКД_04_02) КАК СКД_04_02,
	               |	СУММА(РасходыНаНИОКР.ОБ_04_02__08_08) КАК ОБ_04_02__08_08,
	               |	СУММА(РасходыНаНИОКР.ОБ_91_02__04_02) КАК ОБ_91_02__04_02
	               |ПОМЕСТИТЬ ВТ_РасходыНаНИОКР
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ХозрасчетныйОстаткиИОбороты.Организация КАК Организация,
	               |		ХозрасчетныйОстаткиИОбороты.Субконто1 КАК НИОКР,
	               |		ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт КАК СНД_04_02,
	               |		ХозрасчетныйОстаткиИОбороты.СуммаОборотКт КАК КО_04_02,
	               |		ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СКД_04_02,
	               |		0 КАК ОБ_04_02__08_08,
	               |		0 КАК ОБ_91_02__04_02
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачПериода, &КонПериода, Период, Движения, Счет = &Счет_04_02, &СубконтоНМА, Организация = &Организация) КАК ХозрасчетныйОстаткиИОбороты
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйОборотыДтКт.Организация,
	               |		ХозрасчетныйОборотыДтКт.СубконтоДт1,
	               |		0,
	               |		0,
	               |		0,
	               |		ХозрасчетныйОборотыДтКт.СуммаОборот,
	               |		0
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачПериода, &КонПериода, Период, СчетДт = &Счет_04_02, &СубконтоНМА, СчетКт = &Счет_08_08, , Организация = &Организация) КАК ХозрасчетныйОборотыДтКт
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйОборотыДтКт.Организация,
	               |		ХозрасчетныйОборотыДтКт.СубконтоКт1,
	               |		0,
	               |		0,
	               |		0,
	               |		0,
	               |		ХозрасчетныйОборотыДтКт.СуммаОборот
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачПериода, &КонПериода, Период, СчетДт = &Счет_91_02, , СчетКт = &Счет_04_02, &СубконтоНМА, Организация = &Организация) КАК ХозрасчетныйОборотыДтКт) КАК РасходыНаНИОКР
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	РасходыНаНИОКР.НИОКР,
	               |	РасходыНаНИОКР.Организация
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_РасходыНаНИОКР.НИОКР КАК РасходыНаНИОКР,
	               |	ВЫБОР
	               |		КОГДА ВТ_РасходыНаНИОКР.СНД_04_02 <> 0
	               |			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ, 0)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ЗначениеГрафы04,
	               |	ВЫБОР
	               |		КОГДА ВТ_РасходыНаНИОКР.СНД_04_02 <> 0
	               |			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ, 0) - ВТ_РасходыНаНИОКР.СНД_04_02
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ЗначениеГрафы05,
	               |	ВТ_РасходыНаНИОКР.ОБ_04_02__08_08 КАК ЗначениеГрафы06,
	               |	ВЫБОР
	               |		КОГДА ВТ_РасходыНаНИОКР.ОБ_91_02__04_02 <> 0
	               |			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ, 0)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ЗначениеГрафы07,
	               |	ВЫБОР
	               |		КОГДА ВТ_РасходыНаНИОКР.ОБ_91_02__04_02 <> 0
	               |			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ, 0) - ВТ_РасходыНаНИОКР.ОБ_91_02__04_02
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ЗначениеГрафы08,
	               |	ВТ_РасходыНаНИОКР.КО_04_02 - ВЫБОР
	               |		КОГДА ВТ_РасходыНаНИОКР.ОБ_91_02__04_02 <> 0
	               |			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ, 0)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ + ВЫБОР
	               |		КОГДА ВТ_РасходыНаНИОКР.ОБ_91_02__04_02 <> 0
	               |			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ, 0) - ВТ_РасходыНаНИОКР.ОБ_91_02__04_02
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ЗначениеГрафы09,
	               |	ВЫБОР
	               |		КОГДА ВТ_РасходыНаНИОКР.СКД_04_02 <> 0
	               |			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ, 0)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ЗначениеГрафы10,
	               |	ВЫБОР
	               |		КОГДА ВТ_РасходыНаНИОКР.СКД_04_02 <> 0
	               |			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ, 0) - ВТ_РасходыНаНИОКР.СКД_04_02
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ЗначениеГрафы11,
	               |	ВТ_РасходыНаНИОКР.СНД_04_02 КАК СНД_04_02,
	               |	ВТ_РасходыНаНИОКР.ОБ_04_02__08_08 КАК ОБ_04_02__08_08,
	               |	ВТ_РасходыНаНИОКР.ОБ_91_02__04_02 КАК ОБ_91_02__04_02,
	               |	ВТ_РасходыНаНИОКР.КО_04_02 КАК КО_04_02,
	               |	ВТ_РасходыНаНИОКР.СКД_04_02 КАК СКД_04_02
	               |ИЗ
	               |	ВТ_РасходыНаНИОКР КАК ВТ_РасходыНаНИОКР
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМА КАК ПервоначальныеСведенияНМА
	               |		ПО ВТ_РасходыНаНИОКР.НИОКР = ПервоначальныеСведенияНМА.НематериальныйАктив
	               |			И ВТ_РасходыНаНИОКР.Организация = ПервоначальныеСведенияНМА.Организация
	               |ИТОГИ
	               |	СУММА(ЗначениеГрафы04),
	               |	СУММА(ЗначениеГрафы05),
	               |	СУММА(ЗначениеГрафы06),
	               |	СУММА(ЗначениеГрафы07),
	               |	СУММА(ЗначениеГрафы08),
	               |	СУММА(ЗначениеГрафы09),
	               |	СУММА(ЗначениеГрафы10),
	               |	СУММА(ЗначениеГрафы11),
	               |	СУММА(СНД_04_02),
	               |	СУММА(ОБ_04_02__08_08),
	               |	СУММА(ОБ_91_02__04_02),
	               |	СУММА(КО_04_02),
	               |	СУММА(СКД_04_02)
	               |ПО
	               |	ОБЩИЕ";
	Запрос.УстановитьПараметр("НачПериода", ДатаНачалаПериодаОтчета);
	Запрос.УстановитьПараметр("КонПериода",	ДатаКонцаПериодаОтчета);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Счет_04_02",  Счет("04.02"));
	Запрос.УстановитьПараметр("Счет_08_08",  Счет("08.08"));
	Запрос.УстановитьПараметр("Счет_91_02",  Счет("91.02"));
	Запрос.УстановитьПараметр("СубконтоНМА", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы);

	ВыборкаИтогов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ВыборкаИтогов.Следующий() Тогда
		
		Графа = "04";
		ЗначениеГрафы04 = ВыборкаИтогов["ЗначениеГрафы" + Графа];
		ЗначениеПоказателя = ЗначениеГрафы04;
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, ЗначениеПоказателя);
		
		НаименованиеСтрокиРасшифровки = СтрШаблон(НСтр("ru = 'Строка %1 """"НИОКР - всего"""",  графа ""Первоначальная стоимость на начало года""'"),
			КодСтроки);
		НаименованиеСоставляющей = СтрШаблон(НСтр("ru = 'Первоначальная стоимость расходов на НИОКР, по которым на начало периода имеется остаток на счете %1'"),
			Счет("04.02").Код);
		
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, НаименованиеСтрокиРасшифровки, "+", НаименованиеСоставляющей, ЗначениеГрафы04, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
		
		Графа = "05";
		СНД_04_02 = ВыборкаИтогов.СНД_04_02;
		ЗначениеПоказателя = ВыборкаИтогов["ЗначениеГрафы" + Графа];
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, -ЗначениеПоказателя); // вычитаемый
		
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Часть стоимости, списанной на расходы на начало года""", "+", НаименованиеСоставляющей, ЗначениеГрафы04, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "", "-", "!СНД " + Счет("04.02").Код, -СНД_04_02, ИмяРаздела, НастройкиОСВПоСчету(КонтекстВыполнения, "04.02"));
		
		Графа = "06";
		ОБ_04_02__08_08 = ВыборкаИтогов.ОБ_04_02__08_08;
		ЗначениеПоказателя = ОБ_04_02__08_08;
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, ЗначениеПоказателя);
		
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Изменения за период - поступило""", "+", "!ОБ " + Счет("04.02").Код + " " + Счет("08.08").Код, ОБ_04_02__08_08, ИмяРаздела, НастройкиАнализаСчета(КонтекстВыполнения, "04.02", "08.08"));
		
		Графа = "07";
		ЗначениеГрафы07 = ВыборкаИтогов["ЗначениеГрафы" + Графа];
		ЗначениеПоказателя = ЗначениеГрафы07;
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, -ЗначениеПоказателя); // вычитаемый
		
		НаименованиеСоставляющей = "Первоначальная стоимость расходов на НИОКР, по которым в течении периода имеются проводки с дебета счета " + Счет("91.02").Код + " в кредит счета " + Счет("04.02").Код;
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Изменения за период - выбыло - первоначальная стоимость""", "+", НаименованиеСоставляющей, ЗначениеГрафы07, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
		
		Графа = "08";
		ОБ_91_02__04_02 = ВыборкаИтогов.ОБ_91_02__04_02;
		ЗначениеПоказателя = ВыборкаИтогов["ЗначениеГрафы" + Графа];
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, ЗначениеПоказателя);
		
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Изменения за период - выбыло - часть стоимости, списанной на расходы""", "+", НаименованиеСоставляющей, ЗначениеГрафы07, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "", "-", "!ОБ " +  Счет("91.02").Код + " " + Счет("04.02").Код, -ОБ_91_02__04_02, ИмяРаздела, НастройкиАнализаСчета(КонтекстВыполнения, "91.02", "04.02"));
		
		Графа = "09";
		КО_04_02 = ВыборкаИтогов.КО_04_02;
		ЗначениеПоказателя = ВыборкаИтогов["ЗначениеГрафы" + Графа];
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, -ЗначениеПоказателя); // вычитаемый
		
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Изменения за период - часть стоимости, списанная на расходы за период""", "+", "!КО " + Счет("04.02").Код, КО_04_02, ИмяРаздела, НастройкиОСВПоСчету(КонтекстВыполнения, "04.02"));
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "", "-", "!ОБ " + Счет("91.02").Код + " " + Счет("04.02").Код, -ОБ_91_02__04_02, ИмяРаздела, НастройкиАнализаСчета(КонтекстВыполнения, "91.02", "04.02"));
		
		Графа = "10";
		ЗначениеГрафы10 = ВыборкаИтогов["ЗначениеГрафы" + Графа];
		ЗначениеПоказателя = ЗначениеГрафы10;
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, ЗначениеПоказателя);
		
		НаименованиеСоставляющей = "Первоначальная стоимость расходов на НИОКР, по которым на конец периода имеется остаток на счете " + Счет("04.02").Код;
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Первоначальная стоимость на конец периода""", "+", НаименованиеСоставляющей, ЗначениеГрафы10, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
		
		Графа = "11";
		СКД_04_02 = ВыборкаИтогов.СКД_04_02;
		ЗначениеПоказателя = ВыборкаИтогов["ЗначениеГрафы" + Графа];
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, -ЗначениеПоказателя);  // вычитаемый
		
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Первоначальная стоимость на конец периода""", "+", НаименованиеСоставляющей, ЗначениеГрафы10, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "", "-", "!СКД " + Счет("04.02").Код, -СКД_04_02, ИмяРаздела, НастройкиОСВПоСчету(КонтекстВыполнения, "04.02"));
		
		Выборка = ВыборкаИтогов.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СтрокаМногострочнойЧасти = МногострочнаяЧасть.Добавить();
			СтрокаМногострочнойЧасти["П100100"+ ПараметрыПоказателя.КодСтроки + "01"] = Выборка.РасходыНаНИОКР;
			
			Графа = "04";
			ЗначениеПоказателя = Выборка["ЗначениеГрафы" + Графа];
			СтрокаМногострочнойЧасти["П100100"+ КодСтроки + Графа] = ЗначениеПоказателя;
			 
			Графа = "05";
			ЗначениеПоказателя = Выборка["ЗначениеГрафы" + Графа];
			СтрокаМногострочнойЧасти["П100100"+ КодСтроки + Графа] = -ЗначениеПоказателя; // вычитаемый
			
			Графа = "06";
			ЗначениеПоказателя = Выборка["ЗначениеГрафы" + Графа];
			СтрокаМногострочнойЧасти["П100100"+ КодСтроки + Графа] = ЗначениеПоказателя;
			
			Графа = "07";
			ЗначениеПоказателя = Выборка["ЗначениеГрафы" + Графа];
			СтрокаМногострочнойЧасти["П100100"+ КодСтроки + Графа] = -ЗначениеПоказателя; // вычитаемый
			
			Графа = "08";
			ЗначениеПоказателя = Выборка["ЗначениеГрафы" + Графа];
			СтрокаМногострочнойЧасти["П100100"+ КодСтроки + Графа] = ЗначениеПоказателя;
			
			Графа = "09";
			ЗначениеПоказателя = Выборка["ЗначениеГрафы" + Графа];
			СтрокаМногострочнойЧасти["П100100"+ КодСтроки + Графа] = -ЗначениеПоказателя; // вычитаемый
			
			Графа = "10";
			ЗначениеПоказателя = Выборка["ЗначениеГрафы" + Графа];
			СтрокаМногострочнойЧасти["П100100"+ КодСтроки + Графа] = ЗначениеПоказателя;
			
			Графа = "11";
			ЗначениеПоказателя = Выборка["ЗначениеГрафы" + Графа];
			СтрокаМногострочнойЧасти["П100100"+ КодСтроки + Графа] = -ЗначениеПоказателя; // вычитаемый
		КонецЦикла;
		
	Иначе
		
		Графа = "04";
		ЗначениеГрафы04 = 0;
		ЗначениеПоказателя = ЗначениеГрафы04;
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, ЗначениеПоказателя);
		
		НаименованиеСоставляющей = "Первоначальная стоимость расходов на НИОКР, по которым на начало периода имеется остаток на счете " + Счет("04.02").Код;
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Первоначальная стоимость на начало года""", "+", НаименованиеСоставляющей, ЗначениеГрафы04, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
		
		Графа = "05";
		СНД_04_02 = 0;
		ЗначениеПоказателя = 0;
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, -ЗначениеПоказателя); // вычитаемый
		
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Часть стоимости, списанной на расходы на начало года""", "+", НаименованиеСоставляющей, ЗначениеГрафы04, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "", "-", "!СНД " + Счет("04.02").Код, -СНД_04_02, ИмяРаздела, НастройкиОСВПоСчету(КонтекстВыполнения, "04.02"));
		
		Графа = "06";
		ОБ_04_02__08_08 = 0;
		ЗначениеПоказателя = ОБ_04_02__08_08;
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, ЗначениеПоказателя);
		
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Изменения за период - поступило""", "+", "!ОБ " + Счет("04.02").Код + " " + Счет("08.08").Код, ОБ_04_02__08_08, ИмяРаздела, НастройкиАнализаСчета(КонтекстВыполнения, "04.02", "08.08"));
		
		Графа = "07";
		ЗначениеГрафы07 = 0;
		ЗначениеПоказателя = ЗначениеГрафы07;
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, -ЗначениеПоказателя); // вычитаемый
		
		НаименованиеСоставляющей = "Первоначальная стоимость расходов на НИОКР, по которым в течении периода имеются проводки с дебета счета " + Счет("91.02").Код + " в кредит счета " + Счет("04.02").Код;
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Изменения за период - выбыло - первоначальная стоимость""", "+", НаименованиеСоставляющей, ЗначениеГрафы07, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
		
		Графа = "08";
		ОБ_91_02__04_02 = 0;
		ЗначениеПоказателя = 0;
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, ЗначениеПоказателя);
		
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Изменения за период - выбыло - часть стоимости, списанной на расходы""", "+", НаименованиеСоставляющей, ЗначениеГрафы07, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "", "-", "!ОБ " +  Счет("91.02").Код + " " + Счет("04.02").Код, -ОБ_91_02__04_02, ИмяРаздела, НастройкиАнализаСчета(КонтекстВыполнения, "91.02", "04.02"));
		
		Графа = "09";
		КО_04_02 = 0;
		ЗначениеПоказателя = 0;
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, -ЗначениеПоказателя); // вычитаемый
		
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Изменения за период - часть стоимости, списанная на расходы за период""", "+", "!КО " + Счет("04.02").Код, КО_04_02, ИмяРаздела, НастройкиОСВПоСчету(КонтекстВыполнения, "04.02"));
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "", "-", "!ОБ " + Счет("91.02").Код + " " + Счет("04.02").Код, -ОБ_91_02__04_02, ИмяРаздела, НастройкиАнализаСчета(КонтекстВыполнения, "91.02", "04.02"));
		
		Графа = "10";
		ЗначениеГрафы10 = 0;
		ЗначениеПоказателя = ЗначениеГрафы10;
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, ЗначениеПоказателя);
		
		НаименованиеСоставляющей = "Первоначальная стоимость расходов на НИОКР, по которым на конец периода имеется остаток на счете " + Счет("04.02").Код;
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Первоначальная стоимость на конец периода""", "+", НаименованиеСоставляющей, ЗначениеГрафы10, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
		
		Графа = "11";
		СКД_04_02 = 0;
		ЗначениеПоказателя = 0;
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П000100" + КодСтроки + Графа, -ЗначениеПоказателя); // вычитаемый
		
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "Строка " + КодСтроки + " ""НИОКР - всего"", графа ""Первоначальная стоимость на конец периода""", "+", НаименованиеСоставляющей, ЗначениеГрафы10, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
		ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П000100" + КодСтроки + Графа, "", "-", "!СКД " + Счет("04.02").Код, -СКД_04_02, ИмяРаздела, НастройкиОСВПоСчету(КонтекстВыполнения, "04.02"));
		
	КонецЕсли;
	
	КонтекстВыполнения.Удалить("ПараметрыРасшифровки");
		
	КонтекстВыполнения.МногострочныеПоказатели[ИмяРаздела].Вставить(ИмяГруппы, МногострочнаяЧасть);
	
КонецПроцедуры

#КонецОбласти

#Область ДополнительныеАлгоритмыОДДС

Процедура ЗаполнитьПоказателиОДДС(КонтекстВыполнения, ПараметрыПоказателя, Алгоритм, ЗначениеПоказателя)
	
	ИмяРаздела = КонтекстВыполнения.КонтекстОтчета.ИмяРаздела;
	
	КодГрафы = ПараметрыПоказателя.РегулярнаяГрафа.Ключ;
	
	ЗаголовокГрафы = "";
	КодГода = 0;
	Если ПараметрыПоказателя.РегулярнаяГрафа <> Неопределено Тогда
		Для Каждого РегулярнаяГрафа Из ПараметрыПоказателя.РегулярнаяГрафа.Значение Цикл
			ЗаголовокГрафы = РегулярнаяГрафа.Ключ;
			КодГода = РегулярнаяГрафа.Значение; 
		КонецЦикла;
	КонецЕсли;
	
	ОбщиеПараметрыРасшифровки = Новый Структура;
	ОбщиеПараметрыРасшифровки.Вставить("Организация", КонтекстВыполнения.Организация);
	ОбщиеПараметрыРасшифровки.Вставить("НачалоПериода", ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.НачалоПериодаОтчета, 12*КодГода));
	ОбщиеПараметрыРасшифровки.Вставить("КонецПериода", ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.КонецПериодаОтчета, 12*КодГода));

	СтруктураДДС       = КонтекстВыполнения.Данные[ОбщиеПараметрыРасшифровки.КонецПериода].СтруктураДДС;
	СтруктураНДСвДДС   = КонтекстВыполнения.Данные[ОбщиеПараметрыРасшифровки.КонецПериода].СтруктураНДСвДДС;
	ТаблицаРасшифровки = КонтекстВыполнения.ТаблицаРасшифровки;
	
	ДополнительныеПараметрыРасшифровки = Новый Структура("Организация", КонтекстВыполнения.Организация);
	
	ВыведенПервыйЭлемент = Ложь;
	
	Для Каждого СтрокаСчет Из Алгоритм.Строки Цикл
		
		ИмяОбласти = СтрокаСчет.КодЯчейки + КодГрафы;
		
		ПараметрыПоказателя.ИмяОбластиПоказателя = ИмяОбласти;
		ПараметрыПоказателя.КодГрафы = КодГрафы;

		НайденныеСтрокиПоВидуДДС = СтруктураДДС.НайтиСтроки(Новый Структура("ВидДДС, СчетДДС", СтрокаСчет.ВидДвиженияДенежныхСредств, СтрокаСчет.Счет));
		ТекущееЗначениеПоказателя = 0;

		Для Каждого ЭлементПоказателя Из НайденныеСтрокиПоВидуДДС Цикл
			
			Если НЕ ВыведенПервыйЭлемент Тогда
				ПолноеНаименованиеСтроки = "Строка " + ПараметрыПоказателя.НаименованиеСтроки + """, графа """ + ЗаголовокГрафы + """"; 
				ВыведенПервыйЭлемент = Истина;
			Иначе
				ПолноеНаименованиеСтроки = "";
			КонецЕсли;
	
			Если СтрокаСчет.ТипПоказателя = "ДО" Тогда
				Слагаемое = ЭлементПоказателя.ДО;
				НаименованиеОборота = "Дебетовый";
			ИначеЕсли СтрокаСчет.ТипПоказателя = "КО" Тогда
				Слагаемое = ЭлементПоказателя.КО;
				НаименованиеОборота = "Кредитовый";
			Иначе
				ИмяФункции = "НастраиваемаяБухгалтерскаяОтчетность.ВывестиСтрокуОДДС()";
				СообщениеОшибка = СтрШаблон(НСтр("ru = 'Неверно задана формула для функции %1.'"), ИмяФункции);
				ВызватьИсключение СообщениеОшибка;
			КонецЕсли;
			
			НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(СтрокаСчет.ВидДвиженияДенежныхСредств) + """/";
			ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + Слагаемое;
			
			НастройкиРасшифровки = НастройкиОСВПоВидуДДС(ОбщиеПараметрыРасшифровки, ЭлементПоказателя.СчетДДС, ЭлементПоказателя.ВидДДС);
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "+", НаименованиеСоставляющей, Слагаемое, ИмяРаздела, НастройкиРасшифровки);
			
			// Отражение уменьшения на сумму НДС.
			ОтборСтрокНДС = Новый Структура;
			ОтборСтрокНДС.Вставить("СчетДДС", ЭлементПоказателя.СчетДДС);
			ОтборСтрокНДС.Вставить("ВидДДС", ЭлементПоказателя.ВидДДС);
			СтрокиНДС = СтруктураНДСвДДС.НайтиСтроки(ОтборСтрокНДС);
			
			Если СтрокиНДС.Количество() > 0 Тогда
				Для Каждого СтрокаНДС Из СтрокиНДС Цикл
					Если СтрокаНДС.ДО = 0 И СтрокаНДС.КО = 0 Тогда
						СлагаемоеНДС = 0;
						Если СтрокаСчет.ТипПоказателя = "ДО" Тогда
							СлагаемоеНДС = -СтрокаНДС.ДО_НДС;
						ИначеЕсли СтрокаСчет.ТипПоказателя = "КО" И СтрокаСчет.Вычитаемый Тогда
							СлагаемоеНДС = -СтрокаНДС.КО_НДС;
						КонецЕсли;
						
						Если СлагаемоеНДС < 0 Тогда
							ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + СлагаемоеНДС;
							НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части НДС";
							ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, СлагаемоеНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						КонецЕсли;

					ИначеЕсли СтрокаСчет.ТипПоказателя = "ДО" И СтрокаНДС.ДО < 0 Тогда
						// Сторно выплат комитентам.
						Слагаемое = СтрокаНДС.ДО;
						ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + Слагаемое;
						НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части денежных потоков комиссионеров или агентов";
						ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, Слагаемое, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						
						// Обнаружен НДС по платежам, не связанными с комитентами.
						Если СтрокаНДС.ДО_НДС > 0 Тогда
							СлагаемоеНДС = -СтрокаНДС.ДО_НДС;
							ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + СлагаемоеНДС;
							НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части НДС";
							ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, СлагаемоеНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						КонецЕсли;

					ИначеЕсли СтрокаСчет.ТипПоказателя = "КО" И СтрокаСчет.Вычитаемый И СтрокаНДС.КО < 0 Тогда
						// Сторно выплат комитентам.
						Слагаемое = СтрокаНДС.КО;
						ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + Слагаемое;
						НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части денежных потоков комиссионеров или агентов";
						ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, Слагаемое, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						
						// Обнаружен НДС по платежам, не связанными с комитентами.
						Если СтрокаНДС.КО_НДС > 0 Тогда
							СлагаемоеНДС = -СтрокаНДС.КО_НДС;
							ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + СлагаемоеНДС;
							НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части НДС";
							ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, СлагаемоеНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						КонецЕсли;
						
					КонецЕсли;
				КонецЦикла;
			Иначе
				НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части НДС";
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, 0, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			КонецЕсли	
		КонецЦикла;
		Вычитаемый = СтрокаСчет.ТипПоказателя = "КО" И СтрокаСчет.Вычитаемый;
		ЗначениеПоказателя = ЗначениеПоказателя + ?(Вычитаемый, -1,1)*ТекущееЗначениеПоказателя;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоказателиОДДС_НДС(КонтекстВыполнения, ПараметрыПоказателя, Алгоритм, ЗначениеПоказателя)

	ИмяРаздела = КонтекстВыполнения.КонтекстОтчета.ИмяРаздела;
	
	КодГрафы = ПараметрыПоказателя.РегулярнаяГрафа.Ключ;
	
	ЗаголовокГрафы = "";
	КодГода = 0;
	Если ПараметрыПоказателя.РегулярнаяГрафа <> Неопределено Тогда
		Для Каждого РегулярнаяГрафа Из ПараметрыПоказателя.РегулярнаяГрафа.Значение Цикл
			ЗаголовокГрафы = РегулярнаяГрафа.Ключ;
			КодГода = РегулярнаяГрафа.Значение; 
		КонецЦикла;
	КонецЕсли;
	
	ВидДДСПолучения    = Перечисления.ВидыДвиженийДенежныхСредств.ПрочиеПоступленияПоТекущейДеятельности;
	ВидДДСПеречисления = Перечисления.ВидыДвиженийДенежныхСредств.ПрочиеРасходыПоТекущейДеятельности;
	
	ОбщиеПараметрыРасшифровки = Новый Структура;
	ОбщиеПараметрыРасшифровки.Вставить("Организация", КонтекстВыполнения.Организация);
	ОбщиеПараметрыРасшифровки.Вставить("НачалоПериода", ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.НачалоПериодаОтчета, 12*КодГода));
	ОбщиеПараметрыРасшифровки.Вставить("КонецПериода", ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.КонецПериодаОтчета, 12*КодГода));
	
	СтруктураДДС       = КонтекстВыполнения.Данные[ОбщиеПараметрыРасшифровки.КонецПериода].СтруктураДДС;
	СтруктураНДСвДДС   = КонтекстВыполнения.Данные[ОбщиеПараметрыРасшифровки.КонецПериода].СтруктураНДСвДДС;
	РасчетыПоНДС	   = КонтекстВыполнения.Данные[ОбщиеПараметрыРасшифровки.КонецПериода].РасчетыПоНДС;
	ТаблицаРасшифровки = КонтекстВыполнения.ТаблицаРасшифровки;
	
	ДополнительныеПараметрыРасшифровки = Новый Структура("Организация", КонтекстВыполнения.Организация);
	
	ВыведенПервыйЭлемент = Ложь;
	
	Для Каждого СтрокаСчет Из Алгоритм.Строки Цикл
		
		ИмяОбласти = СтрокаСчет.КодЯчейки + КодГрафы;
		
		ПараметрыПоказателя.ИмяОбластиПоказателя = ИмяОбласти;
		ПараметрыПоказателя.КодГрафы = КодГрафы;
		
		ТекущееЗначениеПоказателя = 0;

		НайденныеСтрокиПоВидуДДС = СтруктураДДС.НайтиСтроки(Новый Структура("ВидДДС, СчетДДС", СтрокаСчет.ВидДвиженияДенежныхСредств, СтрокаСчет.Счет));
		
		Для Каждого ЭлементПоказателя Из НайденныеСтрокиПоВидуДДС Цикл
			
			Если НЕ ВыведенПервыйЭлемент Тогда
				ПолноеНаименованиеСтроки = "Строка " + ПараметрыПоказателя.НаименованиеСтроки + """, графа """ + ЗаголовокГрафы + """"; 
				ВыведенПервыйЭлемент = Истина;
			Иначе
				ПолноеНаименованиеСтроки = "";
			КонецЕсли;
	
			Если СтрокаСчет.ТипПоказателя = "ДО" Тогда
				Слагаемое = ЭлементПоказателя.ДО;
				НаименованиеОборота = "Дебетовый";
			ИначеЕсли СтрокаСчет.ТипПоказателя = "КО" Тогда
				Слагаемое = ЭлементПоказателя.КО;
				НаименованиеОборота = "Кредитовый";
			Иначе
				ИмяФункции = "НастраиваемаяБухгалтерскаяОтчетность.ВывестиСтрокуОДДС()";
				СообщениеОшибка = СтрШаблон(НСтр("ru = 'Неверно задана формула для функции %1.'"), ИмяФункции);
				ВызватьИсключение СообщениеОшибка;
			КонецЕсли;
			
			НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(СтрокаСчет.ВидДвиженияДенежныхСредств) + """/";
			ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + Слагаемое;
			
			НастройкиРасшифровки = НастройкиОСВПоВидуДДС(ОбщиеПараметрыРасшифровки, ЭлементПоказателя.СчетДДС, ЭлементПоказателя.ВидДДС);
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "+", НаименованиеСоставляющей, Слагаемое, ИмяРаздела, НастройкиРасшифровки);
			
			// Отражение уменьшения на сумму НДС.
			ОтборСтрокНДС = Новый Структура;
			ОтборСтрокНДС.Вставить("СчетДДС", ЭлементПоказателя.СчетДДС);
			ОтборСтрокНДС.Вставить("ВидДДС", ЭлементПоказателя.ВидДДС);
			СтрокиНДС = СтруктураНДСвДДС.НайтиСтроки(ОтборСтрокНДС);
			
			Если СтрокиНДС.Количество() > 0 Тогда
				Для Каждого СтрокаНДС Из СтрокиНДС Цикл
					Если СтрокаНДС.ДО = 0 И СтрокаНДС.КО = 0 Тогда
						СлагаемоеНДС = 0;
						Если СтрокаСчет.ТипПоказателя = "ДО" Тогда
							СлагаемоеНДС = -СтрокаНДС.ДО_НДС;
						ИначеЕсли СтрокаСчет.ТипПоказателя = "КО" И СтрокаСчет.Вычитаемый Тогда
							СлагаемоеНДС = -СтрокаНДС.КО_НДС;
						КонецЕсли;
						
						Если СлагаемоеНДС < 0 Тогда
							ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + СлагаемоеНДС;
							НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части НДС";
							ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, СлагаемоеНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						КонецЕсли;

					ИначеЕсли СтрокаСчет.ТипПоказателя = "КО" И СтрокаСчет.Вычитаемый И СтрокаНДС.КО < 0 Тогда
						// Сторно выплат комитентам.
						Слагаемое = СтрокаНДС.КО;
						ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + Слагаемое;
						НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части денежных потоков комиссионеров или агентов";
						ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, Слагаемое, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						
						// Обнаружен НДС по платежам, не связанными с комитентами.
						Если СтрокаНДС.КО_НДС > 0 Тогда
							СлагаемоеНДС = -СтрокаНДС.КО_НДС;
							ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + СлагаемоеНДС;
							НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части НДС";
							ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, СлагаемоеНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						КонецЕсли;
						
					КонецЕсли;
				КонецЦикла;
			Иначе
				НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части НДС";
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, 0, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			КонецЕсли;
			
			// Отражение уменьшения на сумму расчетов по НДС.
			Если ЭлементПоказателя.СчетДДС = Счет("51") Тогда
				Если СтрокаСчет.ТипПоказателя = "ДО" И ЭлементПоказателя.ВидДДС = ВидДДСПолучения Тогда
					Слагаемое = -РасчетыПоНДС.ПолученныйНДС;
					НаименованиеСоставляющей = "Оборот с кредита счета 68.02 в дебет счета 51" + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """";
				ИначеЕсли СтрокаСчет.ТипПоказателя = "КО" И СтрокаСчет.Вычитаемый И ЭлементПоказателя.ВидДДС = ВидДДСПеречисления Тогда
					Слагаемое = -РасчетыПоНДС.ПеречисленныйНДС;
					НаименованиеСоставляющей = "Оборот с кредита счета 51 в дебет счета 68.02" + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """";
				КонецЕсли;
				ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + Слагаемое;
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
					"-", НаименованиеСоставляющей,
					Слагаемое, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗначениеПоказателя = ЗначениеПоказателя + ТекущееЗначениеПоказателя;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьПоказателиОДДС_НДС(КонтекстВыполнения, ПараметрыПоказателя)
	
	ИмяРаздела = КонтекстВыполнения.КонтекстОтчета.ИмяРаздела;
	
	КодГода = 0;
	Если ПараметрыПоказателя.РегулярнаяГрафа <> Неопределено Тогда
		Для Каждого РегулярнаяГрафа Из ПараметрыПоказателя.РегулярнаяГрафа.Значение Цикл
			КодГода = РегулярнаяГрафа.Значение;
			ЗаголовокГрафы = РегулярнаяГрафа.Ключ;
		КонецЦикла;
	КонецЕсли;
		
	ОбщиеПараметрыРасшифровки = Новый Структура;
	ОбщиеПараметрыРасшифровки.Вставить("Организация", КонтекстВыполнения.Организация);
	ОбщиеПараметрыРасшифровки.Вставить("НачалоПериода", ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.НачалоПериодаОтчета, 12*КодГода));
	ОбщиеПараметрыРасшифровки.Вставить("КонецПериода", ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.КонецПериодаОтчета, 12*КодГода));
	
	ВидДДСПолучения    = Перечисления.ВидыДвиженийДенежныхСредств.ПрочиеПоступленияПоТекущейДеятельности;
	ВидДДСПеречисления = Перечисления.ВидыДвиженийДенежныхСредств.ПрочиеРасходыПоТекущейДеятельности;

	СтруктураНДСвДДС   = КонтекстВыполнения.Данные[ОбщиеПараметрыРасшифровки.КонецПериода].СтруктураНДСвДДС;
	РасчетыПоНДС	   = КонтекстВыполнения.Данные[ОбщиеПараметрыРасшифровки.КонецПериода].РасчетыПоНДС;
	ТаблицаРасшифровки = КонтекстВыполнения.ТаблицаРасшифровки;
	РасчетыПоВалютноОбменнымОперациям  = КонтекстВыполнения.Данные[ОбщиеПараметрыРасшифровки.КонецПериода].РасчетыПоВалютноОбменнымОперациям;
	
	ДополнительныеПараметрыРасшифровки = Новый Структура("Организация", КонтекстВыполнения.Организация);
	
	ЗначениеПоказателя = ПараметрыПоказателя.ЗначениеПоказателя;
	ПолноеНаименованиеСтроки = "Строка " + ПараметрыПоказателя.НаименованиеСтроки + """, графа """ + ЗаголовокГрафы + """";
	ИмяОбласти = ПараметрыПоказателя.ИмяОбластиПоказателя;

	Если ПараметрыПоказателя.КодСтроки = "4119" Тогда
		ФормулаРасчета = "ДО";
	Иначе
		ФормулаРасчета = "-КО";
	КонецЕсли;	
		
	// Отражение свернутого НДС.
	НДСПоставщиков = СтруктураНДСвДДС.Итог("КО_НДС");
	НДСПокупателей = СтруктураНДСвДДС.Итог("ДО_НДС");
	
	СвернутыйНДС = НДСПокупателей - НДСПоставщиков - РасчетыПоНДС.ПеречисленныйНДС + РасчетыПоНДС.ПолученныйНДС;
	
	// Отражение свернутого показателя (выгоды или потери) от валютно-обменных операций
	ЕстьВалютоОбменныеОперации = РасчетыПоВалютноОбменнымОперациям.Количество() > 0;
	Если ЕстьВалютоОбменныеОперации Тогда
		ВалютоОбменДО               = РасчетыПоВалютноОбменнымОперациям.Итог("ВалютоОбменДО");
		ВалютоОбменКО               = РасчетыПоВалютноОбменнымОперациям.Итог("ВалютоОбменКО");
		ВалютоОбменПродажДельта     = РасчетыПоВалютноОбменнымОперациям.Итог("ВалютоОбменДельта");
		ВыгодаПотеряВалютногоОбмена = ВалютоОбменДО - ВалютоОбменКО - ВалютоОбменПродажДельта;
	КонецЕсли;
	
	Если ФормулаРасчета = "ДО" Тогда
		Если СвернутыйНДС > 0 Тогда
			// Превышение полученного НДС над перечисленным НДС.
			НаименованиеСоставляющей = НСтр("ru = 'Сумма НДС начисленного к уплате при расчетах с покупателями'");
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
				"+", НаименованиеСоставляющей,
				НДСПокупателей, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			
			НаименованиеСоставляющей = НСтр("ru = 'Сумма НДС начисленного к возмещению при расчетах с поставщиками'");
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
				"-", НаименованиеСоставляющей,
				-НДСПоставщиков, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			
			НаименованиеСоставляющей = СтрШаблон(НСтр("ru = 'Сумма НДС перечисленного в бюджет (Оборот с кредита счета 51 в дебет счета 68.02, по виду движения денежных средств %1)'"),
				Строка(ВидДДСПеречисления));
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
			"-", НаименованиеСоставляющей,
				-РасчетыПоНДС.ПеречисленныйНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			
			НаименованиеСоставляющей = СтрШаблон(НСтр("ru = 'Сумма НДС полученного из бюджета (Оборот с кредита счета 68.02 в дебет счета 51, по виду движения денежных средств %1)'"),
				Строка(ВидДДСПолучения));
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
				"+", НаименованиеСоставляющей,
				РасчетыПоНДС.ПолученныйНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			
			ЗначениеПоказателя = ЗначениеПоказателя + СвернутыйНДС;
			
		КонецЕсли;
		
		Если ЕстьВалютоОбменныеОперации Тогда
			Если ВыгодаПотеряВалютногоОбмена > 0 Тогда
				ЗначениеПоказателяУсловное = ЗначениеПоказателя - ВалютоОбменКО;
			Иначе
				ЗначениеПоказателяУсловное = ЗначениеПоказателя - ВалютоОбменДО;
			КонецЕсли;
			
			Если ЗначениеПоказателяУсловное >= 0 Тогда
				НаименованиеСоставляющей = НСтр("ru = 'Сумма прочих платежей по текущим операциям в части валютно-обменных операций'");
				Если ВыгодаПотеряВалютногоОбмена > 0 Тогда
					ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
						"-", НаименованиеСоставляющей,
						-ВалютоОбменКО, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
				Иначе
					ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
						"-", НаименованиеСоставляющей,
						-ВалютоОбменДО, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
				КонецЕсли;
				
				ЗначениеПоказателя = ЗначениеПоказателяУсловное;
				
			Иначе
				// Разница получилась отрицательной, она должна отразиться в строке 4129
				НаименованиеСоставляющей = НСтр("ru = 'Сумма прочих поступлений по текущим операциям в части валютно-обменных операций'");
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
					"-", НаименованиеСоставляющей,
					-ЗначениеПоказателя, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
				
				ЗначениеПоказателя = 0;
				
				РасчетыПоВалютноОбменнымОперациям[0].ВалютоОбменДельта = ЗначениеПоказателяУсловное;
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ФормулаРасчета = "-КО" Тогда
		Если СвернутыйНДС <= 0 Тогда
			// Превышение перечисленного НДС над полученным НДС.
			НаименованиеСоставляющей = НСтр("ru = 'Сумма НДС начисленного к уплате при расчетах с покупателями'");
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
				"-", НаименованиеСоставляющей,
				-НДСПокупателей, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			
			НаименованиеСоставляющей = НСтр("ru = 'Сумма НДС начисленного к возмещению при расчетах с поставщиками'");
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
				"+", НаименованиеСоставляющей,
				НДСПоставщиков, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			
			НаименованиеСоставляющей = СтрШаблон(НСтр("ru = 'Сумма НДС перечисленного в бюджет (Оборот с кредита счета 51 в дебет счета 68.02, по виду движения денежных средств %1)'"),
				Строка(ВидДДСПеречисления));
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
				"+", НаименованиеСоставляющей,
				РасчетыПоНДС.ПеречисленныйНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			
			НаименованиеСоставляющей = СтрШаблон(НСтр("ru = 'Сумма НДС полученного из бюджета (Оборот с кредита счета 68.02 в дебет счета 51, по виду движения денежных средств %1)'"),
				Строка(ВидДДСПолучения));
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
				"-", НаименованиеСоставляющей,
				-РасчетыПоНДС.ПолученныйНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			
			ЗначениеПоказателя = ЗначениеПоказателя - СвернутыйНДС;
			
		КонецЕсли;
		
		Если ЕстьВалютоОбменныеОперации Тогда
			Если ВыгодаПотеряВалютногоОбмена < 0 Тогда
				ЗначениеПоказателя = ЗначениеПоказателя - ВалютоОбменДО - ВалютоОбменПродажДельта;
				
				НаименованиеСоставляющей = НСтр("ru = 'Сумма прочих поступлений по текущим операциям в части валютно-обменных операций'");
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
					"-", НаименованиеСоставляющей,
					-(ВалютоОбменДО + ВалютоОбменПродажДельта), ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			Иначе
				ЗначениеПоказателя = ЗначениеПоказателя - ВалютоОбменКО - ВалютоОбменПродажДельта;
				
				НаименованиеСоставляющей = НСтр("ru = 'Сумма прочих платежей по текущим операциям в части валютно-обменных операций'");
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки,
					"-", НаименованиеСоставляющей,
					-(ВалютоОбменКО + ВалютоОбменПродажДельта), ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Вычитаемый = ФормулаРасчета = "-КО";
	ПараметрыПоказателя.ЗначениеПоказателя = ?(Вычитаемый, -1, 1)*ЗначениеПоказателя; 
	
КонецПроцедуры

Процедура ЗаполнитьПоказателиОДДС_Агентские(КонтекстВыполнения, ПараметрыПоказателя, Алгоритм, ЗначениеПоказателя)
		
	ИмяРаздела = КонтекстВыполнения.КонтекстОтчета.ИмяРаздела;
	
	КодГрафы = ПараметрыПоказателя.РегулярнаяГрафа.Ключ;
	
	ЗаголовокГрафы = "";
	КодГода = 0;
	Если ПараметрыПоказателя.РегулярнаяГрафа <> Неопределено Тогда
		Для Каждого РегулярнаяГрафа Из ПараметрыПоказателя.РегулярнаяГрафа.Значение Цикл
			ЗаголовокГрафы = РегулярнаяГрафа.Ключ;
			КодГода = РегулярнаяГрафа.Значение; 
		КонецЦикла;
	КонецЕсли;
	
	ОбщиеПараметрыРасшифровки = Новый Структура;
	ОбщиеПараметрыРасшифровки.Вставить("Организация", КонтекстВыполнения.Организация);
	ОбщиеПараметрыРасшифровки.Вставить("НачалоПериода", ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.НачалоПериодаОтчета, 12*КодГода));
	ОбщиеПараметрыРасшифровки.Вставить("КонецПериода", ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.КонецПериодаОтчета, 12*КодГода));

	СтруктураДДС       = КонтекстВыполнения.Данные[ОбщиеПараметрыРасшифровки.КонецПериода].СтруктураДДС;
	СтруктураНДСвДДС   = КонтекстВыполнения.Данные[ОбщиеПараметрыРасшифровки.КонецПериода].СтруктураНДСвДДС;
	ТаблицаРасшифровки = КонтекстВыполнения.ТаблицаРасшифровки;
	
	ДополнительныеПараметрыРасшифровки = Новый Структура("Организация", КонтекстВыполнения.Организация);
	
	ВыведенПервыйЭлемент = Ложь;
	
	Для Каждого СтрокаСчет Из Алгоритм.Строки Цикл
		
		ИмяОбласти = СтрокаСчет.КодЯчейки + КодГрафы;
		
		ПараметрыПоказателя.ИмяОбластиПоказателя = ИмяОбласти;
		ПараметрыПоказателя.КодГрафы = КодГрафы;
		
		ТекущееЗначениеПоказателя = 0;

		НайденныеСтрокиПоВидуДДС = СтруктураДДС.НайтиСтроки(Новый Структура("ВидДДС, СчетДДС", СтрокаСчет.ВидДвиженияДенежныхСредств, СтрокаСчет.Счет));
		
		Для Каждого ЭлементПоказателя Из НайденныеСтрокиПоВидуДДС Цикл
			
			Если НЕ ВыведенПервыйЭлемент Тогда
				ПолноеНаименованиеСтроки = "Строка " + ПараметрыПоказателя.НаименованиеСтроки + """, графа """ + ЗаголовокГрафы + """"; 
				ВыведенПервыйЭлемент = Истина;
			Иначе
				ПолноеНаименованиеСтроки = "";
			КонецЕсли;

			Если СтрокаСчет.ТипПоказателя = "ДО" И НЕ СтрокаСчет.Вычитаемый Тогда
				Слагаемое = ЭлементПоказателя.ДО;
				НаименованиеОборота = "Дебетовый";
			ИначеЕсли СтрокаСчет.ТипПоказателя = "ДО" И СтрокаСчет.Вычитаемый Тогда
				Слагаемое = -ЭлементПоказателя.ДО;
				НаименованиеОборота = "Дебетовый";
			ИначеЕсли СтрокаСчет.ТипПоказателя = "КО" И СтрокаСчет.Вычитаемый Тогда
				Слагаемое = ЭлементПоказателя.КО;
				НаименованиеОборота = "Кредитовый";
			ИначеЕсли СтрокаСчет.ТипПоказателя = "КО" И НЕ СтрокаСчет.Вычитаемый Тогда
				Слагаемое = -ЭлементПоказателя.КО;
				НаименованиеОборота = "Кредитовый";
			Иначе
				ИмяФункции = "НастраиваемаяБухгалтерскаяОтчетность.ВывестиСтрокуОДДС()";
				СообщениеОшибка = СтрШаблон(НСтр("ru = 'Неверно задана формула для функции %1.'"), ИмяФункции);
				ВызватьИсключение СообщениеОшибка;
			КонецЕсли;
			
			НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(СтрокаСчет.ВидДвиженияДенежныхСредств) + """/";
			ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + Слагаемое;
			
			НастройкиРасшифровки = НастройкиОСВПоВидуДДС(ОбщиеПараметрыРасшифровки, ЭлементПоказателя.СчетДДС, ЭлементПоказателя.ВидДДС);
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "+", НаименованиеСоставляющей, Слагаемое, ИмяРаздела, НастройкиРасшифровки);
			
			// Отражение уменьшения на сумму НДС.
			ОтборСтрокНДС = Новый Структура;
			ОтборСтрокНДС.Вставить("СчетДДС", ЭлементПоказателя.СчетДДС);
			ОтборСтрокНДС.Вставить("ВидДДС", ЭлементПоказателя.ВидДДС);
			СтрокиНДС = СтруктураНДСвДДС.НайтиСтроки(ОтборСтрокНДС);
			
			Если СтрокиНДС.Количество() > 0 Тогда
				Для Каждого СтрокаНДС Из СтрокиНДС Цикл
					Если СтрокаНДС.ДО = 0 И СтрокаНДС.КО = 0 Тогда
						
						ЗнакНДС = -1;
						Если СтрокаСчет.ТипПоказателя = "ДО" И НЕ СтрокаСчет.Вычитаемый Тогда
							СлагаемоеНДС = СтрокаНДС.ДО_НДС;
						ИначеЕсли СтрокаСчет.ТипПоказателя = "ДО" И СтрокаСчет.Вычитаемый Тогда
							СлагаемоеНДС = СтрокаНДС.ДО_НДС;
							ЗнакНДС = 1;
						ИначеЕсли СтрокаСчет.ТипПоказателя = "КО" И СтрокаСчет.Вычитаемый Тогда
							СлагаемоеНДС = СтрокаНДС.КО_НДС;
						ИначеЕсли СтрокаСчет.ТипПоказателя = "КО" Тогда
							СлагаемоеНДС = СтрокаНДС.КО_НДС;
							ЗнакНДС = 1;
						КонецЕсли;
						
						Если СлагаемоеНДС > 0 Тогда
							ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + ЗнакНДС * СлагаемоеНДС;
							НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части НДС";
							ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, ЗнакНДС * СлагаемоеНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						КонецЕсли;
	
					ИначеЕсли СтрокаСчет.ТипПоказателя = "ДО" И СтрокаНДС.ДО < 0 Тогда
						// Сторно выплат комитентам.
						Слагаемое = СтрокаНДС.ДО;
						ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + Слагаемое;
						НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части денежных потоков комиссионеров или агентов";
						ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, Слагаемое, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						
						// Обнаружен НДС по платежам, не связанными с комитентами.
						Если СтрокаНДС.ДО_НДС > 0 Тогда
							СлагаемоеНДС = -СтрокаНДС.ДО_НДС;
							ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + СлагаемоеНДС;
							НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части НДС";
							ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, СлагаемоеНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						КонецЕсли;
	
					ИначеЕсли  СтрокаСчет.ТипПоказателя = "КО" И СтрокаСчет.Вычитаемый И СтрокаНДС.КО < 0 Тогда

						// Сторно выплат комитентам.
						Слагаемое = СтрокаНДС.КО;
						ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + Слагаемое;
						НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части денежных потоков комиссионеров или агентов";
						ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, Слагаемое, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						
						// Обнаружен НДС по платежам, не связанными с комитентами.
						Если СтрокаНДС.КО_НДС > 0 Тогда
							СлагаемоеНДС = -СтрокаНДС.КО_НДС;
							ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + СлагаемоеНДС;
							НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части НДС";
							ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, СлагаемоеНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
						КонецЕсли;	

					КонецЕсли;
				КонецЦикла;
			Иначе
				НаименованиеСоставляющей = НаименованиеОборота + " оборот по счету " + СтрокаСчет.СчетКод + " /вид движения """ + Строка(ЭлементПоказателя.ВидДДС) + """ в части НДС";
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, 0, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			КонецЕсли;
		КонецЦикла;
		
		ЗначениеПоказателя = ЗначениеПоказателя + ТекущееЗначениеПоказателя;

	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьПоказателиОДДС_Агентские(КонтекстВыполнения, ПараметрыПоказателя)
		
	ИмяРаздела = КонтекстВыполнения.КонтекстОтчета.ИмяРаздела;

	КодГода = 0;
	Если ПараметрыПоказателя.РегулярнаяГрафа <> Неопределено Тогда
		Для Каждого РегулярнаяГрафа Из ПараметрыПоказателя.РегулярнаяГрафа.Значение Цикл
			КодГода = РегулярнаяГрафа.Значение; 
			ЗаголовокГрафы = РегулярнаяГрафа.Ключ;
		КонецЦикла;
	КонецЕсли;
	
	ОбщиеПараметрыРасшифровки = Новый Структура;
	ОбщиеПараметрыРасшифровки.Вставить("Организация", КонтекстВыполнения.Организация);
	ОбщиеПараметрыРасшифровки.Вставить("НачалоПериода", ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.НачалоПериодаОтчета, 12*КодГода));
	ОбщиеПараметрыРасшифровки.Вставить("КонецПериода", ДобавитьМесяц(КонтекстВыполнения.КонтекстОтчета.КонецПериодаОтчета, 12*КодГода));
	
	СтруктураНДСвДДС   = КонтекстВыполнения.Данные[ОбщиеПараметрыРасшифровки.КонецПериода].СтруктураНДСвДДС;
	ТаблицаРасшифровки = КонтекстВыполнения.ТаблицаРасшифровки;
	
	ДополнительныеПараметрыРасшифровки = Новый Структура("Организация", КонтекстВыполнения.Организация);
	
	ТекущееЗначениеПоказателя = 0;
	ПолноеНаименованиеСтроки = "Строка " + ПараметрыПоказателя.НаименованиеСтроки + """, графа """ + ЗаголовокГрафы + """";;
	ИмяОбласти = ПараметрыПоказателя.ИмяОбластиПоказателя;

	Если ПараметрыПоказателя.КодСтроки = "4111" Тогда
		ФормулаРасчета = "ДО";
	Иначе
		ФормулаРасчета = "-КО";
	КонецЕсли;
		
	// Сторно платежей комитентам.
	Для Каждого СтрокаНДС Из СтруктураНДСвДДС Цикл

		Если ЗначениеЗаполнено(СтрокаНДС.СчетДДС) Тогда
			Продолжить;
		КонецЕсли;
	
		Если ФормулаРасчета = "ДО" И СтрокаНДС.ДО < 0 Тогда
			Слагаемое = СтрокаНДС.ДО;
			СлагаемоеНДС = -СтрокаНДС.ДО_НДС;
			
			ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + Слагаемое;
			НаименованиеСоставляющей = "Поступление денежных средств в части денежных потоков комиссионеров или агентов";
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, Слагаемое, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			
			ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + СлагаемоеНДС;
			НаименованиеСоставляющей = "Поступление денежных средств в части денежных потоков комиссионеров или агентов в части НДС";
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "+", НаименованиеСоставляющей, СлагаемоеНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);

		ИначеЕсли ФормулаРасчета = "-КО" И СтрокаНДС.КО < 0 Тогда
			Слагаемое = СтрокаНДС.КО;
			СлагаемоеНДС = -СтрокаНДС.КО_НДС;
			
			ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + Слагаемое;
			НаименованиеСоставляющей = "Платежи денежных средств в части денежных потоков комиссионеров или агентов";
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "-", НаименованиеСоставляющей, Слагаемое, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			
			ТекущееЗначениеПоказателя = ТекущееЗначениеПоказателя + СлагаемоеНДС;
			НаименованиеСоставляющей = "Платежи денежных средств в части денежных потоков комиссионеров или агентов в части НДС";
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, ИмяОбласти, ПолноеНаименованиеСтроки, "+", НаименованиеСоставляющей, СлагаемоеНДС, ИмяРаздела, ДополнительныеПараметрыРасшифровки);
			
		КонецЕсли;
	КонецЦикла;
	
	Вычитаемый = ФормулаРасчета = "-КО";
	ПараметрыПоказателя.ЗначениеПоказателя = ?(Вычитаемый, -1,1)*(ПараметрыПоказателя.ЗначениеПоказателя + ТекущееЗначениеПоказателя); 
	
КонецПроцедуры

#КонецОбласти

#Область Пояснения5

Процедура ЗаполнитьФормуПояснения5(КонтекстВыполнения, ЗаполнятьПредыдущиеПериоды)
	
	НачалоПериодаОтчета = КонтекстВыполнения.КонтекстОтчета.НачалоПериодаОтчета;
	КонецПериодаОтчета = КонтекстВыполнения.КонтекстОтчета.КонецПериодаОтчета;
	Организация = КонтекстВыполнения.Организация;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтрокиБухОтчетности.КодСтроки КАК КодСтроки,
		|	СтрокиБухОтчетности.НаименованиеРасшифровки КАК НаименованиеРасшифровки,
		|	СтрокиБухОтчетности.КодЯчейки КАК КодЯчейки,
		|	АлгоритмыСбораДанныхБухОтчетностиСчета.Счет КАК Счет,
		|	АлгоритмыСбораДанныхБухОтчетностиСчета.Счет.Вид КАК СчетВид,
		|	АлгоритмыСбораДанныхБухОтчетностиСчета.Счет.Долгосрочный КАК СчетДолгосрочный,
		|	СтрокиБухОтчетности.КодСтрокиПрошлогоПериода КАК КодСтрокиПрошлогоПериода,
		|	СтрокиБухОтчетности.КодЯчейкиПрошлогоПериода КАК КодЯчейкиПрошлогоПериода,
		|	ВЫБОР
		|		КОГДА ПОДСТРОКА(СтрокиБухОтчетности.Родитель.Наименование, 1, 3) = ""5.1""
		|			ТОГДА ""Д""
		|		КОГДА ПОДСТРОКА(СтрокиБухОтчетности.Родитель.Наименование, 1, 3) = ""5.3""
		|			ТОГДА ""К""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ВидЗадолженности
		|ПОМЕСТИТЬ ДанныеСтрок
		|ИЗ
		|	Справочник.СтрокиБухОтчетности КАК СтрокиБухОтчетности
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.АлгоритмыСбораДанныхБухОтчетности.Счета КАК АлгоритмыСбораДанныхБухОтчетностиСчета
		|		ПО СтрокиБухОтчетности.Ссылка = АлгоритмыСбораДанныхБухОтчетностиСчета.Ссылка.Владелец
		|ГДЕ
		|	СтрокиБухОтчетности.ФормаОтчетности = &ФормаОтчетности
		|	И НЕ СтрокиБухОтчетности.ПометкаУдаления
		|	И НЕ АлгоритмыСбораДанныхБухОтчетностиСчета.Ссылка.ПометкаУдаления
		|	И СтрокиБухОтчетности.Владелец = &Настройка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Остатки.Счет КАК Счет,
		|	МАКСИМУМ(Остатки.Вид) КАК Вид
		|ПОМЕСТИТЬ ВидыАПСчетов
		|ИЗ
		|	(ВЫБРАТЬ
		|		ХозрасчетныйОстатки.Счет КАК Счет,
		|		ВЫБОР
		|			КОГДА ХозрасчетныйОстатки.СуммаОстаток > 0
		|				ТОГДА ""А""
		|			ИНАЧЕ ""П""
		|		КОНЕЦ КАК Вид,
		|		ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
		|		ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
		|		ХозрасчетныйОстатки.Субконто3 КАК Субконто3,
		|		ХозрасчетныйОстатки.Валюта КАК Валюта,
		|		ХозрасчетныйОстатки.Подразделение КАК Подразделение,
		|		ХозрасчетныйОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности
		|	ИЗ
		|		РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.АктивноПассивный), , Организация = &Организация) КАК ХозрасчетныйОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ХозрасчетныйОстатки.Счет,
		|		ВЫБОР
		|			КОГДА ХозрасчетныйОстатки.СуммаОстаток > 0
		|				ТОГДА ""А""
		|			ИНАЧЕ ""П""
		|		КОНЕЦ,
		|		ХозрасчетныйОстатки.Субконто1,
		|		ХозрасчетныйОстатки.Субконто2,
		|		ХозрасчетныйОстатки.Субконто3,
		|		ХозрасчетныйОстатки.Валюта,
		|		ХозрасчетныйОстатки.Подразделение,
		|		ХозрасчетныйОстатки.НаправлениеДеятельности
		|	ИЗ
		|		РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецПериода, Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.АктивноПассивный), , Организация = &Организация) КАК ХозрасчетныйОстатки) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Остатки.Вид) = 1
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеСтрок.Счет КАК Счет
		|ПОМЕСТИТЬ Счета
		|ИЗ
		|	ДанныеСтрок КАК ДанныеСтрок
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	План.Ссылка КАК Счет
		|ПОМЕСТИТЬ СчетаРБП
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК План
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
		|		ПО (ХозрасчетныйВидыСубконто.Ссылка = План.Ссылка)
		|			И (ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов))
		|ГДЕ
		|	План.Ссылка В
		|			(ВЫБРАТЬ
		|				ДанныеСтрок.Счет КАК Счет
		|			ИЗ
		|				ДанныеСтрок КАК ДанныеСтрок)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеСтрок.Счет КАК Счет
		|ПОМЕСТИТЬ СчетаРезервов
		|ИЗ
		|	ДанныеСтрок КАК ДанныеСтрок
		|ГДЕ
		|	ДанныеСтрок.Счет В ИЕРАРХИИ(&СчетРезервов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеСтрок.Счет КАК Счет
		|ПОМЕСТИТЬ АСчета
		|ИЗ
		|	ДанныеСтрок КАК ДанныеСтрок
		|ГДЕ
		|	(ДанныеСтрок.СчетВид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|			ИЛИ ДанныеСтрок.Счет В
		|				(ВЫБРАТЬ
		|					ВидыАПСчетов.Счет КАК Счет
		|				ИЗ
		|					ВидыАПСчетов КАК ВидыАПСчетов
		|				ГДЕ
		|					ВидыАПСчетов.Вид = ""А""))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеСтрок.Счет КАК Счет
		|ПОМЕСТИТЬ ПСчета
		|ИЗ
		|	ДанныеСтрок КАК ДанныеСтрок
		|ГДЕ
		|	(ДанныеСтрок.СчетВид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|			ИЛИ ДанныеСтрок.Счет В
		|				(ВЫБРАТЬ
		|					ВидыАПСчетов.Счет КАК Счет
		|				ИЗ
		|					ВидыАПСчетов КАК ВидыАПСчетов
		|				ГДЕ
		|					ВидыАПСчетов.Вид = ""П""))
		|	И НЕ ДанныеСтрок.Счет В ИЕРАРХИИ (&СчетРезервов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеСтрок.Счет КАК Счет
		|ПОМЕСТИТЬ АПСчета
		|ИЗ
		|	ДанныеСтрок КАК ДанныеСтрок
		|ГДЕ
		|	ДанныеСтрок.СчетВид = ЗНАЧЕНИЕ(ВидСчета.АктивноПассивный)
		|	И НЕ ДанныеСтрок.Счет В
		|				(ВЫБРАТЬ
		|					ВидыАПСчетов.Счет КАК Счет
		|				ИЗ
		|					ВидыАПСчетов КАК ВидыАПСчетов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеСтрок.КодСтроки КАК КодСтроки,
		|	ДанныеСтрок.НаименованиеРасшифровки КАК НаименованиеРасшифровки,
		|	ДанныеСтрок.КодЯчейки КАК КодЯчейки,
		|	ДанныеСтрок.КодСтрокиПрошлогоПериода КАК КодСтрокиПрошлогоПериода,
		|	ДанныеСтрок.КодЯчейкиПрошлогоПериода КАК КодЯчейкиПрошлогоПериода,
		|	ДанныеСтрок.Счет КАК Счет,
		|	ДанныеСтрок.СчетВид КАК СчетВид,
		|	ДанныеСтрок.СчетДолгосрочный КАК СчетДолгосрочный,
		|	НЕ СчетаРезервов.Счет ЕСТЬ NULL КАК СчетРезервов,
		|	ДанныеСтрок.ВидЗадолженности КАК ВидЗадолженности
		|ИЗ
		|	ДанныеСтрок КАК ДанныеСтрок
		|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаРезервов КАК СчетаРезервов
		|		ПО ДанныеСтрок.Счет = СчетаРезервов.Счет
		|ИТОГИ ПО
		|	КодСтроки";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериодаОтчета);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериодаОтчета);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Настройка", КонтекстВыполнения.НастройкаЗаполненияБухОтчетности);
	Запрос.УстановитьПараметр("ФормаОтчетности", КонтекстВыполнения.КонтекстОтчета.ФормаОтчетности);
	Запрос.УстановитьПараметр("СчетРезервов", ПланыСчетов.Хозрасчетный.РезервыПоСомнительнымДолгам);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеКЗаполнению = ПолучитьДанныеДляЗаполненияПояснений5(
							Организация,
							НачалоПериодаОтчета,
							КонецПериодаОтчета,
							Запрос.МенеджерВременныхТаблиц);
		
	Если ЗаполнятьПредыдущиеПериоды Тогда
		ДанныеКЗаполнениюПрошлыйПериод = ПолучитьДанныеДляЗаполненияПояснений5(
											Организация,
											ДобавитьМесяц(НачалоПериодаОтчета, -12),
											ДобавитьМесяц(КонецПериодаОтчета, -12),
											Запрос.МенеджерВременныхТаблиц);
	КонецЕсли;
		
	Строки = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Для Каждого Строка Из Строки.Строки Цикл
		СтрокаДанных = Строка.Строки[0];
		
		ЗаполнитьСтрокуПояснений5(КонтекстВыполнения, СтрокаДанных.КодСтроки, СтрокаДанных.КодЯчейки, ДанныеКЗаполнению, Строка.Строки);
		
		Если ЗаполнятьПредыдущиеПериоды И ЗначениеЗаполнено(СтрокаДанных.КодСтрокиПрошлогоПериода) Тогда
			ЗаполнитьСтрокуПояснений5(КонтекстВыполнения, СтрокаДанных.КодСтрокиПрошлогоПериода,
				СтрокаДанных.КодЯчейкиПрошлогоПериода, ДанныеКЗаполнениюПрошлыйПериод, Строка.Строки);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуПояснений5(КонтекстВыполнения, КодСтроки, КодЯчейки, ДанныеЗаполнения, ДанныеСтроки)
	
	ИмяРаздела = КонтекстВыполнения.КонтекстОтчета.ИмяРаздела;
	ТаблицаРасшифровки = КонтекстВыполнения.ТаблицаРасшифровки;
	РассчитанныеПоказатели = КонтекстВыполнения.РассчитанныеПоказатели;
	
	ЗаголовкиГраф51 = ПолучитьЗаголовкиГраф51Пояснений5();
	ЗаголовкиГраф53 = ПолучитьЗаголовкиГраф53Пояснений5();
	
	РасшифровкаЗаголовок = НСтр("ru = 'Строка %1, графа ""%2""'");
	
	ИтогНачальноеСальдо = 0;
	ИтогКонечноеСальдо  = 0;
	ИтогОборотДт        = 0;
	ИтогОборотКт        = 0;
	ИтогШтраф           = 0;
	ИтогПроценты        = 0;
	ИтогПени            = 0;
	ИтогСписание        = 0;
	ИтогРезервыНач      = 0;
	ИтогРезервыКон      = 0;
	ИтогРезервыОборот   = 0;
	ИтогПеревод         = 0;
	
	Для Каждого СтрокаСчета Из ДанныеСтроки Цикл
		
		СтрокаЗаголовок = ЗаполнитьПараметризируемыеНаименования(СтрокаСчета.НаименованиеРасшифровки, КонтекстВыполнения.КонтекстОтчета);
		
		СчетНачальноеСальдо = 0;
		СчетКонечноеСальдо  = 0;
		СчетОборотДт        = 0;
		СчетОборотКт        = 0;
		
		СчетШтраф           = 0;
		СчетПени            = 0;
		СчетСписание        = 0;
		СчетПеревод         = 0;
		
		РезервыСальдоНач	= 0;
		РезервыСальдоКон    = 0;
		РезервыОборотДт     = 0;
		
		// Находим соответствие в резуьтате
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ВидЗадолженности", СтрокаСчета.ВидЗадолженности);
		СтруктураПоиска.Вставить("Счет", СтрокаСчета.Счет);
		
		СчетаРезультата = ДанныеЗаполнения.НайтиСтроки(СтруктураПоиска);
		
		Если СчетаРезультата.Количество()>0 Тогда
			
			СчетНачальноеСальдо = СчетаРезультата[0].НачальноеСальдо;
			СчетКонечноеСальдо  = СчетаРезультата[0].КонечноеСальдо;
			
			Для Каждого СчетРезультата Из СчетаРезультата Цикл
			
				// Делаем подсчет показателя
				Если СтрокаСчета.СчетВид = ВидСчета.АктивноПассивный Тогда
					
					Если  СтрокаСчета.ВидЗадолженности = "Д" Тогда
						
						Если СчетРезультата.ЭтоШтраф = Истина Тогда
							СчетШтраф = СчетШтраф + СчетРезультата.Дебет;
						КонецЕсли;
						
						Если СчетРезультата.ЭтоПени = Истина Тогда
							СчетПени = СчетПени + СчетРезультата.Дебет;
						КонецЕсли;
							
						Если СчетРезультата.ЭтоПеревод = Истина Тогда
							Если СтрокаСчета.СчетДолгосрочный Тогда
								СчетПеревод = СчетПеревод - СчетРезультата.Дебет;
							Иначе
								СчетПеревод = СчетПеревод + СчетРезультата.Дебет;
							КонецЕсли;
						КонецЕсли;
						
						Если СчетРезультата.ЭтоСписание = Истина Тогда
							СчетСписание = СчетСписание + СчетРезультата.Кредит;
						КонецЕсли;
						
						СчетОборотДт = СчетОборотДт + СчетРезультата.Дебет;
						СчетОборотКт = СчетОборотКт + СчетРезультата.Кредит;
						
					ИначеЕсли СтрокаСчета.ВидЗадолженности = "К" Тогда
						
						Если СчетРезультата.ЭтоШтраф = Истина Тогда
							СчетШтраф = СчетШтраф + СчетРезультата.Кредит;
						КонецЕсли;
						
						Если СчетРезультата.ЭтоПени = Истина Тогда
							СчетПени = СчетПени + СчетРезультата.Кредит;
						КонецЕсли;
							
						Если СчетРезультата.ЭтоПеревод = Истина Тогда
							Если СтрокаСчета.СчетДолгосрочный Тогда
								СчетПеревод = СчетПеревод - СчетРезультата.Кредит;
							Иначе
								СчетПеревод = СчетПеревод + СчетРезультата.Кредит;
							КонецЕсли;
						КонецЕсли;
						
						Если СчетРезультата.ЭтоСписание = Истина Тогда
							СчетСписание = СчетСписание + СчетРезультата.Дебет;
						КонецЕсли;
						
						СчетОборотКт = СчетОборотКт + СчетРезультата.Кредит - СчетПени - СчетШтраф;
						СчетОборотДт = СчетОборотДт + СчетРезультата.Дебет - СчетСписание;
						
					КонецЕсли;
				Иначе
					
					Если СтрокаСчета.СчетРезервов = Истина Тогда
						РезервыСальдоНач = РезервыСальдоНач + СчетРезультата.НачальноеСальдо;
						РезервыСальдоКон = РезервыСальдоКон + СчетРезультата.КонечноеСальдо;
						РезервыОборотДт = РезервыОборотДт + СчетРезультата.Дебет;
						
						СчетНачальноеСальдо = СчетНачальноеСальдо - СчетРезультата.НачальноеСальдо;
						СчетКонечноеСальдо  = СчетКонечноеСальдо - СчетРезультата.КонечноеСальдо;
					КонецЕсли;
					
					СчетШтраф = СчетРезультата.Штраф;
					СчетПени = СчетРезультата.Пени;
					СчетСписание = СчетРезультата.Списание;
					СчетПеревод = СчетРезультата.Перевод;
					
					СчетОборотДт = СчетРезультата.Дебет;
					СчетОборотКт = СчетРезультата.Кредит;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		//Добавим показатели по счету в общие показатели по строке
		ИтогНачальноеСальдо = ИтогНачальноеСальдо + СчетНачальноеСальдо;
		ИтогКонечноеСальдо  = ИтогКонечноеСальдо + СчетКонечноеСальдо;
		ИтогШтраф           = ИтогШтраф  + СчетШтраф + СчетПени;
		ИтогСписание        = ИтогСписание + СчетСписание;
		ИтогПеревод         = ИтогПеревод + СчетПеревод;
		ИтогРезервыНач      = ИтогРезервыНач + РезервыСальдоНач;
		ИтогРезервыКон      = ИтогРезервыКон + РезервыСальдоКон;
		ИтогРезервыОборот   = ИтогРезервыОборот + РезервыОборотДт;
		
		Если СтрокаСчета.ВидЗадолженности = "Д" Тогда
			
			ИтогОборотДт    = ИтогОборотДт + СчетОборотДт - СчетШтраф - СчетПени;
			ИтогОборотКт    = ИтогОборотКт + СчетОборотКт  - СчетСписание;
			
			Если Не СтрокаСчета.СчетРезервов Тогда
				
				Если СтрокаСчета.СчетВид = ВидСчета.Пассивный Тогда
					ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
						КодЯчейки + "04",
						СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["04"]),
						"-",
						СтрШаблон(НСтр("ru = 'Сальдо на начало периода по кредиту счета %1 развернутое по всем субконто'"), СтрокаСчета.Счет),
						СчетНачальноеСальдо,
						ИмяРаздела);
				Иначе
					ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
						КодЯчейки + "04",
						СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["04"]),
						"+",
						СтрШаблон(НСтр("ru = 'Сальдо на начало периода по дебету счета %1 развернутое по всем субконто'"), СтрокаСчета.Счет),
						СчетНачальноеСальдо,
						ИмяРаздела);
				КонецЕсли;
				
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "06",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["06"]),
					"+",
					СтрШаблон(НСтр("ru = 'Оборот по дебету счета %1 развернутый по всем субконто'"), СтрокаСчета.Счет),
					СчетОборотДт,
					ИмяРаздела);
					
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, 
					КодЯчейки + "06",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["06"]),
					"-",
					СтрШаблон(НСтр("ru = 'Оборот с кредита счета 91.01 по статьям субконто ""Прочие доходы и расходы"" с видом ""Штрафы, пени и неустойки к получению (уплате)"" и ""Проценты к получению (уплате)"" в дебет счета %1 развернутое по всем субконто'"), СтрокаСчета.Счет),
					СчетШтраф,
					ИмяРаздела);

				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "06",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["06"]),
					"-",
					СтрШаблон(НСтр("ru = 'Оборот по дебету счета %1 по статьям субконто ""Виды платежей в бюджет (фонды)"" с видами ""Пени: доначислено/уплачено(самостоятельно)"", ""Пени: начислено/уплачено(по акту проверки)"" и ""Штраф: начислено/уплачено""'"), СтрокаСчета.Счет),
					СчетПени,
					ИмяРаздела);
					
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "07",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["07"]),
					"+",
					СтрШаблон(НСтр("ru = 'Оборот с кредита счета 91.01 по статьям субконто ""Прочие доходы и расходы"" с видом ""Штрафы, пени и неустойки к получению (уплате)"" и ""Проценты к получению (уплате)"" в дебет счета %1 развернутое по всем субконто'"), СтрокаСчета.Счет),
					СчетШтраф,
					ИмяРаздела);
					
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "07",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["07"]),
					"+",
					СтрШаблон(НСтр("ru = 'Оборот по дебету счета %1 по статьям субконто ""Виды платежей в бюджет (фонды)"" с видами ""Пени: доначислено/уплачено(самостоятельно)"", ""Пени: начислено/уплачено(по акту проверки)"" и ""Штраф: начислено/уплачено""'"), СтрокаСчета.Счет),
					СчетПени,
					ИмяРаздела);
	
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "08",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["08"]),
					"+",
					СтрШаблон(НСтр("ru = 'Оборот по кредиту счета %1 развернутый по всем субконто'"), СтрокаСчета.Счет),
					СчетОборотКт,
					ИмяРаздела);
					
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "08",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["09"]),
					"-",
					СтрШаблон(НСтр("ru = 'Оборот с кредита счета %1 в дебет счета 91.02 по статьям субконто ""Прочие доходы и расходы"" с видом ""Списание дебеторской(кредиторской) задолженности""'"), СтрокаСчета.Счет),
					СчетСписание,
					ИмяРаздела);
					
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "09",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["09"]),
					"+",
					СтрШаблон(НСтр("ru = 'Оборот с кредита счета %1 в дебет счета 91.02 по статьям субконто ""Прочие доходы и расходы"" с видом ""Списание дебеторской(кредиторской) задолженности""'"), СтрокаСчета.Счет),
					СчетСписание,
					ИмяРаздела);
						
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "11",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["11"]),
					"+",
					СтрШаблон(НСтр("ru = 'Оборот по счету %1 в части реклассификации долгосрочных активов и обязательств'"), СтрокаСчета.Счет),
					СчетПеревод,
					ИмяРаздела);
					
				Если СтрокаСчета.СчетВид = ВидСчета.Пассивный Тогда
					ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
						КодЯчейки + "12",
						СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["14"]),
						"-",
						СтрШаблон(НСтр("ru = 'Сальдо на конец периода по кредиту счета %1 развернутое по всем субконто'"), СтрокаСчета.Счет),
						СчетКонечноеСальдо,
						ИмяРаздела);
				Иначе 
					ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
						КодЯчейки + "12",
						СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["14"]),
						"+",
						СтрШаблон(НСтр("ru = 'Сальдо на конец периода по дебету счета %1 развернутое по всем субконто'"), СтрокаСчета.Счет),
						СчетКонечноеСальдо,
						ИмяРаздела);
				КонецЕсли;
			Иначе  
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "05",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["05"]),
					"+",
					СтрШаблон(НСтр("ru = 'Сальдо на начало периода по кредиту счета %1, развернутое по субконто ""Контрагенты"",""Договоры""'"), СтрокаСчета.Счет),
					РезервыСальдоНач,
					ИмяРаздела);
					
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "10",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["10"]),
					"+",
					СтрШаблон(НСтр("ru = 'Оборот по дебету счета %1'"), СтрокаСчета.Счет),
					РезервыОборотДт,
					ИмяРаздела);
					
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "13",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["15"]),
					"+",
					СтрШаблон(НСтр("ru = 'Сальдо на конец периода по кредиту счета %1, развернутое по субконто ""Контрагенты"",""Договоры""'"), СтрокаСчета.Счет),
					РезервыСальдоКон,
					ИмяРаздела);
			КонецЕсли;
		
		ИначеЕсли СтрокаСчета.ВидЗадолженности = "К" Тогда
			
			ИтогОборотКт    = ИтогОборотКт + СчетОборотКт - СчетШтраф - СчетПени;
			ИтогОборотДт    = ИтогОборотДт + СчетОборотДт  - СчетСписание;
			
			Если СтрокаСчета.СчетВид = ВидСчета.Активный Тогда
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "04",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф53["04"]),
					"-",
					СтрШаблон(НСтр("ru = 'Сальдо на начало периода по дебету счета %1 развернутое по всем субконто'"), СтрокаСчета.Счет),
					СчетНачальноеСальдо,
					ИмяРаздела);
			Иначе 
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "04",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф53["04"]),
					"+",
					СтрШаблон(НСтр("ru = 'Сальдо на начало периода по кредиту счета %1 развернутое по всем субконто'"), СтрокаСчета.Счет),
					СчетНачальноеСальдо,
					ИмяРаздела);
			КонецЕсли;
				
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
				КодЯчейки + "05",
				СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф53["05"]),
				"+",
				СтрШаблон(НСтр("ru = 'Оборот по кредиту счета %1 развернутый по всем субконто'"), СтрокаСчета.Счет),
				СчетОборотКт,
				ИмяРаздела);
				
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
				КодЯчейки + "05",
				СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф53["06"]),
				"-",
				СтрШаблон(НСтр("ru = 'Оборот с кредита счета %1 в дебет счета 91.02 по статьям субконто ""Прочие доходы и расходы"" с видом ""Штрафы, пени и неустойки к получению (уплате)"" развернутое по всем субконто'"), СтрокаСчета.Счет),
				СчетШтраф,
				ИмяРаздела);
				
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
				КодЯчейки + "05",
				СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф53["06"]),
				"-",
				СтрШаблон(НСтр("ru = 'Оборот с кредита счета %1 по статьям субконто ""Виды платежей в бюджет (фонды)"" с видами ""Пени: доначислено/уплачено(самостоятельно)"", ""Пени: начислено/уплачено(по акту проверки)"" и ""Штраф: начислено/уплачено""'"), СтрокаСчета.Счет),
				СчетПени,
				ИмяРаздела);
				
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
				КодЯчейки + "06",
				СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф53["06"]),
				"+",
				СтрШаблон(НСтр("ru = 'Оборот с кредита счета %1 в дебет счета 91.02 по статьям субконто ""Прочие доходы и расходы"" с видом ""Штрафы, пени и неустойки к получению (уплате)"" развернутое по всем субконто'"), СтрокаСчета.Счет),
				СчетШтраф,
				ИмяРаздела);
				
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
				КодЯчейки + "06",
				СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф53["06"]),
				"+",
				СтрШаблон(НСтр("ru = 'Оборот с кредита счета %1 по статьям субконто ""Виды платежей в бюджет (фонды)"" с видами ""Пени: доначислено/уплачено(самостоятельно)"", ""Пени: начислено/уплачено(по акту проверки)"" и ""Штраф: начислено/уплачено""'"), СтрокаСчета.Счет),
				СчетПени,
				ИмяРаздела);
				
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
				КодЯчейки + "07",
				СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф53["07"]),
				"+",
				СтрШаблон(НСтр("ru = 'Оборот по дебету счета %1 развернутый по всем субконто'"), СтрокаСчета.Счет),
				СчетОборотДт,
				ИмяРаздела);
				
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
				КодЯчейки + "07",
				СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф53["08"]),
				"-",
				СтрШаблон(НСтр("ru = 'Оборот с кредита счета 91.01 в дебет счета %1 по статьям субконто  ""Прочие доходы и расходы"" с видом ""Списание дебеторской(кредиторской) задолженности""'"), СтрокаСчета.Счет),
				СчетСписание,
				ИмяРаздела);
				
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
				КодЯчейки + "08",
				СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф53["08"]),
				"+",
				СтрШаблон(НСтр("ru = 'Оборот с кредита счета 91.01 в дебет счета %1 по статьям субконто  ""Прочие доходы и расходы"" с видом ""Списание дебеторской(кредиторской) задолженности""'"), СтрокаСчета.Счет),
				СчетСписание,
				ИмяРаздела);
				
			ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
				КодЯчейки + "09",
				СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф51["09"]),
				"+",
				СтрШаблон(НСтр("ru = 'Оборот по счету %1 в части реклассификации долгосрочных активов и обязательств'"), СтрокаСчета.Счет),
				СчетПеревод,
				ИмяРаздела);
				
			Если СтрокаСчета.СчетВид = ВидСчета.Активный Тогда
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "10",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф53["12"]),
					"-",
					СтрШаблон(НСтр("ru = 'Сальдо на конец периода по дебету счета %1 развернутое по всем субконто'"), СтрокаСчета.Счет),
					СчетКонечноеСальдо,
					ИмяРаздела);
			Иначе 
				ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки,
					КодЯчейки + "10",
					СтрШаблон(РасшифровкаЗаголовок, СтрокаЗаголовок, ЗаголовкиГраф53["12"]),
					"+",
					СтрШаблон(НСтр("ru = 'Сальдо на конец периода по кредиту счета %1 развернутое по всем субконто'"), СтрокаСчета.Счет),
					СчетКонечноеСальдо,
					ИмяРаздела);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеСтроки[0].ВидЗадолженности = "Д" Тогда
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "04", ИтогНачальноеСальдо);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "05", -ИтогРезервыНач);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "06", ИтогОборотДт);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "07", ИтогШтраф);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "08", -ИтогОборотКт);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "09", -ИтогСписание);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "10", -ИтогРезервыОборот);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "11", ИтогПеревод);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "12", ИтогКонечноеСальдо);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "13", -ИтогРезервыКон);
	ИначеЕсли ДанныеСтроки[0].ВидЗадолженности = "К" Тогда
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "04", ИтогНачальноеСальдо);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "05", ИтогОборотКт);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "06", ИтогШтраф);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "07", -ИтогОборотДт);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "08", -ИтогСписание);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "09", ИтогПеревод);
		УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, КодЯчейки + "10", ИтогКонечноеСальдо);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьДанныеДляЗаполненияПояснений5(Организация, НачалоПериода, КонецПериода, МенеджерВременныхТаблиц)
	
	ДанныеЗаполнения = ПолучитьТаблицуДанныхПояснений5();
	
	// Активно-Пассивные счета
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Параметры.Вставить("Организация", Организация); 
	Запрос.Параметры.Вставить("НачалоПериода", НачалоПериода);
	Запрос.Параметры.Вставить("КонецПериода", КонецПериода);
	Запрос.Параметры.Вставить("Дт91_02", ПланыСчетов.Хозрасчетный.ПрочиеРасходы);
	Запрос.Параметры.Вставить("Кт91_01", ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	
	ВидыШтрафов = Новый Массив;
	ВидыШтрафов.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыКПолучениюУплате);
	ВидыШтрафов.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ШтрафыПениНеустойкиКПолучениюУплате);
	Запрос.Параметры.Вставить("ВидыШтрафов", ВидыШтрафов);
	
	Запрос.Параметры.Вставить("Списание", Перечисления.ВидыПрочихДоходовИРасходов.СписаниеДебиторскойКредиторскойЗадолженности);
	Запрос.Параметры.Вставить("СубконтоРБП", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов);
	
	ВидыПени = Новый Массив;
	ВидыПени.Добавить(Перечисления.ВидыПлатежейВГосБюджет.ПениСам);
	ВидыПени.Добавить(Перечисления.ВидыПлатежейВГосБюджет.ПениАкт);
	ВидыПени.Добавить(Перечисления.ВидыПлатежейВГосБюджет.Штраф);
	Запрос.Параметры.Вставить("ВидыПени", ВидыПени);
	
	#Область АПСчета
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.НомерСтроки КАК НомерСтроки,
		|	ХозрасчетныйДвиженияССубконто.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ШтрафыДт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетКт В ИЕРАРХИИ (&Кт91_01)
		|				И СубконтоКт1.ВидПрочихДоходовИРасходов В (&ВидыШтрафов)
		|				И СчетДт В
		|					(ВЫБРАТЬ
		|						АПСчета.Счет КАК Счет
		|					ИЗ
		|						АПСчета КАК АПСчета),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.НомерСтроки КАК НомерСтроки,
		|	ХозрасчетныйДвиженияССубконто.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ШтрафыКт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетДт В ИЕРАРХИИ (&Дт91_02)
		|				И СубконтоКт1.ВидПрочихДоходовИРасходов В (&ВидыШтрафов)
		|				И СчетКт В
		|					(ВЫБРАТЬ
		|						АПСчета.Счет КАК Счет
		|					ИЗ
		|						АПСчета КАК АПСчета),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.Регистратор КАК Регистратор,
		|	ХозрасчетныйДвиженияССубконто.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ПениДт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетДт В
		|					(ВЫБРАТЬ
		|						АПСчета.Счет КАК Счет
		|					ИЗ
		|						АПСчета КАК АПСчета)
		|				И СубконтоДт1 В (&ВидыПени),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.Регистратор КАК Регистратор,
		|	ХозрасчетныйДвиженияССубконто.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ПениКт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетКт В
		|					(ВЫБРАТЬ
		|						АПСчета.Счет КАК Счет
		|					ИЗ
		|						АПСчета КАК АПСчета)
		|				И СубконтоКт1 В (&ВидыПени),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.НомерСтроки КАК НомерСтроки,
		|	ХозрасчетныйДвиженияССубконто.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ СписаниеДт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетДт В ИЕРАРХИИ (&Дт91_02)
		|				И СубконтоДт1.ВидПрочихДоходовИРасходов = &Списание
		|				И СчетКт В
		|					(ВЫБРАТЬ
		|						АПСчета.Счет КАК Счет
		|					ИЗ
		|						АПСчета КАК АПСчета),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.НомерСтроки КАК НомерСтроки,
		|	ХозрасчетныйДвиженияССубконто.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ СписаниеКт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетКт В ИЕРАРХИИ (&Кт91_01)
		|				И СубконтоКт1.ВидПрочихДоходовИРасходов = &Списание
		|				И СчетДт В
		|					(ВЫБРАТЬ
		|						АПСчета.Счет КАК Счет
		|					ИЗ
		|						АПСчета КАК АПСчета),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РегламентнаяОперация.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_Реклассификации
		|ИЗ
		|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
		|ГДЕ
		|	РегламентнаяОперация.Организация = &Организация
		|	И РегламентнаяОперация.Проведен
		|	И РегламентнаяОперация.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
		|	И РегламентнаяОперация.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОперацияБух.Ссылка
		|ИЗ
		|	Документ.ОперацияБух КАК ОперацияБух
		|ГДЕ
		|	ОперацияБух.Организация = &Организация
		|	И НЕ ОперацияБух.ПометкаУдаления
		|	И ОперацияБух.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ОперацияБух.Содержание = ""Реклассификация долгосрочных активов и обязательств""
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.НомерСтроки КАК НомерСтроки,
		|	ХозрасчетныйДвиженияССубконто.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ Перевод
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетКт В
		|					(ВЫБРАТЬ
		|						АПСчета.Счет КАК Счет
		|					ИЗ
		|						АПСчета КАК АПСчета)
		|				И Регистратор В
		|					(ВЫБРАТЬ
		|						ВТ_Реклассификации.Ссылка КАК Ссылка
		|					ИЗ
		|						ВТ_Реклассификации КАК ВТ_Реклассификации),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.НомерСтроки,
		|	ХозрасчетныйДвиженияССубконто.Регистратор
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетДт В
		|					(ВЫБРАТЬ
		|						АПСчета.Счет КАК Счет
		|					ИЗ
		|						АПСчета КАК АПСчета)
		|				И Регистратор В
		|					(ВЫБРАТЬ
		|						ВТ_Реклассификации.Ссылка КАК Ссылка
		|					ИЗ
		|						ВТ_Реклассификации КАК ВТ_Реклассификации),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОстаткиИОбороты.Счет КАК Счет,
		|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК Субконто1,
		|	ХозрасчетныйОстаткиИОбороты.Субконто2 КАК Субконто2,
		|	ХозрасчетныйОстаткиИОбороты.Субконто3 КАК Субконто3,
		|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт КАК СуммаНачальныйРазвернутыйОстатокДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт КАК СуммаНачальныйРазвернутыйОстатокКт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйРазвернутыйОстатокДт КАК СуммаКонечныйРазвернутыйОстатокДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйРазвернутыйОстатокКт КАК СуммаКонечныйРазвернутыйОстатокКт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт КАК СуммаОборотДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт КАК СуммаОборотКт
		|ПОМЕСТИТЬ Остатки
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			,
		|			Счет В
		|				(ВЫБРАТЬ
		|					АПСчета.Счет КАК Счет
		|				ИЗ
		|					АПСчета КАК АПСчета),
		|			,
		|			Организация = &Организация) КАК ХозрасчетныйОстаткиИОбороты
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Остатки.Счет КАК Счет,
		|	СУММА(Остатки.СуммаНачальныйРазвернутыйОстатокДт) КАК СуммаНачальныйРазвернутыйОстатокДт,
		|	СУММА(Остатки.СуммаНачальныйРазвернутыйОстатокКт) КАК СуммаНачальныйРазвернутыйОстатокКт,
		|	СУММА(Остатки.СуммаКонечныйРазвернутыйОстатокДт) КАК СуммаКонечныйРазвернутыйОстатокДт,
		|	СУММА(Остатки.СуммаКонечныйРазвернутыйОстатокКт) КАК СуммаКонечныйРазвернутыйОстатокКт
		|ПОМЕСТИТЬ ОстаткиСвернуто
		|ИЗ
		|	Остатки КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОстаткиИОбороты.Счет КАК Счет,
		|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК Субконто1,
		|	ХозрасчетныйОстаткиИОбороты.Субконто2 КАК Субконто2,
		|	ХозрасчетныйОстаткиИОбороты.Субконто3 КАК Субконто3,
		|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт КАК СуммаНачальныйРазвернутыйОстатокДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт КАК СуммаНачальныйРазвернутыйОстатокКт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт КАК СуммаОборотДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт КАК СуммаОборотКт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйРазвернутыйОстатокДт КАК СуммаКонечныйРазвернутыйОстатокДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйРазвернутыйОстатокКт КАК СуммаКонечныйРазвернутыйОстатокКт,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт = 0
		|				И ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт = 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт > 0
		|						ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт < 0
		|						ТОГДА -ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		КОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт > 0
		|				И ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт = 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт > 0
		|						ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт < 0
		|						ТОГДА -ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		КОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт = 0
		|				И ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт > 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт > 0
		|							И ХозрасчетныйОстаткиИОбороты.СуммаОборотДт > ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт
		|						ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт - ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт < 0
		|							И -ХозрасчетныйОстаткиИОбороты.СуммаОборотКт > ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт
		|						ТОГДА -ХозрасчетныйОстаткиИОбороты.СуммаОборотКт - ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДебеторкаОбДт,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт > 0
		|				И ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт = 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт > 0
		|						ТОГДА ВЫБОР
		|								КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт <= ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт
		|									ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
		|								КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт > ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт
		|									ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт
		|								ИНАЧЕ 0
		|							КОНЕЦ
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт < 0
		|						ТОГДА ВЫБОР
		|								КОГДА -ХозрасчетныйОстаткиИОбороты.СуммаОборотДт <= ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт
		|									ТОГДА -ХозрасчетныйОстаткиИОбороты.СуммаОборотДт
		|								КОГДА -ХозрасчетныйОстаткиИОбороты.СуммаОборотДт > ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт
		|									ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт
		|								ИНАЧЕ 0
		|							КОНЕЦ
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДебеторкаОбКт,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт = 0
		|				И ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт > 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт > 0
		|						ТОГДА ВЫБОР
		|								КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт <= ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт
		|									ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт
		|								КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт > ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт
		|									ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт
		|								ИНАЧЕ 0
		|							КОНЕЦ
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт < 0
		|						ТОГДА ВЫБОР
		|								КОГДА -ХозрасчетныйОстаткиИОбороты.СуммаОборотКт <= ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт
		|									ТОГДА -ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
		|								КОГДА -ХозрасчетныйОстаткиИОбороты.СуммаОборотКт > ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт
		|									ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт
		|								ИНАЧЕ 0
		|							КОНЕЦ
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КредиторкаОбДт,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт = 0
		|				И ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт = 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт > 0
		|						ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт < 0
		|						ТОГДА -ХозрасчетныйОстаткиИОбороты.СуммаОборотДт
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		КОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт = 0
		|				И ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт > 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт > 0
		|						ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт < 0
		|						ТОГДА -ХозрасчетныйОстаткиИОбороты.СуммаОборотДт
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		КОГДА ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт > 0
		|				И ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт = 0
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт > 0
		|							И ХозрасчетныйОстаткиИОбороты.СуммаОборотКт > ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт
		|						ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотКт - ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт
		|					КОГДА ХозрасчетныйОстаткиИОбороты.СуммаОборотДт < 0
		|							И -ХозрасчетныйОстаткиИОбороты.СуммаОборотДт > ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт
		|						ТОГДА -ХозрасчетныйОстаткиИОбороты.СуммаОборотДт - ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КредиторкаОбКт,
		|	ВЫБОР
		|		КОГДА НЕ ШтрафыДт.НомерСтроки ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоШтрафДт,
		|	ВЫБОР
		|		КОГДА НЕ ШтрафыКт.НомерСтроки ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоШтрафКт,
		|	ВЫБОР
		|		КОГДА НЕ ПениДт.НомерСтроки ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПениДт,
		|	ВЫБОР
		|		КОГДА НЕ ПениКт.НомерСтроки ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПениКт,
		|	ВЫБОР
		|		КОГДА НЕ СписаниеДт.НомерСтроки ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоСписаниеДт,
		|	ВЫБОР
		|		КОГДА НЕ СписаниеКт.НомерСтроки ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоСписаниеКт,
		|	ВЫБОР
		|		КОГДА НЕ Перевод.НомерСтроки ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПеревод
		|ПОМЕСТИТЬ Обороты
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Запись,
		|			,
		|			Счет В (ВЫБРАТЬ РАЗЛИЧНЫЕ АПСчета.Счет ИЗ Остатки КАК АПСчета),
		|			,
		|			Организация = &Организация) КАК ХозрасчетныйОстаткиИОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ШтрафыДт КАК ШтрафыДт
		|		ПО ХозрасчетныйОстаткиИОбороты.Регистратор = ШтрафыДт.Регистратор
		|			И ХозрасчетныйОстаткиИОбороты.НомерСтроки = ШтрафыДт.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ШтрафыКт КАК ШтрафыКт
		|		ПО ХозрасчетныйОстаткиИОбороты.Регистратор = ШтрафыКт.Регистратор
		|			И ХозрасчетныйОстаткиИОбороты.НомерСтроки = ШтрафыКт.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПениДт КАК ПениДт
		|		ПО ХозрасчетныйОстаткиИОбороты.Регистратор = ПениДт.Регистратор
		|			И ХозрасчетныйОстаткиИОбороты.НомерСтроки = ПениДт.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПениКт КАК ПениКт
		|		ПО ХозрасчетныйОстаткиИОбороты.Регистратор = ПениКт.Регистратор
		|			И ХозрасчетныйОстаткиИОбороты.НомерСтроки = ПениКт.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ СписаниеДт КАК СписаниеДт
		|		ПО ХозрасчетныйОстаткиИОбороты.Регистратор = СписаниеДт.Регистратор
		|			И ХозрасчетныйОстаткиИОбороты.НомерСтроки = СписаниеДт.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ СписаниеКт КАК СписаниеКт
		|		ПО ХозрасчетныйОстаткиИОбороты.Регистратор = СписаниеКт.Регистратор
		|			И ХозрасчетныйОстаткиИОбороты.НомерСтроки = СписаниеКт.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Перевод КАК Перевод
		|		ПО ХозрасчетныйОстаткиИОбороты.Регистратор = Перевод.Регистратор
		|			И ХозрасчетныйОстаткиИОбороты.НомерСтроки = Перевод.НомерСтроки
		|ГДЕ
		|	НЕ(ХозрасчетныйОстаткиИОбороты.СуммаОборотДт <> 0
		|				И ХозрасчетныйОстаткиИОбороты.СуммаОборотКт <> 0)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Обороты.Счет КАК Счет,
		|	СУММА(Обороты.ДебеторкаОбДт) КАК ДебеторкаОбДт,
		|	СУММА(Обороты.ДебеторкаОбКт) КАК ДебеторкаОбКт,
		|	СУММА(Обороты.КредиторкаОбДт) КАК КредиторкаОбДт,
		|	СУММА(Обороты.КредиторкаОбКт) КАК КредиторкаОбКт,
		|	Обороты.ЭтоШтрафДт КАК ЭтоШтрафДт,
		|	Обороты.ЭтоШтрафКт КАК ЭтоШтрафКт,
		|	Обороты.ЭтоПениДт КАК ЭтоПениДт,
		|	Обороты.ЭтоПениКт КАК ЭтоПениКт,
		|	Обороты.ЭтоСписаниеДт КАК ЭтоСписаниеДт,
		|	Обороты.ЭтоСписаниеКт КАК ЭтоСписаниеКт,
		|	Обороты.ЭтоПеревод КАК ЭтоПеревод
		|ПОМЕСТИТЬ ОборотыСвернуто
		|ИЗ
		|	Обороты КАК Обороты
		|
		|СГРУППИРОВАТЬ ПО
		|	Обороты.Счет,
		|	Обороты.ЭтоШтрафДт,
		|	Обороты.ЭтоШтрафКт,
		|	Обороты.ЭтоПениДт,
		|	Обороты.ЭтоПениКт,
		|	Обороты.ЭтоСписаниеДт,
		|	Обороты.ЭтоСписаниеКт,
		|	Обороты.ЭтоПеревод
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОборотыСвернуто.Счет КАК Счет,
		|	ОстаткиСвернуто.СуммаНачальныйРазвернутыйОстатокДт КАК СуммаНачальныйРазвернутыйОстатокДт,
		|	ОстаткиСвернуто.СуммаНачальныйРазвернутыйОстатокКт КАК СуммаНачальныйРазвернутыйОстатокКт,
		|	ОборотыСвернуто.ДебеторкаОбДт КАК ДебеторкаОбДт,
		|	ОборотыСвернуто.ДебеторкаОбКт КАК ДебеторкаОбКт,
		|	ОборотыСвернуто.КредиторкаОбДт КАК КредиторкаОбДт,
		|	ОборотыСвернуто.КредиторкаОбКт КАК КредиторкаОбКт,
		|	ОстаткиСвернуто.СуммаКонечныйРазвернутыйОстатокДт КАК СуммаКонечныйРазвернутыйОстатокДт,
		|	ОстаткиСвернуто.СуммаКонечныйРазвернутыйОстатокКт КАК СуммаКонечныйРазвернутыйОстатокКт,
		|	ОборотыСвернуто.ЭтоШтрафДт КАК ЭтоШтрафДт,
		|	ОборотыСвернуто.ЭтоШтрафКт КАК ЭтоШтрафКт,
		|	ОборотыСвернуто.ЭтоПениДт КАК ЭтоПениДт,
		|	ОборотыСвернуто.ЭтоПениКт КАК ЭтоПениКт,
		|	ОборотыСвернуто.ЭтоСписаниеДт КАК ЭтоСписаниеДт,
		|	ОборотыСвернуто.ЭтоСписаниеКт КАК ЭтоСписаниеКт,
		|	ОборотыСвернуто.ЭтоПеревод КАК ЭтоПеревод
		|ПОМЕСТИТЬ ОстаткиИОбороты
		|ИЗ
		|	ОборотыСвернуто КАК ОборотыСвернуто
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиСвернуто КАК ОстаткиСвернуто
		|		ПО ОборотыСвернуто.Счет = ОстаткиСвернуто.Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	""Д"" КАК ВидЗадолженности,
		|	ОстаткиИОбороты.Счет КАК Счет,
		|	ОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт КАК НачальноеСальдо,
		|	ОстаткиИОбороты.ДебеторкаОбДт КАК Дебет,
		|	ОстаткиИОбороты.ДебеторкаОбКт КАК Кредит,
		|	ОстаткиИОбороты.СуммаКонечныйРазвернутыйОстатокДт КАК КонечноеСальдо,
		|	ОстаткиИОбороты.ЭтоШтрафДт КАК ЭтоШтраф,
		|	ОстаткиИОбороты.ЭтоПениДт КАК ЭтоПени,
		|	ОстаткиИОбороты.ЭтоСписаниеДт КАК ЭтоСписание,
		|	ОстаткиИОбороты.ЭтоПеревод КАК ЭтоПеревод
		|ПОМЕСТИТЬ Итог
		|ИЗ
		|	ОстаткиИОбороты КАК ОстаткиИОбороты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""К"",
		|	ОстаткиИОбороты.Счет,
		|	ОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт,
		|	ОстаткиИОбороты.КредиторкаОбДт,
		|	ОстаткиИОбороты.КредиторкаОбКт,
		|	ОстаткиИОбороты.СуммаКонечныйРазвернутыйОстатокКт,
		|	ОстаткиИОбороты.ЭтоШтрафКт,
		|	ОстаткиИОбороты.ЭтоПениКт,
		|	ОстаткиИОбороты.ЭтоСписаниеКт,
		|	ОстаткиИОбороты.ЭтоПеревод
		|ИЗ
		|	ОстаткиИОбороты КАК ОстаткиИОбороты
		|ГДЕ
		|	НЕ(ОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт = 0
		|				И ОстаткиИОбороты.КредиторкаОбДт = 0
		|				И ОстаткиИОбороты.КредиторкаОбКт = 0
		|				И ОстаткиИОбороты.СуммаКонечныйРазвернутыйОстатокКт = 0)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Итог.ВидЗадолженности КАК ВидЗадолженности,
		|	Итог.Счет КАК Счет,
		|	Итог.НачальноеСальдо КАК НачальноеСальдо,
		|	Итог.Дебет КАК Дебет,
		|	Итог.Кредит КАК Кредит,
		|	Итог.КонечноеСальдо КАК КонечноеСальдо,
		|	Итог.ЭтоШтраф КАК ЭтоШтраф,
		|	Итог.ЭтоПени КАК ЭтоПени,
		|	Итог.ЭтоСписание КАК ЭтоСписание,
		|	Итог.ЭтоПеревод КАК ЭтоПеревод
		|ИЗ
		|	Итог КАК Итог";
	
	ТЗАктивноПассивные = Запрос.Выполнить().Выгрузить();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТЗАктивноПассивные, ДанныеЗаполнения);
	
	Запрос.Текст = "
	 |УНИЧТОЖИТЬ Итог
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ ШтрафыДт
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ ШтрафыКт
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ ПениДт
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ ПениКт
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ СписаниеДт
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ СписаниеКт
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ ВТ_Реклассификации
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ Перевод
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ Остатки
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ ОстаткиСвернуто
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ Обороты
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ ОстаткиИОбороты
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |УНИЧТОЖИТЬ ОборотыСвернуто" ;
	
	Запрос.Выполнить();
	
	#КонецОбласти
	
	#Область АИПСчета
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.СчетДт КАК Счет,
		|	СУММА(ХозрасчетныйДвиженияССубконто.Сумма) КАК СуммаШтраф
		|ПОМЕСТИТЬ Штрафы
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетКт В ИЕРАРХИИ (&Кт91_01)
		|				И СубконтоКт1.ВидПрочихДоходовИРасходов В (&ВидыШтрафов)
		|				И СчетДт В
		|					(ВЫБРАТЬ
		|						АСчета.Счет КАК Счет
		|					ИЗ
		|						АСчета КАК АСчета),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйДвиженияССубконто.СчетДт
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.СчетКт,
		|	СУММА(ХозрасчетныйДвиженияССубконто.Сумма)
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетДт В ИЕРАРХИИ (&Дт91_02)
		|				И СубконтоДт1.ВидПрочихДоходовИРасходов В (&ВидыШтрафов)
		|				И СчетКт В
		|					(ВЫБРАТЬ
		|						ПСчета.Счет КАК Счет
		|					ИЗ
		|						ПСчета КАК ПСчета),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйДвиженияССубконто.СчетКт
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.СчетДт КАК Счет,
		|	СУММА(ХозрасчетныйДвиженияССубконто.Сумма) КАК СуммаПени
		|ПОМЕСТИТЬ Пени
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетДт В
		|					(ВЫБРАТЬ
		|						АСчета.Счет КАК Счет
		|					ИЗ
		|						АСчета КАК АСчета)
		|				И СубконтоДт1 В (&ВидыПени),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйДвиженияССубконто.СчетДт
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.СчетКт,
		|	СУММА(ХозрасчетныйДвиженияССубконто.Сумма)
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетКт В
		|					(ВЫБРАТЬ
		|						ПСчета.Счет КАК Счет
		|					ИЗ
		|						ПСчета КАК ПСчета)
		|				И СубконтоКт1 В (&ВидыПени),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйДвиженияССубконто.СчетКт
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.СчетКт КАК Счет,
		|	СУММА(ХозрасчетныйДвиженияССубконто.Сумма) КАК СуммаСписание
		|ПОМЕСТИТЬ Списание
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетДт В ИЕРАРХИИ (&Дт91_02)
		|				И СубконтоДт1.ВидПрочихДоходовИРасходов = &Списание
		|				И СчетКт В
		|					(ВЫБРАТЬ
		|						АСчета.Счет КАК Счет
		|					ИЗ
		|						АСчета КАК АСчета),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйДвиженияССубконто.СчетКт
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.СчетДт,
		|	СУММА(ХозрасчетныйДвиженияССубконто.Сумма)
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Организация = &Организация
		|				И СчетКт В ИЕРАРХИИ (&Кт91_01)
		|				И СубконтоКт1.ВидПрочихДоходовИРасходов = &Списание
		|				И СчетДт В
		|					(ВЫБРАТЬ
		|						ПСчета.Счет КАК Счет
		|					ИЗ
		|						ПСчета КАК ПСчета),
		|			,
		|			) КАК ХозрасчетныйДвиженияССубконто
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйДвиженияССубконто.СчетДт
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РегламентнаяОперация.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_Реклассификации
		|ИЗ
		|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
		|ГДЕ
		|	РегламентнаяОперация.Организация = &Организация
		|	И РегламентнаяОперация.Проведен
		|	И РегламентнаяОперация.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
		|	И РегламентнаяОперация.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОперацияБух.Ссылка
		|ИЗ
		|	Документ.ОперацияБух КАК ОперацияБух
		|ГДЕ
		|	ОперацияБух.Организация = &Организация
		|	И НЕ ОперацияБух.ПометкаУдаления
		|	И ОперацияБух.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ОперацияБух.Содержание = ""Реклассификация долгосрочных активов и обязательств""
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Счет КАК Счет,
		|	СУММА(ВложенныйЗапрос.СуммаПеревод) КАК СуммаПеревод
		|ПОМЕСТИТЬ ПереводЗадолженности
		|ИЗ
		|	(ВЫБРАТЬ
		|		ХозрасчетныйДвиженияССубконто.СчетКт КАК Счет,
		|		СУММА(ВЫБОР
		|				КОГДА ХозрасчетныйДвиженияССубконто.СчетКт.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|						И ХозрасчетныйДвиженияССубконто.СчетКт.Долгосрочный = ИСТИНА
		|					ТОГДА -ХозрасчетныйДвиженияССубконто.Сумма
		|				КОГДА ХозрасчетныйДвиженияССубконто.СчетКт.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|						И ХозрасчетныйДвиженияССубконто.СчетКт.Долгосрочный = ЛОЖЬ
		|					ТОГДА ХозрасчетныйДвиженияССубконто.Сумма
		|				ИНАЧЕ 0
		|			КОНЕЦ) КАК СуммаПеревод
		|	ИЗ
		|		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|				&НачалоПериода,
		|				&КонецПериода,
		|				Организация = &Организация
		|					И СчетКт В
		|						(ВЫБРАТЬ
		|							АСчета.Счет КАК Счет
		|						ИЗ
		|							АСчета КАК АСчета
		|				
		|						ОБЪЕДИНИТЬ ВСЕ
		|				
		|						ВЫБРАТЬ
		|							ПСчета.Счет
		|						ИЗ
		|							ПСчета КАК ПСчета)
		|					И Регистратор В
		|						(ВЫБРАТЬ
		|							ВТ_Реклассификации.Ссылка КАК Ссылка
		|						ИЗ
		|							ВТ_Реклассификации КАК ВТ_Реклассификации),
		|				,
		|				) КАК ХозрасчетныйДвиженияССубконто
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ХозрасчетныйДвиженияССубконто.СчетКт
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ХозрасчетныйДвиженияССубконто.СчетДт,
		|		СУММА(ВЫБОР
		|				КОГДА ХозрасчетныйДвиженияССубконто.СчетДт.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|						И ХозрасчетныйДвиженияССубконто.СчетДт.Долгосрочный = ЛОЖЬ
		|					ТОГДА ХозрасчетныйДвиженияССубконто.Сумма
		|				КОГДА ХозрасчетныйДвиженияССубконто.СчетДт.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|						И ХозрасчетныйДвиженияССубконто.СчетДт.Долгосрочный = ИСТИНА
		|					ТОГДА -ХозрасчетныйДвиженияССубконто.Сумма
		|				ИНАЧЕ 0
		|			КОНЕЦ)
		|	ИЗ
		|		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
		|				&НачалоПериода,
		|				&КонецПериода,
		|				Организация = &Организация
		|					И СчетДт В
		|						(ВЫБРАТЬ
		|							АСчета.Счет КАК Счет
		|						ИЗ
		|							АСчета КАК АСчета
		|				
		|						ОБЪЕДИНИТЬ ВСЕ
		|				
		|						ВЫБРАТЬ
		|							ПСчета.Счет
		|						ИЗ
		|							ПСчета КАК ПСчета)
		|					И Регистратор В
		|						(ВЫБРАТЬ
		|							ВТ_Реклассификации.Ссылка КАК Ссылка
		|						ИЗ
		|							ВТ_Реклассификации КАК ВТ_Реклассификации),
		|				,
		|				) КАК ХозрасчетныйДвиженияССубконто
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ХозрасчетныйДвиженияССубконто.СчетДт) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Счет
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОстаткиИОбороты.Счет КАК Счет,
		|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт КАК СуммаНачальныйОстатокДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокКт КАК СуммаНачальныйОстатокКт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт КАК СуммаОборотДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт КАК СуммаОборотКт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СуммаКонечныйОстатокДт, 
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт КАК СуммаКонечныйОстатокКт
		|ПОМЕСТИТЬ ОбщиеОстаткиИОбороты
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			,
		|			Счет В
		|				(ВЫБРАТЬ
		|					Счета.Счет КАК Счет
		|				ИЗ
		|					Счета КАК Счета),
		|			,
		|			Организация = &Организация) КАК ХозрасчетныйОстаткиИОбороты 
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбщиеОстаткиИОбороты.Счет КАК Счет,
		|	ОбщиеОстаткиИОбороты.СуммаНачальныйОстатокДт КАК СуммаНачальныйОстаток,
		|	ОбщиеОстаткиИОбороты.СуммаОборотДт КАК СуммаОборотДт,
		|	ОбщиеОстаткиИОбороты.СуммаОборотКт КАК СуммаОборотКт,
		|	ОбщиеОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СуммаКонечныйОстаток,
		|	""Д"" КАК ВидЗадолженности
		|ПОМЕСТИТЬ ОстаткиИОбороты
		|ИЗ
		|	ОбщиеОстаткиИОбороты КАК ОбщиеОстаткиИОбороты
		|	
		|ГДЕ 
		|	ОбщиеОстаткиИОбороты.Счет В
		|				(ВЫБРАТЬ
		|					АСчета.Счет КАК Счет
		|				ИЗ
		|					АСчета КАК АСчета)
		|	И НЕ ОбщиеОстаткиИОбороты.Счет В ИЕРАРХИИ
		|				(ВЫБРАТЬ
		|					СчетаРБП.Счет КАК Счет
		|				ИЗ
		|					СчетаРБП КАК СчетаРБП)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОбщиеОстаткиИОбороты.Счет КАК Счет,
		|	-ОбщиеОстаткиИОбороты.СуммаНачальныйОстатокДт КАК СуммаНачальныйОстаток,
		|	ОбщиеОстаткиИОбороты.СуммаОборотДт КАК СуммаОборотДт,
		|	ОбщиеОстаткиИОбороты.СуммаОборотКт КАК СуммаОборотКт,
		|	-ОбщиеОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СуммаКонечныйОстаток,
		|	""К"" КАК ВидЗадолженности
		|ИЗ
		|	ОбщиеОстаткиИОбороты КАК ОбщиеОстаткиИОбороты
		|	
		|ГДЕ
		|	ОбщиеОстаткиИОбороты.Счет В
		|				(ВЫБРАТЬ
		|					АСчета.Счет КАК Счет
		|				ИЗ
		|					АСчета КАК АСчета)
		|	И НЕ ОбщиеОстаткиИОбороты.Счет В ИЕРАРХИИ
		|				(ВЫБРАТЬ
		|					СчетаРБП.Счет КАК Счет
		|				ИЗ
		|					СчетаРБП КАК СчетаРБП)
		|	И ОбщиеОстаткиИОбороты.Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОбщиеОстаткиИОбороты.Счет,
		|	ОбщиеОстаткиИОбороты.СуммаНачальныйОстатокКт,
		|	ОбщиеОстаткиИОбороты.СуммаОборотДт,
		|	ОбщиеОстаткиИОбороты.СуммаОборотКт,
		|	ОбщиеОстаткиИОбороты.СуммаКонечныйОстатокКт,
		|	""К""
		|ИЗ
		|	ОбщиеОстаткиИОбороты КАК ОбщиеОстаткиИОбороты
		|	
		|ГДЕ
		|	ОбщиеОстаткиИОбороты.Счет В
		|				(ВЫБРАТЬ
		|					ПСчета.Счет КАК Счет
		|				ИЗ
		|					ПСчета КАК ПСчета)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОбщиеОстаткиИОбороты.Счет,
		|	-ОбщиеОстаткиИОбороты.СуммаНачальныйОстатокКт,
		|	ОбщиеОстаткиИОбороты.СуммаОборотДт,
		|	ОбщиеОстаткиИОбороты.СуммаОборотКт,
		|	-ОбщиеОстаткиИОбороты.СуммаКонечныйОстатокКт,
		|	""Д""
		|ИЗ
		|	ОбщиеОстаткиИОбороты КАК ОбщиеОстаткиИОбороты
		|	
		|ГДЕ
		|	ОбщиеОстаткиИОбороты.Счет В
		|				(ВЫБРАТЬ
		|					ПСчета.Счет КАК Счет
		|				ИЗ
		|					ПСчета КАК ПСчета)
		|	И Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ХозрасчетныйОстаткиИОбороты.Счет,
		|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт,
		|	""Д""
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			,
		|			Счет В
		|				(ВЫБРАТЬ
		|					СчетаРБП.Счет КАК Счет
		|				ИЗ
		|					СчетаРБП КАК СчетаРБП),
		|			&СубконтоРБП,
		|			Организация = &Организация
		|				И Субконто1.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыАктивовДляРБП.ДебиторскаяЗадолженность)) КАК ХозрасчетныйОстаткиИОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОстаткиИОбороты.Счет КАК Счет,
		|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК Субконто1,
		|	ХозрасчетныйОстаткиИОбороты.Субконто2 КАК Субконто2,
		|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт КАК НачальноеСальдоКт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт КАК ОборотДт,
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйРазвернутыйОстатокКт КАК КонечноеСальдоКт,
		|	""Д"" КАК ВидЗадолженности
		|ПОМЕСТИТЬ РезервыПоСомнительнымДолгам
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			,
		|			Счет В
		|				(ВЫБРАТЬ
		|					СчетаРезервов.Счет КАК Счет
		|				ИЗ
		|					СчетаРезервов КАК СчетаРезервов),
		|			,
		|			Организация = &Организация) КАК ХозрасчетныйОстаткиИОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиИОбороты.ВидЗадолженности КАК ВидЗадолженности,
		|	ОстаткиИОбороты.Счет КАК Счет,
		|	ОстаткиИОбороты.СуммаНачальныйОстаток КАК НачальноеСальдо,
		|	ОстаткиИОбороты.СуммаОборотДт КАК Дебет,
		|	ОстаткиИОбороты.СуммаОборотКт КАК Кредит,
		|	ОстаткиИОбороты.СуммаКонечныйОстаток КАК КонечноеСальдо,
		|	ВЫБОР
		|		КОГДА НЕ Штрафы.СуммаШтраф ЕСТЬ NULL
		|			ТОГДА Штрафы.СуммаШтраф
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Штраф,
		|	ВЫБОР
		|		КОГДА НЕ Пени.СуммаПени ЕСТЬ NULL
		|			ТОГДА Пени.СуммаПени
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Пени,
		|	ВЫБОР
		|		КОГДА НЕ Списание.СуммаСписание ЕСТЬ NULL
		|			ТОГДА Списание.СуммаСписание
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Списание,
		|	ВЫБОР
		|		КОГДА НЕ ПереводЗадолженности.СуммаПеревод ЕСТЬ NULL
		|			ТОГДА ПереводЗадолженности.СуммаПеревод
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Перевод
		|ИЗ
		|	ОстаткиИОбороты КАК ОстаткиИОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Штрафы КАК Штрафы
		|		ПО ОстаткиИОбороты.Счет = Штрафы.Счет
		|		ЛЕВОЕ СОЕДИНЕНИЕ Пени КАК Пени
		|		ПО ОстаткиИОбороты.Счет = Пени.Счет
		|		ЛЕВОЕ СОЕДИНЕНИЕ Списание КАК Списание
		|		ПО ОстаткиИОбороты.Счет = Списание.Счет
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПереводЗадолженности КАК ПереводЗадолженности
		|		ПО ОстаткиИОбороты.Счет = ПереводЗадолженности.Счет
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РезервыПоСомнительнымДолгам.ВидЗадолженности,
		|	РезервыПоСомнительнымДолгам.Счет,
		|	СУММА(РезервыПоСомнительнымДолгам.НачальноеСальдоКт),
		|	СУММА(РезервыПоСомнительнымДолгам.ОборотДт),
		|	0,
		|	СУММА(РезервыПоСомнительнымДолгам.КонечноеСальдоКт),
		|	0,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	РезервыПоСомнительнымДолгам КАК РезервыПоСомнительнымДолгам
		|
		|СГРУППИРОВАТЬ ПО
		|	РезервыПоСомнительнымДолгам.Счет,
		|	РезервыПоСомнительнымДолгам.ВидЗадолженности
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидЗадолженности,
		|	Счет
		|";
	
	ТЗАктивныеИПассивные = Запрос.Выполнить().Выгрузить();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТЗАктивныеИПассивные, ДанныеЗаполнения);
	
	Запрос.Текст = "
		|УНИЧТОЖИТЬ Штрафы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Пени
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Списание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_Реклассификации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПереводЗадолженности
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОбщиеОстаткиИОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОстаткиИОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ РезервыПоСомнительнымДолгам";
	
	Запрос.Выполнить();
	
	#КонецОбласти
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

Функция ПолучитьТаблицуДанныхПояснений5()
	
	ДанныеКЗаполнению = Новый ТаблицаЗначений;
	ДанныеКЗаполнению.Колонки.Добавить("ВидЗадолженности");
	ДанныеКЗаполнению.Колонки.Добавить("Счет");
	ДанныеКЗаполнению.Колонки.Добавить("НачальноеСальдо");
	ДанныеКЗаполнению.Колонки.Добавить("Дебет");
	ДанныеКЗаполнению.Колонки.Добавить("Кредит");
	ДанныеКЗаполнению.Колонки.Добавить("КонечноеСальдо");
	ДанныеКЗаполнению.Колонки.Добавить("ЭтоШтраф");
	ДанныеКЗаполнению.Колонки.Добавить("ЭтоПроценты");
	ДанныеКЗаполнению.Колонки.Добавить("ЭтоПени");
	ДанныеКЗаполнению.Колонки.Добавить("ЭтоСписание");
	ДанныеКЗаполнению.Колонки.Добавить("ЭтоПеревод");
	ДанныеКЗаполнению.Колонки.Добавить("Штраф");
	ДанныеКЗаполнению.Колонки.Добавить("Проценты");
	ДанныеКЗаполнению.Колонки.Добавить("Пени");
	ДанныеКЗаполнению.Колонки.Добавить("Списание");
	ДанныеКЗаполнению.Колонки.Добавить("Перевод");
	
	ДанныеКЗаполнению.Индексы.Добавить("ВидЗадолженности, Счет");
	
	Возврат ДанныеКЗаполнению;
	
КонецФункции

Функция ПолучитьЗаголовкиГраф51Пояснений5()
	
	ЗаголовкиГраф51 = Новый Соответствие;
	ЗаголовкиГраф51.Вставить("04", "На начало года - учтенная по условиям договора");
	ЗаголовкиГраф51.Вставить("05", "На начало года - величина резерва по сомнительным долгам");
	ЗаголовкиГраф51.Вставить("06", "Изменения за период - поступление - в результате хозяйственных операций (сумма долга по сделке, операции)");
	ЗаголовкиГраф51.Вставить("07", "Изменения за период - поступление - причитающиеся проценты, штрафы и иные начисления");
	ЗаголовкиГраф51.Вставить("08", "Изменения за период - выбыло - погашение");
	ЗаголовкиГраф51.Вставить("09", "Изменения за период - выбыло - списание на финансовый результат");
	ЗаголовкиГраф51.Вставить("10", "Изменения за период - выбыло - восстановление резерва");
	ЗаголовкиГраф51.Вставить("11", "Изменения за период - перевод из долго- в краткосрочную задолженность");
	ЗаголовкиГраф51.Вставить("22", "Изменения за период - перевод из дебиторской в кредиторскую задолженность");
	ЗаголовкиГраф51.Вставить("21", "Изменения за период - перевод из кредиторской в дебиторскую задолженность");
	ЗаголовкиГраф51.Вставить("12", "На конец периода - учтенная по условиям договора");
	ЗаголовкиГраф51.Вставить("13", "На конец периода - величина резерва по сомнительным долгам");
	
	Возврат ЗаголовкиГраф51;
	
КонецФункции

Функция ПолучитьЗаголовкиГраф53Пояснений5()
	
	ЗаголовкиГраф53 = Новый Соответствие;
	ЗаголовкиГраф53.Вставить("04", "Остаток на начало года");
	ЗаголовкиГраф53.Вставить("05", "Изменения за период - поступление - в результате хозяйственных операций (сумма долга по сделке, операции)");
	ЗаголовкиГраф53.Вставить("06", "Изменения за период - поступление - причитающиеся проценты, штрафы и иные начисления");
	ЗаголовкиГраф53.Вставить("07", "Изменения за период - выбыло - погашение");
	ЗаголовкиГраф53.Вставить("08", "Изменения за период - выбыло - списание на финансовый результат");
	ЗаголовкиГраф53.Вставить("09", "Изменения за период - перевод из долго- в краткосрочную задолженность");
	ЗаголовкиГраф53.Вставить("21", "Изменения за период - перевод из дебиторской в кредиторскую задолженность");
	ЗаголовкиГраф53.Вставить("22", "Изменения за период - перевод из кредиторской в дебиторскую задолженность");
	ЗаголовкиГраф53.Вставить("10", "Остаток на конец периода");

	Возврат ЗаголовкиГраф53;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область ОберткиПроцедурИФункций

Процедура УстановитьЗначениеПоказателя(ПоказателиПоРазделам, ИмяРаздела, ИмяПоказателя, ЗначениеПоказателя)
	
	ЗаполнениеБухгалтерскойОтчетности.УстановитьЗначениеПоказателя(ПоказателиПоРазделам, ИмяРаздела, ИмяПоказателя, ЗначениеПоказателя);
	
КонецПроцедуры

Процедура ДобавитьСтрокуРасшифровки(ТаблицаРасшифровок, ИмяПоказателя, НаименованиеПоказателя, ЗнакОперации, НаименованиеСлагаемого, Сумма, ИмяРаздела, ДополнительныеПараметры = Неопределено)
	
	ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(ТаблицаРасшифровок, ИмяПоказателя, НаименованиеПоказателя, ЗнакОперации, НаименованиеСлагаемого, Сумма, ИмяРаздела, ДополнительныеПараметры);
	
КонецПроцедуры

Функция Счет(УсловныйКод)
	
	Возврат ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет(УсловныйКод);
	
КонецФункции

#КонецОбласти

Функция НастройкиОСВПоСчету(ОбщиеНастройки, Счет, ОтборПоАналитике = Неопределено)
	
	Если ОбщиеНастройки.Свойство("ПараметрыРасшифровки") Тогда
		Организация   = ОбщиеНастройки.ПараметрыРасшифровки.Организация;
		НачалоПериода = ОбщиеНастройки.ПараметрыРасшифровки.НачалоПериода;
		КонецПериода  = ОбщиеНастройки.ПараметрыРасшифровки.КонецПериода;
	Иначе
		Организация   = ОбщиеНастройки.Организация;
		НачалоПериода = ОбщиеНастройки.НачалоПериода;
		КонецПериода  = ОбщиеНастройки.КонецПериода;
	КонецЕсли;
	
	НастройкиРасшифровки = Новый Структура;
	НастройкиРасшифровки.Вставить("Организация", Организация);
	
	НастройкиРасшифровки.Вставить("Отчет", Новый Структура);
	НастройкиРасшифровки.Отчет.Вставить("ИмяОтчета", "Отчет.ОборотноСальдоваяВедомостьПоСчету.Форма.ФормаОтчета");
	
	НастройкиРасшифровки.Отчет.Вставить("ПараметрыФормы", Новый Структура);
	НастройкиРасшифровки.Отчет.ПараметрыФормы.Вставить("РежимРасшифровки", Истина);
	НастройкиРасшифровки.Отчет.ПараметрыФормы.Вставить("ВидРасшифровки", 2);
	НастройкиРасшифровки.Отчет.ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Организация",   Организация);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("НачалоПериода", НачалоПериода);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("КонецПериода",  КонецПериода);
	
	Если ТипЗнч(Счет) = Тип("Строка") Тогда
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Счет", Счет(Счет));
	Иначе
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Счет", Счет);
	КонецЕсли;
	
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоСубсчетам", Истина);
	
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательБУ",            Истина);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательНУ",            Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательПР",            Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательВР",            Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательКонтроль",      Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательВалютнаяСумма", Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательКоличество",    Ложь);
	
	Если ОбщиеНастройки.Свойство("РазвернутоеСальдо") Тогда
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("РазвернутоеСальдо", Истина);
	КонецЕсли;
	
	Если ОтборПоАналитике <> Неопределено Тогда
		Отбор = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
		Отбор.ИдентификаторПользовательскойНастройки = "Отбор";
		
		Для Каждого ЭлементОтбора Из ОтборПоАналитике Цикл
			ПолеКомпоновкиДанных = Новый ПолеКомпоновкиДанных(ЭлементОтбора.Ключ);
			Если ТипЗнч(ЭлементОтбора.Значение) = Тип("Структура") Тогда
				ДобавитьЗначениеВОтбор(Отбор, ПолеКомпоновкиДанных, ЭлементОтбора.Значение.ВидСравненияКомпоновкиДанных, ЭлементОтбора.Значение.Значение);
			Иначе
				ДобавитьЗначениеВОтбор(Отбор, ПолеКомпоновкиДанных, ВидСравненияКомпоновкиДанных.Равно, ЭлементОтбора.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	НастройкиРасшифровки.Отчет.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	
	Возврат НастройкиРасшифровки;
	
КонецФункции

Функция НастройкиОСВ(ОбщиеНастройки, Счет, ОтборПоАналитике = Неопределено)
	
	Если ОбщиеНастройки.Свойство("ПараметрыРасшифровки") Тогда
		Организация   = ОбщиеНастройки.ПараметрыРасшифровки.Организация;
		НачалоПериода = ОбщиеНастройки.ПараметрыРасшифровки.НачалоПериода;
		КонецПериода  = ОбщиеНастройки.ПараметрыРасшифровки.КонецПериода;
	Иначе
		Организация   = ОбщиеНастройки.Организация;
		НачалоПериода = ОбщиеНастройки.НачалоПериода;
		КонецПериода  = ОбщиеНастройки.КонецПериода;
	КонецЕсли;
	
	НастройкиРасшифровки = Новый Структура;
	НастройкиРасшифровки.Вставить("Организация", Организация);
	
	НастройкиРасшифровки.Вставить("Отчет", Новый Структура);
	НастройкиРасшифровки.Отчет.Вставить("ИмяОтчета", "Отчет.ОборотноСальдоваяВедомость.Форма.ФормаОтчета");
	
	НастройкиРасшифровки.Отчет.Вставить("ПараметрыФормы", Новый Структура);
	НастройкиРасшифровки.Отчет.ПараметрыФормы.Вставить("РежимРасшифровки", Истина);
	НастройкиРасшифровки.Отчет.ПараметрыФормы.Вставить("ВидРасшифровки", 2);
	НастройкиРасшифровки.Отчет.ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Организация",   Организация);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("НачалоПериода", НачалоПериода);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("КонецПериода",  КонецПериода);
	
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоСубсчетам", Истина);
	
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательБУ",            Истина);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательНУ",            Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательПР",            Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательВР",            Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательКонтроль",      Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательВалютнаяСумма", Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательКоличество",    Ложь);
	
	Отбор = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	Отбор.ИдентификаторПользовательскойНастройки = "Отбор";
	
	ПолеКомпоновкиДанных = Новый ПолеКомпоновкиДанных("Счет");
	
	Счета = Новый СписокЗначений;
	Счета.ЗагрузитьЗначения(Счет);
	ДобавитьЗначениеВОтбор(Отбор, ПолеКомпоновкиДанных, ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии, Счета);
		
	НастройкиРасшифровки.Отчет.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	
	Возврат НастройкиРасшифровки;
	
КонецФункции

Функция НастройкиАнализаСчета(ОбщиеНастройки, Счет, КорСчет, ОтборПоАналитике = Неопределено)
	
	Если ОбщиеНастройки.Свойство("ПараметрыРасшифровки") Тогда
		Организация   = ОбщиеНастройки.ПараметрыРасшифровки.Организация;
		НачалоПериода = ОбщиеНастройки.ПараметрыРасшифровки.НачалоПериода;
		КонецПериода  = ОбщиеНастройки.ПараметрыРасшифровки.КонецПериода;
	Иначе
		Организация   = ОбщиеНастройки.Организация;
		НачалоПериода = ОбщиеНастройки.НачалоПериода;
		КонецПериода  = ОбщиеНастройки.КонецПериода;
	КонецЕсли;
	
	НастройкиРасшифровки = Новый Структура;
	НастройкиРасшифровки.Вставить("Организация", Организация);
	
	НастройкиРасшифровки.Вставить("Отчет", Новый Структура);
	НастройкиРасшифровки.Отчет.Вставить("ИмяОтчета", "Отчет.АнализСчета.Форма.ФормаОтчета");
	
	НастройкиРасшифровки.Отчет.Вставить("ПараметрыФормы", Новый Структура);
	НастройкиРасшифровки.Отчет.ПараметрыФормы.Вставить("РежимРасшифровки", Истина);
	НастройкиРасшифровки.Отчет.ПараметрыФормы.Вставить("ВидРасшифровки", 2);
	НастройкиРасшифровки.Отчет.ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Организация",   Организация);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("НачалоПериода", НачалоПериода);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("КонецПериода",  КонецПериода);
	
	Если ТипЗнч(Счет) = Тип("Строка") Тогда
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Счет", Счет(Счет));
	Иначе
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Счет", Счет);
	КонецЕсли;
	
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоСубсчетам", НЕ ЗначениеЗаполнено(КорСчет));
	
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательБУ",            Истина);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательНУ",            Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательПР",            Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательВР",            Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательКонтроль",      Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательВалютнаяСумма", Ложь);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПоказательКоличество",    Ложь);
	
	Отбор = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	Отбор.ИдентификаторПользовательскойНастройки = "Отбор";
	
	ПолеКомпоновкиДанных = Новый ПолеКомпоновкиДанных("КорСчет");
	Если ТипЗнч(КорСчет) = Тип("Строка") Тогда
		КорСчета = Новый СписокЗначений;
		МассивКодовСчетов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КорСчет);
		Для Каждого КодСчета Из МассивКодовСчетов Цикл
			КорСчета.Добавить(Счет(СокрЛП(КодСчета)));
		КонецЦикла;
		ВидСравненияСчетов = ?(МассивКодовСчетов.Количество() > 1, ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии, ВидСравненияКомпоновкиДанных.ВИерархии);
		ДобавитьЗначениеВОтбор(Отбор, ПолеКомпоновкиДанных, ВидСравненияСчетов, КорСчета);
	ИначеЕсли ТипЗнч(КорСчет) = Тип("Массив") Тогда
		КорСчета = Новый СписокЗначений;
		КорСчета.ЗагрузитьЗначения(КорСчет);
		ВидСравненияСчетов = ?(КорСчет.Количество() > 1, ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии, ВидСравненияКомпоновкиДанных.ВИерархии);
		ДобавитьЗначениеВОтбор(Отбор, ПолеКомпоновкиДанных, ВидСравненияСчетов, КорСчета);
	Иначе 
		ДобавитьЗначениеВОтбор(Отбор, ПолеКомпоновкиДанных, ВидСравненияКомпоновкиДанных.ВИерархии, КорСчет);
	КонецЕсли;
	
	Если ОтборПоАналитике <> Неопределено Тогда
		Отбор = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
		Отбор.ИдентификаторПользовательскойНастройки = "Отбор";
		
		Для Каждого ЭлементОтбора Из ОтборПоАналитике Цикл
			ПолеКомпоновкиДанных = Новый ПолеКомпоновкиДанных(ЭлементОтбора.Ключ);
			Если ТипЗнч(ЭлементОтбора.Значение) = Тип("Структура") Тогда
				ДобавитьЗначениеВОтбор(Отбор, ПолеКомпоновкиДанных, ЭлементОтбора.Значение.ВидСравненияКомпоновкиДанных, ЭлементОтбора.Значение.Значение);
			Иначе
				ДобавитьЗначениеВОтбор(Отбор, ПолеКомпоновкиДанных, ВидСравненияКомпоновкиДанных.Равно, ЭлементОтбора.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	НастройкиРасшифровки.Отчет.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	
	Возврат НастройкиРасшифровки;
	
КонецФункции

Процедура ДобавитьЗначениеВОтбор(Отбор, ЛевоеЗначение, ВидСравнения, ПравоеЗначение) Экспорт
	
	ЭлементОтбора = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбора.ЛевоеЗначение    = ЛевоеЗначение;
	ЭлементОтбора.ВидСравнения     = ВидСравнения;
	ЭлементОтбора.ПравоеЗначение   = ПравоеЗначение;
	ЭлементОтбора.Использование    = Истина;
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
	
КонецПроцедуры

Функция ПолучитьПараметрыПоказателя()
	
	ПараметрыПоказателя = Новый Структура;
	ПараметрыПоказателя.Вставить("НачалоПериода", "");
	ПараметрыПоказателя.Вставить("КонецПериода", "");
	ПараметрыПоказателя.Вставить("КодСтроки", "");
	ПараметрыПоказателя.Вставить("ДопАлгоритмСтроки", "");
	ПараметрыПоказателя.Вставить("ДопАлгоритмГрафы", "");
	ПараметрыПоказателя.Вставить("ПрошлыйПериод", Ложь);
	ПараметрыПоказателя.Вставить("РегулярнаяГрафа", Неопределено);
	ПараметрыПоказателя.Вставить("КодГода", 0);
	ПараметрыПоказателя.Вставить("ЗначениеПоказателя", 0);
	ПараметрыПоказателя.Вставить("ИмяОбластиПоказателя", "");
	ПараметрыПоказателя.Вставить("КодСтрокиПрошлогоПериода", "");
	ПараметрыПоказателя.Вставить("НаименованиеСтроки", "");
	ПараметрыПоказателя.Вставить("КодГрафы", "");
	ПараметрыПоказателя.Вставить("НомерДопСтроки", 0);

	Возврат ПараметрыПоказателя;
	
КонецФункции	

Функция ПолучитьВидМакета(ТипПоказателя, ПараметрыПоказателя)
	
	Если ТипПоказателя = "ОБ" Тогда
		Если ДопАлгоритмОтбораПоДокументам(ПараметрыПоказателя.ДопАлгоритмСтроки) Тогда
			Возврат "ОборотыМеждуСчетамиПоТипуДокумента";
		Иначе
			Возврат "ОборотыМеждуСчетами";
		КонецЕсли;
	ИначеЕсли ТипПоказателя = "СКДР" ИЛИ ТипПоказателя = "СККР" Тогда
		Возврат "СальдоИОборотыПоСчетамИСубконто";
	Иначе
		Возврат "СальдоИОборотыПоСчетам";
	КонецЕсли;
	
КонецФункции

Функция ВычислитьПоказательПоТипу(ТипПоказателя, Данные, Счет, КорСчет = Неопределено)
	
	Если ТипПоказателя = "СКК" Тогда
		Возврат ЗаполнениеБухгалтерскойОтчетностиИтоги.СКК(Счет, Данные);
	ИначеЕсли ТипПоказателя = "СКД" Тогда
		Возврат ЗаполнениеБухгалтерскойОтчетностиИтоги.СКД(Счет, Данные);
	ИначеЕсли ТипПоказателя = "СКДР" Тогда
		Возврат ЗаполнениеБухгалтерскойОтчетностиИтоги.СКДР(Счет, Данные);
	ИначеЕсли ТипПоказателя = "СККР" Тогда
		Возврат ЗаполнениеБухгалтерскойОтчетностиИтоги.СККР(Счет, Данные);
	ИначеЕсли ТипПоказателя = "СНД" Тогда
		Возврат ЗаполнениеБухгалтерскойОтчетностиИтоги.СНД(Счет, Данные);
	ИначеЕсли ТипПоказателя = "СНК" Тогда
		Возврат ЗаполнениеБухгалтерскойОтчетностиИтоги.СНК(Счет, Данные);
	ИначеЕсли ТипПоказателя = "ОБ" Тогда
		Возврат ЗаполнениеБухгалтерскойОтчетностиИтоги.ОБ(Счет, КорСчет, Данные);
	ИначеЕсли ТипПоказателя = "ДО" Тогда
		Возврат ЗаполнениеБухгалтерскойОтчетностиИтоги.ДО(Счет, Данные);
	ИначеЕсли ТипПоказателя = "КО" Тогда
		Возврат ЗаполнениеБухгалтерскойОтчетностиИтоги.КО(Счет, Данные);
	Иначе
		Возврат 0;
	КонецЕсли;	
	
КонецФункции

Функция СтрокаЗаполняетсяСтандартно(ДопАлгоритм)
	
	Если ДопАлгоритм = "НМАСПогашеннойСтоимостью" Тогда
		Возврат Ложь;
	ИначеЕсли ДопАлгоритм = "ПервоначальнаяСтоимостьНМА" Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

Функция ПоказательЗаполняетсяСтандартно(ДопАлгоритм)
	
	Если ДопАлгоритм = "ОДДС" Тогда
		Возврат Ложь;
	ИначеЕсли ДопАлгоритм = "ОДДС_Агентские" Тогда
		Возврат Ложь;
	ИначеЕсли ДопАлгоритм = "ОДДС_НДС" Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

Процедура ДобавитьРегулярныеГрафыФормы(КонтекстВыполнения)
	
	КонтекстОтчета = КонтекстВыполнения.КонтекстОтчета;
	ЗаполнятьПредыдущиеПериоды = КонтекстВыполнения.ЗаполнятьПредыдущиеПериоды;
	
	//Представление за период
	ЗаголовокШаблон = НСтр("ru = 'За %1'");
	
	ЗаголовокПериод = СтрШаблон(ЗаголовокШаблон, ЗаполнениеБухгалтерскойОтчетности.ОписаниеПериода(
				КонтекстОтчета.НачалоПериодаОтчета, КонтекстОтчета.КонецПериодаОтчета));
				
	НачалоПериодПрошлыйГод = ДобавитьМесяц(КонтекстОтчета.НачалоПериодаОтчета,-1*12);
	НачалоПериодПредПрошлыйГод = ДобавитьМесяц(КонтекстОтчета.НачалоПериодаОтчета,-2*12);
	
	Если НЕ КонтекстВыполнения.ЭтоГодовойОтчет 
		И НЕ (КонтекстВыполнения.ЗаполнениеФормОтчетности.ОФР
			ИЛИ КонтекстВыполнения.ЗаполнениеФормОтчетности.ОДДС
			ИЛИ КонтекстВыполнения.ЗаполнениеФормОтчетности.ОЦИПС)Тогда
		ПериодПрошлыйГод = КонецГода(ДобавитьМесяц(КонтекстОтчета.КонецПериодаОтчета,-1*12));
		ПериодПредПрошлыйГод = КонецГода(ДобавитьМесяц(КонтекстОтчета.КонецПериодаОтчета,-2*12));
	Иначе 
		ПериодПрошлыйГод = ДобавитьМесяц(КонтекстОтчета.КонецПериодаОтчета,-1*12);
		ПериодПредПрошлыйГод = ДобавитьМесяц(КонтекстОтчета.КонецПериодаОтчета,-2*12);
	КонецЕсли;
	
	ЗаголовокПериодПрошлыйГод = СтрШаблон(ЗаголовокШаблон, ЗаполнениеБухгалтерскойОтчетности.ОписаниеПериода(
		НачалоПериодПрошлыйГод, ПериодПрошлыйГод));
	ЗаголовокПериодПредПрошлыйГод = СтрШаблон(ЗаголовокШаблон, ЗаполнениеБухгалтерскойОтчетности.ОписаниеПериода(
		НачалоПериодПредПрошлыйГод, ПериодПредПрошлыйГод));
	
	ТекущийГод = Новый Соответствие;
	ТекущийГод.Вставить(ЗаголовокПериод, 0);
	
	ПрошлыйГод = Новый Соответствие;
	ПрошлыйГод.Вставить(ЗаголовокПериодПрошлыйГод, -1);
	
	ПредПрошлыйГод =  Новый Соответствие;
	ПредПрошлыйГод.Вставить(ЗаголовокПериодПредПрошлыйГод, -2);
	
	//Представление на дату
	ЗаголовокШаблон = НСтр("ru = 'На %1 г.'");
	
	ЗаголовокНаКонецТекПериода = СтрШаблон(ЗаголовокШаблон, Формат(КонтекстОтчета.КонецПериодаОтчета, "ДФ = 'дд ММММ гггг'"));
	ЗаголовокКонецПериодаСравнения = СтрШаблон(ЗаголовокШаблон, Формат(ПериодПрошлыйГод, "ДФ = 'дд ММММ гггг'"));
	ЗаголовокНаНачалоПериодаСравнения = СтрШаблон(ЗаголовокШаблон, Формат(ПериодПредПрошлыйГод, "ДФ = 'дд ММММ гггг'"));
				
	КонецТекПериода = Новый Соответствие;
	КонецТекПериода.Вставить(ЗаголовокНаКонецТекПериода, 0);
	
	КонецПериодаСравнения = Новый Соответствие;
	КонецПериодаСравнения.Вставить(ЗаголовокКонецПериодаСравнения, -1);
	
	НачалоПериодаСравнения =  Новый Соответствие;
	НачалоПериодаСравнения.Вставить(ЗаголовокНаНачалоПериодаСравнения, -2);
	
	//Представление за период краткое
	ЗаголовокШаблон = НСтр("ru = 'за %1 г.'");
	
	ЗаголовокТекПериод = СтрШаблон(ЗаголовокШаблон, Формат(КонтекстОтчета.КонецПериодаОтчета, "ДФ=yyyy"));
	ЗаголовокПериодСравнения = СтрШаблон(ЗаголовокШаблон, Формат(ПериодПрошлыйГод, "ДФ=yyyy"));
				
	ТекПериод = Новый Соответствие;
	ТекПериод.Вставить(ЗаголовокТекПериод, 0);
	
	ПериодСравнения = Новый Соответствие;
	ПериодСравнения.Вставить(ЗаголовокПериодСравнения, -1);
	
	РегулярныеГрафы = Новый Соответствие; //Ключ - код графы, значение - соответствие: заголовок, код года
	
	Если КонтекстОтчета.ФормаОтчетности = Перечисления.ФормыБухгалтерскойОтчетности.Баланс Тогда  
		РегулярныеГрафы.Вставить("04", КонецТекПериода);
		Если ЗаполнятьПредыдущиеПериоды Тогда
			РегулярныеГрафы.Вставить("05", КонецПериодаСравнения);
			РегулярныеГрафы.Вставить("06", НачалоПериодаСравнения);
		КонецЕсли;
	ИначеЕсли КонтекстОтчета.ФормаОтчетности = Перечисления.ФормыБухгалтерскойОтчетности.ОФР Тогда
		РегулярныеГрафы.Вставить("04", ТекущийГод);
		Если ЗаполнятьПредыдущиеПериоды Тогда
			РегулярныеГрафы.Вставить("05", ПрошлыйГод);
		КонецЕсли;
	ИначеЕсли КонтекстОтчета.ФормаОтчетности = Перечисления.ФормыБухгалтерскойОтчетности.ОДДС Тогда
		РегулярныеГрафы.Вставить("03", ТекущийГод);
		Если ЗаполнятьПредыдущиеПериоды Тогда
			РегулярныеГрафы.Вставить("04", ПрошлыйГод);
		КонецЕсли;
	ИначеЕсли КонтекстОтчета.ФормаОтчетности = Перечисления.ФормыБухгалтерскойОтчетности.ОЦИС Тогда
		РегулярныеГрафы.Вставить("04", ТекущийГод);
		Если ЗаполнятьПредыдущиеПериоды Тогда
			РегулярныеГрафы.Вставить("05", ПрошлыйГод);
		КонецЕсли;
	ИначеЕсли КонтекстОтчета.ФормаОтчетности = Перечисления.ФормыБухгалтерскойОтчетности.Пояснения6 Тогда
		РегулярныеГрафы.Вставить("03", ТекПериод);
		Если ЗаполнятьПредыдущиеПериоды Тогда
			РегулярныеГрафы.Вставить("04", ПериодСравнения);
		КонецЕсли;
	ИначеЕсли КонтекстОтчета.ФормаОтчетности = Перечисления.ФормыБухгалтерскойОтчетности.Пояснения8 Тогда
		РегулярныеГрафы.Вставить("03", КонецТекПериода);
		Если ЗаполнятьПредыдущиеПериоды Тогда
			РегулярныеГрафы.Вставить("04", КонецПериодаСравнения);
			РегулярныеГрафы.Вставить("05", НачалоПериодаСравнения);
		КонецЕсли;
	КонецЕсли;
	
	КонтекстОтчета.Вставить("РегулярныеГрафы", РегулярныеГрафы);
	
КонецПроцедуры

Функция НоваяТаблицаМногострочнойЧасти(КодСтроки) Экспорт
	
	МногострочнаяЧасть = Новый ТаблицаЗначений;
		
	Если КодСтроки = "5130" Тогда
		
		МногострочнаяЧасть.Колонки.Добавить("П100100513001", Новый ОписаниеТипов("Строка"));
		МногострочнаяЧасть.Колонки.Добавить("П100100513003", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 0, ДопустимыйЗнак.Неотрицательный)));
		МногострочнаяЧасть.Колонки.Добавить("П100100513004", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 0, ДопустимыйЗнак.Неотрицательный)));
		МногострочнаяЧасть.Колонки.Добавить("П100100513005", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 0, ДопустимыйЗнак.Неотрицательный)));
		
		Возврат МногострочнаяЧасть;
	КонецЕсли;
	
	Если КодСтроки = "5140" Тогда
		
		МногострочнаяЧасть.Колонки.Добавить("П100100514001", Новый ОписаниеТипов("Строка"));
		
		МногострочнаяЧасть.Колонки.Добавить("П100100514004", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100514005", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100514006", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100514007", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100514008", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100514009", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100514010", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100514011", Новый ОписаниеТипов("Число"));
		
		МногострочнаяЧасть.Колонки.Добавить("П100100515004", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100515005", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100515006", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100515007", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100515008", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100515009", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100515010", Новый ОписаниеТипов("Число"));
		МногострочнаяЧасть.Колонки.Добавить("П100100515011", Новый ОписаниеТипов("Число"));

		
		Возврат МногострочнаяЧасть;
	КонецЕсли;
	
КонецФункции

Процедура СвернутьТаблицуЗначенийДопСтрок(ТаблицаЗначенийДопСтрок)
	
	СписокГрафДляГруппировки = "";
	СписокГрафДляСуммирования = "";
	
	Для Каждого Колонка Из ТаблицаЗначенийДопСтрок.Колонки Цикл
		
		КолонкаСуммируется = (Колонка.ТипЗначения.СодержитТип(Тип("Число")));
		
		Если КолонкаСуммируется Тогда
			СписокГрафДляСуммирования = СписокГрафДляСуммирования + ?(ПустаяСтрока(СписокГрафДляСуммирования), "", ", ") + Колонка.Имя;
		Иначе
			СписокГрафДляГруппировки = СписокГрафДляГруппировки + ?(ПустаяСтрока(СписокГрафДляГруппировки), "", ", ") + Колонка.Имя;
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаЗначенийДопСтрок.Свернуть(СписокГрафДляГруппировки, СписокГрафДляСуммирования);
	
КонецПроцедуры

Функция НастройкиОСВПоВидуДДС(ОбщиеНастройки, Счет, ВидДДС)
	
	ОтборПоАналитике = Новый Соответствие;
	ВидыСубконто = Счет.ВидыСубконто; //ПланСчетовВидыСубконто.Хозрасчетный
	НомерСубконто = 0;
	
	Для Каждого Субконто Из ВидыСубконто Цикл
		Если Субконто.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств Тогда
			НомерСубконто = Субконто.НомерСтроки;
			ОтборПоАналитике.Вставить("Субконто" + НомерСубконто + ".ВидДвиженияДенежныхСредств", ВидДДС);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	НастройкиРасшифровки = НастройкиОСВПоСчету(ОбщиеНастройки, Счет, ОтборПоАналитике);
	
	Если НомерСубконто > 0 Тогда
		ТабличнаяЧастьГруппировка = Новый Массив;
		
		ОписаниеГруппировки = Новый Структура;
		ОписаниеГруппировки.Вставить("Использование", Истина);
		ОписаниеГруппировки.Вставить("Поле", "Субконто" + НомерСубконто);
		ОписаниеГруппировки.Вставить("Представление", НСтр("ru = 'Статьи движения денежных средств'"));
		ОписаниеГруппировки.Вставить("ТипГруппировки", 0);
		ТабличнаяЧастьГруппировка.Добавить(ОписаниеГруппировки);
		
		НастройкиРасшифровки.Отчет.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Группировка", ТабличнаяЧастьГруппировка);
	КонецЕсли;
	
	Возврат НастройкиРасшифровки;
	
КонецФункции

Функция ПолучитьОтборПоВидамСубконто(Счета)
	
	МаксКоличествоСубконто	= БухгалтерскийУчет.МаксимальноеКоличествоСубконто();
	
	ДанныеСчетов = ОбщиеСубконтоСчетов(Счета, МаксКоличествоСубконто);
	
	ВидыСубконто = Новый СписокЗначений;
	Для Индекс = 1 По МаксКоличествоСубконто Цикл
		Если ДанныеСчетов["ВидСубконто" + Индекс] <> Неопределено Тогда
			ВидСубконто = ДанныеСчетов["ВидСубконто" + Индекс];
			Если ВидСубконто <> Неопределено Тогда
				ВидыСубконто.Добавить(ВидСубконто);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат ВидыСубконто;
	
КонецФункции

Функция ЗаполнитьПараметризируемыеНаименования(Наименование, КонтекстОтчета)
	
	ЗаголовокШаблон = НСтр("ru = '%1 г.'");
	
	ЗаголовокКонецТекПериода = СтрШаблон(ЗаголовокШаблон, Формат(КонтекстОтчета.КонецПериодаОтчета, "ДФ = 'дд ММММ гггг'"));
	ЗаголовокКонецСравнительногоПериода = СтрШаблон(ЗаголовокШаблон, Формат(ДобавитьМесяц(КонтекстОтчета.КонецПериодаОтчета,-1*12), "ДФ = 'дд ММММ гггг'"));
	ЗаголовокНачалоСравнительногоПериода = СтрШаблон(ЗаголовокШаблон, Формат(ДобавитьМесяц(КонтекстОтчета.КонецПериодаОтчета,-2*12), "ДФ = 'дд ММММ гггг'"));
	
	Если СтрНайти(Наименование, "[конец текущего периода]") <> 0 Тогда
		Нименование = СтрЗаменить(Наименование, "[конец текущего периода]", ЗаголовокКонецТекПериода);
	КонецЕсли;
	
	Если СтрНайти(Наименование, "[конец сравнительного периода]") <> 0 Тогда
		Наименование = СтрЗаменить(Наименование, "[конец сравнительного периода]", ЗаголовокКонецСравнительногоПериода);
	КонецЕсли;
	
	Если СтрНайти(Наименование, "[начало сравнительного периода]") <> 0 Тогда
		Наименование = СтрЗаменить(Наименование, "[начало сравнительного периода]", ЗаголовокНачалоСравнительногоПериода);
	КонецЕсли;

	Возврат Наименование;
	
КонецФункции

Функция ЗаполнениеФормОтчетности(ПараметрыОтчета)
	
	ЗаполнениеФормОтчетности = Новый Структура; 
	
	ЗаполнятьБаланс = НЕ ПараметрыОтчета.Свойство("ЗаполнятьБаланс")
		ИЛИ (ПараметрыОтчета.Свойство("ЗаполнятьБаланс") И ПараметрыОтчета.ЗаполнятьБаланс);
	ЗаполнениеФормОтчетности.Вставить("Баланс", ЗаполнятьБаланс);
	
	ЗаполнятьОФР = НЕ ПараметрыОтчета.Свойство("ЗаполнятьОФР")
		ИЛИ (ПараметрыОтчета.Свойство("ЗаполнятьОФР") И ПараметрыОтчета.ЗаполнятьОФР);
	ЗаполнениеФормОтчетности.Вставить("ОФР", ЗаполнятьОФР);
	
	ЗаполнятьОИК = НЕ ПараметрыОтчета.Свойство("ЗаполнятьОИК")
		ИЛИ (ПараметрыОтчета.Свойство("ЗаполнятьОИК") И ПараметрыОтчета.ЗаполнятьОИК);
	ЗаполнениеФормОтчетности.Вставить("ОИК", ЗаполнятьОИК);
	
	ЗаполнятьОДДС = НЕ ПараметрыОтчета.Свойство("ЗаполнятьОДДС")
		ИЛИ (ПараметрыОтчета.Свойство("ЗаполнятьОДДС") И ПараметрыОтчета.ЗаполнятьОДДС);
	ЗаполнениеФормОтчетности.Вставить("ОДДС", ЗаполнятьОДДС);
	
	ЗаполнятьОЦИПС = НЕ ПараметрыОтчета.Свойство("ЗаполнятьОЦИПС")
		ИЛИ (ПараметрыОтчета.Свойство("ЗаполнятьОЦИПС") И ПараметрыОтчета.ЗаполнятьОЦИПС);
	ЗаполнениеФормОтчетности.Вставить("ОЦИПС", ЗаполнятьОЦИПС);
	
	ЗаполнятьПояснения1 = НЕ ПараметрыОтчета.Свойство("ЗаполнятьПояснения1")
		ИЛИ (ПараметрыОтчета.Свойство("ЗаполнятьПояснения1") И ПараметрыОтчета.ЗаполнятьПояснения1);
	ЗаполнениеФормОтчетности.Вставить("Пояснения1", ЗаполнятьПояснения1);

	ЗаполнятьПояснения2 = НЕ ПараметрыОтчета.Свойство("ЗаполнятьПояснения2")
		ИЛИ (ПараметрыОтчета.Свойство("ЗаполнятьПояснения2") И ПараметрыОтчета.ЗаполнятьПояснения2);
	ЗаполнениеФормОтчетности.Вставить("Пояснения2", ЗаполнятьПояснения2);
	
	ЗаполнятьПояснения4 = НЕ ПараметрыОтчета.Свойство("ЗаполнятьПояснения4")
		ИЛИ (ПараметрыОтчета.Свойство("ЗаполнятьПояснения4") И ПараметрыОтчета.ЗаполнятьПояснения4);
	ЗаполнениеФормОтчетности.Вставить("Пояснения4", ЗаполнятьПояснения4);
	
	ЗаполнятьПояснения5 = НЕ ПараметрыОтчета.Свойство("ЗаполнятьПояснения5")
		ИЛИ (ПараметрыОтчета.Свойство("ЗаполнятьПояснения5") И ПараметрыОтчета.ЗаполнятьПояснения5);
	ЗаполнениеФормОтчетности.Вставить("Пояснения5", ЗаполнятьПояснения5);
	
	ЗаполнятьПояснения6 = НЕ ПараметрыОтчета.Свойство("ЗаполнятьПояснения6")
		ИЛИ (ПараметрыОтчета.Свойство("ЗаполнятьПояснения6") И ПараметрыОтчета.ЗаполнятьПояснения6);
	ЗаполнениеФормОтчетности.Вставить("Пояснения6", ЗаполнятьПояснения6);

	
	ЗаполнятьПояснения7 = НЕ ПараметрыОтчета.Свойство("ЗаполнятьПояснения7")
		ИЛИ (ПараметрыОтчета.Свойство("ЗаполнятьПояснения7") И ПараметрыОтчета.ЗаполнятьПояснения7);
	ЗаполнениеФормОтчетности.Вставить("Пояснения7", ЗаполнятьПояснения7);
	
	ЗаполнятьПояснения8 = НЕ ПараметрыОтчета.Свойство("ЗаполнятьПояснения8")
		ИЛИ (ПараметрыОтчета.Свойство("ЗаполнятьПояснения8") И ПараметрыОтчета.ЗаполнятьПояснения8);
	ЗаполнениеФормОтчетности.Вставить("Пояснения8", ЗаполнятьПояснения8);
	
	Возврат ЗаполнениеФормОтчетности;
	
КонецФункции

#КонецОбласти
