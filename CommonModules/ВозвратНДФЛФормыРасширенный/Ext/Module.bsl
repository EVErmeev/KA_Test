#Область СлужебныеПроцедурыИФункции

Процедура ПослеПолученияДанныхНаСервере(Форма, ОписаниеТаблицыНДФЛ) Экспорт
	
	РасчетЗарплатыРасширенныйФормы.ДокументыВыполненияНачисленийДополнитьФорму(Форма, ОписаниеТаблицыНДФЛ, "");
	РасчетЗарплатыРасширенныйФормы.ДокументыНачисленийДополнитьФормуРезультатыРаспределения(Форма, ОписанияТаблиц(ОписаниеТаблицыНДФЛ));
	
	ЗарплатаКадрыРасширенный.ВводРаспределенияРезультатовРасчетаДанныеВРеквизит(Форма, ОписанияТаблиц(ОписаниеТаблицыНДФЛ));
	ОтражениеЗарплатыВБухучетеРасширенный.ОбъектПриЧтенииНаСервереПредставлениеРаспределения(Форма, ОписаниеДокумента(ОписаниеТаблицыНДФЛ));
	
КонецПроцедуры

Процедура РеквизитыВДанные(Форма, ТекущийОбъект, ОписаниеТаблицыНДФЛ) Экспорт 
	
	ЗарплатаКадрыРасширенный.ВводРаспределенияРезультатовРасчетаРеквизитВДанные(Форма, ТекущийОбъект, ОписанияТаблиц(ОписаниеТаблицыНДФЛ));	
	
КонецПроцедуры

Процедура РаспределитьПоИсточникамФинансирования(Объект) Экспорт 
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.СуммыВозврата.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Организация = Объект.Организация;
	Период 		= Объект.Месяц;
	
	ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Сотрудник);
	НакопленныеКорректировки = ВзаиморасчетыССотрудниками.КорректировкиВыплаты(Организация, Период, ФизическиеЛица, Объект.Ссылка);
	НакопленныеКорректировки.Колонки.СуммаКорректировки.Имя = "Сумма";
	
	ТаблицаНДФЛ = Новый ТаблицаЗначений;
	ТаблицаНДФЛ.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаНДФЛ.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаНДФЛ.Колонки.Добавить("ВидУдержания", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	ТаблицаНДФЛ.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	ТаблицаНДФЛ.Колонки.Добавить("ВидДоходаИсполнительногоПроизводства", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДоходовИсполнительногоПроизводства"));
	ТаблицаНДФЛ.Колонки.Добавить("БазаПоУмолчанию", Новый ОписаниеТипов("Булево"));
	
	Для каждого СтрокаТЗ Из Объект.СуммыВозврата Цикл
		
		Если СтрокаТЗ.Налог > 0 Тогда
			НоваяСтрока = ТаблицаНДФЛ.Добавить();
			НоваяСтрока.ИдентификаторСтроки = СтрокаТЗ.НомерСтроки;
			НоваяСтрока.ФизическоеЛицо 		= Объект.Сотрудник;
			НоваяСтрока.Сумма 				= СтрокаТЗ.Налог;
			НоваяСтрока.ВидУдержания 		= Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛ;
			НоваяСтрока.БазаПоУмолчанию 	= (СтрокаТЗ.СтавкаНалогообложенияРезидента <> Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13);
			НоваяСтрока.ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения;
		КонецЕсли;
		
		Если СтрокаТЗ.НалогСПревышения > 0 Тогда
			НоваяСтрока = ТаблицаНДФЛ.Добавить();
			НоваяСтрока.ИдентификаторСтроки = СтрокаТЗ.НомерСтроки;
			НоваяСтрока.ФизическоеЛицо 		= Объект.Сотрудник;
			НоваяСтрока.Сумма 				= СтрокаТЗ.НалогСПревышения;
			НоваяСтрока.ВидУдержания 		= Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛСПревышения;
			НоваяСтрока.БазаПоУмолчанию 	= (СтрокаТЗ.СтавкаНалогообложенияРезидента <> Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13);
			НоваяСтрока.ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения;
		КонецЕсли;
	
	КонецЦикла;
	
	РезультатыРаспределения = ОтражениеЗарплатыВУчете.ВозвратНДФЛПоРабочимМестамИСтатьям(ТаблицаНДФЛ, НакопленныеКорректировки, Организация, Период);
	КодыСтатейФинансирования = ОтражениеЗарплатыВБухучетеРасширенный.КодыСтатейФинансирования();
	ОтражениеЗарплатыВБухучетеРасширенный.ЗаполнитьКодСтатьиФинансирования(РезультатыРаспределения, КодыСтатейФинансирования);
	
	ПараметрыДляПроверкиРезультатаРаспределения = ОтражениеЗарплатыВБухучетеРасширенный.ПараметрыДляПроверкиРезультатовРаспределенияУдержаний();
	
	Отбор = Новый Структура("ИдентификаторСтроки");
	Для Каждого СтрокаТЧ Из Объект.СуммыВозврата Цикл
		
		Отбор.ИдентификаторСтроки = СтрокаТЧ.НомерСтроки;
		СтрокиРаспределения = РезультатыРаспределения.НайтиСтроки(Отбор);
		
		РезультатРаспределения = ЗарплатаКадрыРасширенный.ТаблицаЗначенийВСтруктуру(СтрокиРаспределения); 
		СтрокаТЧ.РезультатРаспределения = РезультатРаспределения;
		
		СтрокаТЧ.КомандаРедактированияРаспределения = ОтражениеЗарплатыВБухучетеРасширенный.ПредставлениеРезультатаРаспределенияСтрокиУдержания(
			СтрокаТЧ.Налог + СтрокаТЗ.НалогСПревышения, РезультатРаспределения, ПараметрыДляПроверкиРезультатаРаспределения);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьФормуДокумента(Форма) Экспорт
	
	Если Форма.Параметры.Ключ.Пустая() Тогда
		
		Если НЕ ЗначениеЗаполнено(Форма.Объект.ПорядокВыплаты) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма, "Объект.ПорядокВыплаты", Перечисления.ХарактерВыплатыЗарплаты.Зарплата, Истина);
		КонецЕсли;
		
	КонецЕсли;	

	ВыплатаГруппа = Форма.Элементы.Вставить("ВыплатаГруппа", Тип("ГруппаФормы"),,Форма.Элементы["КомментарийГруппа"]); 
	ВыплатаГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ВыплатаГруппа.Заголовок				= НСтр("ru = 'Выплата'");
	ВыплатаГруппа.Отображение			= ОтображениеОбычнойГруппы.Нет;
	ВыплатаГруппа.Группировка			= ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;

	ПорядокВыплаты = Форма.Элементы.Вставить("ПорядокВыплаты", Тип("ПолеФормы"), ВыплатаГруппа);
	ПорядокВыплаты.Заголовок				= НСтр("ru = 'Выплата'");
	ПорядокВыплаты.ПутьКДанным				= "Объект.ПорядокВыплаты";
	ПорядокВыплаты.Вид						= ВидПоляФормы.ПолеВвода;
	ПорядокВыплаты.Ширина					= 18;
	ПорядокВыплаты.РастягиватьПоГоризонтали	= Ложь;
	ПорядокВыплаты.РежимВыбораИзСписка		= Истина;
	ПорядокВыплаты.СписокВыбора.ЗагрузитьЗначения(Перечисления.ХарактерВыплатыЗарплаты.ВсеЗначения());
	
КонецПроцедуры	

Функция ОписаниеДокумента(ОписаниеТаблицыНДФЛ)
	
	ОписанияТаблиц = Новый Структура;
	ОписанияТаблиц.Вставить("СуммыВозврата", ОписаниеТаблицыНДФЛ);
	
	Описание = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеРасчетногоДокумента();
	Описание.НДФЛИмя = "СуммыВозврата";
	Описание.ОписанияТаблицДляРаспределенияРезультата = ОписанияТаблиц;
	
	Возврат Описание;
	
КонецФункции

Функция ОписанияТаблиц(ОписаниеТаблицыНДФЛ)

	Возврат ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОписаниеТаблицыНДФЛ);

КонецФункции

#КонецОбласти