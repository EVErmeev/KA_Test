
#Область ПрограммныйИнтерфейс

// Формирует наименование для федеральной льготы (поименованной в НК).
// 
// Параметры:
//  Объект - СправочникОбъект.ОснованияЛьготПоИмущественнымНалогам - объект справочника льгот,
//		для которого нужно сформировать наименование. 
// 
// Возвращаемое значение:
//  Строка - наименование федеральной льготы
//
Функция НаименованиеФедеральнойЛьготы(Объект) Экспорт
		
	Наименование = Новый Массив;
	
	Если Объект.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.Освобождение Тогда
		Наименование.Добавить(Нстр("ru='Освобождение:'"));
	ИначеЕсли Объект.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент
		 И ЗначениеЗаполнено(Объект.ПроцентУменьшения) Тогда
		Наименование.Добавить(СтрШаблон(Нстр("ru='Снижение ставки на %1%%:'"), Объект.ПроцентУменьшения));
	ИначеЕсли Объект.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент
		 Или Объект.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиДоУказанной Тогда
		Наименование.Добавить(Нстр("ru='Снижение ставки:'"));
	ИначеЕсли Объект.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент
		 И ЗначениеЗаполнено(Объект.ПроцентУменьшения) Тогда
		Наименование.Добавить(СтрШаблон(Нстр("ru='Снижение суммы на %1%%:'"), Объект.ПроцентУменьшения));
	ИначеЕсли Объект.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент 
		 Или Объект.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаСумму Тогда
		Наименование.Добавить(Нстр("ru='Снижение суммы:'"));
	ИначеЕсли Объект.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеНалоговойБазыНаСумму Тогда
		Наименование.Добавить(Нстр("ru='Снижение налоговой базы:'"));
	КонецЕсли;	
	
	ОписаниеЛьгот = ЛьготыПоИмущественнымНалогамПовтИсп.ДанныеОЛьготахПоИмущественнымНалогам();
	ОписаниеЛьготы = ОписаниеЛьгот.ФедеральныеЛьготы[Объект.КодЛьготы];	
	Если ОписаниеЛьготы = Неопределено Тогда
		ТекстНаименования = СтрСоединить(Наименование);
		Возврат СтрЗаменить(ТекстНаименования, ":", "");
	Иначе	
		Наименование.Добавить(СтрШаблон(Нстр("ru='%1 (%2)'"), ОписаниеЛьготы.Категория, ОписаниеЛьготы.Основание));
	КонецЕсли;
	
	Возврат СтрСоединить(Наименование, " ");
	
КонецФункции

#КонецОбласти