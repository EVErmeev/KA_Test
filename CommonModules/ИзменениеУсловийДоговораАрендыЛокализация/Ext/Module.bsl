////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Изменение условий договора аренды".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// 
// Параметры:
//  Объект - ДокументОбъект.ИзменениеУсловийДоговораАренды -
//  ВспомогательныеРеквизиты - Структура -
// 	МассивНепроверяемыхРеквизитов - Массив из Строка -
// 	Отказ - Булево
Процедура ОбработкаПроверкиЗаполнения(Объект, ВспомогательныеРеквизиты, МассивНепроверяемыхРеквизитов, Отказ) Экспорт

	//++ Локализация
	
	МассивНепроверяемыхРеквизитов.Добавить("ОС.СтоимостьНУ");
	МассивНепроверяемыхРеквизитов.Добавить("ОС.СрокИспользованияБУ");
	МассивНепроверяемыхРеквизитов.Добавить("ОС.СрокИспользованияНУ");
	
	Если КонецДня(Объект.Дата) + 1 = ВспомогательныеРеквизиты.НачалоУчетаАрендыПоФСБУ25_2018 
		И ЗначениеЗаполнено(Объект.Договор) Тогда
			
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Договор, "Балансодержатель,ТипДоговора");
		
		Если РеквизитыДоговора.ТипДоговора = Перечисления.ТипыДоговоровАренды.Аренда Тогда
				
			ТекстСообщения = НСтр("ru = 'Для перехода на учет по ФСБУ 25/2018 не требуется оформление документа по договору аренды'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект, "Договор",, Отказ);
			
		ИначеЕсли РеквизитыДоговора.ТипДоговора = Перечисления.ТипыДоговоровАренды.Лизинг
			И РеквизитыДоговора.Балансодержатель = Перечисления.БалансодержательПредметовАренды.Арендодатель Тогда
				
			ТекстСообщения = НСтр("ru = 'Для перехода на учет по ФСБУ 25/2018 не требуется оформление документа по договору лизинга, где балансодержатель арендодатель'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект, "Договор",, Отказ);
		 
		КонецЕсли;
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

Функция ТекстЗапросаДляПроверкиОсновныхСредств() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.СтоимостьБУ КАК СтоимостьБУ,
	|	ТаблицаОС.СтоимостьНУ КАК СтоимостьНУ,
	|	ТаблицаОС.СтоимостьУУ КАК СтоимостьУУ,
	|	ТаблицаОС.СрокИспользованияБУ КАК СрокИспользованияБУ,
	|	ТаблицаОС.СрокИспользованияНУ КАК СрокИспользованияНУ,
	|	ТаблицаОС.СрокИспользованияУУ КАК СрокИспользованияУУ
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	&ТаблицаОС КАК ТаблицаОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатПроверки.НомерСтроки КАК НомерСтроки,
	|	РезультатПроверки.ОсновноеСредство КАК ОсновноеСредство,
	|	РезультатПроверки.НеЗаполненаСтоимостьБУ КАК НеЗаполненаСтоимостьБУ,
	|	РезультатПроверки.НеЗаполненаСтоимостьНУ КАК НеЗаполненаСтоимостьНУ,
	|	РезультатПроверки.НеЗаполненаСтоимостьУУ КАК НеЗаполненаСтоимостьУУ,
	|	РезультатПроверки.НеЗаполненСрокИспользованияБУ КАК НеЗаполненСрокИспользованияБУ,
	|	РезультатПроверки.НеЗаполненСрокИспользованияНУ КАК НеЗаполненСрокИспользованияНУ,
	|	РезультатПроверки.НеЗаполненСрокИспользованияУУ КАК НеЗаполненСрокИспользованияУУ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|		ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|		ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеБУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И ТаблицаОС.СтоимостьБУ = 0 КАК НеЗаполненаСтоимостьБУ,
	|
	|		ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеБУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И ТаблицаОС.СрокИспользованияБУ = 0 КАК НеЗаполненСрокИспользованияБУ,
	|
	|		ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеНУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И спрДоговорыАренды.Балансодержатель = ЗНАЧЕНИЕ(Перечисление.БалансодержательПредметовАренды.Арендатор)
	|			И НЕ &ИспользуетсяУчетАрендыПоФСБУ25_2018
	|			И ТаблицаОС.СтоимостьНУ = 0 КАК НеЗаполненаСтоимостьНУ,
	|
	|		ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеНУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И спрДоговорыАренды.Балансодержатель = ЗНАЧЕНИЕ(Перечисление.БалансодержательПредметовАренды.Арендатор)
	|			И ТаблицаОС.СрокИспользованияНУ = 0 КАК НеЗаполненСрокИспользованияНУ,
	|
	|		ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И ТаблицаОС.СтоимостьУУ = 0 КАК НеЗаполненаСтоимостьУУ,
	|
	|		ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|			И ТаблицаОС.СрокИспользованияУУ = 0 КАК НеЗаполненСрокИспользованияУУ
	|	ИЗ
	|		ТаблицаОС КАК ТаблицаОС
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыАренды КАК спрДоговорыАренды
	|			ПО спрДоговорыАренды.Ссылка = &Договор
	|			
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|				&Дата, 
	|				ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И Организация = &Организация
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							ОсновноеСредство
	|						ИЗ
	|							ТаблицаОС)) КАК ПорядокУчетаОСБУ
	|				ПО ПорядокУчетаОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|				&Дата, 
	|				ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|					И Организация = &Организация
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							ОсновноеСредство
	|						ИЗ
	|							ТаблицаОС)) КАК ПорядокУчетаОСУУ
	|				ПО ПорядокУчетаОСУУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		) КАК РезультатПроверки
	|ГДЕ
	|	(РезультатПроверки.НеЗаполненаСтоимостьБУ
	|		ИЛИ РезультатПроверки.НеЗаполненаСтоимостьНУ
	|		ИЛИ РезультатПроверки.НеЗаполненаСтоимостьУУ
	|		ИЛИ РезультатПроверки.НеЗаполненСрокИспользованияБУ
	|		ИЛИ РезультатПроверки.НеЗаполненСрокИспользованияНУ
	|		ИЛИ РезультатПроверки.НеЗаполненСрокИспользованияУУ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СообщитьОПроверкеОсновныхСредств(Выборка, Объект, Отказ) Экспорт
	
	//++ Локализация
	
	ШаблонСообщенияСтоимостьНУ = НСтр("ru = 'Не заполнена колонка ""Стоимость (НУ)"" в строке %1 списка ""Предметы аренды""'");
	ШаблонСообщенияСрокИспользованияНУ = НСтр("ru = 'Не заполнена колонка ""Срок использования (налоговый учет)"" в строке %1 списка ""Предметы аренды""'");

	Если Выборка.НеЗаполненаСтоимостьНУ Тогда
		ТекстСообщения = СтрШаблон(ШаблонСообщенияСтоимостьНУ, Выборка.НомерСтроки);
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", Выборка.НомерСтроки, "СтоимостьНУ");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
	КонецЕсли;

	Если Выборка.НеЗаполненСрокИспользованияНУ Тогда
		ТекстСообщения = СтрШаблон(ШаблонСообщенияСрокИспользованияНУ, Выборка.НомерСтроки);
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", Выборка.НомерСтроки, "СрокИспользованияНУ");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Поле, "", Отказ);
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область Механизмы

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	МеханизмыДокумента.Добавить("ИмущественныеНалоги");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ПроводкиРегУчета

Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "";
	
	//++ Локализация
	
	СписокЗапросов = Новый Массив;
	
	#Область УвеличениеСтоимостиОС // Дт <01.03> - Кт <76.07.1>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Увеличение стоимости ОС (Дт <01.03> - Кт <76.07.1>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.СтоимостьРегл КАК Сумма,
	|	СтрокиДт.Стоимость КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаДт,
	|	СтрокиДт.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	СтрокиДт.Подразделение КАК ПодразделениеДт,
	|	СтрокиДт.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СтрокиДт.СтоимостьНУ КАК СуммаНУДт,
	|	СтрокиДт.СтоимостьПР КАК СуммаПРДт,
	|	СтрокиДт.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	СтрокиКт.Валюта КАК ВалютаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	СтрокиКт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоКт1,
	|	СтрокиКт.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	СтрокиКт.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	СтрокиКт.СуммаРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Увеличение стоимости арендованных ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УвеличениеСтоимостиАрендованныхОС)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область БалансировкаВР_УвеличениеСтоимостиОС // Дт <76.А> - Кт <76.07.1>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Балансировка ВР, увеличение стоимости ОС (Дт <76.А> - Кт <76.07.1>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеДт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.БалансировкаВРПоДоговорамАренды) КАК СчетДт,
	|	СтрокиКт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоДт1,
	|	СтрокиКт.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СтрокиДт.СтоимостьВР КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	-СтрокиДт.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	СтрокиКт.Валюта КАК ВалютаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	СтрокиКт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоКт1,
	|	СтрокиКт.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Балансировка ВР по договору аренды"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УвеличениеСтоимостиАрендованныхОС)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеСтоимостиОС // Дт <76.07.1> - Кт <01.03>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уменьшение стоимости ОС (Дт <76.07.1> - Кт <01.03>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиКт.СтоимостьРегл КАК Сумма,
	|	СтрокиКт.Стоимость КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	СтрокиДт.Валюта КАК ВалютаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоДт1,
	|	СтрокиДт.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	СтрокиДт.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СтрокиДт.СуммаРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаКт,
	|	СтрокиКт.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	СтрокиКт.Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	СтрокиКт.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	СтрокиКт.СтоимостьНУ КАК СуммаНУКт,
	|	СтрокиКт.СтоимостьПР КАК СуммаПРКт,
	|	СтрокиКт.СтоимостьВР КАК СуммаВРКт,
	|	
	|	""Уменьшение стоимости арендованных ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеСтоимостиАрендованныхОС)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область БалансировкаВР_УменьшениеСтоимостиОС // Дт <76.07.1> - Кт <76.А>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Балансировка ВР, уменьшение стоимости ОС (Дт <76.07.1> - Кт <76.А>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	СтрокиДт.Валюта КАК ВалютаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоДт1,
	|	СтрокиДт.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеКт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.БалансировкаВРПоДоговорамАренды) КАК СчетКт,
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоКт1,
	|	СтрокиДт.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	СтрокиКт.СтоимостьВР КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	-СтрокиКт.СтоимостьВР КАК СуммаВРКт,
	|	
	|	""Балансировка ВР по договору аренды"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеСтоимостиАрендованныхОС)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеНДС // Дт <76.07.9> - Кт <76.07.1>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Увеличение НДС (Дт <76.07.9> - Кт <76.07.1>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.СуммаРегл КАК Сумма,
	|	СтрокиДт.СуммаУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСПриАренде) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	СтрокиДт.Валюта КАК ВалютаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоДт1,
	|	СтрокиДт.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	СтрокиДт.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СтрокиДт.СуммаРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	СтрокиКт.Валюта КАК ВалютаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	СтрокиКт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоКт1,
	|	СтрокиКт.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	СтрокиКт.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	СтрокиКт.СуммаРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Увеличение НДС по аренде"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УвеличениеНДСПоАренде)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеНДС // Дт <76.07.1> - Кт <76.07.9>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уменьшение НДС (Дт <76.07.1> - Кт <76.07.9>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.СуммаРегл КАК Сумма,
	|	СтрокиДт.СуммаУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	СтрокиДт.Валюта КАК ВалютаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоДт1,
	|	СтрокиДт.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	СтрокиДт.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СтрокиДт.СуммаРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСПриАренде) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	СтрокиКт.Валюта КАК ВалютаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	СтрокиКт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоКт1,
	|	СтрокиКт.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	СтрокиКт.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	СтрокиКт.СуммаРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Уменьшение НДС по аренде"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеНДСПоАренде)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеПроцентов // Дт <76.07.5> - Кт <76.07.1>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Увеличение процентов (Дт <76.07.5> - Кт <76.07.1>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.СуммаРегл КАК Сумма,
	|	СтрокиДт.СуммаУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НачислениеПроцентовПоАренде) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	СтрокиДт.Валюта КАК ВалютаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоДт1,
	|	СтрокиДт.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	СтрокиДт.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	СтрокиКт.Валюта КАК ВалютаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	СтрокиКт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоКт1,
	|	СтрокиКт.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	СтрокиКт.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Увеличение процентов по аренде"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УвеличениеПроцентовПоАренде)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеПроцентов // Дт <76.07.1> - Кт <76.07.5>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уменьшение процентов (Дт <76.07.1> - Кт <76.07.5>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.СуммаРегл КАК Сумма,
	|	СтрокиДт.СуммаУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	СтрокиДт.Валюта КАК ВалютаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоДт1,
	|	СтрокиДт.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	СтрокиДт.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НачислениеПроцентовПоАренде) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	СтрокиКт.Валюта КАК ВалютаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	СтрокиКт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоКт1,
	|	СтрокиКт.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	СтрокиКт.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Уменьшение процентов по аренде"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеПроцентовПоАренде)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ИзлишнеНачисленныеПроценты // Дт <76.07.5> - Кт <91.01>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Излишне начисленные проценты (Дт <76.07.5> - Кт <91.01>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.СуммаРегл КАК Сумма,
	|	СтрокиДт.СуммаУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НачислениеПроцентовПоАренде) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	СтрокиДт.Валюта КАК ВалютаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоДт1,
	|	СтрокиДт.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	СтрокиДт.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	СтрокиКт.СтатьяДоходов КАК ГруппаФинансовогоУчетаКт,
	|	СтрокиКт.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	СтрокиКт.Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	СтрокиКт.СтатьяДоходов КАК СубконтоКт1,
	|	СтрокиКт.АналитикаДоходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	СтрокиДт.СуммаРегл КАК СуммаВРКт,
	|	
	|	""Излишне начисленные проценты"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеДоходы КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзлишнеНачисленныеПроцентыПоАренде)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область НедоначисленныеПроценты // Дт <91.02> - Кт <76.07.5>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Недоначисленные проценты (Дт <91.02> - Кт <76.07.5>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.СуммаРегл КАК Сумма,
	|	СтрокиДт.СуммаУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	СтрокиДт.СтатьяРасходов КАК АналитикаУчетаДт,
	|	СтрокиДт.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	СтрокиДт.Подразделение КАК ПодразделениеДт,
	|	СтрокиДт.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.СтатьяРасходов КАК СубконтоДт1,
	|	СтрокиДт.АналитикаРасходов КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|			ВЫБОР КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|					ТОГДА ОбъектыСтроительства.СпособСтроительства
	|				ИНАЧЕ 
	|					ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ) 
	|			КОНЕЦ
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СтрокиДт.СуммаРегл - СтрокиДт.ПостояннаяРазница - СтрокиДт.ВременнаяРазница КАК СуммаНУДт,
	|	СтрокиДт.ПостояннаяРазница КАК СуммаПРДт,
	|	СтрокиДт.ВременнаяРазница КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НачислениеПроцентовПоАренде) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	СтрокиКт.Валюта КАК ВалютаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	СтрокиКт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоКт1,
	|	СтрокиКт.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	СтрокиКт.Сумма КАК ВалютнаяСуммаКт,
	|	
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Недоначисленные проценты"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И СтрокиКт.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО СтрокиДт.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО ОбъектыСтроительства.Ссылка = СтрокиДт.АналитикаРасходов
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НедоначисленныеПроцентыПоАренде)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеНакопленнойАмортизации // Дт <91.02> - Кт <02.03>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Увеличение накопленной амортизации (Дт <91.02> - Кт <02.03>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.СуммаРегл КАК Сумма,
	|	СтрокиДт.СуммаУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	СтрокиДт.СтатьяРасходов КАК АналитикаУчетаДт,
	|	СтрокиДт.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	СтрокиДт.Подразделение КАК ПодразделениеДт,
	|	СтрокиДт.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.СтатьяРасходов КАК СубконтоДт1,
	|	СтрокиДт.АналитикаРасходов КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|			ВЫБОР КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|					ТОГДА ОбъектыСтроительства.СпособСтроительства
	|				ИНАЧЕ 
	|					ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ) 
	|			КОНЕЦ
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СтрокиДт.СуммаРегл - СтрокиДт.ПостояннаяРазница - СтрокиДт.ВременнаяРазница КАК СуммаНУДт,
	|	СтрокиДт.ПостояннаяРазница КАК СуммаПРДт,
	|	СтрокиДт.ВременнаяРазница КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА) КАК ВидСчетаКт,
	|	СтрокиКт.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	СтрокиКт.Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	СтрокиКт.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	
	|	0 КАК КоличествоКт,
	|	СтрокиКт.АмортизацияНУ КАК СуммаНУКт,
	|	СтрокиКт.АмортизацияПР КАК СуммаПРКт,
	|	СтрокиКт.АмортизацияВР КАК СуммаВРКт,
	|	
	|	""Увеличение накопленной амортизации"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО СтрокиДт.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО ОбъектыСтроительства.Ссылка = СтрокиДт.АналитикаРасходов
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УвеличениеНакопленнойАмортизацииОС)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеНакопленнойАмортизацииСтатьяАктивов // Дт <91.02> - Кт <02.03>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Увеличение накопленной амортизации (Дт <91.02> - Кт <02.03>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.СуммаРегл КАК Сумма,
	|	СтрокиДт.СуммаУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	СтрокиДт.Подразделение КАК ПодразделениеДт,
	|	СтрокиДт.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	СтрокиДт.НастройкаСчетовУчета.СчетУчета КАК СчетДт,
	|	
	|	СтрокиДт.НастройкаСчетовУчета.Субконто1 КАК СубконтоДт1,
	|	СтрокиДт.НастройкаСчетовУчета.Субконто2 КАК СубконтоДт2,
	|	СтрокиДт.НастройкаСчетовУчета.Субконто3 КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СтрокиДт.СуммаРегл - СтрокиДт.ПостояннаяРазница - СтрокиДт.ВременнаяРазница КАК СуммаНУДт,
	|	СтрокиДт.ПостояннаяРазница КАК СуммаПРДт,
	|	СтрокиДт.ВременнаяРазница КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА) КАК ВидСчетаКт,
	|	СтрокиКт.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	СтрокиКт.Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	СтрокиКт.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	
	|	0 КАК КоличествоКт,
	|	СтрокиКт.АмортизацияНУ КАК СуммаНУКт,
	|	СтрокиКт.АмортизацияПР КАК СуммаПРКт,
	|	СтрокиКт.АмортизацияВР КАК СуммаВРКт,
	|	
	|	""Увеличение накопленной амортизации"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УвеличениеНакопленнойАмортизацииОС)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеНакопленнойАмортизации // Дт <02.03> - Кт <91.01>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уменьшение накопленной амортизации (Дт <02.03> - Кт <91.01>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СтрокиДт.АмортизацияРегл КАК Сумма,
	|	СтрокиДт.Амортизация КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА) КАК ВидСчетаДт,
	|	СтрокиДт.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	СтрокиДт.Подразделение КАК ПодразделениеДт,
	|	СтрокиДт.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СтрокиДт.АмортизацияНУ КАК СуммаНУДт,
	|	СтрокиДт.АмортизацияПР КАК СуммаПРДт,
	|	СтрокиДт.АмортизацияВР КАК СуммаВРДт,
	|
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	СтрокиКт.СтатьяДоходов КАК ГруппаФинансовогоУчетаКт,
	|	СтрокиКт.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	СтрокиКт.Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	СтрокиКт.СтатьяДоходов КАК СубконтоКт1,
	|	СтрокиКт.АналитикаДоходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	СтрокиКт.СуммаРегл КАК СуммаВРКт,
	|	
	|	""Уменьшение накопленной амортизации"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеДоходы КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеНакопленнойАмортизацииОС)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область НедоначисленныеПроцентыСтатьяАктивов // Дт <84.01> - Кт <76.07.5>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Недоначисленные проценты (Дт <84.01> - Кт <76.07.5>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.СуммаРегл КАК Сумма,
	|	СтрокиДт.СуммаУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО  КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	СтрокиДт.Подразделение КАК ПодразделениеДт,
	|	СтрокиДт.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	СтрокиДт.НастройкаСчетовУчета.СчетУчета КАК СчетДт,
	|	
	|	СтрокиДт.НастройкаСчетовУчета.Субконто1 КАК СубконтоДт1,
	|	СтрокиДт.НастройкаСчетовУчета.Субконто2 КАК СубконтоДт2,
	|	СтрокиДт.НастройкаСчетовУчета.Субконто3 КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НачислениеПроцентовПоАренде) КАК ВидСчетаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	СтрокиКт.Валюта КАК ВалютаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	СтрокиКт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоКт1,
	|	СтрокиКт.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	СтрокиКт.Сумма КАК ВалютнаяСуммаКт,
	|	
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Недоначисленные проценты"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И СтрокиКт.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)
	|
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НедоначисленныеПроцентыПоАренде)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область БалансировкаВР_НедоначисленныеПроцентыСтатьяАктивов // Дт <84.01> - Кт <76.А>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Балансировка ВР, недоначисленные проценты (Дт <84.01> - Кт <76.А>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО  КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	СтрокиДт.Подразделение КАК ПодразделениеДт,
	|	СтрокиДт.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	СтрокиДт.НастройкаСчетовУчета.СчетУчета КАК СчетДт,
	|	
	|	СтрокиДт.НастройкаСчетовУчета.Субконто1 КАК СубконтоДт1,
	|	СтрокиДт.НастройкаСчетовУчета.Субконто2 КАК СубконтоДт2,
	|	СтрокиДт.НастройкаСчетовУчета.Субконто3 КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	ВЫРАЗИТЬ(СтрокиКт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.АналитикаУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.БалансировкаВРПоДоговорамАренды) КАК СчетКт,
	|	СтрокиКт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоКт1,
	|	СтрокиКт.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	
	|	0 КАК КоличествоКт,
	|	-СтрокиДт.СуммаРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	СтрокиДт.СуммаРегл КАК СуммаВРКт,
	|	
	|	""Балансировка ВР по договору аренды"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И СтрокиКт.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)
	|
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НедоначисленныеПроцентыПоАренде)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеНакопленнойАмортизации // Дт <02.03> - Кт <84.01>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уменьшение накопленной амортизации (Дт <02.03> - Кт <84.01>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор      КАК Ссылка,
	|	СтрокиДт.Период           КАК Период,
	|	СтрокиДт.Организация      КАК Организация,
	|	НЕОПРЕДЕЛЕНО              КАК ИдентификаторСтроки,
	|
	|	СтрокиДт.АмортизацияРегл  КАК Сумма,
	|	СтрокиДт.Амортизация      КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА) КАК ВидСчетаДт,
	|	СтрокиДт.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО                    КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	СтрокиДт.Подразделение                   КАК ПодразделениеДт,
	|	СтрокиДт.НаправлениеДеятельности         КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.ОсновноеСредство  КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО               КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО               КАК СубконтоДт3,
	|	
	|	0                          КАК ВалютнаяСуммаДт,
	|	0                          КАК КоличествоДт,
	|	СтрокиДт.АмортизацияНУ     КАК СуммаНУДт,
	|	СтрокиДт.АмортизацияПР     КАК СуммаПРДт,
	|	СтрокиДт.АмортизацияВР     КАК СуммаВРДт,
	|
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО                                      КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО                                      КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО               КАК ВалютаКт,
	|	СтрокиКт.Подразделение     КАК ПодразделениеКт,
	|	СтрокиКт.НаправлениеДеятельности               КАК НаправлениеДеятельностиКт,
	|	
	|	СтрокиКт.НастройкаСчетовУчета.СчетУчета КАК СчетКт,
	|	СтрокиКт.НастройкаСчетовУчета.Субконто1 КАК СубконтоКт1,
	|	СтрокиКт.НастройкаСчетовУчета.Субконто2 КАК СубконтоКт2,
	|	СтрокиКт.НастройкаСчетовУчета.Субконто3 КАК СубконтоКт3,
	|	
	|	0                          КАК ВалютнаяСуммаКт,
	|	0                          КАК КоличествоКт,
	|	0                          КАК СуммаНУКт,
	|	0                          КАК СуммаПРКт,
	|	СтрокиКт.СуммаРегл         КАК СуммаВРКт,
	|	
	|	""Уменьшение накопленной амортизации"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеНакопленнойАмортизацииОС)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область БалансировкаВР_УменьшениеНакопленнойАмортизации // Дт <76.А> - Кт <84.01>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Балансировка ВР, уменьшение накопленной амортизации (Дт <76.А> - Кт <84.01>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор      КАК Ссылка,
	|	СтрокиДт.Период           КАК Период,
	|	СтрокиДт.Организация      КАК Организация,
	|	НЕОПРЕДЕЛЕНО              КАК ИдентификаторСтроки,
	|
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО                     КАК ВалютаДт,
	|	СтрокиДт.Подразделение           КАК ПодразделениеДт,
	|	СтрокиДт.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.БалансировкаВРПоДоговорамАренды) КАК СчетДт,
	|	Операция.Контрагент КАК СубконтоДт1,
	|	Операция.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0                          КАК ВалютнаяСуммаДт,
	|	0                          КАК КоличествоДт,
	|	СтрокиДт.АмортизацияВР     КАК СуммаНУДт,
	|	0                          КАК СуммаПРДт,
	|	-СтрокиДт.АмортизацияВР    КАК СуммаВРДт,
	|
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО                                      КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО                                      КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО               КАК ВалютаКт,
	|	СтрокиКт.Подразделение     КАК ПодразделениеКт,
	|	СтрокиКт.НаправлениеДеятельности               КАК НаправлениеДеятельностиКт,
	|	
	|	СтрокиКт.НастройкаСчетовУчета.СчетУчета КАК СчетКт,
	|	СтрокиКт.НастройкаСчетовУчета.Субконто1 КАК СубконтоКт1,
	|	СтрокиКт.НастройкаСчетовУчета.Субконто2 КАК СубконтоКт2,
	|	СтрокиКт.НастройкаСчетовУчета.Субконто3 КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Балансировка ВР по договору аренды"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеУсловийДоговораАренды КАК Операция
	|	ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|		
	|ГДЕ
	|	СтрокиДт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеНакопленнойАмортизацииОС)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеЗалоговойСтоимостиОС // Дт 001
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Увеличение залоговой стоимости ОС (Дт 001 - )
	|ВЫБРАТЬ
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.ЗалоговаяСтоимость КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗабалансовыйУчетВНА) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	Операция.Контрагент КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Увеличение залоговой стоимости арендованных ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеУсловийДоговораАренды КАК Операция
	|	ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|	ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|ГДЕ
	|	Строки.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УвеличениеСтоимостиАрендованныхОС)
	|	И Строки.ЗалоговаяСтоимость <> 0";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеЗалоговойСтоимостиОС // Кт 001
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уменьшение залоговой стоимости ОС (Дт  - Кт 001)
	|ВЫБРАТЬ
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.ЗалоговаяСтоимость КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗабалансовыйУчетВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	Строки.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	Операция.Контрагент КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Уменьшение залоговой стоимости арендованных ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеУсловийДоговораАренды КАК Операция
	|	ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|	ПО Строки.Регистратор = Строки.Регистратор
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ГДЕ
	|	Строки.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеСтоимостиАрендованныхОС)
	|	И Строки.ЗалоговаяСтоимость <> 0";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ПревышениеУменьшенияППАНадБалансовойСтоимостью // Дт <76.07.1> - Кт <91.01>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Превышение уменьшения ППА над балансовой стоимостью (Дт <76.07.1> - Кт <91.01>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиКТ.СуммаРегл КАК Сумма,
	|	СтрокиКТ.СуммаУпр  КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	СтрокиДт.Валюта КАК ВалютаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоДт1,
	|	СтрокиДт.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт, //СтрокиДт.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	СтрокиКт.СтатьяДоходов КАК ГруппаФинансовогоУчетаКт,
	|	СтрокиКт.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	СтрокиКт.Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	СтрокиКт.СтатьяДоходов КАК СубконтоКт1,
	|	СтрокиКт.АналитикаДоходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	СтрокиКТ.СуммаРегл КАК СуммаВРКт,
	|	
	|	""Превышение уменьшения ППА над балансовой стоимостью"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеДоходы КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеСтоимостиАрендованныхОС)
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|ГДЕ
	|	СтрокиКт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеСтоимостиАрендованныхОС)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область БалансировкаВР_ПревышениеУменьшенияППАНадБалансовойСтоимостью // Дт <76.А> - Кт <91.01>
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Превышение уменьшения ППА над балансовой стоимостью (Дт <76.А> - Кт <91.01>)
	|ВЫБРАТЬ
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	СтрокиДт.Валюта КАК ВалютаДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).Подразделение КАК ПодразделениеДт,
	|	ВЫРАЗИТЬ(СтрокиДт.Договор КАК Справочник.ДоговорыАренды).НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.БалансировкаВРПоДоговорамАренды) КАК СчетДт,
	|	
	|	СтрокиДт.АналитикаУчетаПоПартнерам.Контрагент КАК СубконтоДт1,
	|	СтрокиДт.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	-СтрокиКТ.СуммаРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	СтрокиКТ.СуммаРегл  КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	СтрокиКт.СтатьяДоходов КАК ГруппаФинансовогоУчетаКт,
	|	СтрокиКт.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	СтрокиКт.Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	СтрокиКт.СтатьяДоходов КАК СубконтоКт1,
	|	СтрокиКт.АналитикаДоходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Превышение уменьшения ППА над балансовой стоимостью, балансировка ВР"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СтрокиДт
	|	ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|		И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеДоходы КАК СтрокиКт
	|	ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|		И СтрокиКт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеСтоимостиАрендованныхОС)
	|		И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|		И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|ГДЕ
	|	СтрокиКт.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеСтоимостиАрендованныхОС)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти

	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ФормаДокумента

// Описание
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Содержит:
//				* Элементы - ВсеЭлементыФормы - 
Процедура УстановитьУсловноеОформление(Форма) Экспорт

	//++ Локализация
	УсловноеОформление = Форма.УсловноеОформление;
	Элементы = Форма.Элементы;
	
	#Область ДоступностьБУ
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОССтоимостьБУ.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОССрокИспользованияБУ.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОС.ПринятКУчетуБУ");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не принято к учету>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекста);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	#КонецОбласти
	
	#Область ДоступностьНУ
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОССтоимостьНУ.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОССрокИспользованияНУ.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОС.ПринятКУчетуНУ");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не принято к учету>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекста);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	#КонецОбласти

	//-- Локализация
	
КонецПроцедуры

Функция ТекстЗапросаДляЗаполненияСлужебныхРеквизитовТЧ() Экспорт
	
	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СпрОбъектыЭксплуатации.Ссылка,
	|
	|	ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК ПринятКУчетуУУ,
	|	ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеБУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК ПринятКУчетуБУ,
	|
	|	ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеНУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|		И спрДоговорыАренды.Балансодержатель = ЗНАЧЕНИЕ(Перечисление.БалансодержательПредметовАренды.Арендатор) КАК ПринятКУчетуНУ
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК СпрОбъектыЭксплуатации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыАренды КАК спрДоговорыАренды
	|		ПО спрДоговорыАренды.Ссылка = &Договор
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|			&Дата, 
	|			ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И Организация = &Организация 
	|				И ОсновноеСредство В (&СписокОС)) КАК ПорядокУчетаОСУУ
	|		ПО ПорядокУчетаОСУУ.ОсновноеСредство = СпрОбъектыЭксплуатации.Ссылка
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&Дата, 
	|			ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И Организация = &Организация 
	|				И ОсновноеСредство В (&СписокОС)) КАК ПорядокУчетаОСБУ
	|		ПО ПорядокУчетаОСБУ.ОсновноеСредство = СпрОбъектыЭксплуатации.Ссылка
	|ГДЕ
	|	СпрОбъектыЭксплуатации.Ссылка В (&СписокОС)";
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Прочее

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	РеглУчетПроведениеСервер.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- Локализация
	
КонецПроцедуры

Функция ТекстЗапросаЗаполнениеТЧПоДоговору() Экспорт
	
	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	ТекстЗапроса =	
	"ВЫБРАТЬ
	|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	АрендованныеОС.Стоимость КАК СправедливаяСтоимость,
	|	АрендованныеОС.Стоимость КАК Стоимость,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьОстаток, 0) КАК СтоимостьУУ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьРеглОстаток + СтоимостьОС.СтоимостьЦФОстаток, 0) КАК СтоимостьБУ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУОстаток, 0) КАК СтоимостьНУ,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ, 0) КАК СрокИспользованияБУ,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ, 0) КАК СрокИспользованияНУ,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУ.СрокИспользования, 0) КАК СрокИспользованияУУ
	|ИЗ
	|	втАрендованныеОС КАК АрендованныеОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС.Остатки(
	|			&ПериодГраница, 
	|			Организация = &Организация
	|				И ОсновноеСредство В (
	|					ВЫБРАТЬ 
	|						втАрендованныеОС.ОсновноеСредство 
	|					ИЗ 
	|						втАрендованныеОС КАК втАрендованныеОС)) КАК СтоимостьОС
	|		ПО СтоимостьОС.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
	|			&Дата, 
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И ОсновноеСредство В (
	|					ВЫБРАТЬ 
	|						втАрендованныеОС.ОсновноеСредство 
	|					ИЗ 
	|						втАрендованныеОС КАК втАрендованныеОС)) КАК ПараметрыАмортизацииОСБУ
	|		ПО ПараметрыАмортизацииОСБУ.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|			&Дата, 
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И ОсновноеСредство В (
	|					ВЫБРАТЬ 
	|						втАрендованныеОС.ОсновноеСредство 
	|					ИЗ 
	|						втАрендованныеОС КАК втАрендованныеОС)) КАК ПараметрыАмортизацииОСУУ
	|		ПО ПараметрыАмортизацииОСУУ.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	АрендованныеОС.ОсновноеСредство.Наименование,
	|	АрендованныеОС.ОсновноеСредство.ИнвентарныйНомер";
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
