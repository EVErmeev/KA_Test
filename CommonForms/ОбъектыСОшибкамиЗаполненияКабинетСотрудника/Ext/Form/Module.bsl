#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьПозиции = ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура");
	
	ОбновитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСписокОшибокЗаполненияКабинетСотрудника" Тогда
		ОбновитьСписки();
	ИначеЕсли ИмяСобытия = "ИзмененаПубликуемаяСтруктураПредприятия" Тогда
		ОбновитьЭлементыФормы();
		ОбновитьСписки();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьЗаполнениеОбъектов(Команда)
	НачатьПроверкуЗаполнения();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьПроверкуЗаполнения()

	ДлительнаяОперация = ДлительнаяОперацияПроверкаЗаполнения();
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = НСтр("ru = 'Проверка заполнения публикуемых объектов выполнена.'");
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПроверкаЗаполненияЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция ДлительнаяОперацияПроверкаЗаполнения()

	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.Вставить("НаименованиеФоновогоЗадания", НСтр("ru = 'Проверка заполнения публикуемых объектов'"));
	ПараметрыВыполненияВФоне.ОжидатьЗавершение = 0;
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"КабинетСотрудника.ПроверитьЗаполнениеПубликуемыхОбъектовФоновоеЗадание",
		Неопределено,
		ПараметрыВыполненияВФоне);

КонецФункции

&НаКлиенте
Процедура ПроверкаЗаполненияЗавершение(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбновитьСписки();
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(, ДлительнаяОперация.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписки()
	
	Элементы.ФизическиеЛица.Обновить();
	Элементы.Сотрудники.Обновить();
	Если Элементы.СтраницаПозиции.Видимость Тогда
		Элементы.Позиции.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыФормы()

	Показывать = ПоказыватьПозиции И Не КабинетСотрудника.ПубликоватьСтруктуруЮридическихЛиц();
	Элементы.СтраницаПозиции.Видимость 		= Показывать;
	Элементы.СотрудникиПозиция.Видимость 	= Показывать;

КонецПроцедуры

#КонецОбласти