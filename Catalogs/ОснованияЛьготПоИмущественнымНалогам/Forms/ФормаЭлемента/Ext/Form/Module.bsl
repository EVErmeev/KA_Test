
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОписаниеЛьгот = ЛьготыПоИмущественнымНалогамПовтИсп.ДанныеОЛьготахПоИмущественнымНалогам();
	
	Если ЗначениеЗаполнено(Объект.ОснованиеРегиональнойЛьготы) Тогда
		СведенияОПунктеРегЗакона = ЛьготыПоИмущественнымНалогамКлиентСервер.НормативныеДанныеОснованияЛьготы(Объект.ОснованиеРегиональнойЛьготы);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СведенияОПунктеРегЗакона);
	КонецЕсли;	
	
	Если Объект.УстановленаРегиональнымЗаконом Тогда
		// Установка списка выбора регионов
		КодРегиона = Формат(Объект.КодРегиона, "ЧЦ=2; ЧН=; ЧВН=");
		ЗаполнитьСписокВыбораРегиона(Элементы.КодРегионаРегистрации.СписокВыбора, КодРегиона);
		// Заполнение по классификатору 	
		ЗаполнитьИнформациюОРегиональнойЛьготе(ЭтотОбъект, Объект.ВидЛьготы);
	Иначе
		ЗаполнитьИнформациюОФедеральнойЛьготе(ЭтотОбъект);
	КонецЕсли;	

	УстановитьЗаголовок();
	УправлениеФормой(ЭтотОбъект);	
	
	КлючСохраненияПоложенияОкна = Объект.КодЛьготы + ?(Объект.УстановленаРегиональнымЗаконом, "_Региональная", "_Федеральная")
		+ ?(Объект.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент
		 Или Объект.ВидЛьготы = Перечисления.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент, "_ПроцентУменьшения", "");
		
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("Статья"); // проверим ниже
		
	Если Объект.УстановленаРегиональнымЗаконом Тогда
		// Региональная льгота
		Если Не ЗначениеЗаполнено(Статья) И Не ЗначениеЗаполнено(Часть) И Не ЗначениеЗаполнено(Пункт)
			 И Не ЗначениеЗаполнено(Подпункт) И Не ЗначениеЗаполнено(Абзац) И Не ЗначениеЗаполнено(Иное) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Укажите пункт закона, в соответствии с которым предоставляется льгота'"),
				,
				"Статья",
				,
				Отказ);
		КонецЕсли;	 	
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("КодРегиона");
	КонецЕсли;	
		
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.УстановленаРегиональнымЗаконом Тогда
		ТекущийОбъект.ОснованиеРегиональнойЛьготы = ЛьготыПоИмущественнымНалогамКлиентСервер.ПолныйКодОснованияЛьготы(
			Статья, Часть, Пункт, Подпункт, Абзац, Иное);
	КонецЕсли;		
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	УстановитьЗаголовок();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// Так как новый объект создается из формы-владельца не напрямую, а через форму классификатора,
	// то при закрытии оповестим владельца о выборе.
	Если РежимВыбора И Не ЗавершениеРаботы И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОповеститьОВыборе(Объект.Ссылка);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КодРегионаПриИзменении(Элемент)
	Объект.КодРегиона = Число(КодРегиона);
	ЗаполнитьНаименованиеРегиональнойЛьготы();
КонецПроцедуры

&НаКлиенте
Процедура КодРегионаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗаполнитьСписокРегионов(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СтатьяПриИзменении(Элемент)
	ЗаполнитьНаименованиеРегиональнойЛьготы();
КонецПроцедуры

&НаКлиенте
Процедура ЧастьПриИзменении(Элемент)
	ЗаполнитьНаименованиеРегиональнойЛьготы();
КонецПроцедуры

&НаКлиенте
Процедура ПунктПриИзменении(Элемент)
	ЗаполнитьНаименованиеРегиональнойЛьготы();
КонецПроцедуры

&НаКлиенте
Процедура ПодпунктПриИзменении(Элемент)
	ЗаполнитьНаименованиеРегиональнойЛьготы();
КонецПроцедуры

&НаКлиенте
Процедура АбзацПриИзменении(Элемент)
	ЗаполнитьНаименованиеРегиональнойЛьготы();
КонецПроцедуры

&НаКлиенте
Процедура ИноеПриИзменении(Элемент)
	ЗаполнитьНаименованиеРегиональнойЛьготы();
КонецПроцедуры

&НаКлиенте
Процедура ПроцентУменьшенияПриИзменении(Элемент)
	ЗаполнитьНаименованиеРегиональнойЛьготы();
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОснованиеЛьготыПримерЗаполненияНажатие(Элемент)
	
	ОткрытьФорму("Справочник.ОснованияЛьготПоИмущественнымНалогам.Форма.ПримерЗаполненияОснованияЛьготы",, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаРегиональныйЗакон.Видимость = Объект.УстановленаРегиональнымЗаконом;
	
	Элементы.ПроцентУменьшения.Видимость = (Объект.ВидЛьготы = ПредопределенноеЗначение("Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСтавкиНаПроцент")
		Или Объект.ВидЛьготы = ПредопределенноеЗначение("Перечисление.ВидыЛьготПоИмущественнымНалогам.СнижениеСуммыНаПроцент"));
		
	Элементы.ПроцентУменьшения.Доступность = Объект.УстановленаРегиональнымЗаконом;	
		
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовок()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		АвтоЗаголовок = Истина;		
	Иначе
		АвтоЗаголовок = Ложь;
		Заголовок = Нстр("ru='Основание льготы по налогу на имущество'");
	КонецЕсли;	

КонецПроцедуры
 
&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьИнформациюОФедеральнойЛьготе(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	ОписаниеЛьготы = Форма.ОписаниеЛьгот.ФедеральныеЛьготы[Объект.КодЛьготы];	
	
	Если ОписаниеЛьготы = Неопределено Тогда
		Элементы.КодЛьготыРасширеннаяПодсказка.Заголовок = "";
	Иначе
		Элементы.КодЛьготыРасширеннаяПодсказка.Заголовок = ОписаниеЛьготы.Наименование;
	КонецЕсли;	
			
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьИнформациюОРегиональнойЛьготе(Форма, ВидЛьготы)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	ОписаниеЛьготы = Форма.ОписаниеЛьгот.РегиональныеЛьготы[ВидЛьготы];
	
	Если ОписаниеЛьготы = Неопределено Тогда
		Элементы.КодЛьготыРасширеннаяПодсказка.Заголовок = "";
	Иначе
		Элементы.КодЛьготыРасширеннаяПодсказка.Заголовок = ОписаниеЛьготы.Наименование;
	КонецЕсли;	
			
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаименованиеРегиональнойЛьготы()
			
	СведенияОПунктеРегЗакона = ЛьготыПоИмущественнымНалогамКлиентСервер.НовыйСведенияОПунктеРегиональногоЗакона();
	ЗаполнитьЗначенияСвойств(СведенияОПунктеРегЗакона, ЭтотОбъект);
	
	НаименованиеРегиона = "";
	Попытка
		НаименованиеРегиона = Элементы.КодРегионаРегистрации.СписокВыбора.НайтиПоЗначению(КодРегиона).Представление;
	Исключение
	КонецПопытки;	
	
	Объект.Наименование = ЛьготыПоИмущественнымНалогамКлиентСервер.НаименованиеРегиональнойЛьготы(Объект, СведенияОПунктеРегЗакона, НаименованиеРегиона);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокРегионов(Элемент)
	
	// В списке выбора региона может быть 1 элемент, который был заполнен при открытии формы
	// В этом случае требуется заполнить список выбора полностью
	// Если элементов более 1, то считаем, что список уже заполнен полностью
	Если Элемент.СписокВыбора.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;	
	
	СписокРегионов = Новый СписокЗначений;
	ЗаполнитьСписокРегионовСервер(СписокРегионов);
	
	// Чтобы избежать дублирования элементов, очищаем список выбора в поле формы
	Элемент.СписокВыбора.Очистить();
	
	Для каждого Регион Из СписокРегионов Цикл
		Элемент.СписокВыбора.Добавить(Регион.Значение, Регион.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокРегионовСервер(СписокРегионов)
	ЗаполнитьСписокВыбораРегиона(СписокРегионов);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораРегиона(СписокРегионов, КодРегиона = Неопределено)  
	
	СписокРегионов.Очистить();
	
	Если ЗначениеЗаполнено(КодРегиона) И КодРегиона <> "00" Тогда
		
		ШаблонПредставления = НСтр("ru='%1'");  //например: "Москва г"
		
		НаименованиеРегиона = АдресныйКлассификатор.НаименованиеРегионаПоКоду(КодРегиона);
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления, НаименованиеРегиона);
			
		СписокРегионов.Добавить(КодРегиона, Представление);
		Возврат;
		
	КонецЕсли;	
	
	КлассификаторСубъектовРФ = АдресныйКлассификатор.СубъектыРФ();
	
	ТаблицаРегионов = КлассификаторСубъектовРФ;

	ШаблонПредставления = НСтр("ru='%1 %2'");  //например: "Москва г"
	Для Каждого Регион Из ТаблицаРегионов Цикл
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
			Регион.Наименование,
			Регион.Сокращение);
		СписокРегионов.Добавить(Формат(Регион.КодСубъектаРФ, "ЧЦ=2; ЧН=; ЧВН="), Представление);
	КонецЦикла;
	
	// Сортируем по наименованию региона
	СписокРегионов.СортироватьПоПредставлению();
	
КонецПроцедуры

#КонецОбласти