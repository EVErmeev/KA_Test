#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция формирует сведения по данным регистрации в налоговом органе.
//
// Параметры:
//	РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане - регистрация в налоговом органе.
//
// Возвращаемое значение:
//	Структура - Организация, Представление, ПолноеНаименование, КодПоОКПО, ИНН, КПП, ЮридическийАдрес, ФактическийАдрес, НомерСчета, Банк, БИК, КоррСчет.
//
Функция СведенияОПодразделении(РегистрацияВНалоговомОргане, Дата = Неопределено) Экспорт
	
	Сведения = Новый Структура("Организация, Представление, ПолноеНаименование, КодПоОКПО, ИНН, КПП, ОГРН, ЮридическийАдрес, ФактическийАдрес, НомерСчета, Банк, БИК, КоррСчет");
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		
		ИменаРеквизитов = Новый Структура;
		ИменаРеквизитов.Вставить("Организация",        "Владелец");
		ИменаРеквизитов.Вставить("Представление",      "НаименованиеОбособленногоПодразделения");
		ИменаРеквизитов.Вставить("ПолноеНаименование", "НаименованиеОбособленногоПодразделения");
		ИменаРеквизитов.Вставить("КодПоОКПО",          "Владелец.КодПоОКПО");
		ИменаРеквизитов.Вставить("ИНН",                "Владелец.ИНН");
		ИменаРеквизитов.Вставить("КПП",                "КПП");
		ИменаРеквизитов.Вставить("ОГРН",               "Владелец.ОГРН");
		
		ЗаполнитьЗначенияСвойств(
			Сведения,
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РегистрацияВНалоговомОргане, ИменаРеквизитов));
		
		Сведения.Вставить("ЮридическийАдрес", 	ФормированиеПечатныхФорм.ПолучитьАдресИзКонтактнойИнформации(Сведения.Организация,        "Юридический", Дата));
		Сведения.Вставить("ФактическийАдрес", 	ФормированиеПечатныхФорм.ПолучитьАдресИзКонтактнойИнформации(РегистрацияВНалоговомОргане, "Фактический", Дата));
		
		РеквизитыСчета =
			Справочники.БанковскиеСчетаОрганизаций.ПолучитьРеквизитыБанковскогоСчетаОрганизации(
				Справочники.БанковскиеСчетаОрганизаций.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(Сведения.Организация));
		
		ЗаполнитьЗначенияСвойств(
			Сведения,
			РеквизитыСчета,
			"НомерСчета, Банк, БИК, КоррСчет");
		
	КонецЕсли;
	
	Возврат Сведения;
	
КонецФункции

// Функция возвращает значение типа Булево, которое определяет
// возможность ввода сведений о регистрации в налоговом органе
// для обособленных подразделений, выделенных на отдельный баланс.
//
// Возвращаемое значение:
//  Булево - признак возможности ввода сведений о регистрации в налоговом органе.
//
Функция ВозможнаРегистрацияДляОбособленныхПодразделений() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает ссылку на "Регистрацию в налоговом органе" по состоянию на некоторую ДатаАктуальности
// Параметры:
//	СтруктурнаяЕдиница - СправочникСсылка.Организации, СправочникСсылка.ПодразделенияОрганизаций - структурная единица.
//	ДатаАктуальности - Дата - дата, на которую требуется получить сведения.
//
// Возвращаемое значение:
//	СправочникСсылка.РегистрацииВНалоговомОргане - регистрация в налоговом органе.
//
Функция РегистрацияВНалоговомОргане(СтруктурнаяЕдиница, Знач ДатаАктуальности = Неопределено, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	Если РегистрацияВНалоговомОргане <> Неопределено Тогда
		Возврат РегистрацияВНалоговомОргане;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Регистрации.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	РегистрСведений.РегистрацииВНалоговомОргане КАК Регистрации
	|ГДЕ
	|	Регистрации.Организация = &СтруктурнаяЕдиница
	|	И Регистрации.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.РегистрацияВНалоговомОргане
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &СтруктурнаяЕдиница";
	
	//++ НЕ УТ
	Если Не ЗначениеЗаполнено(ДатаАктуальности) Тогда
		ДатаАктуальности = ТекущаяДатаСеанса()
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Регистрации.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане.СрезПоследних(&ДатаАктуальности, СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК Регистрации";
	//-- НЕ УТ
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Возврат Выборка.РегистрацияВНалоговомОргане;
		
	КонецЕсли;
	
	//++ НЕ УТ
	Если ТипЗнч(СтруктурнаяЕдиница) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтруктурнаяЕдиница, "Родитель,Владелец");
		Если ЗначениеЗаполнено(ЗначенияРеквизитов.Родитель) Тогда
			Возврат Справочники.РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане(ЗначенияРеквизитов.Родитель, ДатаАктуальности);
		Иначе
			Возврат Справочники.РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане(ЗначенияРеквизитов.Владелец, ДатаАктуальности);
		КонецЕсли;
		
	КонецЕсли;
	//-- НЕ УТ
	
	Возврат Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка();
	
КонецФункции

//++ НЕ УТ

// Возвращает ссылку на "Регистрацию в налоговом органе" для данных из декларации
// Параметры:
//	Организация - СправочникСсылка.Организации - структурная единица.
//	КПП - Строка - КПП обособленного подразделения.
//	КодНалоговогоОргана - Строка - код налоговой.
//
// Возвращаемое значение:
//	СправочникСсылка.РегистрацииВНалоговомОргане - регистрация в налоговом органе.
//
Функция РегистрацияВНалоговомОрганеПоКодуНО(Организация, КПП, КодНалоговогоОргана) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", БухгалтерскийУчетПереопределяемый.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("КПП", КПП);
	Запрос.УстановитьПараметр("Код", КодНалоговогоОргана);
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РегистрацииВНалоговомОргане.Ссылка
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.Владелец = &ГоловнаяОрганизация
	|	И РегистрацииВНалоговомОргане.Код = &Код
	|	И &УсловиеПоКПП
	|
	|УПОРЯДОЧИТЬ ПО
	|	РегистрацииВНалоговомОргане.КПП";
	
	Если ПустаяСтрока(КПП) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоКПП", "ИСТИНА");
	ИначеЕсли Найти(КПП, "_") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоКПП", "РегистрацииВНалоговомОргане.КПП ПОДОБНО &КПП");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоКПП", "РегистрацииВНалоговомОргане.КПП = &КПП");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка();
	
КонецФункции

//-- НЕ УТ

// Определяет код территории по классификатору ОКТМО или ОКАТО,
// на котором организация или обособленное подразделение зарегистрирована по месту нахождения.
//
// Параметры:
//  РегистрацияВНалоговомОргане	 - СправочникСсылка.РегистрацииВНалоговомОргане
//  ТипКода	- Строка - "ОКТМО" или "ОКАТО"
//          - Дата - с даты применения ОКТМО будет возвращен код по ОКТМО, в остальных случаях - код по ОКАТО
// 
// Возвращаемое значение:
//  Строка - код территории
//
Функция КодТерритории(РегистрацияВНалоговомОргане, Знач ТипКода = "ОКТМО") Экспорт
	
	Если Не ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Возврат "";
	КонецЕсли;
	
	Если ТипЗнч(ТипКода) = Тип("Дата") И Год(ТипКода) < 2014 Тогда
		ТипКода = "ОКАТО";
	ИначеЕсли ТипКода <> "ОКАТО" Тогда
		ТипКода = "ОКТМО";
	КонецЕсли;
	
	Возврат СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РегистрацияВНалоговомОргане, "КодПо" + ТипКода));
	
КонецФункции

// Генерация служебного наименования исходя из регистрации налога на прибыль
// 
// Параметры:
// 	РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане - 
//
// Возвращаемое значение:
// 	Строка - значение служебного наименования
//
Функция НаименованиеСлужебное(РегистрацияВНалоговомОргане) Экспорт

	Если Не ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Возврат "";
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	// НаименованиеСлужебное определяется из наименования организации или ее обособленных подразделений в строгом порядке
	// Порядок (порядок - источник наименования):
	//	1 - НаименованиеПолное организации
	//	2 - НаименованиеПолное обособленного подразделения на отдельном балансе
	//	3 - НаименованиеПолное обособленного подразделения на общем балансе
	//	4 - Наименование организации
	//	5 - Наименование обособленного подразделения на отдельном балансе
	//	6 - Наименование обособленного подразделения на общем балансе
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Организации.НаименованиеПолное = """"
	|			ТОГДА Организации.Наименование
	|		ИНАЧЕ Организации.НаименованиеПолное
	|	КОНЕЦ КАК Наименование,
	|	ВЫБОР
	|		КОГДА НЕ Организации.ОбособленноеПодразделение
	|				И Организации.НаименованиеПолное <> """"
	|			ТОГДА 1
	|		КОГДА Организации.ОбособленноеПодразделение
	|				И Организации.НаименованиеПолное <> """"
	|			ТОГДА 2
	|		КОГДА НЕ Организации.ОбособленноеПодразделение
	|			ТОГДА 4
	|		ИНАЧЕ 5
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане
	|	И НЕ Организации.ПометкаУдаления
	|
	//++ НЕ УТ
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Наименование,
	|	6
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане
	|	И ПодразделенияОрганизаций.ОбособленноеПодразделение
	|	И НЕ ПодразделенияОрганизаций.ПометкаУдаления
	|
	//-- НЕ УТ
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Наименование УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "";
	КонецЕсли;	
	
	// берем первое значение из выборки, т.к. она уже отсортирована по порядку
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Наименование;
	
КонецФункции

// Возвращается код региона (в соответствии с адресным классификатором) по коду налоговой инспекции.
//
// Параметры:
//	КодНалоговогоОргана - Строка - код налогового органа.
//
// Возвращаемое значение:
//	Строка - код региона по адресному классификатору -  см. РегистрыСведений.АдресныеОбъекты.КлассификаторСубъектовРФ
//
Функция КодРегионаПоКодуНалоговогоОргана(Знач КодНалоговогоОргана) Экспорт
	
	КодРегиона = "";
	
	Если СтрДлина(КодНалоговогоОргана) < 2 Тогда
		Возврат КодРегиона;
	КонецЕсли;
	
	КодРегиона = Лев(КодНалоговогоОргана, 2);
	
	// 99 - код г.Байконур и одновременно код инспекций по крупнейшим налогоплательщикам.
	// 9901 - код местной инспекции, все остальные - инспекции по крупнейшим налогоплательщикам - находятся в Москве
	Если КодРегиона = "99" И КодНалоговогоОргана <> "9901" Тогда
		КодРегиона = "77";
	КонецЕсли;
		
	Возврат КодРегиона;
	
КонецФункции

// Заполняет переданный список регионов значениями из адресного классификатора
// Параметры:
//  СписокРегионов     - СписокЗначений 
//  ТолькоИспользуемые - Булево - Истина - в списке будут только те регионы,
//  									которые выбраны в регистрациях в налоговых органах
//									Ложь - все регионы из адресного классификатора.
//	КодРегиона         - Строка - код текущего выбранного региона.
//							Если указан, то в список добавляется только этот 1 регион.
//
Процедура ЗаполнитьСписокВыбораРегиона(СписокРегионов, ТолькоИспользуемые = Ложь, КодРегиона = Неопределено) Экспорт 
	
	СписокРегионов.Очистить();
	
	Если ЗначениеЗаполнено(КодРегиона) И КодРегиона <> "00" Тогда
		
		ШаблонПредставления = НСтр("ru='%1'");  //например: "Москва г"
		
		НаименованиеРегиона = АдресныйКлассификатор.НаименованиеРегионаПоКоду(КодРегиона);
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления, НаименованиеРегиона);
			
		СписокРегионов.Добавить(КодРегиона, Представление);
		Возврат;
		
	КонецЕсли;	
	
	КлассификаторСубъектовРФ = АдресныйКлассификатор.СубъектыРФ();
	
	Если НЕ ТолькоИспользуемые Тогда
		
		ТаблицаРегионов = КлассификаторСубъектовРФ;
		
	Иначе
		
		// Запрос выбирает все коды регионов, которые выбраны в справочнике регистраций в налоговом органе
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РегистрацииВНалоговомОргане.КодРегиона КАК КодРегиона
		|ИЗ
		|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
		|";

		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			Возврат;
		КонецЕсли;	
		
		ВыборкаРегионов = РезультатЗапроса.Выбрать();
		
		ТаблицаРегионов = КлассификаторСубъектовРФ.СкопироватьКолонки();
		Пока ВыборкаРегионов.Следующий() Цикл
			
			СтрокаРегиона = ТаблицаРегионов.Добавить();
			СтрокаРегиона.КодСубъектаРФ = ВыборкаРегионов.КодРегиона;
			
			СтрокаКлассификатора = КлассификаторСубъектовРФ.Найти(СтрокаРегиона.КодСубъектаРФ, "КодСубъектаРФ");
			Если СтрокаКлассификатора <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(СтрокаРегиона, СтрокаКлассификатора);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли; 

	ШаблонПредставления = НСтр("ru='%1 %2'");  //например: "Москва г"
	Для Каждого Регион Из ТаблицаРегионов Цикл
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
			Регион.Наименование,
			Регион.Сокращение);
		СписокРегионов.Добавить(Формат(Регион.КодСубъектаРФ, "ЧЦ=2; ЧН=; ЧВН="), Представление);
	КонецЦикла;
	
	// Сортируем по наименованию региона
	СписокРегионов.СортироватьПоПредставлению();
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Владельцы 
	|	ПО Владельцы.Ссылка = ЭтотСписок.Владелец
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособленныеПодразделения 
	|	ПО ОбособленныеПодразделения.ГоловнаяОрганизация = Владельцы.Ссылка
	|;
	|РазрешитьЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец)
	| ИЛИ ЗначениеРазрешено(ОбособленныеПодразделения.Ссылка)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Владелец)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

//++ НЕ УТ

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Организация") Тогда
		
		Если ЗначениеЗаполнено(Параметры.Отбор.Организация) Тогда
			
			ГоловнаяОрганизация = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Параметры.Отбор.Организация, "ГоловнаяОрганизация");
			
			Если ЗначениеЗаполнено(ГоловнаяОрганизация) Тогда
				
				Параметры.Отбор.Организация = ГоловнаяОрганизация; 
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ТолькоИспользуемые") И Параметры.ТолькоИспользуемые Тогда
		
		//выводим в список только те налоговые органы, которые выбраны в подразделениях данного региона
		
		НалоговыеОрганы = Новый СписокЗначений;
		
		Запрос = Новый Запрос;
		//при данном условии (ТолькоИспользуемые = Истина) в отборе обязательно будут организация и код региона, а параметрах - период
		Запрос.УстановитьПараметр("Организация", Параметры.Отбор.Владелец);
		Запрос.УстановитьПараметр("КодРегиона",  Параметры.Отбор.КодРегиона);
		Запрос.УстановитьПараметр("Период",      Параметры.Период);
		
		// В запросе выбираем действующие регистрации в налоговом органе подразделений организации
		// на выбранный период
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Организации.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	Организации.Ссылка КАК Ссылка,
		|	Организации.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
		|ПОМЕСТИТЬ СтруктурныеЕдиницы_ФилиалыИГО
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	(Организации.Ссылка = &Организация
		|			ИЛИ Организации.ГоловнаяОрганизация = &Организация)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтруктурныеЕдиницы.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
		|ПОМЕСТИТЬ НезаполненнаяИстория
		|ИЗ
		|	СтруктурныеЕдиницы_ФилиалыИГО КАК СтруктурныеЕдиницы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
		|		ПО СтруктурныеЕдиницы.Ссылка = ИсторияРегистрацийВНалоговомОргане.СтруктурнаяЕдиница
		|ГДЕ
		|	СтруктурныеЕдиницы.РегистрацияВНалоговомОргане.КодРегиона = &КодРегиона
		|	И ИсторияРегистрацийВНалоговомОргане.Период ЕСТЬ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсторияНалоговыхОрганов.РегистрацияВНалоговомОргане КАК Ссылка
		|ИЗ
		|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане.СрезПоследних(
		|			&Период,
		|			СтруктурнаяЕдиница В
		|				(ВЫБРАТЬ
		|					СтруктурныеЕдиницы.Ссылка
		|				ИЗ
		|					СтруктурныеЕдиницы_ФилиалыИГО КАК СтруктурныеЕдиницы)) КАК ИсторияНалоговыхОрганов
		|ГДЕ
		|	ИсторияНалоговыхОрганов.РегистрацияВНалоговомОргане.КодРегиона = &КодРегиона
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсторияНалоговыхОргановОП.РегистрацияВНалоговомОргане КАК Ссылка
		|ИЗ
		|	РегистрСведений.РегистрацииВНалоговомОргане.СрезПоследних(
		|			&Период,
		|			Организация В
		|				(ВЫБРАТЬ
		|					СтруктурныеЕдиницы.ГоловнаяОрганизация
		|				ИЗ
		|					СтруктурныеЕдиницы_ФилиалыИГО КАК СтруктурныеЕдиницы)) КАК ИсторияНалоговыхОргановОП
		|ГДЕ
		|	ИсторияНалоговыхОргановОП.РегистрацияВНалоговомОргане.КодРегиона = &КодРегиона
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	НезаполненнаяИстория.РегистрацияВНалоговомОргане
		|ИЗ
		|	НезаполненнаяИстория КАК НезаполненнаяИстория";
				
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			Возврат;
		КонецЕсли;	
		
		// результат запроса используем для отбора списка выбора
		// прямое заполнение данных выбора не используем, чтобы не отключать стандартную обработку
		НалоговыеОрганы.ЗагрузитьЗначения(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка"));
		Параметры.Отбор.Вставить("Ссылка", НалоговыеОрганы);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Код");
	Поля.Добавить("КПП");
	Поля.Добавить("НаименованиеСлужебное");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	Если  ЗначениеЗаполнено(Данные.КПП) И ЗначениеЗаполнено(Данные.НаименованиеСлужебное) Тогда
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'ФНС %1 КПП %2 (%3)'"),
			Данные.Код, Данные.КПП, Данные.НаименованиеСлужебное);
	ИначеЕсли ЗначениеЗаполнено(Данные.КПП) Тогда
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'ФНС %1 КПП %2'"),
			Данные.Код, Данные.КПП);
	Иначе
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'ФНС %1'"),
			Данные.Код);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

//-- НЕ УТ

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.РегистрацииВНалоговомОргане.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.9.75";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("1c55d4f6-9b80-4547-8093-6622a61f4c60");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.РегистрацииВНалоговомОргане.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновление справочника Регистрации в налоговом органе.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Справочники.РегистрацииВНалоговомОргане.ПолноеИмя());
	//++ НЕ УТ
	Читаемые.Добавить(Метаданные.РегистрыСведений.ИсторияРегистрацийВНалоговомОргане.ПолноеИмя());
	//-- НЕ УТ
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.РегистрацииВНалоговомОргане.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Справочники.РегистрацииВНалоговомОргане.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = ПустаяСсылка().Метаданные().ПолноеИмя();
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацииВНалоговомОргане.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.КодРегиона = 0
	|		ИЛИ РегистрацииВНалоговомОргане.НаименованиеСлужебное = """"
	|		ИЛИ РегистрацииВНалоговомОргане.ДатаПостановкиНаУчет = ДАТАВРЕМЯ(1,1,1)";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

// Процедура заполняет новые реквизиты справочника РегистрацииВНалоговомОргане:
// - КодРегиона (по коду налогового органа)
// - НаименованиеСлужебное (по наименованию подразделения, аналогично процедуре НаименованиеСлужебное())
// - ДатаПостановкиНаУчет (по регистру ИсторияРегистрацийВНалоговомОргане)
// 
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	// НаименованиеСлужебное определяется из наименования организации или ее обособленных подразделений в строгом порядке
	// Порядок (порядок - источник наименования):
	//	1 - НаименованиеПолное организации
	//	2 - НаименованиеПолное обособленного подразделения на отдельном балансе
	//	4 - Наименование организации
	//	5 - Наименование обособленного подразделения на отдельном балансе
	//	6 - Наименование обособленного подразделения на общем балансе
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрацииВНалоговомОргане.Ссылка КАК Ссылка,
	|	РегистрацииВНалоговомОргане.Владелец КАК Организация,
	|	РегистрацииВНалоговомОргане.НаименованиеОбособленногоПодразделения КАК НаименованиеОбособленногоПодразделения,
	|	РегистрацииВНалоговомОргане.НаименованиеОбособленногоПодразделения <> """" КАК ЗаполненоНаименованиеПодразделения
	|ПОМЕСТИТЬ РегистрацииВНалоговомОргане
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.Ссылка В (&ОбновляемыеДанные)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВЫБОР
	|		КОГДА Организации.НаименованиеПолное = """"
	|			ТОГДА Организации.Наименование
	|		ИНАЧЕ Организации.НаименованиеПолное
	|	КОНЕЦ КАК Наименование,
	|	ВЫБОР
	|		КОГДА НЕ Организации.ОбособленноеПодразделение
	|				И Организации.НаименованиеПолное <> """"
	|			ТОГДА 1
	|		КОГДА Организации.ОбособленноеПодразделение
	|				И Организации.НаименованиеПолное <> """"
	|			ТОГДА 2
	|		КОГДА НЕ Организации.ОбособленноеПодразделение
	|			ТОГДА 4
	|		ИНАЧЕ 5
	|	КОНЕЦ КАК Порядок
	|ПОМЕСТИТЬ ИсточникиНаименованияНалоговыхОрганов
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|		ПО Организации.РегистрацияВНалоговомОргане = РегистрацииВНалоговомОргане.Ссылка
	|ГДЕ
	|	НЕ Организации.ПометкаУдаления
	|	И (НЕ РегистрацииВНалоговомОргане.ЗаполненоНаименованиеПодразделения)
	//++ НЕ УТ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодразделенияОрганизаций.РегистрацияВНалоговомОргане,
	|	ПодразделенияОрганизаций.Наименование,
	|	6
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|		ПО ПодразделенияОрганизаций.РегистрацияВНалоговомОргане = РегистрацииВНалоговомОргане.Ссылка
	|ГДЕ
	|	ПодразделенияОрганизаций.ОбособленноеПодразделение
	|	И НЕ ПодразделенияОрганизаций.ПометкаУдаления
	|	И (НЕ РегистрацииВНалоговомОргане.ЗаполненоНаименованиеПодразделения)
	//-- НЕ УТ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацииВНалоговомОргане.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ИсточникиНаименованияНалоговыхОрганов.Наименование, РегистрацииВНалоговомОргане.НаименованиеОбособленногоПодразделения) КАК НаименованиеСлужебное,
	|	ЕСТЬNULL(ИсточникиНаименованияНалоговыхОрганов.Порядок, 0) КАК Порядок
	|ИЗ
	|	РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИсточникиНаименованияНалоговыхОрганов КАК ИсточникиНаименованияНалоговыхОрганов
	|		ПО РегистрацииВНалоговомОргане.Ссылка = ИсточникиНаименованияНалоговыхОрганов.РегистрацияВНалоговомОргане
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НаименованиеСлужебное УБЫВ
	|ИТОГИ ПО
	|	Ссылка
	//++ НЕ УТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацииВНалоговомОргане.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(МИНИМУМ(ИсторияРегистрацийВНалоговомОргане.Период),
	|		МИНИМУМ(РегистрацияГоловнойОрганизации.Период)) КАК Период
	|ИЗ
	|	РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
	|		ПО РегистрацииВНалоговомОргане.Ссылка = ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК РегистрацияГоловнойОрганизации
	|		ПО РегистрацииВНалоговомОргане.Организация = РегистрацияГоловнойОрганизации.СтруктурнаяЕдиница
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацииВНалоговомОргане.Ссылка
	//-- НЕ УТ
	|";
	
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	
	Результат = Запрос.ВыполнитьПакет();
	ВыборкаРегистраций   = Результат[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//++ НЕ УТ
	ДатыПостановкиНаУчет = Результат[3].Выбрать();
	//-- НЕ УТ
	
	Пока ВыборкаРегистраций.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаРегистраций.Ссылка);
			Блокировка.Заблокировать();
			
			СправочникОбъект = ВыборкаРегистраций.Ссылка.ПолучитьОбъект();
			
			// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
			Если СправочникОбъект = Неопределено
			 ИЛИ (СправочникОбъект.КодРегиона <> 0 И НЕ ПустаяСтрока(СправочникОбъект.НаименованиеСлужебное)) Тогда
				ОтменитьТранзакцию();
				Параметры.ПрогрессВыполнения.ОбработаноОбъектов = Параметры.ПрогрессВыполнения.ОбработаноОбъектов + 1;
				Продолжить;
			КонецЕсли;
			
			СправочникОбъект.ЗаполнитьКодРегиона();
			
			Если ПустаяСтрока(СправочникОбъект.НаименованиеСлужебное) Тогда
				// берем НаименованиеСлужебное из первого элемента детальной выборки, т.к. она уже отсортирована по порядку
				ВыборкаНаименований = ВыборкаРегистраций.Выбрать();
				ВыборкаНаименований.Следующий();
				СправочникОбъект.НаименованиеСлужебное = ВыборкаНаименований.НаименованиеСлужебное;
			КонецЕсли;
			
			//++ НЕ УТ
			Если НЕ ЗначениеЗаполнено(СправочникОбъект.ДатаПостановкиНаУчет)
				И ДатыПостановкиНаУчет.НайтиСледующий(ВыборкаРегистраций.Ссылка, "Ссылка") Тогда
					СправочникОбъект.ДатаПостановкиНаУчет = ДатыПостановкиНаУчет.Период;
					ДатыПостановкиНаУчет.Сбросить();
			КонецЕсли;
			//-- НЕ УТ

			Если НЕ ЗначениеЗаполнено(СправочникОбъект.ДатаПостановкиНаУчет) Тогда
				СправочникОбъект.ДатаПостановкиНаУчет = ТекущаяДатаСеанса();
			КонецЕсли;
			
			Если СправочникОбъект.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ВыборкаРегистраций.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ВыборкаРегистраций.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
