#Область ОбработчикиСобытийФормы 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	СтруктураЦелевыхПараметров = Отчеты.ИсполнениеКонтрактовГОЗ.СтруктураЦелевыхПараметровКонтрактаГОЗ();
	ЗаполнитьЗначенияСвойств(СтруктураЦелевыхПараметров, Параметры);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураЦелевыхПараметров);
	
	ОбязательстваВыполнены = ЗначениеЗаполнено(ФактическаяДатаЗавершения);
	Элементы.ФактическаяДатаЗавершения.Доступность = ОбязательстваВыполнены;
	
	Элементы.ГруппаОсновныеПараметры.Заголовок = НСтр("ru = 'Основные параметры контракта (руб.)'");
	Элементы.ГруппаЗатраты.Заголовок = НСтр("ru = 'Целевые затраты (руб.)'");
	
	Если ЗначениеЗаполнено(Параметры.ЗаголовокКомандыСохранить) Тогда
		Элементы.ФормаСохранить.Заголовок = Параметры.ЗаголовокКомандыСохранить;
	КонецЕсли;
	
	Элементы.ФормаСохранить.Видимость = НЕ Параметры.ТолькоПросмотр;
	
	ЭтоГосконтракт = ТипНаправленияДеятельности = Перечисления.ТипыНаправленийДеятельности.ГосударственныйКонтракт;
	ЭтотОбъект.АвтоЗаголовок = Не ЭтоГосконтракт;
	ЭтотОбъект.Заголовок = ?(ЭтоГосконтракт, НСтр("ru = 'Целевые параметры госконтракта'"), ЭтотОбъект.Заголовок);
	Элементы.РазделЛицевогоСчета.Видимость = ЭтоГосконтракт;
	Элементы.НомерОтдельногоСчета.Заголовок =
		?(ЭтоГосконтракт, НСтр("ru = 'Номер лицевого счета'"), НСтр("ru = 'Номер отдельного счета'"));
	Элементы.НомерОтдельногоСчета.Подсказка =
		?(ЭтоГосконтракт, НСтр("ru = 'Номер лицевого счета федерального казначейства'"), Элементы.НомерОтдельногоСчета.Подсказка);
	
	ТолькоПросмотр = Параметры.ТолькоПросмотр; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		ТекстПредупреждения = НСтр("ru = 'Данные не перенесены в направление деятельности. После закрытия сделанные изменения сохранены не будут.'");
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросПередЗакрытием", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные не перенесены в направление деятельности. Перенести?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбязательстваВыполнены Тогда
		ПроверяемыеРеквизиты.Добавить("ФактическаяДатаЗавершения");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытияЭлементовФормы

&НаКлиенте
Процедура ОбязательстваВыполненыПриИзменении(Элемент)
	
	Если Не ОбязательстваВыполнены Тогда
		ФактическаяДатаЗавершения = Неопределено;
	КонецЕсли;
	
	Элементы.ФактическаяДатаЗавершения.Доступность = ОбязательстваВыполнены;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	Закрыть(ЦелевыеПараметрыКонтракта());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработчикОповещенияВопросПередЗакрытием(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОчиститьСообщения();
		Отказ = Ложь;
		Модифицированность = Ложь;
		Закрыть(ЦелевыеПараметрыКонтракта());
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЦелевыеПараметрыКонтракта()
	
	ЦелевыеПараметры = Отчеты.ИсполнениеКонтрактовГОЗ.СтруктураЦелевыхПараметровКонтрактаГОЗ();
	ЗаполнитьЗначенияСвойств(ЦелевыеПараметры, ЭтаФорма);
	Возврат ЦелевыеПараметры;
	
КонецФункции

#КонецОбласти
