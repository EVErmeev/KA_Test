#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//	ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета.
//	Отказ - Булево - Передается из параметров обработчика "как есть".
//	СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		Если Не ЗначениеЗаполнено(Параметры.ПараметрКоманды) Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Распределение прочих расходов не выполнено или не требуется.'"),,,,Отказ);
		КонецЕсли;
		
		//@skip-warning
		Если ТипЗнч(Параметры.ПараметрКоманды) = Тип("ДокументСсылка.РаспределениеПрочихЗатрат")
			И Отчеты.БазаРаспределенияПроизводственныхРасходов.ПоддерживаемыеТипыНазначенийРаспределения().Найти(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ПараметрКоманды, "НазначениеНастройкиРаспределения")) = Неопределено 
		Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Отчет не поддерживает данный тип распределения'"),,,,Отказ);
		КонецЕсли;
		
		ЭтаФорма.ФормаПараметры.Отбор.Вставить("Регистратор", Параметры.ПараметрКоманды);
		
	Иначе
		
		Отказ = Истина;
		ВызватьИсключение НСтр("ru= 'Отчет предназначен только для открытия в документе ""Распределение расходов"".'") ;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") И Контекст.Параметры.Свойство("ПараметрКоманды") Тогда
		
		ПараметрыКД = Новый Структура;
		ПараметрыКД.Вставить("НовыеНастройкиКД", НовыеНастройкиКД);
		ПараметрыКД.Вставить("Документ", Контекст.Параметры.ПараметрКоманды);
		
		Если НовыеПользовательскиеНастройкиКД = Неопределено Тогда
			ПараметрыКД.Вставить("НовыеПользовательскиеНастройкиКД", 
				Контекст.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки);
		Иначе
			ПараметрыКД.Вставить("НовыеПользовательскиеНастройкиКД", 
				НовыеПользовательскиеНастройкиКД);
		КонецЕсли;
			
		НастроитьПараметрыОтчетаПоВариантуОтчета(ПараметрыКД);
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	ПараметрРегистратор = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "Регистратор");
	Регистратор         = ПараметрРегистратор.Значение;
	ДанныеСебестоимости = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "ДанныеСебестоимости").Значение;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РаспределениеПрочихЗатрат.Дата КАК Дата,
		|	РаспределениеПрочихЗатрат.Организация КАК Организация,
		|	РаспределениеПрочихЗатрат.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
		|	РаспределениеПрочихЗатрат.Подразделение КАК Подразделение,
		|	РаспределениеПрочихЗатрат.СтатьяРасходов КАК СтатьяРасходов,
		|	РаспределениеПрочихЗатрат.АналитикаРасходов КАК АналитикаРасходов,
		|	РаспределениеПрочихЗатрат.НазначениеНастройкиРаспределения КАК НазначениеНастройкиРаспределения,
		|	РаспределениеПрочихЗатрат.РаспределятьПоПартиям КАК РаспределятьПоПартиям,
		|	РаспределениеПрочихЗатрат.РаспределятьПоПодразделениям КАК РаспределятьПоПодразделениям,
		|	РаспределениеПрочихЗатрат.РаспределятьНаСтатьи КАК РаспределятьНаСтатьи,
		|	РаспределениеПрочихЗатрат.РаспределятьНаОВЗ КАК РаспределятьНаОВЗ,
		|	РаспределениеПрочихЗатрат.ОставитьВНЗП КАК ОставитьВНЗП,
		|	РаспределениеПрочихЗатрат.НаправлениеРаспределения КАК НаправлениеРаспределения,
		|	РаспределениеПрочихЗатрат.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) 
		|		И ТИПЗНАЧЕНИЯ(РаспределениеПрочихЗатрат.АналитикаРасходов) = ТИП(Справочник.ОбъектыВозникновенияЗатрат) КАК ЭтоРаспределениеИзОВЗ,
		|	РаспределениеПрочихЗатрат.Показатель КАК Показатель,
		|	РаспределениеПрочихЗатрат.ПартииУказаныВручную КАК ПартииУказаныВручную,
		|	РаспределениеПрочихЗатрат.ПодразделенияУказаныВручную КАК ПодразделенияУказаныВручную,
		|	РаспределениеПрочихЗатрат.ДоляСтоимостиНаНЗП КАК ДоляСтоимостиНаНЗП,
		|	РаспределениеПрочихЗатрат.ДоляСтоимостиНаПроизводство КАК ДоляСтоимостиНаПроизводство,
		|	РаспределениеПрочихЗатрат.ДоляСтоимостиНаОВЗ КАК ДоляСтоимостиНаОВЗ,
		|	РаспределениеПрочихЗатрат.БазаРаспределенияПоПодразделениям КАК БазаРаспределенияПоПодразделениям,
		|	РаспределениеПрочихЗатрат.БазаРаспределенияПоПартиям КАК БазаРаспределенияПоПартиям,
		|	РаспределениеПрочихЗатрат.ВсегоДолейСтоимости КАК ВсегоДолейСтоимости,
		|	РаспределениеПрочихЗатрат.ДоляСтоимостиНаСтатьи КАК ДоляСтоимостиНаСтатьи,
		|	РаспределениеПрочихЗатрат.НаправлениеДеятельности КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК РаспределениеПрочихЗатрат
		|ГДЕ
		|	РаспределениеПрочихЗатрат.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Регистратор);
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	
	Если Не Реквизиты.Следующий() Тогда
		ВызватьИсключение НСтр("ru= 'Отчет предназначен только для открытия в документе ""Распределение расходов"".'") ;
	КонецЕсли;
	
	НачалоПериода     = НачалоМесяца(Реквизиты.Дата);
	ОкончаниеПериода  = КонецМесяца(Реквизиты.Дата);
	МассивОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Реквизиты.Организация);
	ВалютаПлановойСтоимости = Справочники.Валюты.ПустаяСсылка();
	
	Если Реквизиты.БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции
		Или Реквизиты.БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукцииСУчетомБудущихВыпусков Тогда
		
		ВалютаПлановойСтоимости = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Справочники.ВидыЦен.ВидЦеныПлановойСтоимостиТМЦ(),
																				"ВалютаЦены");
	КонецЕсли;
	
	Если ДанныеСебестоимости = 3
		//++ НЕ УТ

		//++ Локализация
		ИЛИ ДанныеСебестоимости = 4
		//-- Локализация

		//-- НЕ УТ
		Тогда
		ВалютаОтчета = Реквизиты.ВалютаРегламентированногоУчета;
	Иначе 
		ВалютаОтчета = Константы.ВалютаУправленческогоУчета.Получить();
	КонецЕсли;
	
	// Расчет сумм поступивших расходов
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА &ДанныеСебестоимости = 1
		|			ТОГДА Т.СуммаПриход + Т.СуммаНачальныйОстаток
		|		КОГДА &ДанныеСебестоимости = 2
		|			ТОГДА Т.СуммаБезНДСПриход + Т.СуммаБезНДСНачальныйОстаток
		|		КОГДА &ДанныеСебестоимости = 3
		|			ТОГДА Т.СуммаРеглПриход + Т.СуммаРеглНачальныйОстаток
		//++ НЕ УТ

		//++ Локализация
		|		КОГДА &ДанныеСебестоимости = 4
		|			ТОГДА (Т.СуммаРеглПриход - Т.ПостояннаяРазницаПриход - Т.ВременнаяРазницаПриход)
		|					+ (Т.СуммаРеглНачальныйОстаток - Т.ПостояннаяРазницаНачальныйОстаток - Т.ВременнаяРазницаНачальныйОстаток)
		//-- Локализация

		//-- НЕ УТ
		|		КОГДА &ДанныеСебестоимости = 5
		|			ТОГДА Т.СуммаУпрПриход + Т.СуммаУпрНачальныйОстаток
		|	КОНЕЦ КАК ПоступилоРасхода
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы.ОстаткиИОбороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			,
		|			Организация = &Организация
		|				И Подразделение = &Подразделение
		|				И АналитикаРасходов = &Аналитика
		|				И (СтатьяРасходов.ТипРасходов = ЗНАЧЕНИЕ(Перечисление.ТипыРасходов.ВозникновениеЗатратНаОбъектах) 
		|					ИЛИ СтатьяРасходов = &Статья И НаправлениеДеятельности = &НаправлениеДеятельности)
		|	) КАК Т";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("ДанныеСебестоимости", ДанныеСебестоимости);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Подразделение", Реквизиты.Подразделение);
	Запрос.УстановитьПараметр("Статья", Реквизиты.СтатьяРасходов);
	Запрос.УстановитьПараметр("Аналитика", Реквизиты.АналитикаРасходов);
	Запрос.УстановитьПараметр("НаправлениеДеятельности", Реквизиты.НаправлениеДеятельности);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ШаблонПредставления = НСтр("ru = 'Сумма расходов: %1 (%2)'");
	ПредставлениеСумм = "";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПредставлениеСумм = СтрШаблон(ШаблонПредставления, Выборка.ПоступилоРасхода, ВалютаОтчета);
	КонецЕсли;
	
	// Стоимость
	МассивТиповРаспределенияБазыПоСтоимости = Новый Массив();
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат);
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда);
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда);
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции);
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукцииСУчетомБудущихВыпусков);
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат);
	
	// Количество
	МассивТиповРаспределенияБазыПоКоличеству = Новый Массив();
	МассивТиповРаспределенияБазыПоКоличеству.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов);
	МассивТиповРаспределенияБазыПоКоличеству.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов);
	МассивТиповРаспределенияБазыПоКоличеству.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоПродукции);
	МассивТиповРаспределенияБазыПоКоличеству.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоПродукцииСУчетомБудущихВыпусков);
	
	// Объем
	МассивТиповРаспределенияБазыПоОбъему = Новый Массив();
	МассивТиповРаспределенияБазыПоОбъему.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов);
	МассивТиповРаспределенияБазыПоОбъему.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ОбъемПродукции);
	МассивТиповРаспределенияБазыПоОбъему.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ОбъемПродукцииСУчетомБудущихВыпусков);
	
	// Вес
	МассивТиповРаспределенияБазыПоВесу = Новый Массив();
	МассивТиповРаспределенияБазыПоВесу.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов);
	МассивТиповРаспределенияБазыПоВесу.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ВесПродукции);
	МассивТиповРаспределенияБазыПоВесу.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ВесПродукцииСУчетомБудущихВыпусков);
	
	РасчетСебестоимостиВыполнен = Ложь;
	Если Реквизиты.НазначениеНастройкиРаспределения = Перечисления.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат Тогда
		
		СостояниеРасчетаСебестоимости = ЗакрытиеМесяцаСервер.ОпределитьСостояниеЭтаповРасчета(
			Перечисления.ОперацииЗакрытияМесяца.РаспределениеРасходовПоНаправлениямДеятельности,
			НачалоПериода,
			МассивОрганизаций);

		Если СостояниеРасчетаСебестоимости = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно
		 ИЛИ СостояниеРасчетаСебестоимости = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
			РасчетСебестоимостиВыполнен = Истина;
		КонецЕсли;

		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Документы.РаспределениеПрочихЗатрат.РаспределитьРасходыНаФинансовыйРезультат(
			Реквизиты.Дата, 
			ОбщегоНазначенияУТКлиентСервер.Массив(Реквизиты.Организация),
			МенеджерВременныхТаблиц,
			Регистратор);
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Регистратор", Регистратор);
		Запрос.УстановитьПараметр("ДанныеСебестоимости", ДанныеСебестоимости);
		Запрос.УстановитьПараметр("БазаРаспределенияПоПартиям", Реквизиты.БазаРаспределенияПоПартиям);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА &БазаРаспределенияПоПартиям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВыручкаОтПродаж),
			|												ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВаловаяПрибыль),
			|												ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СебестоимостьПродаж))
			|			ТОГДА ""НаНДПоФинРезультатам""
			|		ИНАЧЕ
			|			""НаНДПоПрямымЗатратам""
			|	КОНЕЦ КАК ТипЗаписи,
			|	НЕОПРЕДЕЛЕНО КАК Подразделение,
			|	ДД.НаправлениеДеятельностиПриемник КАК НаправлениеДеятельности,
			|	ВЫБОР
			|		КОГДА ДД.ПартияПроизводства <> ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
			|			ТОГДА ДД.ПартияПроизводства
			|		ИНАЧЕ ДД.ЗаказНаПроизводство
			|	КОНЕЦ КАК ПартияПроизводства,
			|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
			|	СУММА(ЕСТЬNULL(ДанныеБазыРаспределения.База, 0)) КАК ЗначениеПоказателя,
			|	1 КАК ЗначениеПоказателяПроцент,
			|	СУММА(
			|		ВЫБОР
			|			КОГДА &ДанныеСебестоимости = 1
			|				ТОГДА ДД.Сумма
			|			КОГДА &ДанныеСебестоимости = 2
			|				ТОГДА ДД.Сумма
			|			КОГДА &ДанныеСебестоимости = 3
			|				ТОГДА ДД.СуммаРегл
			//++ НЕ УТ

			//++ Локализация
			|			КОГДА &ДанныеСебестоимости = 4
			|				ТОГДА ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
			//-- Локализация

			//-- НЕ УТ
			|			КОГДА &ДанныеСебестоимости = 5
			|				ТОГДА ДД.СуммаУпр
			|		КОНЕЦ) КАК Сумма,
			|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
			|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
			|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
			|	&Регистратор КАК Регистратор,
			|	НЕОПРЕДЕЛЕНО КАК МатериалПродукцияУслуга,
			|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
			|	1 КАК ПорядокВывода,
			|	ИСТИНА КАК СуммироватьКоличествоПоЭтапу
			|ИЗ
			|	РезультатРаспределения КАК ДД
			|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределения КАК ДанныеБазыРаспределения
			|		ПО ДД.НаправлениеДеятельностиПриемник = ДанныеБазыРаспределения.НаправлениеДеятельности
			|			И ДД.ЗаказНаПроизводство = ДанныеБазыРаспределения.ЗаказНаПроизводство
			|			И ДД.ПартияПроизводства = ДанныеБазыРаспределения.ПартияПроизводства
			|			И ДД.КодСтрокиПродукция = ДанныеБазыРаспределения.КодСтрокиПродукция
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.НаправлениеДеятельностиПриемник,
			|	ВЫБОР
			|		КОГДА ДД.ПартияПроизводства <> ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
			|			ТОГДА ДД.ПартияПроизводства
			|		ИНАЧЕ ДД.ЗаказНаПроизводство
			|	КОНЕЦ,
			|	ДД.КодСтрокиПродукция";		
		
	ИначеЕсли Реквизиты.НазначениеНастройкиРаспределения = Перечисления.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаОВЗ Тогда
		
		СостояниеРасчетаСебестоимости = РасчетСебестоимостиПрикладныеАлгоритмы.ТекущееСостояниеРасчета(НачалоПериода, МассивОрганизаций);
		
		Если СостояниеРасчетаСебестоимости = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно
		 ИЛИ СостояниеРасчетаСебестоимости = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
			РасчетСебестоимостиВыполнен = Истина;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
	#Область ТекстЗапроса
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДД.КорАналитикаРасходов КАК АналитикаРасходов,
			|	ДД.КорПодразделение КАК Подразделение,
			|	СУММА(
			|		ВЫБОР
			|			КОГДА &ДанныеСебестоимости = 1
			|				ТОГДА ДД.Сумма
			|			КОГДА &ДанныеСебестоимости = 2
			|				ТОГДА ДД.СуммаБезНДС
			|			КОГДА &ДанныеСебестоимости = 3
			|				ТОГДА ДД.СуммаРегл
			//++ НЕ УТ

			//++ Локализация
			|			КОГДА &ДанныеСебестоимости = 4
			|				ТОГДА ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
			//-- Локализация

			//-- НЕ УТ
			|			КОГДА &ДанныеСебестоимости = 5
			|				ТОГДА ДД.СуммаУпр
			|		КОНЕЦ) КАК Сумма
			|ПОМЕСТИТЬ НаОВЗФакт
			|ИЗ
			|	РегистрНакопления.ПрочиеРасходы КАК ДД
			|ГДЕ
			|	&РаспределятьНаОВЗ
			|	И ДД.Регистратор = &Регистратор
			|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
			|	И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ТранзитРасходовМеждуОВЗ)
			|	И ВЫБОР
			|			КОГДА &ДанныеСебестоимости = 1
			|				ТОГДА ДД.Сумма
			|			КОГДА &ДанныеСебестоимости = 2
			|				ТОГДА ДД.СуммаБезНДС
			|			КОГДА &ДанныеСебестоимости = 3
			|				ТОГДА ДД.СуммаРегл
			//++ НЕ УТ

			//++ Локализация
			|			КОГДА &ДанныеСебестоимости = 4
			|				ТОГДА ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
			//-- Локализация

			//-- НЕ УТ
			|			КОГДА &ДанныеСебестоимости = 5
			|				ТОГДА ДД.СуммаУпр
			|		КОНЕЦ <> 0 
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.КорАналитикаРасходов,
			|	ДД.КорПодразделение
			|;
			|
			|ВЫБРАТЬ 
			|	Т.ОВЗ,
			|	Т.ОВЗ.Подразделение КАК Подразделение,
			|	Т.ЗначениеПоказателя
			|ПОМЕСТИТЬ ПромЗначенияПоказателя
			|ИЗ РегистрСведений.ЗначенияПоказателейДляРаспределенияРасходовНаОВЗ.СрезПоследних(&ОкончаниеПериода, Показатель = &Показатель) КАК Т
			|ГДЕ
			|	Т.Показатель.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВводитсяПриИзменении) ИЛИ Т.Период >= &НачалоПериода
			|;
			|
			|ВЫБРАТЬ 
			|	СУММА(Т.ЗначениеПоказателя) КАК СуммаЗначений
			|ПОМЕСТИТЬ СуммаЗначенийПоказателя
			|ИЗ ПромЗначенияПоказателя КАК Т
			|;
			|
			|ВЫБРАТЬ 
			|	Т.ОВЗ,
			|	Т.Подразделение,
			|	Т.ЗначениеПоказателя,
			|	Т.ЗначениеПоказателя / ТТ.СуммаЗначений * 100 КАК ЗначениеПоказателяПроцент
			|ПОМЕСТИТЬ ЗначенияПоказателя
			|ИЗ ПромЗначенияПоказателя КАК Т
			|	ЛЕВОЕ СОЕДИНЕНИЕ СуммаЗначенийПоказателя КАК ТТ 
			|ПО (ИСТИНА)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДД.НаправлениеДеятельности КАК НаправлениеДеятельности,
			|	ВЫБОР
			|		КОГДА Строки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
			|			ТОГДА Строки.СтатьяРасходов
			|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
			|	КОНЕЦ КАК СтатьяРасходов,
			|	Строки.АналитикаРасходов КАК АналитикаРасходов,
			|	ВЫБОР
			|		КОГДА Строки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
			|			ТОГДА Строки.СтатьяРасходов
			|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
			|	КОНЕЦ КАК СтатьяАктивовПассивов,
			|	Строки.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
			|	СУММА(
			|		ВЫБОР
			|			КОГДА &ДанныеСебестоимости = 1
			|				ТОГДА ДД.Сумма
			|			КОГДА &ДанныеСебестоимости = 2
			|				ТОГДА ДД.СуммаБезНДС
			|			КОГДА &ДанныеСебестоимости = 3
			|				ТОГДА ДД.СуммаРегл
			//++ НЕ УТ

			//++ Локализация
			|			КОГДА &ДанныеСебестоимости = 4
			|				ТОГДА ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
			//-- Локализация

			//-- НЕ УТ
			|			КОГДА &ДанныеСебестоимости = 5
			|				ТОГДА ДД.СуммаУпр
			|		КОНЕЦ) КАК Сумма
			|ПОМЕСТИТЬ НаСтатьиФакт
			|ИЗ
			|	РегистрНакопления.ПрочиеРасходы КАК ДД
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.Списание КАК Строки
			|		ПО ДД.Регистратор = Строки.Ссылка
			|			И ДД.ИдентификаторСтроки = Строки.ИдентификаторСтроки
			|ГДЕ
			|	ДД.Регистратор = &Регистратор
			|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
			|	И ВЫБОР
			|		КОГДА &ДанныеСебестоимости = 1
			|			ТОГДА ДД.Сумма
			|		КОГДА &ДанныеСебестоимости = 2
			|			ТОГДА ДД.СуммаБезНДС
			|		КОГДА &ДанныеСебестоимости = 3
			|			ТОГДА ДД.СуммаРегл
			//++ НЕ УТ

			//++ Локализация
			|		КОГДА &ДанныеСебестоимости = 4
			|			ТОГДА ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
			//-- Локализация

			//-- НЕ УТ
			|		КОГДА &ДанныеСебестоимости = 5
			|			ТОГДА ДД.СуммаУпр
			|	КОНЕЦ <> 0
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.НаправлениеДеятельности,
			|	Строки.СтатьяРасходов,
			|	Строки.АналитикаРасходов,
			|	Строки.АналитикаАктивовПассивов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДД.Организация КАК Организация,
			|	ДД.Подразделение КАК Подразделение,
			|	ДД.АналитикаРасходов КАК АналитикаРасходов,
			|	ВЫБОР
			|		КОГДА &ДанныеСебестоимости = 1
			|			ТОГДА ДД.СуммаОстаток
			|		КОГДА &ДанныеСебестоимости = 2
			|			ТОГДА ДД.СуммаБезНДСОстаток
			|		КОГДА &ДанныеСебестоимости = 3
			|			ТОГДА ДД.СуммаРеглОстаток
			//++ НЕ УТ

			//++ Локализация
			|		КОГДА &ДанныеСебестоимости = 4
			|			ТОГДА ДД.СуммаРеглОстаток - ДД.ПостояннаяРазницаОстаток - ДД.ВременнаяРазницаОстаток
			//-- Локализация

			//-- НЕ УТ
			|		КОГДА &ДанныеСебестоимости = 5
			|			ТОГДА ДД.СуммаУпрОстаток
			|	КОНЕЦ КАК Сумма
			|ПОМЕСТИТЬ НаНЗПФакт
			|ИЗ
			|	РегистрНакопления.ПрочиеРасходы.Остатки(
			|			ДОБАВИТЬКДАТЕ(&ОкончаниеПериода, СЕКУНДА, 1),
			|			Организация = &Организация
			|				И Подразделение = &Подразделение
			|				И АналитикаРасходов = &АналитикаРасходов
			|	) КАК ДД
			|ГДЕ
			|	&ОставитьВНЗП
			|	И ВЫБОР
			|		КОГДА &ДанныеСебестоимости = 1
			|			ТОГДА ДД.СуммаОстаток
			|		КОГДА &ДанныеСебестоимости = 2
			|			ТОГДА ДД.СуммаБезНДСОстаток
			|		КОГДА &ДанныеСебестоимости = 3
			|			ТОГДА ДД.СуммаРеглОстаток
			//++ НЕ УТ

			//++ Локализация
			|		КОГДА &ДанныеСебестоимости = 4
			|			ТОГДА ДД.СуммаРеглОстаток - ДД.ПостояннаяРазницаОстаток - ДД.ВременнаяРазницаОстаток
			//-- Локализация

			//-- НЕ УТ
			|		КОГДА &ДанныеСебестоимости = 5
			|			ТОГДА ДД.СуммаУпрОстаток
			|	КОНЕЦ <> 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Списание.Ссылка КАК Регистратор,
			|	Списание.СтатьяРасходов КАК КорСтатьяРасходов,
			|	Списание.АналитикаРасходов КАК КорАналитикаРасходов,
			|	Списание.АналитикаАктивовПассивов КАК КорАналитикаАктивовПассивов,
			|	СУММА(Списание.ДоляСтоимости) КАК ДоляСтоимости
			|ПОМЕСТИТЬ ДолиСписанияНаСтатьи
			|ИЗ
			|	Документ.РаспределениеПрочихЗатрат.Списание КАК Списание
			|ГДЕ
			|	Списание.Ссылка = &Регистратор
			|
			|СГРУППИРОВАТЬ ПО
			|	Списание.Ссылка,
			|	Списание.СтатьяРасходов,
			|	Списание.АналитикаРасходов,
			|	Списание.АналитикаАктивовПассивов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	""НаОВЗ"" КАК ТипЗаписи,
			|	ЕСТЬNULL(Факт.Подразделение, ДД.Подразделение) КАК Подразделение,
			|	НЕОПРЕДЕЛЕНО КАК ПартияПроизводства,
			|	НЕОПРЕДЕЛЕНО КАК КодСтрокиПродукция,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	ДД.ЗначениеПоказателя КАК ЗначениеПоказателя,
			|	ДД.ЗначениеПоказателяПроцент КАК ЗначениеПоказателяПроцент,
			|	ЕСТЬNULL(Факт.Сумма, 0) КАК Сумма,
			|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
			|	ЕСТЬNULL(Факт.АналитикаРасходов, ДД.ОВЗ) КАК АналитикаРасходов,
			|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
			|	&Регистратор КАК Регистратор,
			|	НЕОПРЕДЕЛЕНО КАК МатериалПродукцияУслуга,
			|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
			|	1 КАК ПорядокВывода,
			|	ЛОЖЬ КАК СуммироватьКоличествоПоЭтапу
			|ИЗ
			|	ЗначенияПоказателя КАК ДД
			|		ПОЛНОЕ СОЕДИНЕНИЕ НаОВЗФакт КАК Факт
			|		ПО ДД.ОВЗ = Факт.АналитикаРасходов
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	""НаДругиеСтатьи"",
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	0,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	ДД.ДоляСтоимости,
			|	ДД.ДоляСтоимости,
			|	ЕСТЬNULL(Факт.Сумма, 0),
			|	ДД.КорСтатьяРасходов,
			|	ДД.КорАналитикаРасходов,
			|	ДД.КорАналитикаАктивовПассивов,
			|	&Регистратор,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	1,
			|	ИСТИНА
			|ИЗ
			|	ДолиСписанияНаСтатьи КАК ДД
			|		ЛЕВОЕ СОЕДИНЕНИЕ НаСтатьиФакт КАК Факт
			|		ПО ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДД.КорСтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
			|			ТОГДА 
			|				ДД.КорСтатьяРасходов = Факт.СтатьяРасходов
			|			ИНАЧЕ
			|				ДД.КорСтатьяРасходов = Факт.СтатьяАктивовПассивов
			|			КОНЕЦ
			|			И ДД.КорАналитикаРасходов = Факт.АналитикаРасходов
			|			И ДД.КорАналитикаАктивовПассивов = Факт.АналитикаАктивовПассивов
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	""НаНЗП"",
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	0,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	Реквизиты.ДоляСтоимостиНаНЗП,
			|	Реквизиты.ДоляСтоимостиНаНЗП,
			|	ЕСТЬNULL(Факт.Сумма, 0),
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	&Регистратор,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	1,
			|	ИСТИНА
			|ИЗ
			|	НаНЗПФакт КАК Факт
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК Реквизиты
			|		ПО Факт.Организация = Факт.Организация
			|			И Факт.Подразделение = Факт.Подразделение
			|			И Факт.АналитикаРасходов = Факт.АналитикаРасходов
			|";
		
	#КонецОбласти	
			
		Запрос.УстановитьПараметр("Регистратор",						Регистратор);
		Запрос.УстановитьПараметр("ДанныеСебестоимости",				ДанныеСебестоимости);
		Запрос.УстановитьПараметр("НачалоПериода",						НачалоПериода);
		Запрос.УстановитьПараметр("ОкончаниеПериода",					ОкончаниеПериода);
		Запрос.УстановитьПараметр("Организация",						Реквизиты.Организация);
		Запрос.УстановитьПараметр("Подразделение",						Реквизиты.Подразделение);
		Запрос.УстановитьПараметр("РаспределятьНаОВЗ",					Реквизиты.РаспределятьНаОВЗ);
		Запрос.УстановитьПараметр("ОставитьВНЗП",						Реквизиты.ОставитьВНЗП);
		Запрос.УстановитьПараметр("НаправлениеДеятельности",			Реквизиты.НаправлениеДеятельности);
		Запрос.УстановитьПараметр("СтатьяРасходов",						Реквизиты.СтатьяРасходов);
		Запрос.УстановитьПараметр("АналитикаРасходов",					Реквизиты.АналитикаРасходов);
		Запрос.УстановитьПараметр("Показатель",							Реквизиты.Показатель);
		
	ИначеЕсли Реквизиты.НазначениеНастройкиРаспределения = Перечисления.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаСебестоимостьПроизводства Тогда
		
		МВТ = РасчетСебестоимостиПостатейныеЗатраты.ДанныеДляРасшифровкиПрямыхПроизводственныхРасходов(НачалоПериода, МассивОрганизаций, Регистратор, РасчетСебестоимостиВыполнен);
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МВТ;
		
	#Область ТекстЗапроса
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДД.ПартияПроизводства КАК ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
			|	ДД.СтатьяРасходов КАК СтатьяРасходов,
			|	ДД.АналитикаРасходов КАК АналитикаРасходов,
			|	ДД.Подразделение КАК Подразделение,
			|	ДД.ГруппаПродукции КАК ГруппаПродукции,
			|	СУММА(
			|		ВЫБОР
			|			КОГДА &ДанныеСебестоимости = 1
			|				ТОГДА ДД.Стоимость
			|			КОГДА &ДанныеСебестоимости = 2
			|				ТОГДА ДД.СтоимостьБезНДС
			|			КОГДА &ДанныеСебестоимости = 3
			|				ТОГДА ДД.СтоимостьРегл
			//++ НЕ УТ

			//++ Локализация
			|			КОГДА &ДанныеСебестоимости = 4
			|				ТОГДА ДД.СтоимостьРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
			//-- Локализация

			//-- НЕ УТ
			|			КОГДА &ДанныеСебестоимости = 5
			|				ТОГДА ДД.СтоимостьУпр
			|		КОНЕЦ) КАК Сумма
			|ПОМЕСТИТЬ НаПартииФакт
			|ИЗ
			|	РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК ДД
			|ГДЕ
			|	ДД.Регистратор = &Регистратор
			|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|	И ВЫБОР
			|			КОГДА &ДанныеСебестоимости = 1
			|				ТОГДА ДД.Стоимость
			|			КОГДА &ДанныеСебестоимости = 2
			|				ТОГДА ДД.СтоимостьБезНДС
			|			КОГДА &ДанныеСебестоимости = 3
			|				ТОГДА ДД.СтоимостьРегл
			//++ НЕ УТ

			//++ Локализация
			|			КОГДА &ДанныеСебестоимости = 4
			|				ТОГДА ДД.СтоимостьРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
			//-- Локализация

			//-- НЕ УТ
			|			КОГДА &ДанныеСебестоимости = 5
			|				ТОГДА ДД.СтоимостьУпр
			|		КОНЕЦ <> 0 
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция,
			|	ДД.СтатьяРасходов,
			|	ДД.АналитикаРасходов,
			|	ДД.Подразделение,
			|	ДД.ГруппаПродукции
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДД.Подразделение КАК Подразделение,
			|	ДД.ПартияПроизводства КАК ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
			|	&БазаРаспределенияПоПартиям КАК ТипБазыРаспределенияПоПартиям,
			|	ВЫБОР 
			|		КОГДА ДД.ГруппаПродукции = НЕОПРЕДЕЛЕНО 
			|			ТОГДА 
			|				ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
			|			ИНАЧЕ
			|				ДД.ГруппаПродукции
			|	КОНЕЦ КАК ГруппаПродукции,
			|	ДД.Показатель КАК МатериалПродукцияУслуга,
			|	МАКСИМУМ(ВЫБОР
			|		КОГДА &ДанныеСебестоимости = 1
			|			ТОГДА ДД.Стоимость
			|		КОГДА &ДанныеСебестоимости = 2
			|			ТОГДА ДД.СтоимостьБезНДС
			|		КОГДА &ДанныеСебестоимости = 3
			|			ТОГДА ДД.СтоимостьРегл
			//++ НЕ УТ

			//++ Локализация
			|		КОГДА &ДанныеСебестоимости = 4
			|			ТОГДА ДД.СтоимостьРегл
			//-- Локализация

			//-- НЕ УТ
			|		КОГДА &ДанныеСебестоимости = 5
			|			ТОГДА ДД.СтоимостьУпр
			|	КОНЕЦ) КАК Знаменатель,
			|	МАКСИМУМ(ВЫБОР
			|		КОГДА &ДанныеСебестоимости = 1
			|			ТОГДА ВЫРАЗИТЬ(ВЫБОР КОГДА БазаСводно.База = 0 ТОГДА 0 ИНАЧЕ ДД.Стоимость / БазаСводно.База КОНЕЦ КАК ЧИСЛО(28, 10))
			|		КОГДА &ДанныеСебестоимости = 2
			|			ТОГДА ВЫРАЗИТЬ(ВЫБОР КОГДА БазаСводно.БазаБезНДС = 0 ТОГДА 0 ИНАЧЕ ДД.СтоимостьБезНДС / БазаСводно.БазаБезНДС КОНЕЦ КАК ЧИСЛО(28, 10))
			|		КОГДА &ДанныеСебестоимости = 3
			|			ТОГДА ВЫРАЗИТЬ(ВЫБОР КОГДА БазаСводно.БазаРегл = 0 ТОГДА 0 ИНАЧЕ ДД.СтоимостьРегл / БазаСводно.БазаРегл КОНЕЦ КАК ЧИСЛО(28, 10))
			//++ НЕ УТ

			//++ Локализация
			|		КОГДА &ДанныеСебестоимости = 4
			|			ТОГДА ВЫРАЗИТЬ(ВЫБОР КОГДА БазаСводно.БазаРегл = 0 ТОГДА 0 ИНАЧЕ ДД.СтоимостьРегл / БазаСводно.БазаРегл КОНЕЦ КАК ЧИСЛО(28, 10))
			//-- Локализация

			//-- НЕ УТ
			|		КОГДА &ДанныеСебестоимости = 5
			|			ТОГДА ВЫРАЗИТЬ(ВЫБОР КОГДА БазаСводно.БазаУпр = 0 ТОГДА 0 ИНАЧЕ ДД.СтоимостьУпр / БазаСводно.БазаУпр КОНЕЦ КАК ЧИСЛО(28, 10))
			|	КОНЕЦ) КАК Доля
			|ПОМЕСТИТЬ НаЭтапыДетально
			|ИЗ
			|	ВтБазыПрямыхРасходовДетально КАК ДД
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтБазыПрямыхРасходов КАК БазаСводно 
			|ПО
			|	ДД.Подразделение = БазаСводно.Подразделение
			|	И ДД.ПартияПроизводства = БазаСводно.ПартияПроизводства
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.Подразделение,
			|	ДД.ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция,
			|	ДД.ГруппаПродукции,
			|	ДД.Показатель
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	""ПоПравилуНаЭтапы"" КАК ТипЗаписи,
			|	ДД.Подразделение КАК Подразделение,
			|	ВЫБОР
			|		КОГДА ДД.ПартияПроизводства <> ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
			|			ТОГДА ДД.ПартияПроизводства
			|		ИНАЧЕ ДД.ЗаказНаПроизводство
			|	КОНЕЦ КАК ПартияПроизводства,
			|	ДД.КодСтрокиПродукция,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	ДД.Знаменатель КАК ЗначениеПоказателя,
			|	ДД.Знаменатель КАК ЗначениеПоказателяПроцент,
			|	ЕСТЬNULL(ВЫРАЗИТЬ(Факт.Сумма КАК ЧИСЛО(28, 10)), 0) КАК Сумма,
			|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
			|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
			|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
			|	&Регистратор КАК Регистратор,
			|	ДД.МатериалПродукцияУслуга КАК МатериалПродукцияУслуга,
			|	ДД.ГруппаПродукции КАК ГруппаПродукции,
			|	1 КАК ПорядокВывода,
			|	ИСТИНА КАК СуммироватьКоличествоПоЭтапу
			|ИЗ
			|	НаЭтапыДетально КАК ДД
			|		ЛЕВОЕ СОЕДИНЕНИЕ НаПартииФакт КАК Факт
			|		ПО ДД.Подразделение = Факт.Подразделение
			|			И ДД.ПартияПроизводства = Факт.ПартияПроизводства
			|			И ДД.ЗаказНаПроизводство = Факт.ЗаказНаПроизводство
			|			И ДД.КодСтрокиПродукция = Факт.КодСтрокиПродукция
			|			И ДД.ГруппаПродукции = Факт.ГруппаПродукции";
		
	#КонецОбласти	
			
		Запрос.УстановитьПараметр("Регистратор",						Регистратор);
		Запрос.УстановитьПараметр("ДанныеСебестоимости",				ДанныеСебестоимости);
		Запрос.УстановитьПараметр("МассивКоличества",					МассивТиповРаспределенияБазыПоКоличеству);
		Запрос.УстановитьПараметр("МассивСтоимости",					МассивТиповРаспределенияБазыПоСтоимости);
		Запрос.УстановитьПараметр("МассивОбъема",						МассивТиповРаспределенияБазыПоОбъему);
		Запрос.УстановитьПараметр("МассивВеса",							МассивТиповРаспределенияБазыПоВесу);
		Запрос.УстановитьПараметр("БазаРаспределенияПоПартиям",			Реквизиты.БазаРаспределенияПоПартиям);
		Запрос.УстановитьПараметр("НачалоПериода",						НачалоПериода);
		Запрос.УстановитьПараметр("ОкончаниеПериода",					ОкончаниеПериода);
		Запрос.УстановитьПараметр("Организация",						Реквизиты.Организация);
		Запрос.УстановитьПараметр("Подразделение",						Реквизиты.Подразделение);
		Запрос.УстановитьПараметр("СтатьяРасходов",						Реквизиты.СтатьяРасходов);
		Запрос.УстановитьПараметр("АналитикаРасходов",					Реквизиты.АналитикаРасходов);
		
	Иначе		
		
		МВТ = РасчетСебестоимостиПостатейныеЗатраты.ДанныеДляРасшифровкиДолейПроизводственныхРасходов(НачалоПериода, МассивОрганизаций, Регистратор, РасчетСебестоимостиВыполнен);
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МВТ;
		
	#Область ТекстЗапроса
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДД.ПартияПроизводства КАК ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
			|	ДД.СтатьяРасходов КАК СтатьяРасходов,
			|	ДД.АналитикаРасходов КАК АналитикаРасходов,
			|	ДД.Подразделение КАК Подразделение,
			|	ДД.ГруппаПродукции КАК ГруппаПродукции,
			|	СУММА(
			|		ВЫБОР
			|			КОГДА &ДанныеСебестоимости = 1
			|				ТОГДА ДД.Стоимость
			|			КОГДА &ДанныеСебестоимости = 2
			|				ТОГДА ДД.СтоимостьБезНДС
			|			КОГДА &ДанныеСебестоимости = 3
			|				ТОГДА ДД.СтоимостьРегл
			//++ НЕ УТ

			//++ Локализация
			|			КОГДА &ДанныеСебестоимости = 4
			|				ТОГДА ДД.СтоимостьРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
			//-- Локализация

			//-- НЕ УТ
			|			КОГДА &ДанныеСебестоимости = 5
			|				ТОГДА ДД.СтоимостьУпр
			|		КОНЕЦ) КАК Сумма
			|ПОМЕСТИТЬ НаПартииФакт
			|ИЗ
			|	РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК ДД
			|ГДЕ
			|	ДД.Регистратор = &Регистратор
			|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|	И ВЫБОР
			|			КОГДА &ДанныеСебестоимости = 1
			|				ТОГДА ДД.Стоимость
			|			КОГДА &ДанныеСебестоимости = 2
			|				ТОГДА ДД.СтоимостьБезНДС
			|			КОГДА &ДанныеСебестоимости = 3
			|				ТОГДА ДД.СтоимостьРегл
			//++ НЕ УТ

			//++ Локализация
			|			КОГДА &ДанныеСебестоимости = 4
			|				ТОГДА ДД.СтоимостьРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
			//-- Локализация

			//-- НЕ УТ
			|			КОГДА &ДанныеСебестоимости = 5
			|				ТОГДА ДД.СтоимостьУпр
			|		КОНЕЦ <> 0 
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция,
			|	ДД.СтатьяРасходов,
			|	ДД.АналитикаРасходов,
			|	ДД.Подразделение,
			|	ДД.ДоляСтоимости,
			|	ДД.ГруппаПродукции
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДД.НаправлениеДеятельности КАК НаправлениеДеятельности,
			|	ВЫБОР
			|		КОГДА Строки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
			|			ТОГДА Строки.СтатьяРасходов
			|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
			|	КОНЕЦ КАК СтатьяРасходов,
			|	Строки.АналитикаРасходов КАК АналитикаРасходов,
			|	ВЫБОР
			|		КОГДА Строки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
			|			ТОГДА Строки.СтатьяРасходов
			|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
			|	КОНЕЦ КАК СтатьяАктивовПассивов,
			|	Строки.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
			|	СУММА(
			|		ВЫБОР
			|			КОГДА &ДанныеСебестоимости = 1
			|				ТОГДА ДД.Сумма
			|			КОГДА &ДанныеСебестоимости = 2
			|				ТОГДА ДД.СуммаБезНДС
			|			КОГДА &ДанныеСебестоимости = 3
			|				ТОГДА ДД.СуммаРегл
			//++ НЕ УТ

			//++ Локализация
			|			КОГДА &ДанныеСебестоимости = 4
			|				ТОГДА ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
			//-- Локализация

			//-- НЕ УТ
			|			КОГДА &ДанныеСебестоимости = 5
			|				ТОГДА ДД.СуммаУпр
			|		КОНЕЦ) КАК Сумма
			|ПОМЕСТИТЬ НаСтатьиФакт
			|ИЗ
			|	РегистрНакопления.ПрочиеРасходы КАК ДД
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.Списание КАК Строки
			|		ПО ДД.Регистратор = Строки.Ссылка
			|			И ДД.ИдентификаторСтроки = Строки.ИдентификаторСтроки
			|ГДЕ
			|	ДД.Регистратор = &Регистратор
			|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
			|	И ВЫБОР
			|		КОГДА &ДанныеСебестоимости = 1
			|			ТОГДА ДД.Сумма
			|		КОГДА &ДанныеСебестоимости = 2
			|			ТОГДА ДД.СуммаБезНДС
			|		КОГДА &ДанныеСебестоимости = 3
			|			ТОГДА ДД.СуммаРегл
			//++ НЕ УТ

			//++ Локализация
			|		КОГДА &ДанныеСебестоимости = 4
			|			ТОГДА ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
			//-- Локализация

			//-- НЕ УТ
			|		КОГДА &ДанныеСебестоимости = 5
			|			ТОГДА ДД.СуммаУпр
			|	КОНЕЦ <> 0
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.НаправлениеДеятельности,
			|	Строки.СтатьяРасходов,
			|	Строки.АналитикаРасходов,
			|	Строки.АналитикаАктивовПассивов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДД.Организация КАК Организация,
			|	ДД.Подразделение КАК Подразделение,
			|	ДД.НаправлениеДеятельности КАК НаправлениеДеятельности,
			|	ДД.СтатьяРасходов КАК СтатьяРасходов,
			|	ДД.АналитикаРасходов КАК АналитикаРасходов,
			|	ВЫБОР
			|		КОГДА &ДанныеСебестоимости = 1
			|			ТОГДА ДД.СуммаОстаток
			|		КОГДА &ДанныеСебестоимости = 2
			|			ТОГДА ДД.СуммаБезНДСОстаток
			|		КОГДА &ДанныеСебестоимости = 3
			|			ТОГДА ДД.СуммаРеглОстаток
			//++ НЕ УТ

			//++ Локализация
			|		КОГДА &ДанныеСебестоимости = 4
			|			ТОГДА ДД.СуммаРеглОстаток - ДД.ПостояннаяРазницаОстаток - ДД.ВременнаяРазницаОстаток
			//-- Локализация

			//-- НЕ УТ
			|		КОГДА &ДанныеСебестоимости = 5
			|			ТОГДА ДД.СуммаУпрОстаток
			|	КОНЕЦ КАК Сумма
			|ПОМЕСТИТЬ НаНЗПФакт
			|ИЗ
			|	РегистрНакопления.ПрочиеРасходы.Остатки(
			|			ДОБАВИТЬКДАТЕ(&ОкончаниеПериода, СЕКУНДА, 1),
			|			Организация = &Организация
			|				И Подразделение = &Подразделение
			|				И АналитикаРасходов = &АналитикаРасходов
			|				И (СтатьяРасходов.ТипРасходов = ЗНАЧЕНИЕ(Перечисление.ТипыРасходов.ВозникновениеЗатратНаОбъектах) 
			|					ИЛИ СтатьяРасходов = &СтатьяРасходов И НаправлениеДеятельности = &НаправлениеДеятельности)
			|	) КАК ДД
			|ГДЕ
			|	&ОставитьВНЗП
			|	И ВЫБОР
			|		КОГДА &ДанныеСебестоимости = 1
			|			ТОГДА ДД.СуммаОстаток
			|		КОГДА &ДанныеСебестоимости = 2
			|			ТОГДА ДД.СуммаБезНДСОстаток
			|		КОГДА &ДанныеСебестоимости = 3
			|			ТОГДА ДД.СуммаРеглОстаток
			//++ НЕ УТ

			//++ Локализация
			|		КОГДА &ДанныеСебестоимости = 4
			|			ТОГДА ДД.СуммаРеглОстаток - ДД.ПостояннаяРазницаОстаток - ДД.ВременнаяРазницаОстаток
			//-- Локализация

			//-- НЕ УТ
			|		КОГДА &ДанныеСебестоимости = 5
			|			ТОГДА ДД.СуммаУпрОстаток
			|	КОНЕЦ <> 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Списание.Ссылка КАК Регистратор,
			|	Списание.СтатьяРасходов КАК КорСтатьяРасходов,
			|	Списание.АналитикаРасходов КАК КорАналитикаРасходов,
			|	Списание.АналитикаАктивовПассивов КАК КорАналитикаАктивовПассивов,
			|	СУММА(Списание.ДоляСтоимости) КАК ДоляСтоимости
			|ПОМЕСТИТЬ ДолиСписанияНаСтатьи
			|ИЗ
			|	Документ.РаспределениеПрочихЗатрат.Списание КАК Списание
			|ГДЕ
			|	Списание.Ссылка = &Регистратор
			|
			|СГРУППИРОВАТЬ ПО
			|	Списание.Ссылка,
			|	Списание.СтатьяРасходов,
			|	Списание.АналитикаРасходов,
			|	Списание.АналитикаАктивовПассивов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДД.Подразделение КАК Подразделение,
			|	СУММА(ДД.Сумма) КАК Сумма
			|ПОМЕСТИТЬ НаПодразделенияФакт
			|ИЗ
			|	НаПартииФакт КАК ДД
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.Подразделение
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДД.КорПодразделение КАК Подразделение,
			|	ДД.ПартияПроизводства КАК ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
			|	ВЫБОР 
			|		КОГДА ДД.ГруппаПродукции = НЕОПРЕДЕЛЕНО 
			|			ТОГДА 
			|				ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
			|			ИНАЧЕ
			|				ДД.ГруппаПродукции
			|	КОНЕЦ КАК ГруппаПродукции,
			|	ДД.Регистратор КАК Регистратор,
			|	&БазаРаспределенияПоПодразделениям КАК ПравилоРаспределенияПоПодразделениям,
			|	ДД.Показатель КАК МатериалПродукцияУслуга,
			|	ВЫБОР
			|		КОГДА &ДанныеСебестоимости = 1
			|			ТОГДА ДД.Стоимость
			|		КОГДА &ДанныеСебестоимости = 2
			|			ТОГДА ДД.СтоимостьБезНДС
			|		КОГДА &ДанныеСебестоимости = 3
			|			ТОГДА ДД.СтоимостьРегл
			//++ НЕ УТ

			//++ Локализация
			|		КОГДА &ДанныеСебестоимости = 4
			|			ТОГДА ДД.СтоимостьРегл
			//-- Локализация

			//-- НЕ УТ
			|		КОГДА &ДанныеСебестоимости = 5
			|			ТОГДА ДД.СтоимостьУпр
			|	КОНЕЦ КАК Знаменатель,
			|	ВЫБОР
			|		КОГДА ИтогиПоЭтапам.Регистратор ЕСТЬ NULL
			|			ТОГДА 0
			|		КОГДА &ДанныеСебестоимости = 1
			|			ТОГДА ДД.Стоимость
			|		КОГДА &ДанныеСебестоимости = 2
			|			ТОГДА ДД.СтоимостьБезНДС
			|		КОГДА &ДанныеСебестоимости = 3
			|			ТОГДА ДД.СтоимостьРегл
			//++ НЕ УТ

			//++ Локализация
			|		КОГДА &ДанныеСебестоимости = 4
			|			ТОГДА ДД.СтоимостьРегл
			//-- Локализация

			//-- НЕ УТ
			|		КОГДА &ДанныеСебестоимости = 5
			|			ТОГДА ДД.СтоимостьУпр
			|	КОНЕЦ КАК ЗначениеПоказателяПроцент
			|ПОМЕСТИТЬ НаПодразделенияДетально
			|ИЗ
			|	БазаПоПодразделениямДетально КАК ДД
			|		ЛЕВОЕ СОЕДИНЕНИЕ ИтогиБазыПоЭтапамПодразделений КАК ИтогиПоЭтапам
			|		ПО (ИтогиПоЭтапам.Регистратор = ДД.Регистратор)
			|			И (ИтогиПоЭтапам.Подразделение = ДД.КорПодразделение)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДД.Подразделение КАК Подразделение,
			|	СУММА(ДД.ЗначениеПоказателяПроцент) КАК ЗначениеПоказателяПроцент
			|ПОМЕСТИТЬ НаПодразделенияСводно
			|ИЗ
			|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|		ДД.Подразделение КАК Подразделение,
			|		ДД.ЗначениеПоказателяПроцент КАК ЗначениеПоказателяПроцент
			|	ИЗ
			|		НаПодразделенияДетально КАК ДД
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ДД.Подразделение,
			|		1
			|	ИЗ
			|		БазаПоЭтапам КАК ДД
			|	ГДЕ
			|		НЕ &РаспределятьПоПодразделениям) КАК ДД
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.Подразделение
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДД.Подразделение КАК Подразделение,
			|	ДД.ПартияПроизводства КАК ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
			|	&БазаРаспределенияПоПартиям КАК ТипБазыРаспределенияПоПартиям,
			|	ВЫБОР 
			|		КОГДА ДД.ГруппаПродукции = НЕОПРЕДЕЛЕНО 
			|			ТОГДА 
			|				ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
			|			ИНАЧЕ
			|				ДД.ГруппаПродукции
			|	КОНЕЦ КАК ГруппаПродукции,
			|	ДД.Показатель КАК МатериалПродукцияУслуга,
			|	МАКСИМУМ(ВЫБОР
			|		КОГДА &ДанныеСебестоимости = 1
			|			ТОГДА ДД.Стоимость
			|		КОГДА &ДанныеСебестоимости = 2
			|			ТОГДА ДД.СтоимостьБезНДС
			|		КОГДА &ДанныеСебестоимости = 3
			|			ТОГДА ДД.СтоимостьРегл
			//++ НЕ УТ

			//++ Локализация
			|		КОГДА &ДанныеСебестоимости = 4
			|			ТОГДА ДД.СтоимостьРегл
			//-- Локализация

			//-- НЕ УТ
			|		КОГДА &ДанныеСебестоимости = 5
			|			ТОГДА ДД.СтоимостьУпр
			|	КОНЕЦ) КАК Знаменатель,
			|	МАКСИМУМ(ВЫБОР
			|		КОГДА &ДанныеСебестоимости = 1
			|			ТОГДА ВЫРАЗИТЬ(ВЫБОР КОГДА БазаСводно.Стоимость = 0 ТОГДА 0 ИНАЧЕ ДД.Стоимость / БазаСводно.Стоимость КОНЕЦ КАК ЧИСЛО(28, 10))
			|		КОГДА &ДанныеСебестоимости = 2
			|			ТОГДА ВЫРАЗИТЬ(ВЫБОР КОГДА БазаСводно.СтоимостьБезНДС = 0 ТОГДА 0 ИНАЧЕ ДД.СтоимостьБезНДС / БазаСводно.СтоимостьБезНДС КОНЕЦ КАК ЧИСЛО(28, 10))
			|		КОГДА &ДанныеСебестоимости = 3
			|			ТОГДА ВЫРАЗИТЬ(ВЫБОР КОГДА БазаСводно.СтоимостьРегл = 0 ТОГДА 0 ИНАЧЕ ДД.СтоимостьРегл / БазаСводно.СтоимостьРегл КОНЕЦ КАК ЧИСЛО(28, 10))
			//++ НЕ УТ

			//++ Локализация
			|		КОГДА &ДанныеСебестоимости = 4
			|			ТОГДА ВЫРАЗИТЬ(ВЫБОР КОГДА БазаСводно.СтоимостьРегл = 0 ТОГДА 0 ИНАЧЕ ДД.СтоимостьРегл / БазаСводно.СтоимостьРегл КОНЕЦ КАК ЧИСЛО(28, 10))
			//-- Локализация

			//-- НЕ УТ
			|		КОГДА &ДанныеСебестоимости = 5
			|			ТОГДА ВЫРАЗИТЬ(ВЫБОР КОГДА БазаСводно.СтоимостьУпр = 0 ТОГДА 0 ИНАЧЕ ДД.СтоимостьУпр / БазаСводно.СтоимостьУпр КОНЕЦ КАК ЧИСЛО(28, 10))
			|	КОНЕЦ) КАК Доля
			|ПОМЕСТИТЬ НаЭтапыДетально
			|ИЗ
			|	БазаПоЭтапамДетально КАК ДД
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ БазаПоЭтапам КАК БазаСводно ПО
			|	ДД.Подразделение = БазаСводно.Подразделение
			|	И ДД.ПартияПроизводства = БазаСводно.ПартияПроизводства
			|	И ДД.ЗаказНаПроизводство = БазаСводно.ЗаказНаПроизводство
			|	И ДД.КодСтрокиПродукция = БазаСводно.КодСтрокиПродукция
			|	И ДД.ГруппаПродукции = БазаСводно.ГруппаПродукции
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.Подразделение,
			|	ДД.ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция,
			|	ДД.ГруппаПродукции,
			|	ДД.Показатель
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДД.ПартияПроизводства КАК ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
			|	ДД.ГруппаПродукции КАК ГруппаПродукции,
			|	ДД.Материал КАК Показатель,
			|	2 КАК ПорядокВывода,
			|	СУММА(ДД.Количество) КАК Количество,
			|	СУММА(ДД.Объем) КАК Объем,
			|	СУММА(ДД.Вес) КАК Вес,
			|	СУММА(
			|		ВЫБОР
			|			КОГДА &ДанныеСебестоимости = 1
			|				ТОГДА ДД.Стоимость
			|			КОГДА &ДанныеСебестоимости = 2
			|				ТОГДА ДД.СтоимостьБезНДС
			|			КОГДА &ДанныеСебестоимости = 3
			|				ТОГДА ДД.СтоимостьРегл
			//++ НЕ УТ

			//++ Локализация
			|			КОГДА &ДанныеСебестоимости = 4
			|				ТОГДА ДД.СтоимостьРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница
			//-- Локализация

			//-- НЕ УТ
			|			КОГДА &ДанныеСебестоимости = 5
			|				ТОГДА ДД.СтоимостьУпр
			|		КОНЕЦ) КАК Стоимость,
			|	0 КАК НормативнаяСтоимость,
			|	ДД.Подразделение КАК Подразделение
			|ПОМЕСТИТЬ ДанныеНеВошедшие
			|ИЗ
			|	ДанныеПоМатериальнымЗатратам КАК ДД
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
			|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
			|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПоЭтапамДетально КАК ОтборПоПартиям
			|		ПО ДД.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
			|			И ДД.ЗаказНаПроизводство = ОтборПоПартиям.ЗаказНаПроизводство
			|			И ДД.КодСтрокиПродукция = ОтборПоПартиям.КодСтрокиПродукция
			|			И ДД.Подразделение = ОтборПоПартиям.Подразделение
			|			И ДД.Материал = ОтборПоПартиям.Показатель
			|ГДЕ
			|	ОтборПоПартиям.Показатель ЕСТЬ NULL
			|	И &БазаРаспределенияПоПартиям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов))
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция,
			|	ДД.ГруппаПродукции,
			|	ДД.Подразделение,
			|	ДД.Материал
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДД.ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция,
			|	ДД.ГруппаПродукции,
			|	ДД.Продукция,
			|	2,
			|	СУММА(ДД.Количество),
			|	СУММА(ДД.Объем),
			|	СУММА(ДД.Вес),
			|	СУММА(ДД.Стоимость),
			|	0,
			|	ДД.Подразделение
			|ИЗ
			|	ДанныеПоПродукции КАК ДД
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
			|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
			|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПоЭтапамДетально КАК ОтборПоПартиям
			|		ПО ДД.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
			|			И ДД.ЗаказНаПроизводство = ОтборПоПартиям.ЗаказНаПроизводство
			|			И ДД.КодСтрокиПродукция = ОтборПоПартиям.КодСтрокиПродукция
			|			И ДД.Подразделение = ОтборПоПартиям.Подразделение
			|ГДЕ
			|	ОтборПоПартиям.Показатель ЕСТЬ NULL
			|	И &БазаРаспределенияПоПартиям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукцииСУчетомБудущихВыпусков), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукцииСУчетомБудущихВыпусков), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукцииСУчетомБудущихВыпусков), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукцииСУчетомБудущихВыпусков))
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция,
			|	ДД.ГруппаПродукции,
			|	ДД.Подразделение,
			|	ДД.Продукция
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДД.ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция,
			|	ДД.ГруппаПродукции,
			|	ДД.ВидРабот,
			|	2,
			|	СУММА(ДД.Количество),
			|	0,
			|	0,
			|	СУММА(
			|		ВЫБОР
			|			КОГДА &ДанныеСебестоимости = 3
			//++ НЕ УТ

			//++ Локализация
			|				ИЛИ &ДанныеСебестоимости = 4
			//-- Локализация

			//-- НЕ УТ
			|				ТОГДА ДД.СтоимостьРегл
			|			ИНАЧЕ
			|				ДД.Стоимость
			|		КОНЕЦ) КАК Стоимость,
			|	СУММА(ДД.НормативнаяСтоимость),
			|	ДД.Подразделение
			|ИЗ
			|	ДанныеПоТрудоЗатратам КАК ДД
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
			|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
			|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПоЭтапамДетально КАК ОтборПоПартиям
			|		ПО ДД.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
			|			И ДД.ЗаказНаПроизводство = ОтборПоПартиям.ЗаказНаПроизводство
			|			И ДД.КодСтрокиПродукция = ОтборПоПартиям.КодСтрокиПродукция
			|			И ДД.Подразделение = ОтборПоПартиям.Подразделение
			|			И ДД.ВидРабот = ОтборПоПартиям.Показатель
			|ГДЕ
			|	ОтборПоПартиям.Показатель ЕСТЬ NULL
			|	И &БазаРаспределенияПоПартиям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда))
			|
			|СГРУППИРОВАТЬ ПО
			|	ДД.ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция,
			|	ДД.ГруппаПродукции,
			|	ДД.Подразделение,
			|	ДД.ВидРабот
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ДД.Подразделение КАК Подразделение,
			|	ДД.ПартияПроизводства КАК ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
			|	ДД.ГруппаПродукции КАК ГруппаПродукции
			|ПОМЕСТИТЬ ПартииНеВошедшие
			|ИЗ
			|	ДанныеПоМатериальнымЗатратам КАК ДД
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
			|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
			|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПоЭтапам КАК БазаПоЭтапам
			|		ПО ДД.Подразделение = БазаПоЭтапам.Подразделение
			|			И ДД.ПартияПроизводства = БазаПоЭтапам.ПартияПроизводства
			|			И ДД.ЗаказНаПроизводство = БазаПоЭтапам.ЗаказНаПроизводство
			|ГДЕ
			|	БазаПоЭтапам.ПартияПроизводства ЕСТЬ NULL
			|	И &БазаРаспределенияПоПартиям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов))
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДД.Подразделение,
			|	ДД.ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция,
			|	ДД.ГруппаПродукции
			|ИЗ
			|	ДанныеПоПродукции КАК ДД
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
			|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
			|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПоЭтапам КАК ОтборПоПартиям
			|		ПО ДД.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
			|			И ДД.ЗаказНаПроизводство = ОтборПоПартиям.ЗаказНаПроизводство
			|			И ДД.Подразделение = ОтборПоПартиям.Подразделение
			|ГДЕ
			|	ОтборПоПартиям.ПартияПроизводства ЕСТЬ NULL
			|	И &БазаРаспределенияПоПартиям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукцииСУчетомБудущихВыпусков), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукцииСУчетомБудущихВыпусков), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукцииСУчетомБудущихВыпусков), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукцииСУчетомБудущихВыпусков))
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДД.Подразделение,
			|	ДД.ПартияПроизводства,
			|	ДД.ЗаказНаПроизводство,
			|	ДД.КодСтрокиПродукция,
			|	ДД.ГруппаПродукции
			|ИЗ
			|	ДанныеПоТрудоЗатратам КАК ДД
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
			|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
			|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПоЭтапам КАК ОтборПоПартиям
			|		ПО ДД.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
			|			И ДД.ЗаказНаПроизводство = ОтборПоПартиям.ЗаказНаПроизводство
			|			И ДД.Подразделение = ОтборПоПартиям.Подразделение
			|ГДЕ
			|	ОтборПоПартиям.ПартияПроизводства ЕСТЬ NULL
			|	И &БазаРаспределенияПоПартиям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда))
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	""ПоПравилуНаПодразделения"" КАК ТипЗаписи,
			|	ДД.Подразделение КАК Подразделение,
			|	ВЫБОР
			|		КОГДА ДД.ПартияПроизводства <> ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
			|			ТОГДА ДД.ПартияПроизводства
			|		ИНАЧЕ ДД.ЗаказНаПроизводство
			|	КОНЕЦ КАК ПартияПроизводства,
			|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	ДД.Знаменатель КАК ЗначениеПоказателя,
			|	ДД.ЗначениеПоказателяПроцент КАК ЗначениеПоказателяПроцент,
			|	ЕСТЬNULL(Факт.Сумма, 0) КАК Сумма,
			|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
			|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
			|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
			|	&Регистратор КАК Регистратор,
			|	ДД.МатериалПродукцияУслуга КАК МатериалПродукцияУслуга,
			|	ДД.ГруппаПродукции КАК ГруппаПродукции,
			|	1 КАК ПорядокВывода,
			|	ИСТИНА КАК СуммироватьКоличествоПоЭтапу
			|ИЗ
			|	НаПодразделенияДетально КАК ДД
			|		ЛЕВОЕ СОЕДИНЕНИЕ НаПодразделенияФакт КАК Факт
			|		ПО ДД.Подразделение = Факт.Подразделение
			|ГДЕ
			|	&БазаРаспределенияПоПодразделениям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат))
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	""ПоПравилуНаПодразделения"",
			|	ДД.Подразделение,
			|	ВЫБОР
			|		КОГДА ДД.ПартияПроизводства <> ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
			|			ТОГДА ДД.ПартияПроизводства
			|		ИНАЧЕ ДД.ЗаказНаПроизводство
			|	КОНЕЦ,
			|	ДД.КодСтрокиПродукция,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	0,
			|	0,
			|	0,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	&Регистратор,
			|	НЕОПРЕДЕЛЕНО,
			|	ДД.ГруппаПродукции,
			|	3,
			|	ЛОЖЬ
			|ИЗ
			|	ПартииНеВошедшие КАК ДД
			|ГДЕ
			|	&БазаРаспределенияПоПодразделениям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат))
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	""ПоПоказателямНаПодразделения"",
			|	ДД.Подразделение,
			|	НЕОПРЕДЕЛЕНО,
			|	0,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	ДД.Знаменатель,
			|	ДД.ЗначениеПоказателяПроцент,
			|	ЕСТЬNULL(Факт.Сумма, 0),
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	&Регистратор,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	1,
			|	ИСТИНА
			|ИЗ
			|	НаПодразделенияДетально КАК ДД
			|		ЛЕВОЕ СОЕДИНЕНИЕ НаПодразделенияФакт КАК Факт
			|		ПО ДД.Подразделение = Факт.Подразделение
			|ГДЕ
			|	&БазаРаспределенияПоПодразделениям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВводитсяЕжемесячно), ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВводитсяПриИзменении))
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	""ПоПравилуНаЭтапы"",
			|	ДД.Подразделение,
			|	ВЫБОР
			|		КОГДА ДД.ПартияПроизводства <> ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
			|			ТОГДА ДД.ПартияПроизводства
			|		ИНАЧЕ ДД.ЗаказНаПроизводство
			|	КОНЕЦ,
			|	ДД.КодСтрокиПродукция,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	ДД.Знаменатель,
			|	ВЫБОР
			|		КОГДА НаПодразделенияСводно.Подразделение ЕСТЬ NULL
			|				ИЛИ НаПодразделенияСводно.ЗначениеПоказателяПроцент = 0
			|			ТОГДА 0
			|		ИНАЧЕ ДД.Знаменатель
			|	КОНЕЦ,
			|	ЕСТЬNULL(ВЫРАЗИТЬ(Факт.Сумма КАК ЧИСЛО(28, 10)), 0),
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	&Регистратор,
			|	ДД.МатериалПродукцияУслуга,
			|	ДД.ГруппаПродукции,
			|	1,
			|	ВЫБОР
			|		КОГДА ДД.ТипБазыРаспределенияПоПартиям В (&МассивКоличества)
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ
			|ИЗ
			|	НаЭтапыДетально КАК ДД
			|		ЛЕВОЕ СОЕДИНЕНИЕ НаПартииФакт КАК Факт
			|		ПО ДД.Подразделение = Факт.Подразделение
			|			И ДД.ПартияПроизводства = Факт.ПартияПроизводства
			|			И ДД.ЗаказНаПроизводство = Факт.ЗаказНаПроизводство
			|			И ДД.КодСтрокиПродукция = Факт.КодСтрокиПродукция
			|			И ДД.ГруппаПродукции = Факт.ГруппаПродукции
			|		ЛЕВОЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
			|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	""ПоПравилуНаЭтапы"",
			|	ДД.Подразделение,
			|	ВЫБОР
			|		КОГДА ДД.ПартияПроизводства <> ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
			|			ТОГДА ДД.ПартияПроизводства
			|		ИНАЧЕ ДД.ЗаказНаПроизводство
			|	КОНЕЦ,
			|	ДД.КодСтрокиПродукция,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	ВЫБОР
			|		КОГДА &БазаРаспределенияПоПартиям В (&МассивОбъема)
			|			ТОГДА ДД.Объем
			|		КОГДА &БазаРаспределенияПоПартиям В (&МассивВеса)
			|			ТОГДА ДД.Вес
			|		КОГДА &БазаРаспределенияПоПартиям В (&МассивСтоимости)
			|			ТОГДА ДД.Стоимость
			|		ИНАЧЕ 0
			|	КОНЕЦ,
			|	0,
			|	0,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	&Регистратор,
			|	НЕОПРЕДЕЛЕНО,
			|	ДД.ГруппаПродукции,
			|	ДД.ПорядокВывода,
			|	ВЫБОР
			|		КОГДА &БазаРаспределенияПоПартиям В (&МассивКоличества)
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ
			|ИЗ
			|	ДанныеНеВошедшие КАК ДД
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПартииНеВошедшие КАК НеВошедшие
			|		ПО ДД.Подразделение = НеВошедшие.Подразделение
			|			И ДД.ПартияПроизводства = НеВошедшие.ПартияПроизводства
			|			И ДД.ЗаказНаПроизводство = НеВошедшие.ЗаказНаПроизводство
			|ГДЕ
			|	НеВошедшие.ПартияПроизводства ЕСТЬ NULL
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	""ПоПравилуНаЭтапы"",
			|	ДД.Подразделение,
			|	НЕОПРЕДЕЛЕНО,
			|	0,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	0,
			|	0,
			|	0,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	&Регистратор,
			|	ВЫБОР
			|		КОГДА ДД.ПартияПроизводства <> ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
			|			ТОГДА ДД.ПартияПроизводства
			|		ИНАЧЕ ДД.ЗаказНаПроизводство
			|	КОНЕЦ,
			|	НЕОПРЕДЕЛЕНО,
			|	3,
			|	ЛОЖЬ
			|ИЗ
			|	ПартииНеВошедшие КАК ДД
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	""НаДругиеСтатьи"",
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	0,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	ДД.ДоляСтоимости,
			|	ДД.ДоляСтоимости,
			|	ЕСТЬNULL(Факт.Сумма, 0),
			|	ДД.КорСтатьяРасходов,
			|	ДД.КорАналитикаРасходов,
			|	ДД.КорАналитикаАктивовПассивов,
			|	&Регистратор,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	1,
			|	ИСТИНА
			|ИЗ
			|	ДолиСписанияНаСтатьи КАК ДД
			|		ЛЕВОЕ СОЕДИНЕНИЕ НаСтатьиФакт КАК Факт
			|		ПО ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДД.КорСтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
			|			ТОГДА 
			|				ДД.КорСтатьяРасходов = Факт.СтатьяРасходов
			|			ИНАЧЕ
			|				ДД.КорСтатьяРасходов = Факт.СтатьяАктивовПассивов
			|			КОНЕЦ
			|			И ДД.КорАналитикаРасходов = Факт.АналитикаРасходов
			|			И ДД.КорАналитикаАктивовПассивов = Факт.АналитикаАктивовПассивов
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	""НаНЗП"",
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	0,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	Реквизиты.ДоляСтоимостиНаНЗП,
			|	Реквизиты.ДоляСтоимостиНаНЗП,
			|	ЕСТЬNULL(Факт.Сумма, 0),
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	&Регистратор,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	1,
			|	ИСТИНА
			|ИЗ
			|	ОставитьВНЗП КАК ДД
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Реквизиты
			|		ПО ДД.Регистратор = Реквизиты.Регистратор
			|		ЛЕВОЕ СОЕДИНЕНИЕ НаНЗПФакт КАК Факт
			|		ПО ДД.Организация = Факт.Организация
			|			И ДД.Подразделение = Факт.Подразделение
			|			И ДД.НаправлениеДеятельности = Факт.НаправлениеДеятельности
			|			И ДД.СтатьяРасходов = Факт.СтатьяРасходов
			|			И ДД.АналитикаРасходов = Факт.АналитикаРасходов
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	""НаЭтапыВручную"",
			|	ДД.КорПодразделение,
			|	ВЫБОР
			|		КОГДА ДД.ПартияПроизводства <> ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
			|			ТОГДА ДД.ПартияПроизводства
			|		ИНАЧЕ ДД.ЗаказНаПроизводство
			|	КОНЕЦ,
			|	ДД.КодСтрокиПродукция,
			|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
			|	ДД.ДоляСтоимости,
			|	ДД.ДоляСтоимости,
			|	ЕСТЬNULL(Факт.Сумма, 0),
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	&Регистратор,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	1,
			|	ИСТИНА
			|ИЗ
			|	ДолиПроизводственныхРасходов КАК ДД
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК Реквизиты
			|	ПО ДД.Регистратор = Реквизиты.Ссылка
			|		И Реквизиты.ПартииУказаныВручную
			|
			|	ЛЕВОЕ СОЕДИНЕНИЕ НаПартииФакт КАК Факт
			|	ПО ДД.КорПодразделение = Факт.Подразделение
			|		И ДД.ПартияПроизводства = Факт.ПартияПроизводства
			|		И ДД.ЗаказНаПроизводство = Факт.ЗаказНаПроизводство
			|		И ДД.КодСтрокиПродукция = Факт.КодСтрокиПродукция";
		
	#КонецОбласти	
			
		Запрос.УстановитьПараметр("Регистратор",						Регистратор);
		Запрос.УстановитьПараметр("ДанныеСебестоимости",				ДанныеСебестоимости);
		Запрос.УстановитьПараметр("МассивКоличества",					МассивТиповРаспределенияБазыПоКоличеству);
		Запрос.УстановитьПараметр("МассивСтоимости",					МассивТиповРаспределенияБазыПоСтоимости);
		Запрос.УстановитьПараметр("МассивОбъема",						МассивТиповРаспределенияБазыПоОбъему);
		Запрос.УстановитьПараметр("МассивВеса",							МассивТиповРаспределенияБазыПоВесу);
		Запрос.УстановитьПараметр("БазаРаспределенияПоПартиям",			Реквизиты.БазаРаспределенияПоПартиям);
		Запрос.УстановитьПараметр("БазаРаспределенияПоПодразделениям",	Реквизиты.БазаРаспределенияПоПодразделениям);
		Запрос.УстановитьПараметр("НачалоПериода",						НачалоПериода);
		Запрос.УстановитьПараметр("ОкончаниеПериода",					ОкончаниеПериода);
		Запрос.УстановитьПараметр("Организация",						Реквизиты.Организация);
		Запрос.УстановитьПараметр("Подразделение",						Реквизиты.Подразделение);
		Запрос.УстановитьПараметр("РаспределятьПоПодразделениям",		Реквизиты.РаспределятьПоПодразделениям);
		Запрос.УстановитьПараметр("ОставитьВНЗП",						Реквизиты.ОставитьВНЗП);
		Запрос.УстановитьПараметр("НаправлениеДеятельности",			Реквизиты.НаправлениеДеятельности);
		Запрос.УстановитьПараметр("СтатьяРасходов",						Реквизиты.СтатьяРасходов);
		Запрос.УстановитьПараметр("АналитикаРасходов",					Реквизиты.АналитикаРасходов);
	
	КонецЕсли;
	
	Результат = Запрос.ВыполнитьПакет();
	Распределение = Результат[Результат.ВГраница()].Выгрузить();
	
	ВнешниеНаборыДанных = Новый Структура("Распределение", Распределение);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;

	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВыводитьПредставлениеСумм", Не ПустаяСтрока(ПредставлениеСумм), Истина);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВыводитьПредставлениеРасчетаСебестоимости", Не РасчетСебестоимостиВыполнен, Истина);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	//Пост обработка отчета.
	//Устанавливаем отражение правила распределения, меняем заголовки колонок и т.д.
	
	ТабличныйДокумент = ПроцессорВывода.ЗакончитьВывод();
	
	ЗаголовокШаблон = "%000%";
	ПредставлениеСуммШаблон = "%005%";
	ПредставлениеРасчетаСебестоимостиШаблон = "%010%";
	
	Область = ТабличныйДокумент.НайтиТекст(ЗаголовокШаблон);
	Если Область <> Неопределено Тогда
		
		Если Реквизиты.ЭтоРаспределениеИзОВЗ Тогда
			ЗаголовокОтчета = НСтр("ru = 'По ОВЗ ""%1"" в подразделении ""%2"" за %3'");
			ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ЗаголовокОтчета,
				Реквизиты.АналитикаРасходов,
				Реквизиты.Подразделение,
				Формат(Реквизиты.Дата, "ДФ='ММММ гггг'"));
		ИначеЕсли ЗначениеЗаполнено(Реквизиты.АналитикаРасходов) Тогда
			ЗаголовокОтчета = НСтр("ru = 'По статье ""%1 (%2)"" в подразделении ""%3"" за %4'");
			ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ЗаголовокОтчета,
				Реквизиты.СтатьяРасходов,
				Реквизиты.АналитикаРасходов,
				Реквизиты.Подразделение,
				Формат(Реквизиты.Дата,НСтр("ru='ДФ=''ММММ гггг'''")));
		Иначе
			ЗаголовокОтчета = НСтр("ru = 'По статье ""%1"" в подразделении ""%2"" за %3'");
			ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ЗаголовокОтчета,
				Реквизиты.СтатьяРасходов,
				Реквизиты.Подразделение,
				Формат(Реквизиты.Дата,НСтр("ru='ДФ=''ММММ гггг'''")));
		КонецЕсли;
		
		Область.Текст = СтрЗаменить(Область.Текст, ЗаголовокШаблон, ЗаголовокОтчета);
		
	КонецЕсли;
	
	//Вывод представления сумм
	Область = ТабличныйДокумент.НайтиТекст(ПредставлениеСуммШаблон);
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, ПредставлениеСуммШаблон, ПредставлениеСумм);
	КонецЕсли;
	
	//Вывод информации о состоянии расчета себестоимости
	Область = ТабличныйДокумент.НайтиТекст(ПредставлениеРасчетаСебестоимостиШаблон);
	Если Область <> Неопределено Тогда
		Если Реквизиты.НазначениеНастройкиРаспределения = Перечисления.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат Тогда
			ПредставлениеРасчетаСебестоимости = НСтр("ru = 'Не выполнен этап распределения расходов по направлениям деятельности, суммы расходов будут актуализированы после расчета.'");
		Иначе
			ПредставлениеРасчетаСебестоимости = НСтр("ru = 'Не выполнен расчет себестоимости, суммы расходов будут актуализированы после расчета.'");
		КонецЕсли;
		Область.Текст = СтрЗаменить(Область.Текст, ПредставлениеРасчетаСебестоимостиШаблон, ПредставлениеРасчетаСебестоимости);
	КонецЕсли;
	
	//Инициализация переменных для оформления правила по подразделению
	Если ЗначениеЗаполнено(ВалютаПлановойСтоимости) Тогда
		ПредставлениеПоказателяСтоимость = СтрШаблон(НСтр("ru = 'Стоимость, %1'"), ВалютаПлановойСтоимости);
	Иначе
		ПредставлениеПоказателяСтоимость = СтрШаблон(НСтр("ru = 'Стоимость, %1'"), ВалютаОтчета);
	КонецЕсли;
	
	ПредставлениеПозицииПоПодразделению  = НСтр("ru = 'Материалы/Работы/Продукция'");
	ПредставлениеПоказателяПодразделений = НСтр("ru = 'Значение показателя'");
	ПредставлениеПравилаПоПодразделениям  = НСтр("ru = 'Пропорционально стоимости материальных затрат'");
	
	//Инициализация переменных для оформления правила по этапу
	ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Материалы/Работы/Продукция'");
	ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Значение показателя'");
	
	ОтборПоГруппамПродукции = Регистратор.ОтборПоГруппамПродукции.Количество() > 0;
	
	БазаРаспределенияПоПодразделениям = Реквизиты.БазаРаспределенияПоПодразделениям;
	БазаРаспределенияПоПартиям        = Реквизиты.БазаРаспределенияПоПартиям;
	
	Если Реквизиты.РаспределятьПоПодразделениям Тогда
		Если БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат Тогда 
			ПредставлениеПозицииПоПодразделению  = НСтр("ru = 'Материал'");
			ПредставлениеПоказателяПодразделений = ПредставлениеПоказателяСтоимость;
			ПредставлениеПравилаПоПодразделениям  = НСтр("ru = '%1 пропорционально стоимости материалов %2'");
		ИначеЕсли БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда Тогда
			ПредставлениеПозицииПоПодразделению  = НСтр("ru = 'Вид работ'");
			ПредставлениеПоказателяПодразделений = ПредставлениеПоказателяСтоимость;
			ПредставлениеПравилаПоПодразделениям  = НСтр("ru = '%1 пропорционально расходам на оплату труда %2'");
		ИначеЕсли БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат Тогда
			ПредставлениеПозицииПоПодразделению  = НСтр("ru = 'Материал \ Вид работ'");
			ПредставлениеПоказателяПодразделений = ПредставлениеПоказателяСтоимость;
			ПредставлениеПравилаПоПодразделениям  = НСтр("ru = '%1 пропорционально прямым затратам %2'");
		ИначеЕсли БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.ВводитсяЕжемесячно Тогда
			ПредставлениеПравилаПоПодразделениям  = НСтр("ru = '%1 по переменным показателям %2'");
		ИначеЕсли БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.ВводитсяПриИзменении Тогда
			ПредставлениеПравилаПоПодразделениям  = НСтр("ru = '%1 по постоянным показателям %2'");
		Иначе
			ПредставлениеПоказателяПодразделений = НСтр("ru = 'Доля стоимости'");
			ПредставлениеПравилаПоПодразделениям = НСтр("ru = '%1 вручную %2'");
		КонецЕсли;
	КонецЕсли;
	
	Если БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат Тогда 
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Материал'");
		ПредставлениеПоказателяПоЭтапу = ПредставлениеПоказателяСтоимость;
			
	ИначеЕсли БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Материал'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Количество'");
			
	ИначеЕсли БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Материал'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Объем'");
		
	ИначеЕсли БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Материал'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Вес'");
		
	ИначеЕсли БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Вид работ'");;
		ПредставлениеПоказателяПоЭтапу = ПредставлениеПоказателяСтоимость;
		
	ИначеЕсли БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Вид работ'");;
		ПредставлениеПоказателяПоЭтапу = ПредставлениеПоказателяСтоимость;
		
	ИначеЕсли БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Вид работ'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Количество'");
		
	ИначеЕсли БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции
		Или БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукцииСУчетомБудущихВыпусков Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Продукция'");
		ПредставлениеПоказателяПоЭтапу = ПредставлениеПоказателяСтоимость;
		
	ИначеЕсли БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоПродукции
		Или БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукцииСУчетомБудущихВыпусков Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Продукция'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Количество'");
		
	ИначеЕсли БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемПродукции
		Или БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемПродукцииСУчетомБудущихВыпусков Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Продукция'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Объем'");
		
	ИначеЕсли БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ВесПродукции
		Или БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ВесПродукцииСУчетомБудущихВыпусков Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Продукция'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Вес'");
		
	ИначеЕсли БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Материал \ Вид работ'");
		ПредставлениеПоказателяПоЭтапу = ПредставлениеПоказателяСтоимость;
		
	КонецЕсли;
	
	ПредставлениеНаСтатьи = "";
	ПрефиксПредставленияПравила = "";
	ПостфиксПредставленияПравила = "";
	ПредставлениеПравилаПоЭтапам = "";
	
	ПогрешностьРасчета = 0;
	Если Реквизиты.ВсегоДолейСтоимости = 0 Тогда
		ПроцентНаПроизводство = ?(Реквизиты.РаспределятьПоПартиям,100,0);
		ПроцентНаОВЗ = ?(Реквизиты.РаспределятьНаОВЗ,100,0);
	Иначе
		
		ПроцентПоСтатьям = Окр(Реквизиты.ДоляСтоимостиНаСтатьи * 100/ Реквизиты.ВсегоДолейСтоимости);
		ПроцентНаНЗП = Окр(Реквизиты.ДоляСтоимостиНаНЗП * 100/ Реквизиты.ВсегоДолейСтоимости);
		ПроцентНаПроизводство = Окр(Реквизиты.ДоляСтоимостиНаПроизводство * 100/ Реквизиты.ВсегоДолейСтоимости);
		ПроцентНаОВЗ = Окр(Реквизиты.ДоляСтоимостиНаОВЗ * 100/ Реквизиты.ВсегоДолейСтоимости);
		ПогрешностьРасчета = 100 - ПроцентНаПроизводство - ПроцентНаНЗП - ПроцентПоСтатьям - ПроцентНаОВЗ;
		
	КонецЕсли;
	
	Если Не ПогрешностьРасчета = 0 Тогда
		
		МаксимальныйПроцент = Макс(ПроцентПоСтатьям, ПроцентНаНЗП, ПроцентНаПроизводство, ПроцентНаОВЗ);
		Если ПроцентНаПроизводство = МаксимальныйПроцент Тогда
			ПроцентНаПроизводство = ПроцентНаПроизводство + ПогрешностьРасчета;
		ИначеЕсли ПроцентНаОВЗ = МаксимальныйПроцент Тогда
			ПроцентНаОВЗ = ПроцентНаОВЗ + ПогрешностьРасчета;
		ИначеЕсли ПроцентПоСтатьям = МаксимальныйПроцент Тогда
			ПроцентПоСтатьям = ПроцентПоСтатьям + ПогрешностьРасчета;
		ИначеЕсли ПроцентНаНЗП = МаксимальныйПроцент Тогда
			ПроцентНаНЗП = ПроцентНаНЗП + ПогрешностьРасчета;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Реквизиты.ДоляСтоимостиНаОВЗ > 0 И Реквизиты.ВсегоДолейСтоимости <> Реквизиты.ДоляСтоимостиНаОВЗ Тогда
		ПредставлениеНаОВЗ = СтрШаблон(НСтр("ru = '%1%% распределяется на другие ОВЗ'"), ПроцентПоСтатьям);
	Иначе
		ПредставлениеНаОВЗ = НСтр("ru = 'Расходы полностью распределяются на другие ОВЗ'");
	КонецЕсли;
	
	Если Реквизиты.ДоляСтоимостиНаСтатьи > 0 И Реквизиты.ВсегоДолейСтоимости <> Реквизиты.ДоляСтоимостиНаСтатьи Тогда
		ПредставлениеНаСтатьи = СтрШаблон(НСтр("ru = '%1%% распределяется на другие статьи'"), ПроцентПоСтатьям);
	Иначе
		ПредставлениеНаСтатьи = НСтр("ru = 'Расходы полностью распределяются на другие статьи'");
	КонецЕсли;
	
	Если Реквизиты.ДоляСтоимостиНаНЗП > 0 И Не Реквизиты.ДоляСтоимостиНаНЗП = Реквизиты.ВсегоДолейСтоимости Тогда
		ПредставлениеНаНЗП = СтрШаблон(НСтр("ru = '%1%% остается в НЗП'"), ПроцентНаНЗП);
	Иначе
		ПредставлениеНаНЗП = НСтр("ru = 'Расходы полностью остаются в НЗП'");
	КонецЕсли;
	
	ПрефиксПредставленияПравила = СтрШаблон(НСтр("ru = '%1%% распределяется'"), ПроцентНаПроизводство);
	
	Если ОтборПоГруппамПродукции Тогда
		ПостфиксПредставленияПравила = НСтр("ru = 'на выбранные группы продукции'");
	КонецЕсли;
	
	Если Реквизиты.РаспределятьПоПодразделениям Тогда
		ПредставлениеПравилаПоПодразделениям = СтрШаблон(ПредставлениеПравилаПоПодразделениям, 
														ПрефиксПредставленияПравила, 
														ПостфиксПредставленияПравила);
	КонецЕсли;
	
	Если Реквизиты.РаспределятьПоПартиям Тогда
		
		Если ЗначениеЗаполнено(ПрефиксПредставленияПравила) Тогда
			ПрефиксПредставленияПравила = СтрШаблон(НСтр("ru = '%1 по партиям'"), ПрефиксПредставленияПравила);
		Иначе
			ПрефиксПредставленияПравила = НСтр("ru = 'Расходы распределяются по партиям'");
		КонецЕсли;
		
		ШаблонПредставления = НСтр("ru = '%1 %2 %3'");
		НаправлениеРаспределения = "";
		Если Реквизиты.ПартииУказаныВручную Тогда
			НаправлениеРаспределения = НСтр("ru = 'вручную'");
		ИначеЕсли Реквизиты.ПодразделенияУказаныВручную Тогда
			НаправлениеРаспределения = НСтр("ru = 'подразделений'");
		ИначеЕсли Не ЗначениеЗаполнено(Реквизиты.НаправлениеРаспределения) Тогда
			НаправлениеРаспределения = НСтр("ru = 'всех подразделений'");
		ИначеЕсли Реквизиты.НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Вышестоящее Тогда
			НаправлениеРаспределения = НСтр("ru = 'вышестоящего подразделения'");
		ИначеЕсли Реквизиты.НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Нижестоящие Тогда
			НаправлениеРаспределения = НСтр("ru = 'нижестоящих подразделений'");
		ИначеЕсли Реквизиты.НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Текущее Тогда
			НаправлениеРаспределения = НСтр("ru = 'подразделения'");
		ИначеЕсли Реквизиты.НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Указанные Тогда
			НаправлениеРаспределения = НСтр("ru = 'указанных подразделений'");
		КонецЕсли;
		
		ПредставлениеПравилаПоЭтапам = СокрЛП(СтрШаблон(ШаблонПредставления,
													ПрефиксПредставленияПравила,
													НаправлениеРаспределения,
													ПостфиксПредставленияПравила));
															
	ИначеЕсли Реквизиты.НазначениеНастройкиРаспределения = 
			Перечисления.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаСебестоимостьПроизводства Тогда
		ПредставлениеПравилаПоЭтапам = НСтр("ru = 'Расходы распределяются непосредственно на партии производства'")
	КонецЕсли;
	
	//Заменим для подразделений
	ПредставлениеПравилаПоПодразделениямШаблон = "%001%";
	ПредставлениеНаСтатьиШаблон = "%002%";
	ПредставлениеНаЭтапыВручнуюШаблон = "%003%";
	ПредставлениеПравилаПоЭтапамШаблон = "%004%";
	ПредставлениеНаНЗПШаблон = "%006%";
	ПредставлениеНаНДШаблон = "%007%";
	ПредставлениеНаОВЗШаблон = "%008%";
	ВалютаОтчетаШаблон = "%100%";
	ЗначППШаблон = "%101%";
	ЗначПЭШаблон = "%102%";
	ПредставлениеПозицииПоПодразделениюШаблон = "%201%";
	ПредставлениеПозицииПоЭтапамШаблон = "%202%";
	ПредставлениеПозицииВСтрокеШаблон = "%203%";
	
	Область = ТабличныйДокумент.НайтиТекст(ПредставлениеПравилаПоПодразделениямШаблон);
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, ПредставлениеПравилаПоПодразделениямШаблон, ПредставлениеПравилаПоПодразделениям);
	КонецЕсли;
	
	Область = ТабличныйДокумент.НайтиТекст(ПредставлениеНаЭтапыВручнуюШаблон);
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, ПредставлениеНаЭтапыВручнуюШаблон, ПредставлениеПравилаПоЭтапам);
	КонецЕсли;
	
	Область = ТабличныйДокумент.НайтиТекст(ЗначППШаблон);
	Пока Область <> Неопределено Цикл
		Область.Текст = ПредставлениеПоказателяПодразделений;
		Область = ТабличныйДокумент.НайтиТекст(ЗначППШаблон);
	КонецЦикла;
	
	Область = ТабличныйДокумент.НайтиТекст(ПредставлениеПозицииПоПодразделениюШаблон);
	Пока Область <> Неопределено Цикл
		Область.Текст = ПредставлениеПозицииПоПодразделению;
		Область = ТабличныйДокумент.НайтиТекст(ПредставлениеПозицииПоПодразделениюШаблон);
	КонецЦикла;
	
	//Заменим для этапов
	
	Область = ТабличныйДокумент.НайтиТекст(ПредставлениеПравилаПоЭтапамШаблон);
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, ПредставлениеПравилаПоЭтапамШаблон, ПредставлениеПравилаПоЭтапам);
	КонецЕсли;
	
	Область = ТабличныйДокумент.НайтиТекст(ПредставлениеПозицииПоЭтапамШаблон);
	Пока Область <> Неопределено Цикл
		Область.Текст = ПредставлениеПозицииПоЭтапу;
		Область = ТабличныйДокумент.НайтиТекст(ПредставлениеПозицииПоЭтапамШаблон);
	КонецЦикла;
	
	Область = ТабличныйДокумент.НайтиТекст(ЗначПЭШаблон);
	Пока ТипЗнч(Область) <> Тип("Неопределено") Цикл
		Область.Текст = ПредставлениеПоказателяПоЭтапу;
		Область = ТабличныйДокумент.НайтиТекст(ЗначПЭШаблон);
	КонецЦикла;
	
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПозицииВСтроке%");
	Пока Область <> Неопределено Цикл
		
		Если ПредставлениеПозицииПоЭтапу = НСтр("ru = 'Продукция'") Тогда
			ПредставлениеПозицииВСтроке = НСтр("ru = '<другая продукция>'");
		Иначе
			ПредставлениеПозицииВСтроке = СтрШаблон(НСтр("ru = '<другие %1>'"), НРег(ПредставлениеПозицииПоЭтапу));
		КонецЕсли;
		
		Область.Текст = СтрЗаменить(Область.Текст, "%ПредставлениеПозицииВСтроке%", ПредставлениеПозицииВСтроке);
		Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПозицииВСтроке%");
		
	КонецЦикла;
	
	//Заменим для распределения на ОВЗ
	
	Область = ТабличныйДокумент.НайтиТекст(ПредставлениеНаОВЗШаблон);
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, ПредставлениеНаОВЗШаблон, ПредставлениеНаОВЗ);
	КонецЕсли;
	
	//Заменим для распределения на статьи
	
	Область = ТабличныйДокумент.НайтиТекст(ПредставлениеНаСтатьиШаблон);
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, ПредставлениеНаСтатьиШаблон, ПредставлениеНаСтатьи);
	КонецЕсли;
	
	// Замена заголовка НЗП.
	Область = ТабличныйДокумент.НайтиТекст(ПредставлениеНаНЗПШаблон);
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, ПредставлениеНаНЗПШаблон, ПредставлениеНаНЗП);
	КонецЕсли;
	
	// Замена заголовка НД.
	Область = ТабличныйДокумент.НайтиТекст(ПредставлениеНаНДШаблон);
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, ПредставлениеНаНДШаблон, НСтр("ru = 'Расходы распределяются между направлениями деятельности'"));
	КонецЕсли;
	
	//Установка заголовков валют.
	
	Область = ТабличныйДокумент.НайтиТекст(ВалютаОтчетаШаблон);
	Пока Область <> Неопределено Цикл
		Область.Текст = СтрЗаменить(Область.Текст, ВалютаОтчетаШаблон, ВалютаОтчета);
		Область = ТабличныйДокумент.НайтиТекст(ВалютаОтчетаШаблон);
	КонецЦикла;
	
	ТабличныйДокумент.ПоказатьУровеньГруппировокСтрок(2);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


// Настраивает доступные значения у параметра компоновки данных "Данные по себестоимости" в зависимости от варианта.
// 
// Параметры:
//	ПараметрыКД - Структура - Описание:
//		* Документ - ДокументСсылка.РаспределениеПрочихЗатрат - расшифровывающийся документ.
//		* НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных - настройки схемы компоновки данных.
Процедура НастроитьПараметрыОтчетаПоВариантуОтчета(ПараметрыКД)
	
	ПараметрДанныеОтчета = СхемаКомпоновкиДанных.Параметры.Найти("ДанныеСебестоимости");
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыКД.Документ, "РегламентированныйУчет, УправленческийУчет,
	//++ НЕ УТ

	//++ Локализация
	|НалоговыйУчет,
	//-- Локализация

	//-- НЕ УТ
	|НазначениеНастройкиРаспределения");
	
	СписокВыбора = Новый СписокЗначений;
	
	Если ЗначенияРеквизитов.УправленческийУчет Тогда
		
		СписокВыбора.Добавить(1, НСтр("ru = 'В валюте упр. учета с НДС'"));
		Если Не ЗначенияРеквизитов.НазначениеНастройкиРаспределения = Перечисления.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат Тогда
			СписокВыбора.Добавить(2, НСтр("ru = 'В валюте упр. учета без НДС'"));
		КонецЕсли;
		
		Если РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций() Тогда
			СписокВыбора.Добавить(5, НСтр("ru = 'В валюте упр. учета'"));
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначенияРеквизитов.РегламентированныйУчет 
		//++ НЕ УТ

		//++ Локализация
		ИЛИ ЗначенияРеквизитов.НалоговыйУчет
		//-- Локализация

		//-- НЕ УТ
		Тогда
		
		СписокВыбора.Добавить(3, НСтр("ru = 'В валюте регл. учета'"));
		//++ НЕ УТ

		//++ Локализация
		Если ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
			
			Если ЗначенияРеквизитов.РегламентированныйУчет Тогда
				СписокВыбора[СписокВыбора.Количество() - 1].Представление = НСтр("ru = 'В валюте регл. учета (БУ)'");
			Иначе
				СписокВыбора.Удалить(СписокВыбора[СписокВыбора.Количество() - 1]);
			КонецЕсли;
			
			Если ЗначенияРеквизитов.НалоговыйУчет Тогда
				СписокВыбора.Добавить(4, НСтр("ru = 'В валюте регл. учета (НУ)'"));
			КонецЕсли;
			
		КонецЕсли;
		//-- Локализация

		//-- НЕ УТ
		
	КонецЕсли;
	
	ПараметрДанныеОтчета.УстановитьДоступныеЗначения(СписокВыбора);
	
	Если ПараметрыКД.НовыеНастройкиКД = Неопределено
		Или ПараметрыКД.НовыеПользовательскиеНастройкиКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеПараметраДанныеОтчета = ПараметрыКД.НовыеНастройкиКД.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДанныеСебестоимости"));
	НастройкаДанныеОтчета = ПараметрыКД.НовыеПользовательскиеНастройкиКД.Элементы.Найти(ЗначениеПараметраДанныеОтчета.ИдентификаторПользовательскойНастройки);
	
	Если Не НастройкаДанныеОтчета = Неопределено
		И СписокВыбора.НайтиПоЗначению(НастройкаДанныеОтчета.Значение) = Неопределено Тогда
		НастройкаДанныеОтчета.Значение = СписокВыбора[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
