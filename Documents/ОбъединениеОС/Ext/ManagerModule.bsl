#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("ОбесценениеВНА");
	
	ОбъединениеОСЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда

		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоОС(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаПервоначальныеСведенияОС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаМестонахождениеОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаУзлыКомпонентыАмортизации(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаСоставЕГДС(Запрос, ТекстыЗапроса, Регистры);
		
		
		ОбъединениеОСЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка			 - ДокументСсылка.ОбъединениеОС - Документ, для которого формируются движения
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаПервоначальныеСведенияОС(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаУзлыКомпонентыАмортизации(ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаОбесцененияВНА(ТекстыЗапроса);
	ТекстЗапросаПрочиеДоходы(ТекстыЗапроса, ТекстыЗапроса);
	ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	ОбъединениеОСЛокализация.ТаблицыОтложенногоФормированияДвижений(Запрос, ТекстыЗапроса);
	
	ТаблицыДляДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, Неопределено);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

#КонецОбласти

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Команда = ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаСписка";
	КонецЕсли;
	
	ОбъединениеОСЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Объединение ОС".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ОбъединениеОС) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ОбъединениеОС.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ОбъединениеОС);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ОбъединениеОСЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ОбъединениеОСЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки	 - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Представление реквизитов.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  Соответствие - Представление реквизитов
Функция ПредставлениеРеквизитов(Организация) Экспорт

	ПредставлениеРеквизитов = Новый Соответствие;

	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	
	ВалютаРеглПредставление = "";
	Если ЗначениеЗаполнено(ВалютаРегл) Тогда
		ВалютаРеглПредставление = "(" + Строка(ВалютаРегл) + ")";
	КонецЕсли;
	
	ВедетсяРегламентированныйУчетВНА = ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА();
	
	Если ВедетсяРегламентированныйУчетВНА Тогда
		
		ПредставлениеРеквизитов.Вставить("ЛиквидационнаяСтоимость", СтрШаблон(НСтр("ru = 'Ликвидационная стоимость (%1)'"), Строка(ВалютаУпр)));
		
	Иначе
		
		Если ВалютаРегл = ВалютаУпр Тогда
			ПредставлениеРеквизитов.Вставить("ЛиквидационнаяСтоимость", НСтр("ru = 'Ликвидационная стоимость'"));
		Иначе
			ПредставлениеРеквизитов.Вставить("ЛиквидационнаяСтоимость", СтрШаблон(НСтр("ru = 'Ликвидационная стоимость (%1)'"), Строка(ВалютаУпр)));
			ПредставлениеРеквизитов.Вставить("ЛиквидационнаяСтоимостьРегл", СокрЛП(СтрШаблон(НСтр("ru = 'Ликвидационная стоимость %1'"), ВалютаРеглПредставление)));
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат ПредставлениеРеквизитов;
	
КонецФункции

// Возвращает параметры выбора ОС.
// 
// Параметры:
// 	ПараметрыПроцедуры - Структура - 
// 	
// Возвращаемое значение:
// 	Структура - Описание:
//       * ЗначенияПараметров - Структура -:
//           ** СрокИспользованияУУ - Число - 
//           ** НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности -
//       * ДоступныПараметрыАмортизацииУУ - Булево -
//       * ПоНаработкеУУ - Булево -
//       * ПервоеОСПредставление - Строка -
//       * ПроблемныеОС - Неопределено -
//       * ПервоеОСПредставление - Строка -
//       * ПервоеОС - СправочникСсылка.ОбъектыЭксплуатации -
//       * ИспользуетсяЦФ - Булево -
//       * ДоступныПараметрыАмортизацииБУ - Булево -
//       * ДоступныПараметрыАмортизацииУУ - Булево -
//       * ПоНаработкеБУ - Булево -
//       * ПоНаработкеУУ - Булево -
//       * ЗначенияПараметров - Неопределено -
//       * ПроблемныеОС - Неопределено -
Функция ПараметрыВыбораОсновныхСредств(ПараметрыПроцедуры) Экспорт

	ПараметрыВыбораОсновныхСредств = Новый Структура;
	ПараметрыВыбораОсновныхСредств.Вставить("ПроблемныеОС", Неопределено);
	ПараметрыВыбораОсновныхСредств.Вставить("ЗначенияПараметров", Неопределено);
	ПараметрыВыбораОсновныхСредств.Вставить("ПоНаработкеУУ", Ложь);
	ПараметрыВыбораОсновныхСредств.Вставить("ПоНаработкеБУ", Ложь);
	ПараметрыВыбораОсновныхСредств.Вставить("ДоступныПараметрыАмортизацииУУ", Ложь);
	ПараметрыВыбораОсновныхСредств.Вставить("ДоступныПараметрыАмортизацииБУ", Ложь);
	ПараметрыВыбораОсновныхСредств.Вставить("ИспользуетсяЦФ", Ложь);
	ПараметрыВыбораОсновныхСредств.Вставить("ПервоеОС", ?(ПараметрыПроцедуры.СписокОС.Количество() <> 0, ПараметрыПроцедуры.СписокОС[0], Неопределено));
	ПараметрыВыбораОсновныхСредств.Вставить("ПервоеОСПредставление", "");
	
	Если ПараметрыПроцедуры.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОбъединениеОС
			И ПараметрыПроцедуры.СписокОС.Количество() = 0 
		ИЛИ ПараметрыПроцедуры.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрисоединениеОС
			И НЕ ЗначениеЗаполнено(ПараметрыПроцедуры.ОсновноеСредство) Тогда
		Возврат ПараметрыВыбораОсновныхСредств;
	КонецЕсли;
	
	ТекстЗапроса = ОбъединениеОСЛокализация.ТекстЗапросаДляПроверкиОС();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	МестонахождениеОС.ОсновноеСредство КАК ОсновноеСредство,
		|	МестонахождениеОС.Организация КАК Организация,
		|	МестонахождениеОС.Местонахождение КАК Местонахождение
		|ПОМЕСТИТЬ МестонахождениеОС
		|ИЗ
		|	РегистрСведений.МестонахождениеОС.СрезПоследних(
		|			&Дата,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И ОсновноеСредство В (&СписокВсехОС)) КАК МестонахождениеОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
		|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
		|	ПервоначальныеСведенияОС.ПорядокУчетаУУ КАК ПорядокУчетаУУ
		|ПОМЕСТИТЬ ПервоначальныеСведенияОС
		|ИЗ
		|	РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|			&Дата,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И (ОсновноеСредство, Организация) В
		|					(ВЫБРАТЬ
		|						МестонахождениеОС.ОсновноеСредство,
		|						МестонахождениеОС.Организация
		|					ИЗ
		|						МестонахождениеОС
		|					ГДЕ
		|						МестонахождениеОС.ОсновноеСредство = &ПервоеОС)) КАК ПервоначальныеСведенияОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПараметрыАмортизацииОСУУ.ОсновноеСредство КАК ОсновноеСредство,
		|	ПараметрыАмортизацииОСУУ.СрокИспользования КАК СрокИспользования,
		|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации
		|ПОМЕСТИТЬ ПараметрыАмортизацииОСУУ
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
		|			&Дата,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И (ОсновноеСредство, Организация) В
		|					(ВЫБРАТЬ
		|						МестонахождениеОС.ОсновноеСредство,
		|						МестонахождениеОС.Организация
		|					ИЗ
		|						МестонахождениеОС)) КАК ПараметрыАмортизацииОСУУ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПараметрыАмортизацииОСБУ.ОсновноеСредство КАК ОсновноеСредство,
		|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользованияБУ,
		|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользованияНУ
		|ПОМЕСТИТЬ ПараметрыАмортизацииОСБУ
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
		|			&Дата,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И (ОсновноеСредство, Организация) В
		|					(ВЫБРАТЬ
		|						МестонахождениеОС.ОсновноеСредство,
		|						МестонахождениеОС.Организация
		|					ИЗ
		|						МестонахождениеОС)) КАК ПараметрыАмортизацииОСБУ
		|ГДЕ
		|	&ОтражатьВРеглУчете
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности
		|ПОМЕСТИТЬ ПорядокУчетаОС
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОС.СрезПоследних(
		|			&Дата,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И ОсновноеСредство В (&СписокВсехОС)) КАК ПорядокУчетаОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПорядокУчетаОСУУ.ОсновноеСредство КАК ОсновноеСредство,
		|	ПорядокУчетаОСУУ.Состояние КАК Состояние
		|ПОМЕСТИТЬ ПорядокУчетаОСУУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
		|			&Дата,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И (ОсновноеСредство, Организация) В
		|					(ВЫБРАТЬ
		|						МестонахождениеОС.ОсновноеСредство,
		|						МестонахождениеОС.Организация
		|					ИЗ
		|						МестонахождениеОС)) КАК ПорядокУчетаОСУУ
		|ГДЕ
		|	&ОтражатьВУпрУчете
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПорядокУчетаОСБУ.ОсновноеСредство КАК ОсновноеСредство,
		|	ПорядокУчетаОСБУ.СостояниеБУ КАК СостояниеБУ
		|ПОМЕСТИТЬ ПорядокУчетаОСБУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
		|			&Дата,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И (ОсновноеСредство, Организация) В
		|					(ВЫБРАТЬ
		|						МестонахождениеОС.ОсновноеСредство,
		|						МестонахождениеОС.Организация
		|					ИЗ
		|						МестонахождениеОС)) КАК ПорядокУчетаОСБУ
		|ГДЕ
		|	&ОтражатьВРеглУчете
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство,
		|	АрендованныеОС.Договор КАК Договор
		|ПОМЕСТИТЬ АрендованныеОС
		|ИЗ
		|	РегистрСведений.АрендованныеОС.СрезПоследних(
		|			&Дата,
		|			Регистратор <> &Ссылка
		|				И ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						МестонахождениеОС.ОсновноеСредство
		|					ИЗ
		|						МестонахождениеОС)) КАК АрендованныеОС
		|ГДЕ
		|	АрендованныеОС.Состояние В (
		|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ЗаключенДоговорАренды),
		|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВАренде))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|			ТОГДА ЕСТЬNULL(ПорядокУчетаПервыйОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) 
		|		ИНАЧЕ ЕСТЬNULL(ПорядокУчетаНовыйОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) 
		|	КОНЕЦ КАК НаправлениеДеятельности,
		|
		|	ЕСТЬNULL(ПервоеОС.Представление, """") КАК ПервоеОСПредставление,
		|
		|	ВЫБОР
		|		КОГДА &Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|			ТОГДА &Подразделение
		|		ИНАЧЕ ЕСТЬNULL(МестонахождениеОС.Местонахождение, НЕОПРЕДЕЛЕНО) 
		|	КОНЕЦ КАК Местонахождение,
		|
		|	ВЫБОР
		|		КОГДА &Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ТОГДА &Организация
		|		ИНАЧЕ ЕСТЬNULL(МестонахождениеОС.Организация, НЕОПРЕДЕЛЕНО) 
		|	КОНЕЦ КАК Организация,
		|
		|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСУУ.СрокИспользования, 0) КАК СрокИспользованияУУ,
		|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСБУ.СрокПолезногоИспользованияБУ, 0) КАК СрокИспользованияБУ,
		|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСУУ.МетодНачисленияАмортизации, 0) КАК МетодНачисленияАмортизацииУУ,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ, НЕОПРЕДЕЛЕНО) КАК МетодНачисленияАмортизацииБУ,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.ПорядокУчетаУУ, НЕОПРЕДЕЛЕНО) КАК ПорядокУчетаУУ,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.ПорядокУчетаБУ, НЕОПРЕДЕЛЕНО) КАК ПорядокУчетаБУ,
		|
		|	ВЫБОР
		|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|			ТОГДА ЕСТЬNULL(ПорядокУчетаОСБУПервогоОС.СостояниеБУ, НЕОПРЕДЕЛЕНО) 
		|		ИНАЧЕ ЕСТЬNULL(ПорядокУчетаОСБУНовыйОС.СостояниеБУ, НЕОПРЕДЕЛЕНО) 
		|	КОНЕЦ КАК СостояниеБУ,
		|
		|	ВЫБОР
		|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|			ТОГДА ЕСТЬNULL(ПорядокУчетаОСУУПервогоОС.Состояние, НЕОПРЕДЕЛЕНО) 
		|		ИНАЧЕ ЕСТЬNULL(ПорядокУчетаОСУУНовыйОС.Состояние, НЕОПРЕДЕЛЕНО) 
		|	КОНЕЦ КАК СостояниеУУ
		|
		|ПОМЕСТИТЬ ЗначенияПараметров
		|ИЗ
		|	Справочник.ОбъектыЭксплуатации КАК НовыйОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ПервоеОС
		|		ПО (ПервоеОС.Ссылка = &ПервоеОС)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииПервогоОСУУ
		|		ПО (ПараметрыАмортизацииПервогоОСУУ.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииПервогоОСБУ
		|		ПО (ПараметрыАмортизацииПервогоОСБУ.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО (ПервоначальныеСведенияОС.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
		|		ПО (МестонахождениеОС.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаНовыйОС
		|		ПО (ПорядокУчетаНовыйОС.ОсновноеСредство = НовыйОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаПервыйОС
		|		ПО (ПорядокУчетаПервыйОС.ОсновноеСредство = &ПервоеОС)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУПервогоОС
		|		ПО (ПорядокУчетаОСБУПервогоОС.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУПервогоОС
		|		ПО (ПорядокУчетаОСУУПервогоОС.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУНовыйОС
		|		ПО (ПорядокУчетаОСБУНовыйОС.ОсновноеСредство = НовыйОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУНовыйОС
		|		ПО (ПорядокУчетаОСУУНовыйОС.ОсновноеСредство = НовыйОС.Ссылка)
		|
		|ГДЕ
		|	НовыйОС.Ссылка = &ОсновноеСредство
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ПорядокУчетаПервоеОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|	ПервоеОС.Представление,
		|
		|	ВЫБОР
		|		КОГДА &Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|			ТОГДА &Подразделение
		|		ИНАЧЕ ЕСТЬNULL(МестонахождениеОС.Местонахождение, НЕОПРЕДЕЛЕНО) 
		|	КОНЕЦ,
		|
		|	ВЫБОР
		|		КОГДА &Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ТОГДА &Организация
		|		ИНАЧЕ ЕСТЬNULL(МестонахождениеОС.Организация, НЕОПРЕДЕЛЕНО) 
		|	КОНЕЦ,
		|
		|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСУУ.СрокИспользования, 0),
		|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСБУ.СрокПолезногоИспользованияБУ, 0),
		|	ЕСТЬNULL(ПараметрыАмортизацииПервогоОСУУ.МетодНачисленияАмортизации, НЕОПРЕДЕЛЕНО),
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ, НЕОПРЕДЕЛЕНО),
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.ПорядокУчетаУУ, НЕОПРЕДЕЛЕНО),
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.ПорядокУчетаБУ, НЕОПРЕДЕЛЕНО),
		|	ЕСТЬNULL(ПорядокУчетаОСБУПервогоОСБУ.СостояниеБУ, НЕОПРЕДЕЛЕНО),
		|	ЕСТЬNULL(ПорядокУчетаОСБУПервогоОСУУ.Состояние, НЕОПРЕДЕЛЕНО)
		|
		|ИЗ
		|	Справочник.ОбъектыЭксплуатации КАК ПервоеОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииПервогоОСУУ
		|		ПО (ПараметрыАмортизацииПервогоОСУУ.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииПервогоОСБУ
		|		ПО (ПараметрыАмортизацииПервогоОСБУ.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО (ПервоначальныеСведенияОС.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
		|		ПО (МестонахождениеОС.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаПервоеОС
		|		ПО (ПорядокУчетаПервоеОС.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУПервогоОСБУ
		|		ПО (ПорядокУчетаОСБУПервогоОСБУ.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ КАК ПорядокУчетаОСБУПервогоОСУУ
		|		ПО (ПорядокУчетаОСБУПервогоОСУУ.ОсновноеСредство = ПервоеОС.Ссылка)
		|
		|ГДЕ
		|	ПервоеОС.Ссылка = &ПервоеОС
		|	И &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УзлыКомпоненты.УзелКомпонент,
		|	УзлыКомпоненты.ОсновноеСредство
		|ПОМЕСТИТЬ УзлыКомпонентыВсехОС
		|ИЗ
		|	РегистрСведений.УзлыКомпонентыАмортизации.СрезПоследних(&Дата, УзелКомпонент В(&СписокВсехОС)) КАК УзлыКомпоненты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УзлыКомпонентыТаблицы.УзелКомпонент КАК УзелКомпонент,
		|	ИСТИНА КАК ОСВладелецНеСовпадает
		|ПОМЕСТИТЬ НесоответствияУзловКомпонентов
		|ИЗ
		|	УзлыКомпонентыВсехОС КАК УзлыКомпонентыШапки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ УзлыКомпонентыВсехОС КАК УзлыКомпонентыТаблицы
		|		ПО УзлыКомпонентыШапки.ОсновноеСредство <> УзлыКомпонентыТаблицы.ОсновноеСредство
		|ГДЕ
		|	УзлыКомпонентыШапки.УзелКомпонент = &ОсновноеСредство
		|	И УзлыКомпонентыТаблицы.УзелКомпонент В (&СписокОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УзлыКомпонентыТаблицы.УзелКомпонент КАК УзелКомпонент,
		|	ИСТИНА КАК ОСВладелецНеСовпадает
		|ИЗ
		|	УзлыКомпонентыВсехОС КАК УзлыКомпонентыТаблицы
		|ГДЕ
		|	УзлыКомпонентыТаблицы.УзелКомпонент В (&СписокОС)
		|	И УзлыКомпонентыТаблицы.ОсновноеСредство <> &ОсновноеСредство
		|	И ВЫРАЗИТЬ(&ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации).ТипОС В
		|	(ЗНАЧЕНИЕ(Перечисление.ТипыОС.ОбъектЭксплуатации), ЗНАЧЕНИЕ(Перечисление.ТипыОС.ГрупповоеОС))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗначенияПараметров.ПервоеОСПредставление КАК ПервоеОСПредставление,
		|	ЗначенияПараметров.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ЗначенияПараметров.СрокИспользованияУУ КАК СрокИспользованияУУ,
		|	ЗначенияПараметров.СрокИспользованияБУ КАК СрокИспользованияБУ,
		|	ЗначенияПараметров.МетодНачисленияАмортизацииУУ КАК МетодНачисленияАмортизацииУУ,
		|	ЗначенияПараметров.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
		|	ЗначенияПараметров.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
		|	ЗначенияПараметров.ПорядокУчетаУУ КАК ПорядокУчетаУУ
		|ИЗ
		|	ЗначенияПараметров КАК ЗначенияПараметров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РезультатПроверки.ОсновноеСредство КАК ОсновноеСредство,
		|	РезультатПроверки.ОсновноеСредствоПредставление КАК ОсновноеСредствоПредставление,
		|	РезультатПроверки.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	РезультатПроверки.НаправлениеДеятельности.Представление КАК НаправлениеДеятельностиПредставление,
		|	РезультатПроверки.СрокИспользованияУУ КАК СрокИспользованияУУ,
		|
		|	РезультатПроверки.ОтличаетсяНаправлениеДеятельности КАК ОтличаетсяНаправлениеДеятельности,
		|	РезультатПроверки.ОтличаетсяМестонахождение КАК ОтличаетсяМестонахождение,
		|	РезультатПроверки.ОтличаетсяСрокИспользованияУУ КАК ОтличаетсяСрокИспользованияУУ,
		|	РезультатПроверки.ОтличаетсяСрокИспользованияБУ КАК ОтличаетсяСрокИспользованияБУ,
		|	РезультатПроверки.ОСВладелецНеСовпадает КАК ОСВладелецНеСовпадает,
		|	РезультатПроверки.ПоДоговоруЛизинга КАК ПоДоговоруЛизинга,
		|	РезультатПроверки.НеПринятоУУ КАК НеПринятоУУ,
		|	РезультатПроверки.НеПринятоБУ КАК НеПринятоБУ,
		|	РезультатПроверки.ПринятоУУ КАК ПринятоУУ,
		|	РезультатПроверки.ПринятоБУ КАК ПринятоБУ
		|
		|ИЗ
		|	(ВЫБРАТЬ
		|		ИсходныеОС.Ссылка КАК ОсновноеСредство,
		|		ИсходныеОС.Представление КАК ОсновноеСредствоПредставление,
		|		ЗначенияПараметров.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		ЗначенияПараметров.СрокИспользованияУУ КАК СрокИспользованияУУ,
		|
		|		(&ОбъединениеОднотипныхОС
		|				И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрисоединениеОС)
		|				И ЕСТЬNULL(ПорядокУчетаИсходныеОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) <> ЗначенияПараметров.НаправлениеДеятельности
		|			ИЛИ НЕ &ОбъединениеОднотипныхОС
		|				И ЕСТЬNULL(ПорядокУчетаИсходныеОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) <> ЗначенияПараметров.НаправлениеДеятельности
		|				И ЕСТЬNULL(ПорядокУчетаИсходныеОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК ОтличаетсяНаправлениеДеятельности,
		|
		|		ЕСТЬNULL(МестонахождениеОС.Организация, НЕОПРЕДЕЛЕНО) <> ЗначенияПараметров.Организация
		|			ИЛИ ЕСТЬNULL(МестонахождениеОС.Местонахождение, НЕОПРЕДЕЛЕНО) <> ЗначенияПараметров.Местонахождение КАК ОтличаетсяМестонахождение,
		|
		|		&ОтражатьВУпрУчете
		|			И ЕСТЬNULL(ПараметрыАмортизацииОСУУ.СрокИспользования, 0) <> ЗначенияПараметров.СрокИспользованияУУ
		|			И НЕ ИсходныеОС.ТипОС В (&ТипыУзловКомпонентов) КАК ОтличаетсяСрокИспользованияУУ,
		|
		|		&ОтражатьВРеглУчете
		|			И ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ, 0) <> ЗначенияПараметров.СрокИспользованияБУ
		|			И НЕ ИсходныеОС.ТипОС В (&ТипыУзловКомпонентов) КАК ОтличаетсяСрокИспользованияБУ,
		|
		|		ЕСТЬNULL(НесоответствияУзловКомпонентов.ОСВладелецНеСовпадает, ЛОЖЬ) КАК ОСВладелецНеСовпадает,
		|
		|		ВЫБОР
		|			КОГДА АрендованныеОС.Договор ССЫЛКА Справочник.ДоговорыАренды
		|					И ЕСТЬNULL(АрендованныеОС.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК ПоДоговоруЛизинга,
		|
		|		&ОтражатьВУпрУчете
		|			И ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК НеПринятоУУ,
		|
		|		&ОтражатьВРеглУчете
		|			И ЗначенияПараметров.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
		|			И ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеБУ, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК НеПринятоБУ,
		|
		|		&ОтражатьВУпрУчете
		|			И ЗначенияПараметров.СостояниеУУ <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
		|			И ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК ПринятоУУ,
		|
		|		&ОтражатьВРеглУчете
		|			И ЗначенияПараметров.СостояниеБУ <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
		|			И ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеБУ, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК ПринятоБУ
		|
		|	ИЗ
		|		Справочник.ОбъектыЭксплуатации КАК ИсходныеОС
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ ЗначенияПараметров КАК ЗначенияПараметров
		|			ПО (ИСТИНА)
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
		|			ПО (ПараметрыАмортизацииОСБУ.ОсновноеСредство = ИсходныеОС.Ссылка)
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
		|			ПО (ПараметрыАмортизацииОСУУ.ОсновноеСредство = ИсходныеОС.Ссылка)
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
		|			ПО (ПорядокУчетаОСБУ.ОсновноеСредство = ИсходныеОС.Ссылка)
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУ
		|			ПО (ПорядокУчетаОСУУ.ОсновноеСредство = ИсходныеОС.Ссылка)
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
		|			ПО (МестонахождениеОС.ОсновноеСредство = ИсходныеОС.Ссылка)
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаИсходныеОС
		|			ПО (ПорядокУчетаИсходныеОС.ОсновноеСредство = ИсходныеОС.Ссылка)
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ АрендованныеОС КАК АрендованныеОС
		|			ПО (АрендованныеОС.ОсновноеСредство = ИсходныеОС.Ссылка)
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ НесоответствияУзловКомпонентов КАК НесоответствияУзловКомпонентов
		|			ПО (НесоответствияУзловКомпонентов.УзелКомпонент = ИсходныеОС.Ссылка)
		|	ГДЕ
		|		ИсходныеОС.Ссылка В(&СписокОС)
		|
		|	) КАК РезультатПроверки
		|
		|ГДЕ
		|	(РезультатПроверки.ОтличаетсяНаправлениеДеятельности
		|		ИЛИ РезультатПроверки.ОтличаетсяМестонахождение
		|		ИЛИ РезультатПроверки.ОтличаетсяСрокИспользованияУУ
		|		ИЛИ РезультатПроверки.ОтличаетсяСрокИспользованияБУ
		|		ИЛИ РезультатПроверки.ОСВладелецНеСовпадает
		|		ИЛИ РезультатПроверки.ПоДоговоруЛизинга
		|		ИЛИ РезультатПроверки.НеПринятоУУ
		|		ИЛИ РезультатПроверки.НеПринятоБУ
		|		ИЛИ РезультатПроверки.ПринятоУУ
		|		ИЛИ РезультатПроверки.ПринятоБУ)";
		
	КонецЕсли; 
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Дата", ?(ПараметрыПроцедуры.Дата <> '000101010000', ПараметрыПроцедуры.Дата, ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("СписокОС", ПараметрыПроцедуры.СписокОС);
	Запрос.УстановитьПараметр("Организация", ПараметрыПроцедуры.Организация);
	Запрос.УстановитьПараметр("Подразделение", ПараметрыПроцедуры.Местонахождение);
	Запрос.УстановитьПараметр("ОбъединениеОднотипныхОС", ПараметрыПроцедуры.ОбъединениеОднотипныхОС);
	Запрос.УстановитьПараметр("Ссылка", ПараметрыПроцедуры.Ссылка);
	Запрос.УстановитьПараметр("ПервоеОС", ПараметрыВыбораОсновныхСредств.ПервоеОС);
	Запрос.УстановитьПараметр("ОсновноеСредство", ПараметрыПроцедуры.ОсновноеСредство);
	Запрос.УстановитьПараметр("ОтражатьВУпрУчете", ПараметрыПроцедуры.ОтражатьВУпрУчете);
	Запрос.УстановитьПараметр("ОтражатьВРеглУчете", ПараметрыПроцедуры.ОтражатьВРеглУчете);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", ПараметрыПроцедуры.ХозяйственнаяОперация);
	
	МассивТиповОС = Новый Массив;
	МассивТиповОС.Добавить(Перечисления.ТипыОС.Узел);
	МассивТиповОС.Добавить(Перечисления.ТипыОС.КомпонентАмортизации);
	Запрос.УстановитьПараметр("ТипыУзловКомпонентов", МассивТиповОС);
	
	СписокВсехОС = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ПараметрыПроцедуры.СписокОС);
	Если ЗначениеЗаполнено(ПараметрыПроцедуры.ОсновноеСредство) Тогда
		СписокВсехОС.Добавить(ПараметрыПроцедуры.ОсновноеСредство);
	КонецЕсли; 
	Запрос.УстановитьПараметр("СписокВсехОС", СписокВсехОС);
	
	Результат = Запрос.ВыполнитьПакет();
	ВыборкаПараметры = Результат[Результат.ВГраница()-1].Выбрать();
	ВыборкаПараметры.Следующий();
	
	ПроблемныеОС = ?(НЕ Результат[Результат.ВГраница()].Пустой(), Результат[Результат.ВГраница()].Выгрузить(), Неопределено);
	ПараметрыВыбораОсновныхСредств.Вставить("ПроблемныеОС", ПроблемныеОС);
	
	ПараметрыВыбораОсновныхСредств.Вставить("ПервоеОСПредставление", ВыборкаПараметры.ПервоеОСПредставление);
	
	ПоНаработкеУУ = (ВыборкаПараметры.МетодНачисленияАмортизацииУУ = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции);
	ПараметрыВыбораОсновныхСредств.Вставить("ПоНаработкеУУ", ПоНаработкеУУ);
	
	ДоступныПараметрыАмортизацииУУ = (ВыборкаПараметры.ПорядокУчетаУУ = Перечисления.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию);
	ПараметрыВыбораОсновныхСредств.Вставить("ДоступныПараметрыАмортизацииУУ", ДоступныПараметрыАмортизацииУУ);
	
	ДоступныПараметрыАмортизацииБУ = (ВыборкаПараметры.ПорядокУчетаБУ = Перечисления.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации);
	ПараметрыВыбораОсновныхСредств.Вставить("ДоступныПараметрыАмортизацииБУ", ДоступныПараметрыАмортизацииБУ);
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("НаправлениеДеятельности", ВыборкаПараметры.НаправлениеДеятельности);
	ЗначенияПараметров.Вставить("СрокИспользованияУУ", ВыборкаПараметры.СрокИспользованияУУ);
	ЗначенияПараметров.Вставить("СрокИспользованияБУ", ВыборкаПараметры.СрокИспользованияБУ);
	ЗначенияПараметров.Вставить("ПорядокУчетаБУ", ВыборкаПараметры.ПорядокУчетаБУ);
	ПараметрыВыбораОсновныхСредств.Вставить("ЗначенияПараметров", ЗначенияПараметров);
	
	ОбъединениеОСЛокализация.ДополнитьПараметрыВыбораОсновныхСредств(ВыборкаПараметры, ПараметрыВыбораОсновныхСредств);
	
	Возврат ПараметрыВыбораОсновныхСредств;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ПолноеИмяОбъекта()

	Возврат "Документ.ОбъединениеОС";

КонецФункции

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = ПолноеИмяОбъекта();
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров,
										ТекстыЗапросаВременныхТаблиц);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.АдресМестонахождения КАК АдресМестонахождения,
	|	ДанныеДокумента.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.КоэффициентУскоренияУУ КАК КоэффициентУскоренияУУ,
	|	ДанныеДокумента.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	ДанныеДокумента.ЛиквидационнаяСтоимостьРегл КАК ЛиквидационнаяСтоимостьРегл,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.МетодНачисленияАмортизацииУУ КАК МетодНачисленияАмортизацииУУ,
	|	ДанныеДокумента.МОЛ КАК МОЛ,
	|	ДанныеДокумента.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ДанныеДокумента.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
	|	ДанныеДокумента.ОбъединениеОднотипныхОС КАК ОбъединениеОднотипныхОС,
	|	ДанныеДокумента.ОбъемНаработки КАК ОбъемНаработки,
	|	ДанныеДокумента.ОбъемНаработкиОстаточный КАК ОбъемНаработкиОстаточный,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ОсновноеСредство КАК ОсновноеСредство,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ПоказательНаработки КАК ПоказательНаработки,
	|	ДанныеДокумента.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ДанныеДокумента.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	ДанныеДокумента.СобытиеОС КАК СобытиеОС,
	|	ДанныеДокумента.СрокИспользованияОстаточныйБУ КАК СрокИспользованияОстаточныйБУ,
	|	ДанныеДокумента.СрокИспользованияОстаточныйУУ КАК СрокИспользованияОстаточныйУУ,
	|	ДанныеДокумента.СрокИспользованияБУ КАК СрокИспользованияБУ,
	|	ДанныеДокумента.СрокИспользованияУУ КАК СрокИспользованияУУ,
	|	ДанныеДокумента.СтатьяРасходовБУ КАК СтатьяРасходовБУ,
	|	ДанныеДокумента.АналитикаРасходовБУ КАК АналитикаРасходовБУ,
	|	ДанныеДокумента.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
	|	ВЫБОР КОГДА ДанныеДокумента.СтатьяРасходовУУ = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			И ДанныеДокумента.ОтражатьВУпрУчете
	|		ТОГДА ДанныеДокумента.СтатьяРасходовБУ
	|		ИНАЧЕ ДанныеДокумента.СтатьяРасходовУУ 
	|	КОНЕЦ КАК СтатьяРасходовУУ,
	|	ВЫБОР КОГДА ДанныеДокумента.АналитикаРасходовУУ = НЕОПРЕДЕЛЕНО
	|			И ДанныеДокумента.ОтражатьВУпрУчете
	|		ТОГДА ДанныеДокумента.АналитикаРасходовБУ
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходовУУ 
	|	КОНЕЦ КАК АналитикаРасходовУУ,
	|	ДанныеДокумента.КоэффициентУскоренияБУ КАК КоэффициентУскоренияБУ,
	|	ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ) КАК НачислятьИзнос,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ЕГДС КАК ЕГДС,
	|	ТаблицаОС.ОсновноеСредство КАК ПервоеОС,
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровАмортизацииОС) КАК НастройкаХозяйственнойОперацииАмортизация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияОС) КАК НастройкаХозяйственнойОперацииОбесценения,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВосстановлениеОбесцененияОС) КАК НастройкаХозяйственнойОперацииВосстановлениеОбесценения,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиОС) КАК НастройкаХозяйственнойОперацииСтоимость,
	|	&ОбъединениеОС_РеквизитыДокумента
	|ИЗ
	|	Документ.ОбъединениеОС КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|			И ТаблицаОС.НомерСтроки = 1
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ОбъединениеОСЛокализация.ДобавитьВТекстЗапросаРеквизитыДокумента(Запрос.Текст, "ДанныеДокумента");
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяОбъекта()));
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_ОС", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства);
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Объединение ОС'"));

	Если Реквизиты <> Неопределено Тогда
		
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
		
		ЗначенияПараметровПроведения.Вставить(
			"РеглУчетВНАВедетсяНезависимо", 
			НастройкиНалоговУчетныхПолитикПовтИсп.РеглУчетВНАВедетсяНезависимо(Реквизиты.Организация, Реквизиты.Период));
			
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&НаправлениеДеятельности                КАК НаправлениеДеятельности,
	|	&Подразделение                          КАК Подразделение,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	ЛОЖЬ                                    КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                            КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                            КАК ИсправляемыйДокумент,
	|	&Период                                 КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК Приоритет";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	0                                       КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&СобытиеОС                              КАК СобытиеОС,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Проведен                               КАК Проведен,
	|	&ОтражатьВРеглУчете                     КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                      КАК ОтражатьВУпрУчете,
	|	&ОсновноеСредство                       КАК ОсновноеСредство
	|ИЗ
	|	Документ.ОбъединениеОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ОсновноеСредство <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ОсновноеСредство <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|			ТОГДА ТаблицаОС.НомерСтроки
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)    
	|	КОНЕЦ                                   КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&СобытиеОС                              КАК СобытиеОС,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Проведен                               КАК Проведен,
	|	&ОтражатьВРеглУчете                     КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                      КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство
	|ИЗ
	|	Документ.ОбъединениеОС КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И (&ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|			ИЛИ НЕ ТаблицаОС.Ссылка ЕСТЬ NULL)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаМестонахождениеОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МестонахождениеОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                       КАК Период,
	|	&Организация                  КАК Организация,
	|	&ОсновноеСредство             КАК ОсновноеСредство,
	|	&МОЛ                          КАК МОЛ,
	|	&Подразделение                КАК Местонахождение,
	|	&АдресМестонахождения         КАК АдресМестонахождения,
	|	&ОтражатьВРеглУчете           КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете            КАК ОтражатьВУпрУчете
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&Организация                            КАК Организация,
	|	&ОсновноеСредство                       КАК ОсновноеСредство,
	|	&НаправлениеДеятельности                КАК НаправлениеДеятельности,
	|	&ГруппаФинансовогоУчета                 КАК ГруппаФинансовогоУчета,
	|	&ПоказательНаработки                    КАК ПоказательНаработки,
	|	&ОбъемНаработки                         КАК ОбъемНаработки,
	|	&ОтражатьВРеглУчете                     КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                      КАК ОтражатьВУпрУчете
	|ГДЕ
	|	НЕ &ОбъединениеОднотипныхОС
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&Организация                            КАК Организация,
	|	&ОсновноеСредство                       КАК ОсновноеСредство,
	|	ПорядокУчетаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.ПоказательНаработки      КАК ПоказательНаработки,
	|	ПорядокУчетаОС.ОбъемНаработки           КАК ОбъемНаработки,
	|	&ОтражатьВРеглУчете                     КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                      КАК ОтражатьВУпрУчете
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И ОсновноеСредство = &ПервоеОС) КАК ПорядокУчетаОС
	|ГДЕ
	|	&ОбъединениеОднотипныхОС
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса, "Документ.ОбъединениеОС");
	
	ТекстЗапроса = ОбъединениеОСЛокализация.ТекстЗапросаТаблицаПорядокУчетаОСБУ();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Период                                 КАК Период,
		|	&Организация                            КАК Организация,
		|	&ОсновноеСредство                       КАК ОсновноеСредство,
		|
		|	ДанныеРегистра.СостояниеБУ              КАК СостояниеБУ,
		|
		|	&НачислятьАмортизациюБУ                 КАК НачислятьАмортизациюБУ,
		|	
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
		|			ТОГДА &СтатьяРасходовБУ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СтатьяРасходовБУ,
		|	
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
		|			ТОГДА &АналитикаРасходовБУ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК АналитикаРасходовБУ,
		|
		|	НЕОПРЕДЕЛЕНО КАК СпособОтраженияРасходовБУ
		|ИЗ
		|	Документ.ОбъединениеОС КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ПервоеОС) КАК ДанныеРегистра
		|		ПО ДанныеРегистра.ОсновноеСредство = &ПервоеОС
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И &РеглУчетВНАВедетсяНезависимо
		|	И НЕ &ОбъединениеОднотипныхОС
		|	И &ОтражатьВРеглУчете
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период                                               КАК Период,
		|	&Организация                                          КАК Организация,
		|	&ОсновноеСредство                                     КАК ОсновноеСредство,
		|	ДанныеРегистра.СостояниеБУ                            КАК СостояниеБУ,
		|	ДанныеРегистра.НачислятьАмортизациюБУ                 КАК НачислятьАмортизациюБУ,
		|	ДанныеРегистра.СтатьяРасходовБУ                       КАК СтатьяРасходовБУ,
		|	ДанныеРегистра.АналитикаРасходовБУ                    КАК АналитикаРасходовБУ,
		|	ДанныеРегистра.СпособОтраженияРасходовБУ              КАК СпособОтраженияРасходовБУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ПервоеОС) КАК ДанныеРегистра
		|ГДЕ
		|	&ОбъединениеОднотипныхОС
		|	И &ОтражатьВРеглУчете
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Списание исходных ОС
		|ВЫБРАТЬ
		|	&Период                                               КАК Период,
		|	&Организация                                          КАК Организация,
		|	ДанныеРегистра.ОсновноеСредство                       КАК ОсновноеСредство,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)        КАК СостояниеБУ,
		|	ЛОЖЬ                                                  КАК НачислятьАмортизациюБУ,
		|	НЕОПРЕДЕЛЕНО                                          КАК СтатьяРасходовБУ,
		|	НЕОПРЕДЕЛЕНО                                          КАК АналитикаРасходовБУ,
		|	НЕОПРЕДЕЛЕНО                                          КАК СпособОтраженияРасходовБУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство В (ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ ВтСписокОС КАК СписокОС)
		|				И ОсновноеСредство <> &ОсновноеСредство) КАК ДанныеРегистра
		|ГДЕ
		|	&ОтражатьВРеглУчете
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период                                 КАК Период,
		|	&Организация                            КАК Организация,
		|	&ОсновноеСредство                       КАК ОсновноеСредство,
		|
		|	ДанныеРегистра.СостояниеБУ              КАК СостояниеБУ,
		|
		|	&НачислятьАмортизациюУУ                 КАК НачислятьАмортизациюБУ,
		|	&СтатьяРасходовУУ                       КАК СтатьяРасходовБУ,
		|	&АналитикаРасходовУУ                    КАК АналитикаРасходовБУ,
		|	НЕОПРЕДЕЛЕНО КАК СпособОтраженияРасходовБУ
		|
		|ИЗ
		|	Документ.ОбъединениеОС КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ПервоеОС) КАК ДанныеРегистра
		|		ПО ДанныеРегистра.ОсновноеСредство = &ПервоеОС
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И НЕ &РеглУчетВНАВедетсяНезависимо
		|	И НЕ &ОбъединениеОднотипныхОС
		|	И &ОтражатьВРеглУчете
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|";
		
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                                 КАК Период,
	|	&Организация                                            КАК Организация,
	|	&ОсновноеСредство                                       КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)        КАК Состояние,
	|	&НачислятьАмортизациюУУ                                 КАК НачислятьАмортизациюУУ,
	|	НЕОПРЕДЕЛЕНО                                            КАК СпособОтраженияРасходов,
	|	&СтатьяРасходовУУ                                       КАК СтатьяРасходов,
	|	&АналитикаРасходовУУ                                    КАК АналитикаРасходов,
	|	&ПередаватьРасходыВДругуюОрганизацию                    КАК ПередаватьРасходыВДругуюОрганизацию,
	|	&ОрганизацияПолучательРасходов                          КАК ОрганизацияПолучательРасходов
	|ГДЕ
	|	НЕ &ОбъединениеОднотипныхОС
	|	И &ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                 КАК Период,
	|	&Организация                                            КАК Организация,
	|	&ОсновноеСредство                                       КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)        КАК Состояние,
	|	ПорядокУчетаОСУУ.НачислятьАмортизациюУУ                 КАК НачислятьАмортизациюУУ,
	|	ПорядокУчетаОСУУ.СпособОтраженияРасходов                КАК СпособОтраженияРасходов,
	|	ПорядокУчетаОСУУ.СтатьяРасходов                         КАК СтатьяРасходов,
	|	ПорядокУчетаОСУУ.АналитикаРасходов                      КАК АналитикаРасходов,
	|	ПорядокУчетаОСУУ.ПередаватьРасходыВДругуюОрганизацию    КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчетаОСУУ.ОрганизацияПолучательРасходов          КАК ОрганизацияПолучательРасходов
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И Организация = &Организация
	|				И ОсновноеСредство = &ПервоеОС) КАК ПорядокУчетаОСУУ
	|ГДЕ
	|	&ОбъединениеОднотипныхОС
	|	И &ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|
	// Списание исходных ОС.
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                 КАК Период,
	|	&Организация                                            КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                              КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)          КАК Состояние,
	|	ЛОЖЬ                                                    КАК НачислятьАмортизациюУУ,
	|	НЕОПРЕДЕЛЕНО                                            КАК СпособОтраженияРасходов,
	|	НЕОПРЕДЕЛЕНО                                            КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                            КАК АналитикаРасходов,
	|	ЛОЖЬ                                                    КАК ПередаватьРасходыВДругуюОрганизацию,
	|	НЕОПРЕДЕЛЕНО                                            КАК ОрганизацияПолучательРасходов
	|ИЗ
	|	Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяТаблицаСтоимостьВНА(ТекстыЗапроса, Запрос);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяТаблицаАмортизацияВНА(ТекстыЗапроса, Запрос);
	Документы.ОбъединениеОС.ТекстЗапросаВтСтоимостьИсходныхОС(Запрос, ТекстыЗапроса);
	Документы.ОбъединениеОС.ТекстЗапросаВтАмортизацияИсходныхОС(Запрос, ТекстыЗапроса);
	Документы.ОбъединениеОС.ТекстЗапросаВтНаработкиОбъектовЭксплуатации(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = ОбъединениеОСЛокализация.ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Период                         КАК Период,
		|	&Период                         КАК ДатаПоследнегоИзменения,
		|	&Организация                    КАК Организация,
		|	&ОсновноеСредство               КАК ОсновноеСредство,
		|	&СрокИспользованияБУ            КАК СрокПолезногоИспользованияБУ,
		|	&КоэффициентУскоренияБУ         КАК КоэффициентУскорения,
		|	&ЛиквидационнаяСтоимостьРегл    КАК ЛиквидационнаяСтоимость,
		|	&ОбъемНаработкиОстаточный       КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	&СрокИспользованияОстаточныйБУ  КАК СрокИспользованияДляВычисленияАмортизации,
		|
		|	ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0) 
		|		+ ЕСТЬNULL(СтоимостьВНА.СтоимостьЦФ, 0)
		|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияРегл, 0)
		|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияЦФ, 0) КАК СтоимостьДляВычисленияАмортизации
		|
		|ИЗ
		|	Документ.ОбъединениеОС КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
		|		ПО ИСТИНА
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияИсходныхОС КАК АмортизацияВНА
		|		ПО ИСТИНА
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И &РеглУчетВНАВедетсяНезависимо
		|	И НЕ &ОбъединениеОднотипныхОС
		|	И &ОтражатьВРеглУчете
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период                                       КАК Период,
		|	&Период                                       КАК ДатаПоследнегоИзменения,
		|	&Организация                                  КАК Организация,
		|	&ОсновноеСредство                             КАК ОсновноеСредство,
		|	ДанныеРегистра.СрокПолезногоИспользованияБУ   КАК СрокПолезногоИспользованияБУ,
		|	ДанныеРегистра.КоэффициентУскорения           КАК КоэффициентУскорения,
		|	ДанныеРегистра.ЛиквидационнаяСтоимость        КАК ЛиквидационнаяСтоимость,
		|	&ОбъемНаработкиОстаточный                     КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	&СрокИспользованияОстаточныйБУ                КАК СрокИспользованияДляВычисленияАмортизации,
		|
		|	ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0) 
		|		+ ЕСТЬNULL(СтоимостьВНА.СтоимостьЦФ, 0)
		|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияРегл, 0)
		|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияЦФ, 0) КАК СтоимостьДляВычисленияАмортизации
		|
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ПервоеОС) КАК ДанныеРегистра
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
		|		ПО ИСТИНА
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияИсходныхОС КАК АмортизацияВНА
		|		ПО ИСТИНА
		|ГДЕ
		|	&ОбъединениеОднотипныхОС
		|	И &РеглУчетВНАВедетсяНезависимо
		|	И &ОтражатьВРеглУчете
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период                                       КАК Период,
		|	&Период                                       КАК ДатаПоследнегоИзменения,
		|	&Организация                                  КАК Организация,
		|	&ОсновноеСредство                             КАК ОсновноеСредство,
		|	ДанныеРегистра.СрокПолезногоИспользованияБУ   КАК СрокПолезногоИспользованияБУ,
		|	ДанныеРегистра.КоэффициентУскорения           КАК КоэффициентУскорения,
		|	ДанныеРегистра.ЛиквидационнаяСтоимость        КАК ЛиквидационнаяСтоимость,
		|
		|	ДанныеРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации
		|		- ЕСТЬNULL(НаработкиОбъектовЭксплуатации.Значение, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|
		|	ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации
		|		- РАЗНОСТЬДАТ(ЕСТЬNULL(ДанныеРегистра.ДатаПоследнегоИзменения, &Период), &Период, МЕСЯЦ) КАК СрокИспользованияДляВычисленияАмортизации,
		|
		// Для расчета амортизации используется остаточная стоимость
		|	ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0) 
		|		+ ЕСТЬNULL(СтоимостьВНА.СтоимостьЦФ, 0)
		|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияРегл, 0)
		|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияЦФ, 0)
		|		+ ЕСТЬNULL(СтоимостьИсходныхОС.СтоимостьРегл, 0) 
		|		+ ЕСТЬNULL(СтоимостьИсходныхОС.СтоимостьЦФ, 0)
		|		- ЕСТЬNULL(АмортизацияИсходныхОС.АмортизацияРегл, 0)
		|		- ЕСТЬNULL(АмортизацияИсходныхОС.АмортизацияЦФ, 0) КАК СтоимостьДляВычисленияАмортизации
		|
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ОсновноеСредство) КАК ДанныеРегистра
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьВНА
		|		ПО СтоимостьВНА.Ссылка = &Ссылка
		|			И СтоимостьВНА.ОбъектУчета = &ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияВНА
		|		ПО АмортизацияВНА.Ссылка = &Ссылка
		|			И АмортизацияВНА.ОбъектУчета = &ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьИсходныхОС
		|		ПО ИСТИНА
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияИсходныхОС КАК АмортизацияИсходныхОС
		|		ПО ИСТИНА
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНаработкиОбъектовЭксплуатации КАК НаработкиОбъектовЭксплуатации
		|		ПО ИСТИНА
		|ГДЕ
		|	&ОтражатьВРеглУчете
		|	И &РеглУчетВНАВедетсяНезависимо
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрисоединениеОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период                         КАК Период,
		|	&Период                         КАК ДатаПоследнегоИзменения,
		|	&Организация                    КАК Организация,
		|	&ОсновноеСредство               КАК ОсновноеСредство,
		|	&СрокИспользованияУУ            КАК СрокПолезногоИспользованияБУ,
		|	&КоэффициентУскоренияУУ         КАК КоэффициентУскорения,
		|	&ЛиквидационнаяСтоимостьРегл    КАК ЛиквидационнаяСтоимость,
		|	&ОбъемНаработкиОстаточный       КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	&СрокИспользованияОстаточныйУУ  КАК СрокИспользованияДляВычисленияАмортизации,
		|
		|	ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0) 
		|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияРегл, 0) КАК СтоимостьДляВычисленияАмортизации
		|
		|ИЗ
		|	Документ.ОбъединениеОС КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
		|		ПО ИСТИНА
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияИсходныхОС КАК АмортизацияВНА
		|		ПО ИСТИНА
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И НЕ &РеглУчетВНАВедетсяНезависимо
		|	И НЕ &ОбъединениеОднотипныхОС
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период                                       КАК Период,
		|	&Период                                       КАК ДатаПоследнегоИзменения,
		|	&Организация                                  КАК Организация,
		|	&ОсновноеСредство                             КАК ОсновноеСредство,
		|	ДанныеРегистра.СрокПолезногоИспользованияБУ   КАК СрокПолезногоИспользованияБУ,
		|	ДанныеРегистра.КоэффициентУскорения           КАК КоэффициентУскорения,
		|	ДанныеРегистра.ЛиквидационнаяСтоимость        КАК ЛиквидационнаяСтоимость,
		|	&ОбъемНаработкиОстаточный                     КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	&СрокИспользованияОстаточныйУУ                КАК СрокИспользованияДляВычисленияАмортизации,
		|
		|	ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0) 
		|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияРегл, 0) КАК СтоимостьДляВычисленияАмортизации
		|
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ПервоеОС) КАК ДанныеРегистра
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
		|		ПО ИСТИНА
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияИсходныхОС КАК АмортизацияВНА
		|		ПО ИСТИНА
		|ГДЕ
		|	&ОбъединениеОднотипныхОС
		|	И НЕ &РеглУчетВНАВедетсяНезависимо
		|	И &ОтражатьВРеглУчете
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период                                       КАК Период,
		|	&Период                                       КАК ДатаПоследнегоИзменения,
		|	&Организация                                  КАК Организация,
		|	&ОсновноеСредство                             КАК ОсновноеСредство,
		|	ДанныеРегистра.СрокПолезногоИспользованияБУ   КАК СрокПолезногоИспользованияБУ,
		|	ДанныеРегистра.КоэффициентУскорения           КАК КоэффициентУскорения,
		|	ДанныеРегистра.ЛиквидационнаяСтоимость        КАК ЛиквидационнаяСтоимость,
		|
		|	ДанныеРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации
		|		- ЕСТЬNULL(НаработкиОбъектовЭксплуатации.Значение, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|
		|	ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации
		|		- РАЗНОСТЬДАТ(ЕСТЬNULL(ДанныеРегистра.ДатаПоследнегоИзменения, &Период), &Период, МЕСЯЦ) КАК СрокИспользованияДляВычисленияАмортизации,
		|
		// Для расчета амортизации используется остаточная стоимость
		|	ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0) 
		|		+ ЕСТЬNULL(СтоимостьВНА.СтоимостьЦФ, 0)
		|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияРегл, 0)
		|		- ЕСТЬNULL(АмортизацияВНА.АмортизацияЦФ, 0)
		|		+ ЕСТЬNULL(СтоимостьИсходныхОС.СтоимостьРегл, 0) 
		|		+ ЕСТЬNULL(СтоимостьИсходныхОС.СтоимостьЦФ, 0)
		|		- ЕСТЬNULL(АмортизацияИсходныхОС.АмортизацияРегл, 0)
		|		- ЕСТЬNULL(АмортизацияИсходныхОС.АмортизацияЦФ, 0) КАК СтоимостьДляВычисленияАмортизации
		|
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ОсновноеСредство) КАК ДанныеРегистра
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьВНА
		|		ПО СтоимостьВНА.Ссылка = &Ссылка
		|			И СтоимостьВНА.ОбъектУчета = &ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияВНА
		|		ПО АмортизацияВНА.Ссылка = &Ссылка
		|			И АмортизацияВНА.ОбъектУчета = &ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьИсходныхОС
		|		ПО ИСТИНА
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияИсходныхОС КАК АмортизацияИсходныхОС
		|		ПО ИСТИНА
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНаработкиОбъектовЭксплуатации КАК НаработкиОбъектовЭксплуатации
		|		ПО ИСТИНА
		|ГДЕ
		|	&ОтражатьВРеглУчете
		|	И НЕ &РеглУчетВНАВедетсяНезависимо
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрисоединениеОС)
		|";
		
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяТаблицаСтоимостьВНА(ТекстыЗапроса, Запрос);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяТаблицаАмортизацияВНА(ТекстыЗапроса, Запрос);
	ТекстЗапросаВтСтоимостьИсходныхОС(Запрос, ТекстыЗапроса);
	ТекстЗапросаВтАмортизацияИсходныхОС(Запрос, ТекстыЗапроса);
	ТекстЗапросаВтНаработкиОбъектовЭксплуатации(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                             КАК Период,
	|	&Период                             КАК ДатаПоследнегоИзменения,
	|	&Организация                        КАК Организация,
	|	&ОсновноеСредство                   КАК ОсновноеСредство,
	|	&СрокИспользованияУУ                КАК СрокИспользования,
	|	&СрокИспользованияОстаточныйУУ      КАК СрокИспользованияДляВычисленияАмортизации,
	|	&ОбъемНаработкиОстаточный           КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	&КоэффициентУскоренияУУ             КАК КоэффициентУскорения,
	|	&МетодНачисленияАмортизацииУУ       КАК МетодНачисленияАмортизации,
	|
	|	&ЛиквидационнаяСтоимость            КАК ЛиквидационнаяСтоимость,
	|	&ЛиквидационнаяСтоимостьРегл        КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	ЕСТЬNULL(СтоимостьВНА.Стоимость, 0) 
	|		- ЕСТЬNULL(АмортизацияВНА.Амортизация, 0) КАК СтоимостьДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ВедетсяРегламентированныйУчетВНА
	|			ТОГДА ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияВНА.АмортизацияРегл, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл
	|ИЗ
	|	Документ.ОбъединениеОС КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
	|		ПО ИСТИНА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияИсходныхОС КАК АмортизацияВНА
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И НЕ &ОбъединениеОднотипныхОС
	|	И &ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                КАК Период,
	|	&Период                                                КАК ДатаПоследнегоИзменения,
	|	&Организация                                           КАК Организация,
	|	&ОсновноеСредство                                      КАК ОсновноеСредство,
	|	ПараметрыАмортизацииОСУУ.СрокИспользования             КАК СрокИспользования,
	|	&СрокИспользованияОстаточныйУУ                         КАК СрокИспользованияДляВычисленияАмортизации,
	|	&ОбъемНаработкиОстаточный                              КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения          КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации    КАК МетодНачисленияАмортизации,
	|
	|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость       КАК ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл   КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	ЕСТЬNULL(СтоимостьВНА.Стоимость, 0) 
	|		- ЕСТЬNULL(АмортизацияВНА.Амортизация, 0) КАК СтоимостьДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ВедетсяРегламентированныйУчетВНА
	|			ТОГДА ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияВНА.АмортизацияРегл, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И Организация = &Организация
	|				И ОсновноеСредство = &ПервоеОС) КАК ПараметрыАмортизацииОСУУ
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
	|		ПО ИСТИНА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияИсходныхОС КАК АмортизацияВНА
	|		ПО ИСТИНА
	|ГДЕ
	|	&ОбъединениеОднотипныхОС
	|	И &ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                КАК Период,
	|	&Период                                                КАК ДатаПоследнегоИзменения,
	|	&Организация                                           КАК Организация,
	|	&ОсновноеСредство                                      КАК ОсновноеСредство,
	|	ПараметрыАмортизацииОСУУ.СрокИспользования             КАК СрокИспользования,
	|
	|	ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации
	|		- РАЗНОСТЬДАТ(ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, &Период), &Период, МЕСЯЦ) КАК СрокИспользованияДляВычисленияАмортизации,
	|
	|	&ОбъемНаработкиОстаточный                              КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения          КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации    КАК МетодНачисленияАмортизации,
	|
	|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость       КАК ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл   КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	ЕСТЬNULL(СтоимостьВНА.Стоимость, 0) 
	|		- ЕСТЬNULL(АмортизацияВНА.Амортизация, 0)
	|		+ ЕСТЬNULL(СтоимостьИсходныхОС.Стоимость, 0) 
	|		- ЕСТЬNULL(АмортизацияИсходныхОС.Амортизация, 0) КАК СтоимостьДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ВедетсяРегламентированныйУчетВНА
	|			ТОГДА ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0) 
	|					- ЕСТЬNULL(АмортизацияВНА.АмортизацияРегл, 0)
	|					+ ЕСТЬNULL(СтоимостьИсходныхОС.СтоимостьРегл, 0) 
	|					- ЕСТЬNULL(АмортизацияИсходныхОС.АмортизацияРегл, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И Организация = &Организация
	|				И ОсновноеСредство = &ОсновноеСредство) КАК ПараметрыАмортизацииОСУУ
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьВНА
	|		ПО СтоимостьВНА.Ссылка = &Ссылка
	|			И СтоимостьВНА.ОбъектУчета = &ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияВНА
	|		ПО АмортизацияВНА.Ссылка = &Ссылка
	|			И АмортизацияВНА.ОбъектУчета = &ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьИсходныхОС
	|		ПО ИСТИНА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтАмортизацияИсходныхОС КАК АмортизацияИсходныхОС
	|		ПО ИСТИНА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНаработкиОбъектовЭксплуатации КАК НаработкиОбъектовЭксплуатации
	|		ПО ИСТИНА
	|ГДЕ
	|	&ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрисоединениеОС)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПервоначальныеСведенияОС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаВтСтоимостьИсходныхОС(Запрос, ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса = ОбъединениеОСЛокализация.ТекстЗапросаТаблицаПервоначальныеСведенияОС(Запрос, ТекстыЗапроса);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Период                                     КАК Период,
		|	&ОсновноеСредство                           КАК ОсновноеСредство,
		|	&Организация                                КАК Организация,
		|
		|	ВЫБОР 
		|		КОГДА ПервоначальныеСведенияПервогоОС.ДатаВводаВЭксплуатациюБУ = ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА ДАТАВРЕМЯ(1,1,1)
		|		КОГДА &ОтражатьВРеглУчете 
		|			ТОГДА &Период 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ
		|	КОНЕЦ КАК ДатаВводаВЭксплуатациюБУ,
		|
		|	ВЫБОР 
		|		КОГДА &ОтражатьВУпрУчете 
		|			ТОГДА &Период 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ
		|	КОНЕЦ КАК ДатаВводаВЭксплуатациюУУ,
		|
		|	ВЫБОР 
		|		КОГДА ПервоначальныеСведенияПервогоОС.ДатаВводаВЭксплуатациюБУ = ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА &ОтражатьВРеглУчете 
		|			ТОГДА &Ссылка 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ
		|	КОНЕЦ КАК ДокументВводаВЭксплуатациюБУ,
		|
		|	ВЫБОР 
		|		КОГДА &ОтражатьВУпрУчете 
		|			ТОГДА &Ссылка 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ
		|	КОНЕЦ КАК ДокументВводаВЭксплуатациюУУ,
		|
		|	ВЫБОР 
		|		КОГДА &ОтражатьВРеглУчете 
		|			ТОГДА &МетодНачисленияАмортизацииБУ 
		|		ИНАЧЕ ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ
		|	КОНЕЦ КАК МетодНачисленияАмортизацииБУ,
		|
		|	НЕОПРЕДЕЛЕНО КАК ДокументСнятияСУчетаБУ,
		|	НЕОПРЕДЕЛЕНО КАК ДокументСнятияСУчетаУУ,
		|
		|	ВЫБОР 
		|		КОГДА &ОтражатьВРеглУчете 
		|			ТОГДА &ПорядокУчетаБУ 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаБУ
		|	КОНЕЦ КАК ПорядокУчетаБУ,
		|
		|	ВЫБОР 
		|		КОГДА &ОтражатьВУпрУчете 
		|			ТОГДА &ПорядокУчетаУУ 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаУУ
		|	КОНЕЦ КАК ПорядокУчетаУУ,
		|
		|	ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0)     КАК ПервоначальнаяСтоимостьБУ,
		|	ЕСТЬNULL(СтоимостьВНА.Стоимость, 0)         КАК ПервоначальнаяСтоимостьУУ
		|ИЗ
		|	Документ.ОбъединениеОС КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ОсновноеСредство) КАК ПервоначальныеСведенияОС
		|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = &ОсновноеСредство
		|
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ПервоеОС) КАК ПервоначальныеСведенияПервогоОС
		|		ПО ПервоначальныеСведенияПервогоОС.ОсновноеСредство = &ПервоеОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
		|		ПО ИСТИНА
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И &РеглУчетВНАВедетсяНезависимо
		|	И НЕ &ОбъединениеОднотипныхОС
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период                                     КАК Период,
		|	&ОсновноеСредство                           КАК ОсновноеСредство,
		|	&Организация                                КАК Организация,
		|
		|	ВЫБОР 
		|		КОГДА ПервоначальныеСведенияПервогоОС.ДатаВводаВЭксплуатациюБУ = ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА ДАТАВРЕМЯ(1,1,1)
		|		КОГДА &ОтражатьВРеглУчете 
		|			ТОГДА &Период 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ
		|	КОНЕЦ КАК ДатаВводаВЭксплуатациюБУ,
		|
		|	ВЫБОР 
		|		КОГДА &ОтражатьВУпрУчете 
		|			ТОГДА &Период 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ
		|	КОНЕЦ КАК ДатаВводаВЭксплуатациюУУ,
		|
		|	ВЫБОР 
		|		КОГДА ПервоначальныеСведенияПервогоОС.ДатаВводаВЭксплуатациюБУ = ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		КОГДА &ОтражатьВРеглУчете 
		|			ТОГДА &Ссылка 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ
		|	КОНЕЦ КАК ДокументВводаВЭксплуатациюБУ,
		|
		|	ВЫБОР 
		|		КОГДА &ОтражатьВУпрУчете 
		|			ТОГДА &Ссылка 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ
		|	КОНЕЦ КАК ДокументВводаВЭксплуатациюУУ,
		|
		|	ВЫБОР 
		|		КОГДА &ОтражатьВРеглУчете 
		|			ТОГДА ПервоначальныеСведенияПервогоОС.МетодНачисленияАмортизацииБУ 
		|		ИНАЧЕ ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ
		|	КОНЕЦ КАК МетодНачисленияАмортизацииБУ,
		|
		|	НЕОПРЕДЕЛЕНО КАК ДокументСнятияСУчетаБУ,
		|	НЕОПРЕДЕЛЕНО КАК ДокументСнятияСУчетаУУ,
		|
		|	ВЫБОР 
		|		КОГДА &ОтражатьВРеглУчете 
		|			ТОГДА ПервоначальныеСведенияПервогоОС.ПорядокУчетаБУ 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаБУ
		|	КОНЕЦ КАК ПорядокУчетаБУ,
		|
		|	ВЫБОР 
		|		КОГДА &ОтражатьВУпрУчете 
		|			ТОГДА ПервоначальныеСведенияПервогоОС.ПорядокУчетаУУ 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаУУ
		|	КОНЕЦ КАК ПорядокУчетаУУ,
		|
		|	ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0)     КАК ПервоначальнаяСтоимостьБУ,
		|	ЕСТЬNULL(СтоимостьВНА.Стоимость, 0)         КАК ПервоначальнаяСтоимостьУУ
		|ИЗ
		|	Документ.ОбъединениеОС КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ОсновноеСредство) КАК ПервоначальныеСведенияОС
		|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = &ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ПервоеОС) КАК ПервоначальныеСведенияПервогоОС
		|		ПО ПервоначальныеСведенияПервогоОС.ОсновноеСредство = &ПервоеОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
		|		ПО ИСТИНА
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И &ОбъединениеОднотипныхОС
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Снятие с учета исходных ОС.
		|ВЫБРАТЬ
		|	&Период                                                КАК Период,
		|	ПервоначальныеСведенияОС.ОсновноеСредство              КАК ОсновноеСредство,
		|	&Организация                                           КАК Организация,
		|
		|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ      КАК ДатаВводаВЭксплуатациюБУ,
		|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ      КАК ДатаВводаВЭксплуатациюУУ,
		|
		|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ  КАК ДокументВводаВЭксплуатациюБУ,
		|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ  КАК ДокументВводаВЭксплуатациюУУ,
		|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ  КАК МетодНачисленияАмортизацииБУ,
		|
		|	ВЫБОР
		|		КОГДА &ОтражатьВРеглУчете
		|			ТОГДА &Ссылка 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументСнятияСУчетаБУ
		|
		|	КОНЕЦ КАК ДокументСнятияСУчетаБУ,
		|
		|	ВЫБОР
		|		КОГДА &ОтражатьВУпрУчете
		|			ТОГДА &Ссылка 
		|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументСнятияСУчетаУУ
		|	КОНЕЦ КАК ДокументСнятияСУчетаУУ,
		|
		|	ПервоначальныеСведенияОС.ПорядокУчетаБУ                КАК ПорядокУчетаБУ,
		|	ПервоначальныеСведенияОС.ПорядокУчетаУУ                КАК ПорядокУчетаУУ,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ     КАК ПервоначальнаяСтоимостьБУ,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ     КАК ПервоначальнаяСтоимостьУУ
		|ИЗ
		|	РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство В (ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОС КАК СписокОС)
		|				И ОсновноеСредство <> &ОсновноеСредство) КАК ПервоначальныеСведенияОС
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период                                     КАК Период,
		|	&ОсновноеСредство                           КАК ОсновноеСредство,
		|	&Организация                                КАК Организация,
		|
		|	&Период КАК ДатаВводаВЭксплуатациюБУ,
		|	&Период КАК ДатаВводаВЭксплуатациюУУ,
		|	&Ссылка КАК ДокументВводаВЭксплуатациюБУ,
		|	&Ссылка КАК ДокументВводаВЭксплуатациюУУ,
		|	&МетодНачисленияАмортизацииУУ КАК МетодНачисленияАмортизацииБУ,
		|
		|	НЕОПРЕДЕЛЕНО КАК ДокументСнятияСУчетаБУ,
		|	НЕОПРЕДЕЛЕНО КАК ДокументСнятияСУчетаУУ,
		|
		|	ВЫБОР &ПорядокУчетаУУ
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеНачислятьАмортизацию)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.СтоимостьНеПогашается)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
		|	КОНЕЦ КАК ПорядокУчетаБУ,
		|
		|	&ПорядокУчетаУУ КАК ПорядокУчетаУУ,
		|
		|	ЕСТЬNULL(СтоимостьВНА.СтоимостьРегл, 0)     КАК ПервоначальнаяСтоимостьБУ,
		|	ЕСТЬNULL(СтоимостьВНА.Стоимость, 0)         КАК ПервоначальнаяСтоимостьУУ
		|ИЗ
		|	Документ.ОбъединениеОС КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ОсновноеСредство) КАК ПервоначальныеСведенияОС
		|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = &ОсновноеСредство
		|
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|			&Период,
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И Организация = &Организация
		|				И ОсновноеСредство = &ПервоеОС) КАК ПервоначальныеСведенияПервогоОС
		|		ПО ПервоначальныеСведенияПервогоОС.ОсновноеСредство = &ПервоеОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимостьИсходныхОС КАК СтоимостьВНА
		|		ПО ИСТИНА
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И НЕ &РеглУчетВНАВедетсяНезависимо
		|	И НЕ &ОбъединениеОднотипныхОС
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОбъединениеОС)
		|";
		
	КонецЕсли; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СтоимостьОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяТаблицаСтоимостьВНА(ТекстыЗапроса, Запрос);
	ТекстЗапросаТаблицаВтСведенияОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Увеличение стоимости
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)        КАК ВидДвижения,
	|	&Период                                       КАК Период,
	|	&Организация                                  КАК Организация,
	|	&Подразделение                                КАК Подразделение,
	|	втСведенияОС.ГруппаФинансовогоУчетаПолучателя  КАК ГруппаФинансовогоУчета,
	|	втСведенияОС.НаправлениеДеятельностиПолучателя КАК НаправлениеДеятельности,
	|	втСведенияОС.Арендатор                        КАК Арендатор,
	|	&ХозяйственнаяОперация                        КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                           КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                            КАК ОтражатьВУпрУчете,
	|	&ОсновноеСредство                             КАК ОсновноеСредство,
	|	втСведенияОС.НалогообложениеНДСПолучателя     КАК НалогообложениеНДС,
	|
	|	&Подразделение                                КАК КорПодразделение,
	|	втСведенияОС.НаправлениеДеятельности          КАК КорНаправлениеДеятельности,
	|	втСведенияОС.ГруппаФинансовогоУчета           КАК КорГруппаФинансовогоУчета,
	|	НЕ ВТ_СтоимостьВНА.ОбъектУчета ЕСТЬ NULL      КАК РасчетСтоимости,
	|	0                                             КАК ПредварительнаяСтоимость,
	|	0                                             КАК ПредварительнаяСтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_СтоимостьВНА.Стоимость
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК Стоимость,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьНУ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьПР
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьПР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьВР
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьВР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьНУЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьНУЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьПРЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьВРЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьВРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_СтоимостьВНА.РезервПереоценкиСтоимости
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК РезервПереоценкиСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.РезервПереоценкиСтоимостиРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК РезервПереоценкиСтоимостиРегл,
	|
	|	0 КАК ПриростСтоимости,
	|	0 КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                 КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперацииСтоимость      КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОС КАК втСведенияОС
	|		ПО втСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)        КАК ВидДвижения,
	|	&Период                                       КАК Период,
	|	&Организация                                  КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                  КАК Подразделение,
	|	втСведенияОС.ГруппаФинансовогоУчетаПолучателя  КАК ГруппаФинансовогоУчета,
	|	втСведенияОС.НаправлениеДеятельностиПолучателя КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                  КАК Арендатор,
	|	&ХозяйственнаяОперация                        КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                           КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                            КАК ОтражатьВУпрУчете,
	|	&ОсновноеСредство                             КАК ОсновноеСредство,
	|	втСведенияОС.НалогообложениеНДСПолучателя     КАК НалогообложениеНДС,
	|
	|	&Подразделение                                КАК КорПодразделение,
	|	втСведенияОС.НаправлениеДеятельности          КАК КорНаправлениеДеятельности,
	|	втСведенияОС.ГруппаФинансовогоУчета           КАК КорГруппаФинансовогоУчета,
	|	НЕ ВТ_СтоимостьВНА.ОбъектУчета ЕСТЬ NULL      КАК РасчетСтоимости,
	|
	|	0                                             КАК ПредварительнаяСтоимость,
	|	0                                             КАК ПредварительнаяСтоимостьРегл,
	|	0                                             КАК Стоимость,
	|	0                                             КАК СтоимостьРегл,
	|	0                                             КАК СтоимостьНУ,
	|	0                                             КАК СтоимостьПР,
	|	0                                             КАК СтоимостьВР,
	|	0                                             КАК СтоимостьЦФ,
	|	0                                             КАК СтоимостьНУЦФ,
	|	0                                             КАК СтоимостьПРЦФ,
	|	0                                             КАК СтоимостьВРЦФ,
	|	0                                             КАК РезервПереоценкиСтоимости,
	|	0                                             КАК РезервПереоценкиСтоимостиРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_СтоимостьВНА.ПриростСтоимости
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПриростСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.ПриростСтоимостиРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                 КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперацииСтоимость      КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОС КАК втСведенияОС
	|		ПО втСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И (&ОтражатьВУпрУчете И ВТ_СтоимостьВНА.ПриростСтоимости <> 0
	|		ИЛИ &ОтражатьВРеглУчете И ВТ_СтоимостьВНА.ПриростСтоимостиРегл <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Списание исходных ОС
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)        КАК ВидДвижения,
	|	&Период                                       КАК Период,
	|	&Организация                                  КАК Организация,
	|	&Подразделение                                КАК Подразделение,
	|	втСведенияОС.ГруппаФинансовогоУчета           КАК ГруппаФинансовогоУчета,
	|	втСведенияОС.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	втСведенияОС.Арендатор                        КАК Арендатор,
	|	&ХозяйственнаяОперация                        КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                           КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                            КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство                    КАК ОсновноеСредство,
	|	втСведенияОС.НалогообложениеНДСПолучателя     КАК НалогообложениеНДС,
	|
	|	&Подразделение                                КАК КорПодразделение,
	|	втСведенияОС.НаправлениеДеятельностиПолучателя КАК КорНаправлениеДеятельности,
	|	втСведенияОС.ГруппаФинансовогоУчетаПолучателя  КАК КорГруппаФинансовогоУчета,
	|	НЕ ВТ_СтоимостьВНА.ОбъектУчета ЕСТЬ NULL      КАК РасчетСтоимости,
	|            
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_СтоимостьВНА.ПредварительнаяСтоимость
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК ПредварительнаяСтоимость,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.ПредварительнаяСтоимостьРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК ПредварительнаяСтоимостьРегл,
	|            
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_СтоимостьВНА.Стоимость
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК Стоимость,
	|            
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьРегл,
	|            
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьНУ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьНУ,
	|            
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьПР
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьПР,
	|            
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьВР
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьВР,
	|            
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьЦФ,
	|            
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьНУЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьНУЦФ,
	|            
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьПРЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьПРЦФ,
	|            
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.СтоимостьВРЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК СтоимостьВРЦФ,
	|            
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_СтоимостьВНА.РезервПереоценкиСтоимости
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК РезервПереоценкиСтоимости,
	|            
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.РезервПереоценкиСтоимостиРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                         КАК РезервПереоценкиСтоимостиРегл,
	|
	|	0 КАК ПриростСтоимости,
	|	0 КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                 КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперацииСтоимость      КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОС КАК втСведенияОС
	|		ПО втСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)        КАК ВидДвижения,
	|	&Период                                       КАК Период,
	|	&Организация                                  КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                  КАК Подразделение,
	|	втСведенияОС.ГруппаФинансовогоУчета           КАК ГруппаФинансовогоУчета,
	|	втСведенияОС.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                  КАК Арендатор,
	|	&ХозяйственнаяОперация                        КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                           КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                            КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство                    КАК ОсновноеСредство,
	|	втСведенияОС.НалогообложениеНДСПолучателя     КАК НалогообложениеНДС,
	|
	|	&Подразделение                                КАК КорПодразделение,
	|	втСведенияОС.НаправлениеДеятельностиПолучателя КАК КорНаправлениеДеятельности,
	|	втСведенияОС.ГруппаФинансовогоУчетаПолучателя  КАК КорГруппаФинансовогоУчета,
	|	НЕ ВТ_СтоимостьВНА.ОбъектУчета ЕСТЬ NULL      КАК РасчетСтоимости,
	|            
	|	0                                             КАК ПредварительнаяСтоимость,
	|	0                                             КАК ПредварительнаяСтоимостьРегл,
	|	0                                             КАК Стоимость,
	|	0                                             КАК СтоимостьРегл,
	|	0                                             КАК СтоимостьНУ,
	|	0                                             КАК СтоимостьПР,
	|	0                                             КАК СтоимостьВР,
	|	0                                             КАК СтоимостьЦФ,
	|	0                                             КАК СтоимостьНУЦФ,
	|	0                                             КАК СтоимостьПРЦФ,
	|	0                                             КАК СтоимостьВРЦФ,
	|	0                                             КАК РезервПереоценкиСтоимости,
	|	0                                             КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_СтоимостьВНА.ПриростСтоимости
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПриростСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_СтоимостьВНА.ПриростСтоимостиРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПриростСтоимостиРегл,
	|
	|	ТаблицаОС.ИдентификаторСтроки                 КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперацииСтоимость      КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОС КАК втСведенияОС
	|		ПО втСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И (&ОтражатьВУпрУчете И ВТ_СтоимостьВНА.ПриростСтоимости <> 0
	|		ИЛИ &ОтражатьВРеглУчете И ВТ_СтоимостьВНА.ПриростСтоимостиРегл <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаУзлыКомпонентыАмортизации(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "УзлыКомпонентыАмортизации";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтУзлыОсновныхСредств(ТекстыЗапроса, "Документ.ОбъединениеОС.ОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	УзлыКомпоненты.УзелКомпонент КАК УзелКомпонент,
	|	&ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УзлыКомпонентыАмортизации.СрезПоследних(&Период, УзелКомпонент В
	|			(ВЫБРАТЬ
	|				ТаблицаУзлов.УзелКомпонент КАК УзелКомпонент
	|			ИЗ
	|				ВтУзлыОсновныхСредств КАК ТаблицаУзлов)) КАК УзлыКомпоненты
	|		ПО ТаблицаОС.ОсновноеСредство = УзлыКомпоненты.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.ОсновноеСредство.ТипОС В (ЗНАЧЕНИЕ(Перечисление.ТипыОС.ОбъектЭксплуатации),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОС.ГрупповоеОС))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияОС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтСведенияОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Перенос амортизации из исходных ОС.
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)              КАК ВидДвижения,
	|	&Ссылка                                             КАК Регистратор,
	|	&Период                                             КАК Период,
	|	&Организация                                        КАК Организация,
	|	&Подразделение                                      КАК Подразделение,
	|	&ОсновноеСредство                                   КАК ОсновноеСредство,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_АмортизацияВНА.Амортизация
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК Амортизация,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияНУ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияПР
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияПР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияВР
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияВР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияНУЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияНУЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияПРЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияВРЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияВРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_АмортизацияВНА.РезервПереоценкиАмортизации
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК РезервПереоценкиАмортизации,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.РезервПереоценкиАмортизацииРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК РезервПереоценкиАмортизацииРегл,
	|
	|	втСведенияОС.ГруппаФинансовогоУчетаПолучателя       КАК ГруппаФинансовогоУчета,
	|	втСведенияОС.ГруппаФинансовогоУчета                 КАК КорГруппаФинансовогоУчета,
	|	втСведенияОС.НаправлениеДеятельностиПолучателя      КАК НаправлениеДеятельности,
	|	втСведенияОС.НаправлениеДеятельности                КАК КорНаправлениеДеятельности,
	|	&ХозяйственнаяОперация                              КАК ХозяйственнаяОперация,
	|
	|	ВЫБОР
	|		КОГДА &ОбъединениеОднотипныхОС
	|			ТОГДА ВТ_АмортизацияВНА.НачислятьИзнос
	|		ИНАЧЕ &НачислятьИзнос
	|	КОНЕЦ КАК НачислятьИзнос,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                       КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперацииАмортизация          КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК ВТ_АмортизацияВНА
	|		ПО ВТ_АмортизацияВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_АмортизацияВНА.Ссылка = &Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОС КАК втСведенияОС
	|		ПО втСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Списание амортизации исходных ОС.
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)              КАК ВидДвижения,
	|	&Ссылка                                             КАК Регистратор,
	|	&Период                                             КАК Период,
	|	&Организация                                        КАК Организация,
	|	&Подразделение                                      КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство                          КАК ОсновноеСредство,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_АмортизацияВНА.Амортизация
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК Амортизация,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияНУ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияПР
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияПР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияВР
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияВР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияНУЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияНУЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияПРЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.АмортизацияВРЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК АмортизацияВРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_АмортизацияВНА.РезервПереоценкиАмортизации
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК РезервПереоценкиАмортизации,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_АмортизацияВНА.РезервПереоценкиАмортизацииРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК РезервПереоценкиАмортизацииРегл,
	|
	|	втСведенияОС.ГруппаФинансовогоУчета                 КАК ГруппаФинансовогоУчета,
	|	втСведенияОС.ГруппаФинансовогоУчетаПолучателя       КАК КорГруппаФинансовогоУчета,
	|	втСведенияОС.НаправлениеДеятельности                КАК НаправлениеДеятельности,
	|	втСведенияОС.НаправлениеДеятельностиПолучателя      КАК КорНаправлениеДеятельности,
	|	&ХозяйственнаяОперация                              КАК ХозяйственнаяОперация,
	|	ВТ_АмортизацияВНА.НачислятьИзнос                    КАК НачислятьИзнос,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                       КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперацииАмортизация          КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК ВТ_АмортизацияВНА
	|		ПО ВТ_АмортизацияВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_АмортизацияВНА.Ссылка = &Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОС КАК втСведенияОС
	|		ПО втСведенияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаВтСтарыеЕГДС(ТекстыЗапроса)

	ИмяТаблицы = "втСтарыеЕГДС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СоставЕГДССрезПоследних.ВнеоборотныйАктив КАК ОсновноеСредство,
	|	СоставЕГДССрезПоследних.ЕГДС
	|ПОМЕСТИТЬ втСтарыеЕГДС
	|ИЗ
	|	РегистрСведений.СоставЕГДС.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И ВнеоборотныйАктив В
	|					(ВЫБРАТЬ
	|						СписокОС.ОсновноеСредство
	|					ИЗ
	|						Документ.ОбъединениеОС.ОС КАК СписокОС
	|					ГДЕ
	|						СписокОС.Ссылка = &Ссылка)
	|		) КАК СоставЕГДССрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ЕГДС
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаВтОтражениеДоходовРасходов(ТекстыЗапроса)

	ИмяТаблицы = "ВтОтражениеДоходовРасходов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПоследниеДокументы.ВнеоборотныйАктив КАК ВнеоборотныйАктив,
	|	МАКСИМУМ(ОбесценениеВНА.СтатьяРасходов) КАК СтатьяРасходов,
	|	МАКСИМУМ(ОбесценениеВНА.АналитикаРасходов) КАК АналитикаРасходов,
	|	МАКСИМУМ(ОбесценениеВНА.СтатьяДоходов) КАК СтатьяДоходов,
	|	МАКСИМУМ(ОбесценениеВНА.АналитикаДоходов) КАК АналитикаДоходов
	|ПОМЕСТИТЬ ВтОтражениеДоходовРасходов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументыОбесценения.ВнеоборотныйАктив,
	|		МАКСИМУМ(ДокументыОбесценения.Дата) КАК Дата
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ОбесценениеВНА.Ссылка.Дата КАК Дата,
	|			Таблица.ОсновноеСредство КАК ВнеоборотныйАктив,
	|			ОбесценениеВНА.Ссылка КАК Документ
	|		ИЗ
	|			втТаблицаОС КАК Таблица
	|				ЛЕВОЕ СОЕДИНЕНИЕ втСтарыеЕГДС КАК втСтарыеЕГДС
	|				ПО втСтарыеЕГДС.ОсновноеСредство = Таблица.ОсновноеСредство
	|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОбесценениеВНА.ЕдиницыГенерирующиеДенежныеСредства КАК ОбесценениеВНА
	|				ПО ОбесценениеВНА.ЕГДС = втСтарыеЕГДС.ЕГДС
	|				И ОбесценениеВНА.Ссылка.Организация = Таблица.Организация
	|				И ОбесценениеВНА.Ссылка.Проведен
	|				И ОбесценениеВНА.Ссылка.Дата <= &Период
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			ОбесценениеВНА.Ссылка.Дата КАК Дата,
	|			Таблица.ОсновноеСредство КАК ВнеоборотныйАктив,
	|			ОбесценениеВНА.Ссылка КАК Документ
	|		ИЗ
	|			втТаблицаОС КАК Таблица
	|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОбесценениеВНА.ОсновныеСредства КАК ОбесценениеВНА
	|				ПО ОбесценениеВНА.ОсновноеСредство = Таблица.ОсновноеСредство
	|				И ОбесценениеВНА.Ссылка.Организация = Таблица.Организация
	|				И ОбесценениеВНА.Ссылка.Проведен
	|				И ОбесценениеВНА.Ссылка.Дата <= &Период) КАК ДокументыОбесценения
	|	СГРУППИРОВАТЬ ПО
	|		ДокументыОбесценения.ВнеоборотныйАктив) КАК ПоследниеДокументы
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОбесценениеВНА КАК ОбесценениеВНА
	|		ПО ОбесценениеВНА.Дата = ПоследниеДокументы.Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоследниеДокументы.ВнеоборотныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВнеоборотныйАктив
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаОбесцененияВНА(ТекстыЗапроса)

	ИмяРегистра = "ОбесценениеВНА";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтУзлыОсновныхСредств(ТекстыЗапроса, "ВтТаблицаОС", Ложь);
	ТекстЗапросаВтСтарыеЕГДС(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Перенос обесценения из исходных ОС.
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)              КАК ВидДвижения,
	|	&Ссылка                                             КАК Регистратор,
	|	&Период                                             КАК Период,
	|	&Организация                                        КАК Организация,
	|	&Подразделение                                      КАК Подразделение,
	|	&ОсновноеСредство                                   КАК ВнеоборотныйАктив,
	|	ВТ_ОбесценениеВНА.ЕГДС                              КАК ЕГДС,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеУпр
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК ОбесценениеУпр,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК ОбесценениеРегл,
	|
	|	&ГруппаФинансовогоУчета                             КАК ГруппаФинансовогоУчета,
	|	&НаправлениеДеятельности                            КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперация                              КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                       КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперацииОбесценения          КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ВТ_ОбесценениеВНА
	|		ПО ВТ_ОбесценениеВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_ОбесценениеВНА.Ссылка = &Ссылка
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтарыеЕГДС КАК ВтСтарыеЕГДС
	|		ПО ВтСтарыеЕГДС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			
	|ГДЕ
	|	ЕСТЬNULL(ВтСтарыеЕГДС.ЕГДС, ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка)) = &ЕГДС
	|	И НЕ &ОбъединениеОднотипныхОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)              КАК ВидДвижения,
	|	&Ссылка                                             КАК Регистратор,
	|	&Период                                             КАК Период,
	|	&Организация                                        КАК Организация,
	|	&Подразделение                                      КАК Подразделение,
	|	&ОсновноеСредство                                   КАК ВнеоборотныйАктив,
	|	ВТ_ОбесценениеВНА.ЕГДС                              КАК ЕГДС,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеУпр
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК ОбесценениеУпр,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК ОбесценениеРегл,
	|
	|	ТаблицаОС.ГруппаФинансовогоУчета                    КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности                   КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперация                              КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                       КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперацииОбесценения          КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ВТ_ОбесценениеВНА
	|		ПО ВТ_ОбесценениеВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_ОбесценениеВНА.Ссылка = &Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтарыеЕГДС КАК ВтСтарыеЕГДС
	|		ПО ВтСтарыеЕГДС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			
	|ГДЕ
	|	ЕСТЬNULL(ВтСтарыеЕГДС.ЕГДС, ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка)) = &ЕГДС
	|	И &ОбъединениеОднотипныхОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Списание обесценения исходных ОС.
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)              КАК ВидДвижения,
	|	&Ссылка                                             КАК Регистратор,
	|	&Период                                             КАК Период,
	|	&Организация                                        КАК Организация,
	|	&Подразделение                                      КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство                          КАК ВнеоборотныйАктив,
	|	ВТ_ОбесценениеВНА.ЕГДС                              КАК ЕГДС,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеУпр
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК ОбесценениеУпр,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК ОбесценениеРегл,
	|
	|	ТаблицаОС.ГруппаФинансовогоУчета                    КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности                   КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперация                              КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                       КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперацииОбесценения          КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ВТ_ОбесценениеВНА
	|		ПО ВТ_ОбесценениеВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_ОбесценениеВНА.Ссылка = &Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтарыеЕГДС КАК ВтСтарыеЕГДС
	|		ПО ВтСтарыеЕГДС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			
	|ГДЕ
	|	ЕСТЬNULL(ВтСтарыеЕГДС.ЕГДС, ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка)) = &ЕГДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// восстановление обесценения при смене ЕГДС
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)              КАК ВидДвижения,
	|	&Ссылка                                             КАК Регистратор,
	|	&Период                                             КАК Период,
	|	&Организация                                        КАК Организация,
	|	&Подразделение                                      КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство                          КАК ВнеоборотныйАктив,
	|	ВТ_ОбесценениеВНА.ЕГДС                              КАК ЕГДС,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеУпр
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК ОбесценениеУпр,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК ОбесценениеРегл,
	|
	|	ТаблицаОС.ГруппаФинансовогоУчета                    КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности                   КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперация                              КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                            КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперацииВосстановлениеОбесценения КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ВТ_ОбесценениеВНА
	|		ПО ВТ_ОбесценениеВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_ОбесценениеВНА.Ссылка = &Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтарыеЕГДС КАК ВтСтарыеЕГДС
	|		ПО ВтСтарыеЕГДС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			
	|ГДЕ
	|	ЕСТЬNULL(ВтСтарыеЕГДС.ЕГДС, ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка)) <> &ЕГДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)              КАК ВидДвижения,
	|	&Ссылка                                             КАК Регистратор,
	|	&Период                                             КАК Период,
	|	&Организация                                        КАК Организация,
	|	&Подразделение                                      КАК Подразделение,
	|	УзлыКомпоненты.ОсновноеСредство                     КАК ВнеоборотныйАктив,
	|	ВТ_ОбесценениеВНА.ЕГДС                              КАК ЕГДС,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеУпр
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК ОбесценениеУпр,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК ОбесценениеРегл,
	|
	|	ТаблицаОС.ГруппаФинансовогоУчета                    КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности                   КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперация                              КАК ХозяйственнаяОперация,
	|	
	|	УзлыКомпоненты.ИдентификаторСтроки                  КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперацииВосстановлениеОбесценения КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УзлыКомпонентыАмортизации.СрезПоследних(&Период, УзелКомпонент В
	|			(ВЫБРАТЬ
	|				ТаблицаУзлов.УзелКомпонент КАК УзелКомпонент
	|			ИЗ
	|				ВтУзлыОсновныхСредств КАК ТаблицаУзлов)) КАК УзлыКомпоненты
	|		ПО УзлыКомпоненты.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ВТ_ОбесценениеВНА
	|		ПО ВТ_ОбесценениеВНА.ОбъектУчета = УзлыКомпоненты.ОсновноеСредство
	|			И ВТ_ОбесценениеВНА.Ссылка = &Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтарыеЕГДС КАК ВтСтарыеЕГДС
	|		ПО ВтСтарыеЕГДС.ОсновноеСредство = УзлыКомпоненты.ОсновноеСредство
	|			
	|ГДЕ
	|	ЕСТЬNULL(ВтСтарыеЕГДС.ЕГДС, ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка)) <> &ЕГДС
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаПрочиеДоходы(Запрос, ТекстыЗапроса)

	ИмяРегистра = "ПрочиеДоходы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ТекстЗапросаВтСтарыеЕГДС(ТекстыЗапроса);
	ТекстЗапросаВтОтражениеДоходовРасходов(ТекстыЗапроса);

	ТекстЗапроса = 
	"
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)              КАК ВидДвижения,
	|	&Ссылка                                             КАК Регистратор,
	|	&Период                                             КАК Период,
	|	&Организация                                        КАК Организация,
	|	&Подразделение                                      КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности                   КАК НаправлениеДеятельности,
	|	ВтОтражениеДоходовРасходов.СтатьяДоходов            КАК СтатьяДоходов,
	|	ВтОтражениеДоходовРасходов.АналитикаДоходов         КАК АналитикаДоходов,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеУпр
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК СуммаУпр,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеУпр
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК Сумма,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ВТ_ОбесценениеВНА.ОбесценениеРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                               КАК СуммаРегл,
	|	ТаблицаОС.ИдентификаторСтроки                            КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперацииВосстановлениеОбесценения КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ВТ_ОбесценениеВНА
	|		ПО ВТ_ОбесценениеВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_ОбесценениеВНА.Ссылка = &Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтарыеЕГДС КАК ВтСтарыеЕГДС
	|		ПО ВтСтарыеЕГДС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтОтражениеДоходовРасходов КАК ВтОтражениеДоходовРасходов
	|		ПО ВтОтражениеДоходовРасходов.ВнеоборотныйАктив = ТаблицаОС.ОсновноеСредство
	|			
	|ГДЕ
	|	ЕСТЬNULL(ВтСтарыеЕГДС.ЕГДС, ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка)) <> &ЕГДС
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ТекстЗапросаВтОтражениеДоходовРасходов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"// Выделение амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровАмортизацииОС) КАК ХозяйственнаяОперация,
	|	&Период                           КАК Период,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	&НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	&ОсновноеСредство                 КАК АналитикаАктивовПассивов,
	|	&ГруппаФинансовогоУчета           КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	&Организация                      КАК КорОрганизация,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА АмортизацияОС.Амортизация
	|		ИНАЧЕ 0 
	|	КОНЕЦ                             КАК Сумма,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА АмортизацияОС.Амортизация
	|		ИНАЧЕ 0 
	|	КОНЕЦ                             КАК СуммаУпр,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА АмортизацияОС.АмортизацияРегл + АмортизацияОС.АмортизацияЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                             КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = &Ссылка
	|ГДЕ
	|	НЕ &ОбъединениеОднотипныхОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровАмортизацииОС) КАК ХозяйственнаяОперация,
	|	&Период                           КАК Период,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	&ОсновноеСредство                 КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	&Организация                      КАК КорОрганизация,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА АмортизацияОС.Амортизация
	|		ИНАЧЕ 0 
	|	КОНЕЦ                             КАК Сумма,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА АмортизацияОС.Амортизация
	|		ИНАЧЕ 0 
	|	КОНЕЦ                             КАК СуммаУпр,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА АмортизацияОС.АмортизацияРегл + АмортизацияОС.АмортизацияЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                             КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = &Ссылка
	|ГДЕ
	|	&ОбъединениеОднотипныхОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Перемещение стоимости
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиОС) КАК ХозяйственнаяОперация,
	|	&Период                           КАК Период,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	&НаправлениеДеятельности          КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	&ОсновноеСредство                 КАК КорАналитикаАктивовПассивов,
	|	&ГруппаФинансовогоУчета           КАК КорГруппаФинансовогоУчета,
	|
	|	&Организация                      КАК КорОрганизация,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА СтоимостьОС.Стоимость
	|		ИНАЧЕ 0 
	|	КОНЕЦ                             КАК Сумма,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА СтоимостьОС.Стоимость
	|		ИНАЧЕ 0 
	|	КОНЕЦ                             КАК СуммаУпр,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА СтоимостьОС.СтоимостьРегл + СтоимостьОС.СтоимостьЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                             КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = &Ссылка
	|ГДЕ
	|	НЕ &ОбъединениеОднотипныхОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиОС) КАК ХозяйственнаяОперация,
	|	&Период                           КАК Период,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	&ОсновноеСредство                 КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	&Организация                      КАК КорОрганизация,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА СтоимостьОС.Стоимость
	|		ИНАЧЕ 0 
	|	КОНЕЦ                             КАК Сумма,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА СтоимостьОС.Стоимость
	|		ИНАЧЕ 0 
	|	КОНЕЦ                             КАК СуммаУпр,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА СтоимостьОС.СтоимостьРегл + СтоимостьОС.СтоимостьЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ                             КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = &Ссылка
	|ГДЕ
	|	&ОбъединениеОднотипныхОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Реклассификация аналитики обесценения
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияОС) КАК ХозяйственнаяОперация,
	|	&Период КАК Период,
	|	&Организация                                КАК Организация,
	|
	|	&Подразделение                              КАК Подразделение,
	|	&НаправлениеДеятельности                    КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства) КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                КАК АналитикаРасходов,
	|	ВТ_ОбесценениеВНА.ОбъектУчета               КАК АналитикаАктивовПассивов,
	|	&ГруппаФинансовогоУчета                     КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                              КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности           КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства) КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                КАК КорАналитикаРасходов,
	|	ВТ_ОбесценениеВНА.ОбъектУчета               КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета            КАК КорГруппаФинансовогоУчета,
	|
	|	ВТ_ОбесценениеВНА.Организация               КАК КорОрганизация,
	|
	|	ВТ_ОбесценениеВНА.ОбесценениеУпр            КАК Сумма,
	|	ВТ_ОбесценениеВНА.ОбесценениеУпр            КАК СуммаУпр,
	|	ВТ_ОбесценениеВНА.ОбесценениеРегл           КАК СуммаРегл
	|
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ВТ_ОбесценениеВНА
	|		ПО ВТ_ОбесценениеВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_ОбесценениеВНА.Ссылка = &Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтарыеЕГДС КАК ВтСтарыеЕГДС
	|		ПО ВтСтарыеЕГДС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			
	|ГДЕ
	|	ЕСТЬNULL(ВтСтарыеЕГДС.ЕГДС, ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка)) = &ЕГДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Восстановление обесценения при выбытии из состава ЕГДС
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВосстановлениеОбесцененияОС) КАК ХозяйственнаяОперация,
	|	&Период,
	|	&Организация                                КАК Организация,
	|
	|	&Подразделение                              КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства) КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                КАК АналитикаРасходов,
	|	ВТ_ОбесценениеВНА.ОбъектУчета               КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета            КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                              КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности           КАК КорНаправлениеДеятельности,
	|	ВтОтражениеДоходовРасходов.СтатьяДоходов    КАК КорСтатья,
	|	ВтОтражениеДоходовРасходов.АналитикаДоходов КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                КАК КорАналитикаРасходов,
	|	ВТ_ОбесценениеВНА.ОбъектУчета               КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета            КАК КорГруппаФинансовогоУчета,
	|
	|	ВТ_ОбесценениеВНА.Организация               КАК КорОрганизация,
	|
	|	ВТ_ОбесценениеВНА.ОбесценениеУпр            КАК Сумма,
	|	ВТ_ОбесценениеВНА.ОбесценениеУпр            КАК СуммаУпр,
	|	ВТ_ОбесценениеВНА.ОбесценениеРегл           КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ВТ_ОбесценениеВНА
	|		ПО ВТ_ОбесценениеВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И ВТ_ОбесценениеВНА.Ссылка = &Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтОтражениеДоходовРасходов КАК ВтОтражениеДоходовРасходов
	|		ПО ВтОтражениеДоходовРасходов.ВнеоборотныйАктив = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтарыеЕГДС КАК ВтСтарыеЕГДС
	|		ПО ВтСтарыеЕГДС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			
	|ГДЕ
	|	ЕСТЬNULL(ВтСтарыеЕГДС.ЕГДС, ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка)) <> &ЕГДС
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры


Процедура ТекстЗапросаСоставЕГДС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СоставЕГДС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОбъединениеОС.Ссылка КАК Регистратор,
	|	ОбъединениеОС.Дата КАК Период,
	|	ОбъединениеОС.Организация КАК Организация,
	|	ОбъединениеОС.ОсновноеСредство КАК ВнеоборотныйАктив,
	|	ОбъединениеОС.ЕГДС КАК ЕГДС
	|ИЗ
	|	Документ.ОбъединениеОС КАК ОбъединениеОС
	|ГДЕ
	|	ОбъединениеОС.Ссылка = &Ссылка
	|	И ОбъединениеОС.ЕГДС <> ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка)";
	
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаВтСтоимостьИсходныхОС(Запрос, ТекстыЗапроса) Экспорт

	ИмяТаблицы = "ВтСтоимостьИсходныхОС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяТаблицаСтоимостьВНА(ТекстыЗапроса, Запрос);
		
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СУММА(СтоимостьВНА.Стоимость)          КАК Стоимость,
	|	СУММА(СтоимостьВНА.СтоимостьРегл)      КАК СтоимостьРегл,
	|	СУММА(СтоимостьВНА.СтоимостьНУ)        КАК СтоимостьНУ,
	|	СУММА(СтоимостьВНА.СтоимостьПР)        КАК СтоимостьПР,
	|	СУММА(СтоимостьВНА.СтоимостьВР)        КАК СтоимостьВР,
	|	СУММА(СтоимостьВНА.СтоимостьЦФ)        КАК СтоимостьЦФ,
	|	СУММА(СтоимостьВНА.СтоимостьНУЦФ)      КАК СтоимостьНУЦФ,
	|	СУММА(СтоимостьВНА.СтоимостьПРЦФ)      КАК СтоимостьПРЦФ,
	|	СУММА(СтоимостьВНА.СтоимостьВРЦФ)      КАК СтоимостьВРЦФ
	|ПОМЕСТИТЬ ВтСтоимостьИсходныхОС
	|ИЗ
	|	Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьВНА
	|		ПО СтоимостьВНА.Ссылка = ТаблицаОС.Ссылка
	|			И СтоимостьВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаВтАмортизацияИсходныхОС(Запрос, ТекстыЗапроса) Экспорт

	ИмяТаблицы = "ВтАмортизацияИсходныхОС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяТаблицаАмортизацияВНА(ТекстыЗапроса, Запрос);
		
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СУММА(АмортизацияВНА.Амортизация)          КАК Амортизация,
	|	СУММА(АмортизацияВНА.АмортизацияРегл)      КАК АмортизацияРегл,
	|	СУММА(АмортизацияВНА.АмортизацияНУ)        КАК АмортизацияНУ,
	|	СУММА(АмортизацияВНА.АмортизацияПР)        КАК АмортизацияПР,
	|	СУММА(АмортизацияВНА.АмортизацияВР)        КАК АмортизацияВР,
	|	СУММА(АмортизацияВНА.АмортизацияЦФ)        КАК АмортизацияЦФ,
	|	СУММА(АмортизацияВНА.АмортизацияНУЦФ)      КАК АмортизацияНУЦФ,
	|	СУММА(АмортизацияВНА.АмортизацияПРЦФ)      КАК АмортизацияПРЦФ,
	|	СУММА(АмортизацияВНА.АмортизацияВРЦФ)      КАК АмортизацияВРЦФ
	|ПОМЕСТИТЬ ВтАмортизацияИсходныхОС
	|ИЗ
	|	Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияВНА
	|		ПО АмортизацияВНА.Ссылка = ТаблицаОС.Ссылка
	|			И АмортизацияВНА.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаВтНаработкиОбъектовЭксплуатации(Запрос, ТекстыЗапроса) Экспорт

	ИмяТаблицы = "ВтНаработкиОбъектовЭксплуатации";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаВтПорядокУчетаОСШапки(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НаработкиОбъектовЭксплуатации.Значение КАК Значение
	|ПОМЕСТИТЬ ВтНаработкиОбъектовЭксплуатации
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|			КОНЕЦПЕРИОДА(&Период, МЕСЯЦ),
	|			(ОбъектЭксплуатации, ПоказательНаработки) В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство,
	|					СписокОС.ПоказательНаработки
	|				ИЗ
	|					втПорядокУчетаОСШапки КАК СписокОС)) КАК НаработкиОбъектовЭксплуатации
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрисоединениеОС)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаВтПорядокУчетаОСШапки(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "втПорядокУчетаОСШапки";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПорядокУчета.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчета.ПоказательНаработки КАК ПоказательНаработки
	|ПОМЕСТИТЬ втПорядокУчетаОСШапки
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|				И ОсновноеСредство = &ОсновноеСредство) КАК ПорядокУчета
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрисоединениеОС)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтСведенияОС(ТекстыЗапроса)
	
	ИмяТаблицы = "втСведенияОС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтМестонахождениеОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаВтНалогообложениеНДС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрисоединениеОС)
	|			ТОГДА втПорядокУчетаОСПолучателя.ГруппаФинансовогоУчета
	|		КОГДА &ОбъединениеОднотипныхОС
	|			ТОГДА втПорядокУчетаОС.ГруппаФинансовогоУчета
	|		ИНАЧЕ &ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГруппаФинансовогоУчетаПолучателя,
	|
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрисоединениеОС)
	|			ТОГДА втПорядокУчетаОСПолучателя.НаправлениеДеятельности
	|		КОГДА &ОбъединениеОднотипныхОС
	|			ТОГДА втПорядокУчетаОС.НаправлениеДеятельности
	|		ИНАЧЕ &НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиПолучателя,
	|
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрисоединениеОС)
	|			ТОГДА втНалогообложениеНДСПолучателя.НалогообложениеНДС
	|		КОГДА &ОбъединениеОднотипныхОС
	|			ТОГДА втНалогообложениеНДС.НалогообложениеНДС
	|		ИНАЧЕ &НалогообложениеНДС
	|	КОНЕЦ КАК НалогообложениеНДСПолучателя,
	|
	|	втПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втПорядокУчетаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	втМестонахождениеОС.Арендатор КАК Арендатор
	|
	|ПОМЕСТИТЬ втСведенияОС
	|ИЗ
	|	Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК втПорядокУчетаОС
	|		ПО втПорядокУчетаОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК втПорядокУчетаОСПолучателя
	|		ПО втПорядокУчетаОСПолучателя.ОсновноеСредство = &ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК втМестонахождениеОС
	|		ПО втМестонахождениеОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНалогообложениеНДС КАК втНалогообложениеНДС
	|		ПО втНалогообложениеНДС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНалогообложениеНДС КАК втНалогообложениеНДСПолучателя
	|		ПО втНалогообложениеНДСПолучателя.ОсновноеСредство = &ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат ОбъединениеОСЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ОбъединениеОСЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ОбъединениеОСЛокализация.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);

КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыОС1(ПараметрыПечати, МассивОбъектов) Экспорт

	Возврат ОбъединениеОСЛокализация.ПолучитьДанныеДляПечатнойФормыОС1(МассивОбъектов);

КонецФункции

Функция ПолучитьДанныеДляПечатнойФормыОС4(МассивОбъектов) Экспорт

	Возврат ОбъединениеОСЛокализация.ПолучитьДанныеДляПечатнойФормыОС4(МассивОбъектов);

КонецФункции
 
#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбораСтатьиИАналитики = Новый Массив;
	
	ОбъединениеОСЛокализация.ДополнитьПараметрыВыбораСтатейИАналитик(ПараметрыВыбораСтатьиИАналитики);
	
	// СтатьяРасходовБУ
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходовБУ";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовБУ";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовБУ");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовБУ");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	// СтатьяРасходовУУ
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходовУУ";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовУУ";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовУУ");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовУУ");
	
	ПараметрыВыбораСтатьиИАналитики.Добавить(ПараметрыВыбора);
	
	Возврат ПараметрыВыбораСтатьиИАналитики;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
