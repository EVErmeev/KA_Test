#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	ДействияПередЗаписью();
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	ДействияПриЗаписи();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПервичноеЗаполнение

Процедура ЗаполнитьДату() Экспорт
	ТекущаяДата = ТекущаяДатаСеанса();
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДата;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ИдентификаторСообщения) Тогда
		Возврат;
	КонецЕсли;
	НачалоТекущегоДня = НачалоДня(ТекущаяДата);
	ВходящееСообщение = СЭДОФСС.ВходящееСообщение(ИдентификаторСообщения);
	Если ЗначениеЗаполнено(ВходящееСообщение.Дата)
		И ВходящееСообщение.Дата < НачалоТекущегоДня
		И ВходящееСообщение.Дата < Дата Тогда
		Дата = ВходящееСообщение.Дата;
	КонецЕсли;
	Если ЗначениеЗаполнено(ВходящееСообщение.ДатаСоздания)
		И ВходящееСообщение.ДатаСоздания < НачалоТекущегоДня
		И ВходящееСообщение.ДатаСоздания < Дата Тогда
		Дата = ВходящееСообщение.ДатаСоздания;
	КонецЕсли;
	Если ЗначениеЗаполнено(ВходящееСообщение.ДатаЗагрузки)
		И ВходящееСообщение.ДатаЗагрузки < НачалоТекущегоДня
		И ВходящееСообщение.ДатаЗагрузки < Дата Тогда
		Дата = ВходящееСообщение.ДатаЗагрузки;
	КонецЕсли;
КонецПроцедуры

Функция ЗаполнитьПервичныйДокумент() Экспорт
	Возврат СЭДОФСС.ЗаполнитьПервичныйДокументДляРасчетаПособияФСС(ЭтотОбъект, Ложь);
КонецФункции

Функция ЗаполнитьДатыОтправки() Экспорт
	Если Обработан Тогда
		МаксДатаОтвета = '00010101';
	Иначе
		МаксДатаОтвета = Документы.ВходящийЗапросФССДляРасчетаПособия.МаксимальнаяДатаОтвета(ЭтотОбъект);
	КонецЕсли;
	ЕстьИзменения = (МаксДатаОтвета <> МаксимальнаяДатаОтвета);
	МаксимальнаяДатаОтвета = МаксДатаОтвета;
	Возврат ЕстьИзменения;
КонецФункции

#КонецОбласти

#Область ПередЗаписью

Процедура ДействияПередЗаписью() Экспорт
	Если Не ЗначениеЗаполнено(ДатаСоздания) Тогда
		ДатаСоздания = ТекущаяДата(); // АПК:143 Для фильтрации событий в журнале регистрации требуется дата сервера.
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЗначенияРеквизитовДоЗаписи", ЗначенияРеквизитовДоЗаписи());
	
	ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Страхователь);
	
	// Физлицо заполняется безусловно, т.к. определяет права.
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "ФизическоеЛицо");
	ИначеЕсли ЗначениеЗаполнено(ФизическоеЛицо) И ЗначениеЗаполнено(Страхователь) Тогда
		УстановитьПривилегированныйРежим(Истина);
		КадровыеДанные = КадровыйУчет.КадровыеДанныеОсновногоСотрудникаФизическогоЛица(
			Страхователь,
			ФизическоеЛицо,
			"Сотрудник, Организация",
			ТекущаяДатаСеанса(),
			Ложь,
			СотрудникТекстОшибкиПоиска);
		УстановитьПривилегированныйРежим(Ложь);
		Если КадровыеДанные <> Неопределено Тогда
			Если ЗначениеЗаполнено(КадровыеДанные.Организация) Тогда
				Организация = КадровыеДанные.Организация;
			КонецЕсли;
			Сотрудник = КадровыеДанные.Сотрудник;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ЗначенияРеквизитовДоЗаписи()
	ИменаРеквизитов = "ГоловнаяОрганизация, НомерЛН, ПометкаУдаления";
	Если ЭтоНовый() Тогда
		Возврат ОбщегоНазначенияБЗК.ЗначенияСвойств(ЭтотОбъект, ИменаРеквизитов);
	Иначе
		Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов);
	КонецЕсли;
КонецФункции

#КонецОбласти

#Область ПриЗаписи

Процедура ДействияПриЗаписи() Экспорт
	РегистрыСведений.СведенияОбЭЛН.ПриЗаписиВходящегоЗапроса(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли