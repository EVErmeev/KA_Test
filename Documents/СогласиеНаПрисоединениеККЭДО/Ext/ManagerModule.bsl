#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = ИдентификаторКомандыПечатиЗаявления();
	КомандаПечати.Представление = НСтр("ru = 'Заявление на согласие'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Документ.СогласиеНаПрисоединениеККЭДО";
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(
		КоллекцияПечатныхФорм,
		ИдентификаторКомандыПечатиЗаявления());
	Если ПечатнаяФорма <> Неопределено Тогда
		ПечатнаяФорма.ТабличныйДокумент = ПечатьСогласияНаПрисоединениеККЭДО(МассивОбъектов, ОбъектыПечати);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Согласие на присоединение к КЭДО'");
		ПечатнаяФорма.ПолныйПутьКМакету = Метаданные.Документы.СогласиеНаПрисоединениеККЭДО.Макеты.ПФ_MXL_СогласиеНаПрисоединениеККЭДО_БумажныйДокумент.ПолноеИмя();
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИдентификаторКомандыПечатиЗаявления() Экспорт
	Возврат "ПФ_MXL_СогласиеНаПрисоединениеККЭДО";
КонецФункции

Функция ПечатьСогласияНаПрисоединениеККЭДО(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	СогласиеНаПрисоединениеККЭДО.Дата КАК Дата,
	               |	СогласиеНаПрисоединениеККЭДО.Номер КАК Номер,
	               |	СогласиеНаПрисоединениеККЭДО.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СогласиеНаПрисоединениеККЭДО.Организация КАК Организация,
	               |	СогласиеНаПрисоединениеККЭДО.ФизическоеЛицо.ФИО КАК ФизическоеЛицоФИО,
	               |	СогласиеНаПрисоединениеККЭДО.ФизическоеЛицо.ИНН КАК ФизическоеЛицоИНН,
	               |	СогласиеНаПрисоединениеККЭДО.ФизическоеЛицо.СтраховойНомерПФР КАК ФизическоеЛицоСНИЛС,
	               |	СогласиеНаПрисоединениеККЭДО.ФизическоеЛицо.ДатаРождения КАК ФизическоеЛицоДатаРождения,
	               |	СогласиеНаПрисоединениеККЭДО.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	               |	СогласиеНаПрисоединениеККЭДО.Организация.ИНН КАК ОрганизацияИНН,
	               |	СогласиеНаПрисоединениеККЭДО.Организация.НаименованиеСокращенное КАК ОрганизацияНаименованиеСокращенное,
	               |	СогласиеНаПрисоединениеККЭДО.Ссылка КАК Ссылка,
	               |	СогласиеНаПрисоединениеККЭДО.Руководитель КАК Руководитель,
	               |	СогласиеНаПрисоединениеККЭДО.ДолжностьРуководителя КАК ДолжностьРуководителя,
	               |	СогласиеНаПрисоединениеККЭДО.ОснованиеПодписиРуководителя КАК ОснованиеПодписиРуководителя
	               |ИЗ
	               |	Документ.СогласиеНаПрисоединениеККЭДО КАК СогласиеНаПрисоединениеККЭДО
	               |ГДЕ
	               |	СогласиеНаПрисоединениеККЭДО.Ссылка В(&МассивСогласий)";
	Запрос.УстановитьПараметр("МассивСогласий", МассивОбъектов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Истина);
	НастройкиИнтеграции = РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.НастройкиИнтеграции();
	СпособПолученияСогласия = НастройкиИнтеграции.СпособПолученияСогласияНаПрисоединениеККЭДО;
	Макет = УправлениеПечатью.МакетПечатнойФормы(СоответствиеСпособПолученияСогласияИИмяМакета()[СпособПолученияСогласия]);		
	НастройкиСервиса = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока Выборка.Следующий() Цикл
		
		ПараметрыОбластьДокумент = Новый Структура;
		ПараметрыОбластьДокумент.Вставить("Номер", Выборка.Номер);
		ПараметрыОбластьДокумент.Вставить("Организация", Выборка.Организация);
		ПараметрыОбластьДокумент.Вставить("Дата", Формат(Выборка.Дата, "ДЛФ=DD"));
		ПараметрыОбластьДокумент.Вставить("ФИО", Выборка.ФизическоеЛицоФИО);
		ПараметрыОбластьДокумент.Вставить("ОрганизацияНаименованиеПолное", Выборка.ОрганизацияНаименованиеПолное);
		ПараметрыОбластьДокумент.Вставить("РуководительДолжность", Выборка.ДолжностьРуководителя);
		ПараметрыОбластьДокумент.Вставить("РуководительФИОПолные", Выборка.Руководитель);
		ПараметрыОбластьДокумент.Вставить("ОснованиеРуководителя", Выборка.ОснованиеПодписиРуководителя);
		Если ЗначениеЗаполнено(НастройкиСервиса.АдресПриложенияПоИмени) Тогда
			ПараметрыОбластьДокумент.Вставить("АдресЛичногоКабинета", НастройкиСервиса.АдресПриложенияПоИмени);
		Иначе
			ПараметрыОбластьДокумент.Вставить("АдресЛичногоКабинета", НастройкиСервиса.АдресПриложения);
		КонецЕсли;
		
		ОбластьДокумент = Макет.ПолучитьОбласть("Документ");
		ОбластьДокумент.Параметры.Заполнить(ПараметрыОбластьДокумент);
		ТабличныйДокумент.Вывести(ОбластьДокумент);
		
		ОрганизацияЮрАдрес = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(
			Выборка.Организация,
			ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации"),
			,
			Выборка.Дата);
		ОрганизацияФактАдрес = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(
			Выборка.Организация,
			ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ФактАдресОрганизации"),
			,
			Выборка.Дата);
		АдресМестаПроживания = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(
			Выборка.ФизическоеЛицо,
			ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица"),
			,
			Выборка.Дата);
		ДокументУдостоверяющийЛичность = РегистрыСведений.ДокументыФизическихЛиц.ДокументУдостоверяющийЛичностьФизлица(
			Выборка.ФизическоеЛицо);

		ПараметрыОбластьПодписи = Новый Структура;
		ПараметрыОбластьПодписи.Вставить("ОрганизацияНаименованиеПолное", Выборка.ОрганизацияНаименованиеПолное);
		ПараметрыОбластьПодписи.Вставить("ОрганизацияНаименованиеСокращенное", Выборка.ОрганизацияНаименованиеСокращенное);
		ПараметрыОбластьПодписи.Вставить("ОрганизацияЮрАдрес", ОрганизацияЮрАдрес);
		ПараметрыОбластьПодписи.Вставить("ОрганизацияФактАдрес", ОрганизацияФактАдрес);
		ПараметрыОбластьПодписи.Вставить("ОрганизацияИНН", Выборка.ОрганизацияИНН);
		ПараметрыОбластьПодписи.Вставить("ФИО", Выборка.ФизическоеЛицоФИО);
		ПараметрыОбластьПодписи.Вставить("АдресМестаПроживания", АдресМестаПроживания);
		ПараметрыОбластьПодписи.Вставить("ДокументУдостоверяющийЛичность", ДокументУдостоверяющийЛичность);
		ПараметрыОбластьПодписи.Вставить("СНИЛС", Выборка.ФизическоеЛицоСНИЛС);
		
		ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
		ОбластьПодписи.Параметры.Заполнить(ПараметрыОбластьПодписи);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		Если Макет.Области.Найти("ШтампыЭП") <> Неопределено Тогда
			ОбластьШтампыЭП = Макет.ПолучитьОбласть("ШтампыЭП");
			ТабличныйДокумент.Вывести(ОбластьШтампыЭП);
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 1, ОбъектыПечати, Выборка.Ссылка);
		
	КонецЦикла;
	
    Возврат ТабличныйДокумент;
	
КонецФункции

Функция СоответствиеСпособПолученияСогласияИИмяМакета()
	
	Соответствие = Новый Соответствие;
	
	Соответствие.Вставить(
		Перечисления.СпособыПолученияСогласияНаПрисоединениеККЭДО.БумажныйДокумент,
		Метаданные.Документы.СогласиеНаПрисоединениеККЭДО.Макеты.ПФ_MXL_СогласиеНаПрисоединениеККЭДО_БумажныйДокумент.ПолноеИмя());
	Соответствие.Вставить(
		Перечисления.СпособыПолученияСогласияНаПрисоединениеККЭДО.УНЭП,
		Метаданные.Документы.СогласиеНаПрисоединениеККЭДО.Макеты.ПФ_MXL_СогласиеНаПрисоединениеККЭДО_УНЭП.ПолноеИмя());
		
	Для Каждого КлючИЗначение Из Соответствие Цикл
		Соответствие[КлючИЗначение.Ключ] = СтрЗаменить(КлючИЗначение.Значение, "Макет.", "");	
	КонецЦикла;
		
	Возврат Соответствие;
	
КонецФункции

#КонецОбласти

#КонецЕсли