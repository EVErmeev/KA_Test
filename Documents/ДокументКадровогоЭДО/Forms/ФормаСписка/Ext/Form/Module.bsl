#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.КомандыФормыГруппа;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СтруктураПараметровОтбора = Новый Структура();
	ЗарплатаКадры.ДобавитьПараметрОтбора(СтруктураПараметровОтбора, "ФизическоеЛицо",
		Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"), НСтр("ru = 'Сотрудник'"));
	
	ЗарплатаКадры.ДобавитьПараметрОтбора(СтруктураПараметровОтбора, "ВидОснования",
		Новый ОписаниеТипов(), НСтр("ru = 'Вид основания'"));
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список",,
		СтруктураПараметровОтбора, "СписокКритерииОтбора");
	
	ЗапрещенныеРасширения = РаботаСФайламиСлужебный.СписокЗапрещенныхРасширений();
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
	Иначе
		Элементы.Список.РежимВыбора = Ложь;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	ПоказыватьРеквизитыПериодаИПубликации = Ложь;
	Если Параметры.РасчетныеЛисты Тогда
		
		СкрытьПодменюВыбораВидаСписка();
		ПоказыватьРеквизитыПериодаИПубликации = Истина;
		Заголовок = НСтр("ru = 'Документы кадрового ЭДО (расчетные листки)'");
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "КатегорияДокумента", Перечисления.КатегорииДокументовКадровогоЭДО.РасчетныйЛисток, ВидСравненияКомпоновкиДанных.Равно, ,
			Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	ИначеЕсли Параметры.ДокументыНаПодпись Тогда
		СкрытьПодменюВыбораВидаСписка();
		УстановитьВидСписка(ЭтотОбъект, Команды.НаПодпись);
	Иначе
		УстановитьВидСписка(ЭтотОбъект, Команды.ВсеДокументы);
	КонецЕсли;
	
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Список, "СодержимоеДокументов", КадровыйЭДО.ДоступныеСодержанияДокументовПользователя(Пользователи.ТекущийПользователь()), Истина);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Период",
		"Видимость",
		ПоказыватьРеквизитыПериодаИПубликации);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДатаПубликации",
		"Видимость",
		ПоказыватьРеквизитыПериодаИПубликации);
	
	НайденныеПараметры = ЭтотОбъект.ПараметрыКритерияОтбора.НайтиСтроки(Новый Структура("ИмяПараметра", "ВидОснования"));
	Если НайденныеПараметры.Количество() > 0 Тогда
		Элемент = Элементы.Найти(НайденныеПараметры[0].ИмяЭлементаФормыПараметра);
		Если Элемент <> Неопределено Тогда
			Элемент.РежимВыбораИзСписка = Истина;
			Элемент.ВыбиратьТип = Ложь;
			Элемент.КнопкаОчистки = Истина;
			Элемент.СписокВыбора.Очистить();
			Элемент.СписокВыбора.Добавить(Перечисления.КатегорииДокументовКадровогоЭДО.РасчетныйЛисток,
				Строка(Перечисления.КатегорииДокументовКадровогоЭДО.РасчетныйЛисток));
			Элемент.СписокВыбора.Добавить(Перечисления.КатегорииДокументовКадровогоЭДО.ЗаявлениеСотрудника,
				Строка(Перечисления.КатегорииДокументовКадровогоЭДО.ЗаявлениеСотрудника));
			Для Каждого ТипСПечатнымиФормами Из Метаданные.ОпределяемыеТипы.ОбъектСПечатнымиФормами.Тип.Типы() Цикл
				Если ТипСПечатнымиФормами = Тип("ДокументСсылка.ДокументКадровогоЭДО") Тогда
					Элемент.СписокВыбора.Добавить(ТипСПечатнымиФормами, НСтр("ru = 'Внешний документ'"));
				Иначе
					Элемент.СписокВыбора.Добавить(ТипСПечатнымиФормами, Строка(ТипСПечатнымиФормами));
				КонецЕсли;
			КонецЦикла;
			Элемент.СписокВыбора.СортироватьПоПредставлению();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновленыДанныеДокументовКЭДО" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура Подключаемый_ПараметрОтбораПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ПараметрОтбораНаФормеСДинамическимСпискомПриИзменении(ЭтотОбъект, Элемент.Имя);
	Если СтрНачинаетсяС(Элемент.Имя, "ВидОснования_") Тогда
		
		НайденныеПараметры = ЭтотОбъект.ПараметрыКритерияОтбора.НайтиСтроки(Новый Структура("ИмяПараметра", "ВидОснования"));
		Если НайденныеПараметры.Количество() > 0 Тогда
			
			Использование = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
				ЭтотОбъект, НайденныеПараметры[0].ИмяРеквизитаФормыПараметраИспользование);
			
			ВидОснования = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
				ЭтотОбъект, НайденныеПараметры[0].ИмяРеквизитаФормыПараметра);
			
			Если ВидОснования = Неопределено Тогда
				ВидОснования = Тип("Строка");
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				Список, "Внешний", Истина,
				Использование И ВидОснования = Тип("ДокументСсылка.ДокументКадровогоЭДО"));
			
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				Список, "ВидОснованияКатегория", ВидОснования,
				Использование И ТипЗнч(ВидОснования) = Тип("ПеречислениеСсылка.КатегорииДокументовКадровогоЭДО"));
			
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				Список, "ВидОснованияДокумент", ВидОснования,
				Использование И ТипЗнч(ВидОснования) <> Тип("ПеречислениеСсылка.КатегорииДокументовКадровогоЭДО"));
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Если Строки.Количество() > 0 Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ЭлектронныеДокументы = Новый Массив;
		СтрокиЭлектронныхДокументов = Новый Соответствие;
		Для Каждого СтрокаСписка Из Строки Цикл
			Если ЗначениеЗаполнено(СтрокаСписка.Значение.Данные.ЭлектронныйДокумент) Тогда
				ЭлектронныеДокументы.Добавить(СтрокаСписка.Значение.Данные.ЭлектронныйДокумент);
				СтрокиЭлектронныхДокументов.Вставить(СтрокаСписка.Значение.Данные.ЭлектронныйДокумент, СтрокаСписка);
			КонецЕсли;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Исполнитель", Пользователи.ТекущийПользователь());
		Запрос.УстановитьПараметр("ПрисоединенныеФайлы", ЭлектронныеДокументы);
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТаблицаРегистра.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
			|	ВЫБОР
			|		КОГДА ТаблицаРегистра.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСФайламиДокументовКЭДО.Подписать)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ОжидаетПодписания,
			|	ВЫБОР
			|		КОГДА ТаблицаРегистра.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСФайламиДокументовКЭДО.Ознакомиться)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ТребуетВнимания
			|ИЗ
			|	РегистрСведений.ЗапланированныеДействияСФайламиДокументовКЭДО КАК ТаблицаРегистра
			|ГДЕ
			|	ТаблицаРегистра.ПрисоединенныйФайл В(&ПрисоединенныеФайлы)
			|	И ТаблицаРегистра.Исполнитель = &Исполнитель
			|	И ТаблицаРегистра.Действие В (ЗНАЧЕНИЕ(Перечисление.ДействияСФайламиДокументовКЭДО.Подписать), ЗНАЧЕНИЕ(Перечисление.ДействияСФайламиДокументовКЭДО.Ознакомиться))";
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		
		Пока Выборка.Следующий() Цикл
			СтрокаСписка = СтрокиЭлектронныхДокументов.Получить(Выборка.ПрисоединенныйФайл);
			Если СтрокаСписка <> Неопределено Тогда
				Если Выборка.ОжидаетПодписания Тогда
					СтрокаСписка.Значение.Данные.ОжидаетПодписания = Истина;
				КонецЕсли; 
				СтрокаСписка.Значение.Данные.ТребуетВнимания = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Запрос.УстановитьПараметр("ДокументыКЭДО", ОбщегоНазначения.ВыгрузитьКолонку(Строки, "Ключ"));
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПодписиДокументовКЭДО.Объект КАК Объект,
			|	ПодписиДокументовКЭДО.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ПодписиДокументовКЭДО.Отпечаток КАК Отпечаток,
			|	ПодписиДокументовКЭДО.ДатаПодписи КАК ДатаПодписи,
			|	ПодписиДокументовКЭДО.РезультатСогласования КАК РезультатСогласования
			|ПОМЕСТИТЬ ВТПодписи
			|ИЗ
			|	РегистрСведений.ПодписиДокументовКЭДО КАК ПодписиДокументовКЭДО
			|ГДЕ
			|	ПодписиДокументовКЭДО.Объект В(&ДокументыКЭДО)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДокументКадровогоЭДОВнешниеПодписанты.Ссылка КАК Ссылка,
			|	ДокументКадровогоЭДОВнешниеПодписанты.ФизическоеЛицо КАК ФизическоеЛицо,
			|	МАКСИМУМ(ВЫБОР
			|			КОГДА ПодписанныеОбъектыПользователей.ПодписанныйОбъект ЕСТЬ NULL
			|				ТОГДА ЛОЖЬ
			|			ИНАЧЕ ИСТИНА
			|		КОНЕЦ) КАК ЕстьПодписи
			|ПОМЕСТИТЬ ВТПодписанты
			|ИЗ
			|	Документ.ДокументКадровогоЭДО.ВнешниеПодписанты КАК ДокументКадровогоЭДОВнешниеПодписанты
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодписанныеОбъектыПользователей КАК ПодписанныеОбъектыПользователей
			|		ПО ДокументКадровогоЭДОВнешниеПодписанты.Ссылка.ЭлектронныйДокумент = ПодписанныеОбъектыПользователей.ПодписанныйОбъект
			|ГДЕ
			|	ДокументКадровогоЭДОВнешниеПодписанты.Ссылка В(&ДокументыКЭДО)
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокументКадровогоЭДОВнешниеПодписанты.Ссылка,
			|	ДокументКадровогоЭДОВнешниеПодписанты.ФизическоеЛицо
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Подписанты.Ссылка КАК Ссылка,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Подписанты.ФизическоеЛицо) КАК КоличествоПодписантов,
			|	СУММА(ВЫБОР
			|			КОГДА ЕСТЬNULL(Подписи.РезультатСогласования, ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.Отклонено)
			|				ТОГДА 1
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК КоличествоОтказов,
			|	СУММА(ВЫБОР
			|			КОГДА ЕСТЬNULL(Подписи.РезультатСогласования, ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.Отклонено)
			|				ТОГДА ВЫБОР
			|						КОГДА ЕСТЬNULL(Подписи.Отпечаток, """") <> """"
			|							ТОГДА 1
			|						ИНАЧЕ 0
			|					КОНЕЦ
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК КоличествоУНЭПУКЭП,
			|	СУММА(ВЫБОР
			|			КОГДА ЕСТЬNULL(Подписи.РезультатСогласования, ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.Отклонено)
			|				ТОГДА ВЫБОР
			|						КОГДА ЕСТЬNULL(Подписи.Отпечаток, НЕОПРЕДЕЛЕНО) = """"
			|							ТОГДА 1
			|						ИНАЧЕ 0
			|					КОНЕЦ
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК КоличествоБезУНЭПУКЭП,
			|	СУММА(ВЫБОР
			|			КОГДА ЕСТЬNULL(Подписи.РезультатСогласования, ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.Отклонено)
			|				ТОГДА ВЫБОР
			|						КОГДА Подписи.ФизическоеЛицо ЕСТЬ NULL
			|							ТОГДА 1
			|						ИНАЧЕ 0
			|					КОНЕЦ
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК КоличествоНеОзнакомленных,
			|	Подписанты.ЕстьПодписи КАК ЕстьПодписи
			|ИЗ
			|	ВТПодписанты КАК Подписанты
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПодписи КАК Подписи
			|		ПО Подписанты.Ссылка = Подписи.Объект
			|			И Подписанты.ФизическоеЛицо = Подписи.ФизическоеЛицо
			|
			|СГРУППИРОВАТЬ ПО
			|	Подписанты.Ссылка,
			|	Подписанты.ЕстьПодписи";
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		РасширенияДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ЭлектронныеДокументы, "Расширение");
		УстановитьПривилегированныйРежим(Ложь);
		
		Пока Выборка.Следующий() Цикл
			СтрокаСписка = Строки.Получить(Выборка.Ссылка);
			Если СтрокаСписка <> Неопределено Тогда
				СтрокаСписка.Данные.БЗК_ДокументСВерсиейДляПечати = Выборка.ЕстьПодписи;
				СтрокаСписка.Данные.Расширение = РасширенияДокументов.Получить(СтрокаСписка.Данные.ЭлектронныйДокумент);
				Если Выборка.КоличествоОтказов > 0 Тогда
					ПодписанСотрудниками = СтрШаблон(
						НСтр("ru = 'Отклонено (%1)'"),
						Выборка.КоличествоОтказов);
				ИначеЕсли Выборка.КоличествоНеОзнакомленных > 0
					И Выборка.КоличествоУНЭПУКЭП = 0
					И Выборка.КоличествоБезУНЭПУКЭП = 0 Тогда
					
					ПодписанСотрудниками = СтрШаблон(
						НСтр("ru = 'Не подписано (%1)'"),
						Выборка.КоличествоНеОзнакомленных);
				ИначеЕсли Выборка.КоличествоУНЭПУКЭП > 0
					И Выборка.КоличествоБезУНЭПУКЭП = 0
					И Выборка.КоличествоНеОзнакомленных = 0 Тогда
					
					ПодписанСотрудниками = СтрШаблон(
						НСтр("ru = 'Подписано ЭП (%1)'"),
						Выборка.КоличествоУНЭПУКЭП);
					СтрокаСписка.Данные.БЗК_ДокументСВерсиейДляПечати = Истина;
				ИначеЕсли Выборка.КоличествоБезУНЭПУКЭП > 0
					И Выборка.КоличествоУНЭПУКЭП = 0
					И Выборка.КоличествоНеОзнакомленных = 0 Тогда
					
					ПодписанСотрудниками = СтрШаблон(
						НСтр("ru = 'Требуется собственноручная подпись (%1)'"),
						Выборка.КоличествоБезУНЭПУКЭП);
				ИначеЕсли Выборка.КоличествоУНЭПУКЭП > 0
					И Выборка.КоличествоБезУНЭПУКЭП > 0
					И Выборка.КоличествоНеОзнакомленных = 0 Тогда
					
					ПодписанСотрудниками = СтрШаблон(
						НСтр("ru = 'Подписано ЭП (%1), требуется собственноручная подпись (%2)'"),
						Выборка.КоличествоУНЭПУКЭП,
						Выборка.КоличествоБезУНЭПУКЭП);
					СтрокаСписка.Данные.БЗК_ДокументСВерсиейДляПечати = Истина;
				ИначеЕсли Выборка.КоличествоУНЭПУКЭП > 0
					И Выборка.КоличествоНеОзнакомленных > 0
					И Выборка.КоличествоБезУНЭПУКЭП = 0 Тогда
					
					ПодписанСотрудниками = СтрШаблон(
						НСтр("ru = 'Подписано ЭП (%1), не подписано (%2)'"),
						Выборка.КоличествоУНЭПУКЭП,
						Выборка.КоличествоНеОзнакомленных);
					СтрокаСписка.Данные.БЗК_ДокументСВерсиейДляПечати = Истина;
				ИначеЕсли Выборка.КоличествоБезУНЭПУКЭП > 0
					И Выборка.КоличествоНеОзнакомленных > 0
					И Выборка.КоличествоУНЭПУКЭП = 0 Тогда
					
					ПодписанСотрудниками = СтрШаблон(
						НСтр("ru = 'Требуется собственноручная подпись (%1), не подписано (%2)'"),
						Выборка.КоличествоБезУНЭПУКЭП,
						Выборка.КоличествоНеОзнакомленных);
				ИначеЕсли Выборка.КоличествоУНЭПУКЭП = 0
					И Выборка.КоличествоБезУНЭПУКЭП = 0
					И Выборка.КоличествоНеОзнакомленных = 0 Тогда
					
					ПодписанСотрудниками = НСтр("ru = 'Подписи не требуются'");
				Иначе
					ПодписанСотрудниками = СтрШаблон(
						НСтр("ru = 'Подписано ЭП (%1), требуется собственноручная подпись (%2), не подписано (%3)'"),
						Выборка.КоличествоУНЭПУКЭП,
						Выборка.КоличествоБезУНЭПУКЭП,
						Выборка.КоличествоНеОзнакомленных);
					СтрокаСписка.Данные.БЗК_ДокументСВерсиейДляПечати = Истина;
				КонецЕсли;
				Если Выборка.КоличествоПодписантов = 1 Тогда
					СтрокаСписка.Данные.ПодписанСотрудниками = СтрЗаменить(ПодписанСотрудниками, " (1)", "");
				Иначе
					СтрокаСписка.Данные.ПодписанСотрудниками = ПодписанСотрудниками;
				КонецЕсли;
			КонецЕсли;
			Если СтрокаСписка.Данные.БЗК_ДокументСВерсиейДляПечати Тогда
				Если СтрокаСписка.Данные.Внешний Тогда
					СтрокаСписка.Данные.БЗК_ДокументСВерсиейДляПечати = КадровыйЭДОКлиентСервер.ЭтоРасширениеФайлаСПредставлением(СтрокаСписка.Данные.Расширение);
				Иначе
					СтрокаСписка.Данные.БЗК_ДокументСВерсиейДляПечати = Не КадровыйЭДОКлиентСервер.ЭтоРасширениеJSONДокумента(СтрокаСписка.Данные.Расширение);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ВсеДокументы(Команда)
	
	УстановитьВидСписка(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура НаПодпись(Команда)
	
	УстановитьВидСписка(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подписанные(Команда)
	
	УстановитьВидСписка(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СкрытьПодменюВыбораВидаСписка()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВидСпискаПодменю",
		"Видимость",
		Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидСписка(УправляемаяФорма, Команда)
	
	Элементы = УправляемаяФорма.Элементы;
	Команды = УправляемаяФорма.Команды;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		УправляемаяФорма.Список, "Исполнители", УправляемаяФорма.ТекущийПользователь,
		Команда = Команды.НаПодпись);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		УправляемаяФорма.Список, "Подписанты", УправляемаяФорма.ТекущийПользователь,
		Команда = Команды.Подписанные);
	
	Если Команда = Команды.ВсеДокументы Тогда
		УправляемаяФорма.Заголовок = НСтр("ru = 'Документы кадрового ЭДО'");
		УстановитьПометкуВидаСпискаСписка(УправляемаяФорма, Элементы.ВсеДокументы);
	ИначеЕсли Команда = Команды.НаПодпись Тогда
		УправляемаяФорма.Заголовок = НСтр("ru = 'Документы кадрового ЭДО (на подпись мне)'");
		УстановитьПометкуВидаСпискаСписка(УправляемаяФорма, Элементы.НаПодпись);
	ИначеЕсли Команда = Команды.Подписанные Тогда
		УправляемаяФорма.Заголовок = НСтр("ru = 'Документы кадрового ЭДО (подписанные мною)'");
		УстановитьПометкуВидаСпискаСписка(УправляемаяФорма, Элементы.Подписанные);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование		= Истина;
	ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Список.ЭлектронныйДокумент");
	ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.НеЗаполнено;
	ЭлементОтбора.ПравоеЗначение	= Истина;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("Список.ОснованиеДокумента"));
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ЭлектронныйДокумент");
	ОформляемоеПоле.Использование = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПометкуВидаСпискаСписка(УправляемаяФорма, ВыбранныйВидСписка)
	
	Элементы = УправляемаяФорма.Элементы;
	Для Каждого ПодчиненныйЭлемент Из Элементы.ВидСпискаПодменю.ПодчиненныеЭлементы Цикл
		ПодчиненныйЭлемент.Пометка = ПодчиненныйЭлемент = ВыбранныйВидСписка;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
