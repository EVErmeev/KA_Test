
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетНДС");
	
	ИзменениеУсловийДоговораАрендыЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры);

		ТекстЗапросаТаблицаАрендованныеОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаУсловияДоговоровАренды(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикОплатУслугПоАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикНачисленияУслугПоАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
	
		ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаТоварыКОформлениюДокументовИмпорта(Запрос, ТекстыЗапроса, Регистры);
		
		
		ИзменениеУсловийДоговораАрендыЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	КонецЕсли;

	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИзменениеУсловийДоговораАренды - Документ, для которого формируются движения.
//  МенеджерВременныхТаблиц	- МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Неопределено);

	ТекстЗапросаТаблицаУсловияДоговоровАренды(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Неопределено);

	ТаблицыДляДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(
							Запрос, ТекстыЗапроса, Неопределено);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

#КонецОбласти

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);

КонецПроцедуры

// Добавляет команду создания документа "Изменение условий договора аренды".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Порядок - Число -
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании, Порядок = 0) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ИзменениеУсловийДоговораАренды) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ИзменениеУсловийДоговораАренды.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ИзменениеУсловийДоговораАренды);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.Порядок = Порядок;
	
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки	 - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область Прочее

// Возвращает параметры механизма взаиморасчетов.
//
// Параметры:
// 	ДанныеЗаполнения - ДокументОбъект, СправочникОбъект, ДокументСсылка, СправочникСсылка, Структура, ДанныеФормыСтруктура - Объект или коллекция для
//              расчета параметров взаиморасчетов.
//
// Возвращаемое значение:
// 	См. ВзаиморасчетыСервер.ПараметрыМеханизма
//
Функция ПараметрыВзаиморасчеты(ДанныеЗаполнения = Неопределено) Экспорт
	
	СтруктураПараметров = ВзаиморасчетыСервер.ПараметрыМеханизма();
	
	СтруктураПараметров.ЭтоПродажаЗакупка                = Истина;
	СтруктураПараметров.ИзменяетПланОплаты               = Ложь;
	СтруктураПараметров.ИзменяетПланОтгрузкиПоставки     = Ложь;

	#Область НеобязательныеПараметры
	
	СтруктураПараметров.ВалютаВзаиморасчетов             = "Объект.ВалютаВзаиморасчетов";
	СтруктураПараметров.СуммаВзаиморасчетов              = "Объект.СуммаВзаиморасчетов";
	СтруктураПараметров.НаправлениеДеятельности          = "";
	СтруктураПараметров.ПорядокРасчетов                  = "";
	СтруктураПараметров.ПутьКДаннымТЧ                    = "";
	СтруктураПараметров.Соглашение                       = "";
	СтруктураПараметров.БанковскийСчетОрганизации        = "";
	СтруктураПараметров.БанковскийСчетКонтрагента        = "";
	СтруктураПараметров.Касса                            = "";
	СтруктураПараметров.ФормаОплаты                      = "";
	СтруктураПараметров.ОплатаВВалюте                    = "";
	СтруктураПараметров.ИдентификаторПлатежа             = "";
	СтруктураПараметров.Договор                          = "";
	
	// Реквизиты для объекта расчетов, используются в проведении.
	СтруктураПараметров.ГруппаФинансовогоУчета           = "";
	СтруктураПараметров.Подразделение                    = "";
	СтруктураПараметров.КурсЧислитель                    = "Объект.КурсЧислитель";
	СтруктураПараметров.КурсЗнаменатель                  = "Объект.КурсЗнаменатель";
	
	СтруктураПараметров.ЭлементыФормы.НадписьВалюты = "ДекорацияВалюты";
	СтруктураПараметров.ВалютыИКурсДокументаТолькоПросмотр = Ложь;
	СтруктураПараметров.ВалютаДокументаТолькоПросмотр = Ложь;
	
	СтруктураПараметров.ВалютаВзаиморасчетовТолькоПросмотр = Истина;
	
	#КонецОбласти
	
	Возврат СтруктураПараметров;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос)
	
	Если Запрос.Параметры.Свойство("КлючиАналитикиУчетаПоПартнерамИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация                    КАК Организация,
	|	ДанныеДокумента.Партнер                        КАК Партнер,
	|	ДанныеДокумента.Контрагент                     КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ДанныеДокумента.Договор.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.Организация = РегистрАналитикаУчетаПоПартнерам.Организация
	|			И ДанныеДокумента.Партнер = РегистрАналитикаУчетаПоПартнерам.Партнер
	|			И ДанныеДокумента.Контрагент = РегистрАналитикаУчетаПоПартнерам.Контрагент
	|			И ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) = РегистрАналитикаУчетаПоПартнерам.Договор
	|			И ДанныеДокумента.Договор.НаправлениеДеятельности = РегистрАналитикаУчетаПоПартнерам.НаправлениеДеятельности
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И РегистрАналитикаУчетаПоПартнерам.КлючАналитики ЕСТЬ NULL";
	
	ЗапросАналитик = Новый Запрос(ТекстЗапроса);
	ЗапросАналитик.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	РезультатЗапроса = ЗапросАналитик.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаПоПартнерам.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КлючиАналитикиУчетаПоПартнерамИнициализированы", Истина);
	
КонецПроцедуры

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ИзменениеУсловийДоговораАренды";
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("Подразделение", "ДанныеДокумента.Договор.Подразделение");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	КонецЕсли;
		
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров,
										ТекстыЗапросаВременныхТаблиц);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                     КАК Ссылка,
	|	ДанныеДокумента.Дата                                       КАК Период,
	|	ДанныеДокумента.Номер                                      КАК Номер,
	|	ДанныеДокумента.Организация                                КАК Организация,
	|	ДанныеДокумента.Партнер                                    КАК Партнер,
	|	ДанныеДокумента.Контрагент                                 КАК Контрагент,
	|	ДанныеДокумента.Договор                                    КАК Договор,
	|	ДанныеДокумента.Договор.ТипДоговора                        КАК ТипДоговора,
	|	ДанныеДокумента.Договор.Балансодержатель                   КАК Балансодержатель,
	|	ДанныеДокумента.Договор.НаправлениеДеятельности            КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(ДанныеДокумента.Договор.УчитыватьПредметыАрендыВРеглУчете, ЛОЖЬ)  КАК УчитыватьПредметыАрендыВРеглУчете,
	|	ЕСТЬNULL(ДанныеДокумента.Договор.УчитыватьПредметыАрендыВУпрУчете, ЛОЖЬ)   КАК УчитыватьПредметыАрендыВУпрУчете,
	|	ДанныеДокумента.Договор.Подразделение                      КАК Подразделение,
	|	ЕСТЬNULL(СпрДоговорАренды.ТребуетсяОформлениеЗаявленийОВвозе, ЛОЖЬ) КАК ТребуетсяОформлениеЗаявленийОВвозе,
	|	ДанныеДокумента.ВалютаВзаиморасчетов                       КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.СуммаВзаиморасчетов                        КАК СуммаВзаиморасчетов,
	|	ДанныеДокумента.СуммаДокумента                             КАК СуммаДокумента,
	|	ДанныеДокумента.Валюта                                     КАК Валюта,
	|	ДанныеДокумента.Ответственный                              КАК Ответственный,
	|	ДанныеДокумента.Комментарий                                КАК Комментарий,
	|	ДанныеДокумента.Проведен                                   КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления                            КАК ПометкаУдаления,
	|	ДанныеДокумента.Сумма                                      КАК Сумма,
	|	ДанныеДокумента.СуммаОбеспечительногоПлатежа               КАК СуммаОбеспечительногоПлатежа,
	|	ДанныеДокумента.СуммаУслугПоАренде                         КАК СуммаУслугПоАренде,
	|	ДанныеДокумента.СуммаВыкупаПредметовАренды                 КАК СуммаВыкупаПредметовАренды,
	|	ДанныеДокумента.СуммаНДСВыкупаПредметовАренды              КАК СуммаНДСВыкупаПредметовАренды,
	|	ДанныеДокумента.СуммаПроцентов                             КАК СуммаПроцентов,
	|	ДанныеДокумента.СуммаСНДС                                  КАК СуммаСНДС,
	|	ДанныеДокумента.СуммаНДС                                   КАК СуммаНДС,
	|	ДанныеДокумента.СтавкаНДС                                  КАК СтавкаНДС,
	|	ДанныеДокумента.СрокЛет                                    КАК СрокЛет,
	|	ДанныеДокумента.СрокМес                                    КАК СрокМес,
	|	ДанныеДокумента.СрокДней                                   КАК СрокДней,
	|	ДанныеДокумента.ДатаНачалаАренды                           КАК ДатаНачалаАренды,
	|	ДанныеДокумента.ДатаПервогоПлатежа                         КАК ДатаПервогоПлатежа,
	|	ДанныеДокумента.ДатаПервогоОбеспечительногоПлатежа         КАК ДатаПервогоОбеспечительногоПлатежа,
	|	ДанныеДокумента.ДатаВыкупа                                 КАК ДатаВыкупа,
	|	ДанныеДокумента.ПроцентнаяСтавка                           КАК ПроцентнаяСтавка,
	|	ДанныеДокумента.ПериодичностьОплат                         КАК ПериодичностьОплат,
	|	ДанныеДокумента.ПериодичностьНачислений                    КАК ПериодичностьНачислений,
	|	ДанныеДокумента.СтоимостьПредметовАренды                   КАК СтоимостьПредметовАренды,
	|	ДанныеДокумента.ОплатаУслугиПоАренде                       КАК ОплатаУслугиПоАренде,
	|	ДанныеДокумента.НачислениеУслугиПоАренде                   КАК НачислениеУслугиПоАренде,
	|	ДанныеДокумента.ИдентификаторДокумента                     КАК ИдентификаторДокумента,
	|	ДанныеДокумента.СтатьяРасходов                             КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов                          КАК АналитикаРасходов,
	|	ДанныеДокумента.АналитикаАктивовПассивовРасходы            КАК АналитикаАктивовПассивовРасходы,
	|	ДанныеДокумента.НастройкаСчетовУчетаРасходы                КАК НастройкаСчетовУчетаРасходы,
	|	ДанныеДокумента.СтатьяДоходов                              КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов                           КАК АналитикаДоходов,
	|	ДанныеДокумента.Договор.СтатьяРасходовПроценты             КАК СтатьяРасходовПроценты,
	|	ДанныеДокумента.Договор.АналитикаРасходовПроценты          КАК АналитикаРасходовПроценты,
	|	ДанныеДокумента.Договор.ПодразделениеПроценты              КАК ПодразделениеПроценты,
	|	ДанныеДокумента.ГрафикНачисленияПроцентовВведенВручную     КАК ГрафикНачисленияПроцентовВведенВручную,
	|	ДанныеДокумента.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыАренды КАК СпрДоговорАренды
	|		ПО ДанныеДокумента.Договор = СпрДоговорАренды.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ВнеоборотныеАктивы.ДополнитьЗначенияПараметровПроведенияДокументаАренды(ЗначенияПараметровПроведения);

	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ИзменениеУсловийДоговораАренды"));
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ИзменениеУсловийДоговораАренды);
	ЗначенияПараметровПроведения.Вставить("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	ЗначенияПараметровПроведения.Вставить("ОтражатьВРеглУчете", Истина);
	ЗначенияПараметровПроведения.Вставить("ОтражатьВУпрУчете", Истина);

	ЗначенияПараметровПроведения.Вставить("ХО_УвеличениеНакопленнойАмортизации", Перечисления.ХозяйственныеОперации.УвеличениеНакопленнойАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_УвеличениеНакопленнойАмортизации", Справочники.НастройкиХозяйственныхОпераций.УвеличениеНакопленнойАмортизацииОС);

	ЗначенияПараметровПроведения.Вставить("ХО_УменьшениеНакопленнойАмортизации", Перечисления.ХозяйственныеОперации.УменьшениеНакопленнойАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_УменьшениеНакопленнойАмортизации", Справочники.НастройкиХозяйственныхОпераций.УменьшениеНакопленнойАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("ИдентификаторЗаявлениеОВвозеТоваров", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ЗаявлениеОВвозеТоваров"));

	Если Реквизиты <> Неопределено Тогда
		
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
		ЗначенияПараметровПроведения.Вставить("НачалоМесяца", НачалоМесяца(Реквизиты.Период));
		ЗначенияПараметровПроведения.Вставить("КонецМесяца", КонецМесяца(Реквизиты.Период));
		
		ЗначенияПараметровПроведения.Вставить("ИспользуетсяУчетАрендыПоФСБУ25_2018",
			ВнеоборотныеАктивы.ИспользуетсяУчетАрендыПоФСБУ25_2018(Реквизиты.Организация, Реквизиты.Период, Истина));
		
		Коэффициенты = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
			Реквизиты.Валюта, Реквизиты.ВалютаВзаиморасчетов, Реквизиты.Период, Реквизиты.Организация);
		ЗначенияПараметровПроведения.Вставить("КоэффициентПересчетаВВалютуУпр",  Коэффициенты.КоэффициентПересчетаВВалютуУпр);
		ЗначенияПараметровПроведения.Вставить("КоэффициентПересчетаВВалютуРегл", Коэффициенты.КоэффициентПересчетаВВалютуРегл);
		
		КоэффициентПересчетаИзВалютыУпр = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
			ЗначенияПараметровПроведения.ВалютаУправленческогоУчета, 
			Реквизиты.ВалютаВзаиморасчетов, 
			Реквизиты.Период);
		ЗначенияПараметровПроведения.Вставить("КоэффициентПересчетаИзВалютыУпр", КоэффициентПересчетаИзВалютыУпр);
		
		КоэффициентПересчетаИзВалютыРегл = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
			Реквизиты.ВалютаРегламентированногоУчета, 
			Реквизиты.ВалютаВзаиморасчетов, 
			Реквизиты.Период);
		ЗначенияПараметровПроведения.Вставить("КоэффициентПересчетаИзВалютыРегл", КоэффициентПересчетаИзВалютыРегл);
		
		ПараметрыУчетаПредметовАренды = ВнеоборотныеАктивыКлиентСервер.ПараметрыУчетаПредметовАренды(
			ЗначенияПараметровПроведения.ИспользуетсяУчетАрендыПоФСБУ25_2018, Реквизиты);
		
		ЗначенияПараметровПроведения.Вставить("ЕстьУчетУУ", ПараметрыУчетаПредметовАренды.ЕстьУчетУУ);
		ЗначенияПараметровПроведения.Вставить("ЕстьУчетБУ", ПараметрыУчетаПредметовАренды.ЕстьУчетБУ);
		ЗначенияПараметровПроведения.Вставить("ЕстьУчетНУ", ПараметрыУчетаПредметовАренды.ЕстьУчетНУ);
		
		ЗначенияПараметровПроведения.Вставить("СозданДляПерехода", 
						ВнеоборотныеАктивы.СозданДляПереходаНаФСБУ25_2018(Реквизиты.Период, Реквизиты.Организация));
			
		ЗначенияПараметровПроведения.Вставить("ДокументЗаключенияДоговораАренды", Справочники.ДоговорыАренды.ДокументЗаключенияДоговораАренды(Реквизиты.Договор));
		
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

#Область ТекстыЗапросаДляПроведения

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустыеТаблицыОтложенныхДвижений(ТекстыЗапроса, Запрос);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииОСУУ(ТекстыЗапроса, "Документ.ИзменениеУсловийДоговораАренды");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПереоценки(ТекстыЗапроса, Запрос, "ОС", "Документ.ИзменениеУсловийДоговораАренды");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                                                        КАК Регистратор,
	|	&Период                                                        КАК Период,
	|	&Организация                                                   КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                                     КАК ОсновноеСредство,
	|
	|	ДанныеРегистра.КоэффициентУскорения                            КАК КоэффициентУскорения,
	|	ДанныеРегистра.МетодНачисленияАмортизации                      КАК МетодНачисленияАмортизации,
	|	ДанныеРегистра.ЛиквидационнаяСтоимость                         КАК ЛиквидационнаяСтоимость,
	|	ДанныеРегистра.ЛиквидационнаяСтоимостьРегл                     КАК ЛиквидационнаяСтоимостьРегл,
	|	ДанныеРегистра.ДатаПоследнегоИзменения                         КАК ДатаПоследнегоИзменения,
	|	ДанныеРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации     КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ТаблицаОС.СрокИспользованияУУ                                  КАК СрокИспользования,
	|
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.СрокИспользования <> ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации + (ТаблицаОС.СрокИспользованияУУ - ДанныеРегистра.СрокИспользования)
	|		ИНАЧЕ ТаблицаОС.СрокИспользованияУУ
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиУУ, 0) = 0
	|				И ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиУУ, 0) = 0
	|			ТОГДА ДанныеРегистра.СтоимостьДляВычисленияАмортизации
	|		КОГДА ДанныеРегистра.СрокИспользования <> ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ТаблицаОС.СтоимостьУУ 
	|					- (ЕСТЬNULL(ТаблицаПереоценки.НакопленнаяАмортизацияУУ, 0)
	|						+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ, 0)
	|						- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиАмортизацииУУ, 0))
	|		ИНАЧЕ ДанныеРегистра.СтоимостьДляВычисленияАмортизации 
	|				+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиУУ, 0)
	|				- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиУУ, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	|	ВЫБОР 
	|		КОГДА &ВедетсяРегламентированныйУчетВНА
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиУУ, 0) = 0
	|				И ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиУУ, 0) = 0
	|			ТОГДА ДанныеРегистра.СтоимостьДляВычисленияАмортизацииРегл
	|		КОГДА ДанныеРегистра.СрокИспользования <> ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ТаблицаОС.СтоимостьБУ 
	|					- (ЕСТЬNULL(ТаблицаПереоценки.НакопленнаяАмортизацияБУ, 0)
	|						+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ, 0)
	|						- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиАмортизацииБУ, 0))
	|		ИНАЧЕ ДанныеРегистра.СтоимостьДляВычисленияАмортизациирегл 
	|				+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиБУ, 0)
	|				- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиБУ, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл
	|
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыАмортизацииОСУУ КАК ДанныеРегистра
	|		ПО ДанныеРегистра.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПереоценки КАК ТаблицаПереоценки
	|		ПО ТаблицаПереоценки.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И (ТаблицаОС.СрокИспользованияУУ <> ДанныеРегистра.СрокИспользования
	|		ИЛИ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиУУ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиУУ, 0) <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ПараметрыАмортизацииОСБУ";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииОСБУ(ТекстыЗапроса, "Документ.ИзменениеУсловийДоговораАренды");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПереоценки(ТекстыЗапроса, Запрос, "ОС", "Документ.ИзменениеУсловийДоговораАренды");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                      КАК Регистратор,
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	ТаблицаОС.ОсновноеСредство   КАК ОсновноеСредство,
	
	|	ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизацииЦФ  КАК СтоимостьДляВычисленияАмортизацииЦФ,
	|	ПараметрыАмортизацииОСБУ.ПРДляВычисленияАмортизации           КАК ПРДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения              КАК ДатаПоследнегоИзменения,
	|	ПараметрыАмортизацииОСБУ.КоэффициентАмортизацииБУ             КАК КоэффициентАмортизацииБУ,
	|	ПараметрыАмортизацииОСБУ.КоэффициентУскорения                 КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииОСБУ.ГрафикАмортизации                    КАК ГрафикАмортизации,
	|	ПараметрыАмортизацииОСБУ.СпециальныйКоэффициент               КАК СпециальныйКоэффициент,
	|	ПараметрыАмортизацииОСБУ.ЛиквидационнаяСтоимость              КАК ЛиквидационнаяСтоимость,
	|
	|	ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	// СрокПолезногоИспользованияБУ
	|	ВЫБОР
	|		КОГДА ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ <> 0
	|			ТОГДА ТаблицаОС.СрокИспользованияБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СрокПолезногоИспользованияБУ,
	|
	// СрокПолезногоИспользованияНУ
	|	ВЫБОР
	|		КОГДА ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ <> 0
	|			ТОГДА ТаблицаОС.СрокИспользованияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СрокПолезногоИспользованияНУ,
	|
	// СрокИспользованияДляВычисленияАмортизации
	|	ВЫБОР
	|		КОГДА ТаблицаОС.СрокИспользованияБУ = ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|		КОГДА ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ <> ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации + (ТаблицаОС.СрокИспользованияБУ - ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ)
	|		ИНАЧЕ ТаблицаОС.СрокИспользованияБУ
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизации
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиБУ, 0) = 0
	|				И ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиБУ, 0) = 0
	|			ТОГДА ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации
	|
	|		КОГДА ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ <> ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ТаблицаОС.СтоимостьБУ 
	|					- (ЕСТЬNULL(ТаблицаПереоценки.НакопленнаяАмортизацияБУ, 0)
	|						+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ, 0)
	|						- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиАмортизацииБУ, 0))
	|		ИНАЧЕ ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации 
	|				+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиБУ, 0)
	|				- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиБУ, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации
	|
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
	|		ПО (ПараметрыАмортизацииОСБУ.Организация = &Организация)
	|			И (ПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПереоценки КАК ТаблицаПереоценки
	|		ПО ТаблицаПереоценки.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И (ТаблицаОС.СрокИспользованияБУ <> ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ
	|		ИЛИ ТаблицаОС.СрокИспользованияНУ <> ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ
	|		ИЛИ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиБУ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиБУ, 0) <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СтоимостьОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;

	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, "Документ.ИзменениеУсловийДоговораАренды", "втСведенияОбУчете");
	
	ТекстЗапросаТаблицаВтУвеличениеСтоимости(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтУменьшениеСтоимости(Запрос, ТекстыЗапроса);
	ИнициализироватьВтКорректировкаСтоимостиОС(Запрос, ТекстыЗапроса);
	
	ТекстЗапросаТаблицаВтБалансоваяСтоимостьОС(Запрос, ТекстыЗапроса);

	СписокЗапросовОбъединение = Новый Массив;
	
	#Область УвеличениеСтоимостиОС // Дт <01.03> - Кт <76.07.1>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УвеличениеСтоимости.ВидДвижения                       КАК ВидДвижения,
	|	УвеличениеСтоимости.Регистратор                       КАК Регистратор,
	|	УвеличениеСтоимости.Период                            КАК Период,
	|	УвеличениеСтоимости.Организация                       КАК Организация,
	|	УвеличениеСтоимости.ОсновноеСредство                  КАК ОсновноеСредство,
	|	УвеличениеСтоимости.Подразделение                     КАК Подразделение,
	|	УвеличениеСтоимости.ГруппаФинансовогоУчета            КАК ГруппаФинансовогоУчета,
	|	УвеличениеСтоимости.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	УвеличениеСтоимости.Арендатор                         КАК Арендатор,
	|	СУММА(УвеличениеСтоимости.Стоимость)                  КАК Стоимость,
	|	СУММА(УвеличениеСтоимости.СтоимостьРегл)              КАК СтоимостьРегл,
	|	СУММА(УвеличениеСтоимости.СтоимостьНУ)                КАК СтоимостьНУ,
	|	СУММА(УвеличениеСтоимости.СтоимостьВР)                КАК СтоимостьВР,
	|	СУММА(УвеличениеСтоимости.ЗалоговаяСтоимость)         КАК ЗалоговаяСтоимость,
	|	СУММА(УвеличениеСтоимости.НеУчитываемаяСтоимостьНУ)   КАК НеУчитываемаяСтоимостьНУ,
	|	УвеличениеСтоимости.ИдентификаторФинЗаписи            КАК ИдентификаторФинЗаписи,
	|	УвеличениеСтоимости.ХозяйственнаяОперация             КАК ХозяйственнаяОперация,
	|	УвеличениеСтоимости.НастройкаХозяйственнойОперации    КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)   КАК ВидДвижения,
	|		&Ссылка                                  КАК Регистратор,
	|		&Период                                  КАК Период,
	|		&Организация                             КАК Организация,
	|		УвеличениеСтоимости.ОсновноеСредство     КАК ОсновноеСредство,
	|		СведенияОбУчете.Местонахождение          КАК Подразделение,
	|		СведенияОбУчете.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|		СведенияОбУчете.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|		СведенияОбУчете.Арендатор                КАК Арендатор,
	|		УвеличениеСтоимости.Стоимость            КАК Стоимость,
	|		УвеличениеСтоимости.СтоимостьРегл        КАК СтоимостьРегл,
	|		УвеличениеСтоимости.СтоимостьНУ          КАК СтоимостьНУ,
	|
	|		УвеличениеСтоимости.СтоимостьРегл 
	|			- УвеличениеСтоимости.СтоимостьНУ    КАК СтоимостьВР,
	|
	|		УвеличениеСтоимости.ЗалоговаяСтоимость   КАК ЗалоговаяСтоимость,
	|		0                                        КАК НеУчитываемаяСтоимостьНУ,
	|		УвеличениеСтоимости.ИдентификаторСтроки  КАК ИдентификаторФинЗаписи,
	|		&ХО_УвеличениеСтоимостиАрендованныхОС    КАК ХозяйственнаяОперация,
	|
	|		&НастройкаХО_УвеличениеСтоимостиАрендованныхОС КАК НастройкаХозяйственнойОперации
	|	ИЗ
	|		втУвеличениеСтоимости КАК УвеличениеСтоимости
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|			ПО СведенияОбУчете.ОсновноеСредство = УвеличениеСтоимости.ОсновноеСредство
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)         КАК ВидДвижения,
	|		&Ссылка                                        КАК Регистратор,
	|		&Период                                        КАК Период,
	|		&Организация                                   КАК Организация,
	|		ВтНачисленияВосстановления.ОсновноеСредство    КАК ОсновноеСредство,
	|		СведенияОбУчете.Местонахождение                КАК Подразделение,
	|		СведенияОбУчете.ГруппаФинансовогоУчета         КАК ГруппаФинансовогоУчета,
	|		СведенияОбУчете.НаправлениеДеятельности        КАК НаправлениеДеятельности,
	|		СведенияОбУчете.Арендатор                      КАК Арендатор,
	|		-ВтНачисленияВосстановления.СуммаПроцентовУпр  КАК ИзменениеСтоимостиПроцентыУпр,
	|		-ВтНачисленияВосстановления.СуммаПроцентовРегл КАК ИзменениеСтоимостиПроцентыРегл,
	|		0                                              КАК ИзменениеСтоимостиПроцентыНУ,
	|		-ВтНачисленияВосстановления.СуммаПроцентовРегл КАК ИзменениеСтоимостиПроцентыВР,
	|		0                                              КАК ЗалоговаяСтоимость,
	|		0                                              КАК НеУчитываемаяСтоимостьНУ,
	|		ВтНачисленияВосстановления.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|		&ХО_УвеличениеСтоимостиАрендованныхОС          КАК ХозяйственнаяОперация,
	|		&НастройкаХО_УвеличениеСтоимостиАрендованныхОС КАК НастройкаХозяйственнойОперации
	|	ИЗ
	|		ВтНачисленияВосстановления КАК ВтНачисленияВосстановления
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|			ПО СведенияОбУчете.ОсновноеСредство = ВтНачисленияВосстановления.ОсновноеСредство
	|	ГДЕ
	|		ВтНачисленияВосстановления.СуммаПроцентовУпр < 0
	|		ИЛИ ВтНачисленияВосстановления.СуммаПроцентовРегл < 0) КАК УвеличениеСтоимости
	|
	|СГРУППИРОВАТЬ ПО
	|	УвеличениеСтоимости.ВидДвижения,
	|	УвеличениеСтоимости.Регистратор,
	|	УвеличениеСтоимости.Период,
	|	УвеличениеСтоимости.Организация,
	|	УвеличениеСтоимости.ОсновноеСредство,
	|	УвеличениеСтоимости.Подразделение,
	|	УвеличениеСтоимости.ГруппаФинансовогоУчета,
	|	УвеличениеСтоимости.НаправлениеДеятельности,
	|	УвеличениеСтоимости.Арендатор,
	|	УвеличениеСтоимости.ИдентификаторФинЗаписи,
	|	УвеличениеСтоимости.ХозяйственнаяОперация,
	|	УвеличениеСтоимости.НастройкаХозяйственнойОперации";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);

	#КонецОбласти
	
	#Область УменьшениеСтоимостиОС // Дт <76.07.1> - Кт <01.03>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УменьшениеСтоимости.ВидДвижения                      КАК ВидДвижения,
	|	УменьшениеСтоимости.Регистратор                      КАК Регистратор,
	|	УменьшениеСтоимости.Период                           КАК Период,
	|	УменьшениеСтоимости.Организация                      КАК Организация,
	|	УменьшениеСтоимости.ОсновноеСредство                 КАК ОсновноеСредство,
	|	УменьшениеСтоимости.Подразделение                    КАК Подразделение,
	|	УменьшениеСтоимости.ГруппаФинансовогоУчета           КАК ГруппаФинансовогоУчета,
	|	УменьшениеСтоимости.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	УменьшениеСтоимости.Арендатор                        КАК Арендатор,
	|	СУММА(УменьшениеСтоимости.Стоимость)                 КАК Стоимость,
	|	СУММА(УменьшениеСтоимости.СтоимостьРегл)             КАК СтоимостьРегл,
	|	СУММА(УменьшениеСтоимости.СтоимостьНУ)               КАК СтоимостьНУ,
	|	СУММА(УменьшениеСтоимости.СтоимостьВР)               КАК СтоимостьВР,
	|	СУММА(УменьшениеСтоимости.ЗалоговаяСтоимость)        КАК ЗалоговаяСтоимость,
	|	СУММА(УменьшениеСтоимости.НеУчитываемаяСтоимостьНУ)  КАК НеУчитываемаяСтоимостьНУ,
	|	УменьшениеСтоимости.ИдентификаторФинЗаписи           КАК ИдентификаторФинЗаписи,
	|	УменьшениеСтоимости.ХозяйственнаяОперация            КАК ХозяйственнаяОперация,
	|	УменьшениеСтоимости.НастройкаХозяйственнойОперации   КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|		&Ссылка                                   КАК Регистратор,
	|		&Период                                   КАК Период,
	|		&Организация                              КАК Организация,
	|		УменьшениеСтоимости.ОсновноеСредство      КАК ОсновноеСредство,
	|		СведенияОбУчете.Местонахождение           КАК Подразделение,
	|		СведенияОбУчете.ГруппаФинансовогоУчета    КАК ГруппаФинансовогоУчета,
	|		СведенияОбУчете.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|		СведенияОбУчете.Арендатор                 КАК Арендатор,
	|
	|		ВЫБОР
	|			КОГДА УменьшениеСтоимости.Стоимость < БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|				ТОГДА УменьшениеСтоимости.Стоимость
	|			ИНАЧЕ БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|		КОНЕЦ КАК Стоимость,
	|
	|		ВЫБОР
	|			КОГДА УменьшениеСтоимости.СтоимостьРегл < БалансоваяСтоимость.БалансоваяСтоимостьРегл
	|				ТОГДА УменьшениеСтоимости.СтоимостьРегл
	|			ИНАЧЕ БалансоваяСтоимость.БалансоваяСтоимостьРегл
	|		КОНЕЦ КАК СтоимостьРегл,
	|
	|		УменьшениеСтоимости.СтоимостьНУ         КАК СтоимостьНУ,
	|
	|		УменьшениеСтоимости.СтоимостьРегл - 
	|				УменьшениеСтоимости.СтоимостьНУ КАК СтоимостьВР,
	|
	|		УменьшениеСтоимости.ЗалоговаяСтоимость  КАК ЗалоговаяСтоимость,
	|		0                                       КАК НеУчитываемаяСтоимостьНУ,
	|		УменьшениеСтоимости.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|		&ХО_УменьшениеСтоимостиАрендованныхОС   КАК ХозяйственнаяОперация,
	|
	|		&НастройкаХО_УменьшениеСтоимостиАрендованныхОС КАК НастройкаХозяйственнойОперации
	|	ИЗ
	|		втУменьшениеСтоимости КАК УменьшениеСтоимости
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|			ПО СведенияОбУчете.ОсновноеСредство = УменьшениеСтоимости.ОсновноеСредство
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВтБалансоваяСтоимостьОС КАК БалансоваяСтоимость
	|			ПО УменьшениеСтоимости.ОсновноеСредство = БалансоваяСтоимость.ОсновноеСредство
	|			И БалансоваяСтоимость.Ссылка = &Ссылка
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)           КАК ВидДвижения,
	|		&Ссылка                                          КАК Регистратор,
	|		&Период                                          КАК Период,
	|		&Организация                                     КАК Организация,
	|		ВтНачисленияВосстановления.ОсновноеСредство      КАК ОсновноеСредство,
	|		СведенияОбУчете.Местонахождение                  КАК Подразделение,
	|		СведенияОбУчете.ГруппаФинансовогоУчета           КАК ГруппаФинансовогоУчета,
	|		СведенияОбУчете.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|		СведенияОбУчете.Арендатор                        КАК Арендатор,
	|
	|		ВЫБОР
	|			КОГДА ВтНачисленияВосстановления.СуммаПроцентовУпр < БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|				ТОГДА ВтНачисленияВосстановления.СуммаПроцентовУпр
	|			ИНАЧЕ БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|		КОНЕЦ КАК ИзменениеСтоимостиПроцентыУпр,
	|
	|		ВЫБОР
	|			КОГДА ВтНачисленияВосстановления.СуммаПроцентовРегл < БалансоваяСтоимость.БалансоваяСтоимостьРегл
	|				ТОГДА ВтНачисленияВосстановления.СуммаПроцентовРегл
	|			ИНАЧЕ БалансоваяСтоимость.БалансоваяСтоимостьРегл
	|		КОНЕЦ КАК ИзменениеСтоимостиПроцентыРегл,
	|
	|		0                                                КАК ИзменениеСтоимостиПроцентыНУ,
	|
	|		ВЫБОР
	|			КОГДА ВтНачисленияВосстановления.СуммаПроцентовРегл < БалансоваяСтоимость.БалансоваяСтоимостьРегл
	|				ТОГДА ВтНачисленияВосстановления.СуммаПроцентовРегл
	|			ИНАЧЕ БалансоваяСтоимость.БалансоваяСтоимостьРегл
	|		КОНЕЦ КАК ИзменениеСтоимостиПроцентыВР,
	|
	|		0                                                КАК ЗалоговаяСтоимость,
	|		0                                                КАК НеУчитываемаяСтоимостьНУ,
	|		ВтНачисленияВосстановления.ИдентификаторСтроки   КАК ИдентификаторФинЗаписи,
	|		&ХО_УменьшениеСтоимостиАрендованныхОС            КАК ХозяйственнаяОперация,
	|		&НастройкаХО_УменьшениеСтоимостиАрендованныхОС   КАК НастройкаХозяйственнойОперации
	|	ИЗ
	|		ВтНачисленияВосстановления КАК ВтНачисленияВосстановления
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|			ПО СведенияОбУчете.ОсновноеСредство = ВтНачисленияВосстановления.ОсновноеСредство
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВтБалансоваяСтоимостьОС КАК БалансоваяСтоимость
	|			ПО ВтНачисленияВосстановления.ОсновноеСредство = БалансоваяСтоимость.ОсновноеСредство
	|			И БалансоваяСтоимость.Ссылка = &Ссылка
	|	ГДЕ
	|		ВтНачисленияВосстановления.СуммаПроцентовУпр > 0
	|		ИЛИ ВтНачисленияВосстановления.СуммаПроцентовРегл > 0) КАК УменьшениеСтоимости
	|
	|СГРУППИРОВАТЬ ПО
	|	УменьшениеСтоимости.ВидДвижения,
	|	УменьшениеСтоимости.Регистратор,
	|	УменьшениеСтоимости.Период,
	|	УменьшениеСтоимости.Организация,
	|	УменьшениеСтоимости.ОсновноеСредство,
	|	УменьшениеСтоимости.Подразделение,
	|	УменьшениеСтоимости.ГруппаФинансовогоУчета,
	|	УменьшениеСтоимости.НаправлениеДеятельности,
	|	УменьшениеСтоимости.Арендатор,
	|	УменьшениеСтоимости.ИдентификаторФинЗаписи,
	|	УменьшениеСтоимости.ХозяйственнаяОперация,
	|	УменьшениеСтоимости.НастройкаХозяйственнойОперации";

	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "АмортизацияОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, "Документ.ИзменениеУсловийДоговораАренды", "втСведенияОбУчете");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустыеТаблицыОтложенныхДвижений(ТекстыЗапроса, Запрос);
	
	#Область УменьшениеНакопленнойАмортизацииПриПереходеНаФСБУ25
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	&Период                                      КАК Период,
	|	&Организация                                 КАК Организация,
	|	втСведенияОбУчете.Местонахождение            КАК Подразделение,
	|	Проценты.ОсновноеСредство                    КАК ОсновноеСредство,
	|	втСведенияОбУчете.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|	втСведенияОбУчете.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВУпрУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_СтоимостьВНА.Стоимость ЕСТЬ НЕ NULL 
	|						ТОГДА (1 - (ВТ_СтоимостьВНА.Стоимость - Проценты.СуммаПроцентовУпр) / ВТ_СтоимостьВНА.Стоимость) 
	|								* ВТ_АмортизацияВНА.Амортизация
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Амортизация,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВРеглУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_АмортизацияВНА.НачислятьИзнос
	|						ТОГДА 0
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ВТ_СтоимостьВНА.СтоимостьРегл ЕСТЬ НЕ NULL 
	|								ТОГДА (1 - (ВТ_СтоимостьВНА.СтоимостьРегл - Проценты.СуммаПроцентовРегл) / ВТ_СтоимостьВНА.СтоимостьРегл) 
	|										* ВТ_АмортизацияВНА.АмортизацияРегл
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияРегл,
	|
	|	0 КАК АмортизацияНУ,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВРеглУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_АмортизацияВНА.НачислятьИзнос
	|						ТОГДА 0
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ВТ_СтоимостьВНА.СтоимостьРегл ЕСТЬ НЕ NULL 
	|								ТОГДА (1 - (ВТ_СтоимостьВНА.СтоимостьРегл - Проценты.СуммаПроцентовРегл) / ВТ_СтоимостьВНА.СтоимостьРегл) 
	|										* ВТ_АмортизацияВНА.АмортизацияРегл
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияВР,
	|
	|	Проценты.ИдентификаторСтроки                  КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеНакопленнойАмортизации          КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеНакопленнойАмортизации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтНачисленияВосстановления КАК Проценты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО Проценты.ОсновноеСредство = ВТ_СтоимостьВНА.ОбъектУчета
	|			И (ВТ_СтоимостьВНА.Ссылка = &Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК ВТ_АмортизацияВНА
	|		ПО Проценты.ОсновноеСредство = ВТ_АмортизацияВНА.ОбъектУчета
	|			И (ВТ_АмортизацияВНА.Ссылка = &Ссылка)
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК втСведенияОбУчете
	|		ПО Проценты.ОсновноеСредство = втСведенияОбУчете.ОсновноеСредство
	|ГДЕ
	|	&СозданДляПерехода";
	
	#КонецОбласти
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаАрендованныеОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "АрендованныеОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтАрендованныеОС(ТекстыЗапроса, "Документ.ИзменениеУсловийДоговораАренды");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                       КАК Период,
	|	ТаблицаОС.ОсновноеСредство    КАК ОсновноеСредство,
	|	ТаблицаОС.Стоимость           КАК Стоимость,
	|	&Организация                  КАК Организация,
	|	&Партнер                      КАК Партнер,
	|	&Контрагент                   КАК Контрагент,
	|	&Договор                      КАК Договор,
	|	ВтАрендованныеОС.Состояние    КАК Состояние
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеУсловийДоговораАренды.ОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАрендованныеОС КАК ВтАрендованныеОС
	|		ПО ВтАрендованныеОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.Стоимость <> ВтАрендованныеОС.Стоимость";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаУсловияДоговоровАренды(ТекстыЗапроса, Регистры)

	ИмяРегистра = "УсловияДоговоровАренды";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                                КАК Регистратор,
	|	&Период                                КАК Период,
	|	&Договор                               КАК Договор,
	|	&СуммаОбеспечительногоПлатежа          КАК СуммаОбеспечительногоПлатежа,
	|	&СуммаУслугПоАренде                    КАК СуммаУслугПоАренде,
	|	&СуммаВыкупаПредметовАренды            КАК СуммаВыкупаПредметовАренды,
	|	&СуммаПроцентов                        КАК СуммаПроцентов,
	|	&СуммаНДС                              КАК СуммаНДС,
	|	&СтавкаНДС                             КАК СтавкаНДС,
	|	&СрокЛет                               КАК СрокЛет,
	|	&СрокМес                               КАК СрокМес,
	|	&СрокДней                              КАК СрокДней,
	|	&ДатаНачалаАренды                      КАК ДатаНачалаАренды,
	|	&ДатаПервогоПлатежа                    КАК ДатаПервогоПлатежа,
	|	&ДатаПервогоОбеспечительногоПлатежа    КАК ДатаПервогоОбеспечительногоПлатежа,
	|	&ДатаВыкупа                            КАК ДатаВыкупа,
	|	&ПроцентнаяСтавка                      КАК ПроцентнаяСтавка,
	|	&ПериодичностьОплат                    КАК ПериодичностьОплат,
	|	&ПериодичностьНачислений               КАК ПериодичностьНачислений,
	|	&СтоимостьПредметовАренды              КАК СтоимостьПредметовАренды,
	|	&ОплатаУслугиПоАренде                  КАК ОплатаУслугиПоАренде,
	|	&НачислениеУслугиПоАренде              КАК НачислениеУслугиПоАренде,
	|	&ГрафикНачисленияПроцентовВведенВручную КАК ГрафикНачисленияПроцентовВведенВручную,
	|	&Ссылка                                КАК АктуальныеУсловияДоговора,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Действует) КАК Состояние
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
КонецПроцедуры

Функция ТекстЗапросаТаблицаГрафикОплатУслугПоАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикОплатУслугПоАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                 КАК ДатаИзменения,
	|	&Договор                                КАК Договор,
	|	&Ссылка                                 КАК АктуальныеУсловияДоговора,
	|	ТаблицаГрафика.Дата                     КАК Дата,
	|	ТаблицаГрафика.УслугаПоАренде           КАК УслугаПоАренде,
	|	ТаблицаГрафика.УслугаПоАрендеНДС        КАК УслугаПоАрендеНДС,
	|	ТаблицаГрафика.ОбеспечительныйПлатеж    КАК ОбеспечительныйПлатеж,
	|	ТаблицаГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаГрафика.Дата >= &ДатаВыкупа
	|			ТОГДА ТаблицаГрафика.ВыкупнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыкупнаяСтоимость,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаГрафика.Дата < &ДатаВыкупа
	|			ТОГДА ТаблицаГрафика.ВыкупнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыкупнаяСтоимостьАванс,
	|
	|	ТаблицаГрафика.ВыкупнаяСтоимостьНДС КАК ВыкупнаяСтоимостьНДС
	|
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикОплатУслуг КАК ТаблицаГрафика
	|ГДЕ
	|	ТаблицаГрафика.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаГрафикНачисленияУслугПоАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикНачисленияУслугПоАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                 КАК ДатаИзменения,
	|	&Договор                                КАК Договор,
	|	&Ссылка                                 КАК АктуальныеУсловияДоговора,
	|	ТаблицаГрафика.Дата                     КАК Дата,
	|	ТаблицаГрафика.УслугаПоАренде           КАК УслугаПоАренде,
	|	ТаблицаГрафика.УслугаПоАрендеНДС        КАК УслугаПоАрендеНДС,
	|	ТаблицаГрафика.ОбеспечительныйПлатеж    КАК ОбеспечительныйПлатеж,
	|	ТаблицаГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|	ВЫБОР 
	|		КОГДА &ДатаВыкупа = ТаблицаГрафика.Дата
	|			ТОГДА &СуммаВыкупаПредметовАренды
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыкупнаяСтоимость,
	|	ВЫБОР 
	|		КОГДА &ДатаВыкупа = ТаблицаГрафика.Дата
	|			ТОГДА &СуммаНДСВыкупаПредметовАренды
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыкупнаяСтоимостьНДС
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияУслуг КАК ТаблицаГрафика
	|ГДЕ
	|	ТаблицаГрафика.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                 КАК ДатаИзменения,
	|	&Договор                                КАК Договор,
	|	&Ссылка                                 КАК АктуальныеУсловияДоговора,
	|	&ДатаВыкупа                             КАК Дата,
	|	0                                       КАК УслугаПоАренде,
	|	0                                       КАК УслугаПоАрендеНДС,
	|	0                                       КАК ОбеспечительныйПлатеж,
	|	0                                       КАК ОбеспечительныйПлатежНДС,
	|	&СуммаВыкупаПредметовАренды             КАК ВыкупнаяСтоимость,
	|	&СуммаНДСВыкупаПредметовАренды          КАК ВыкупнаяСтоимостьНДС
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияУслуг КАК ТаблицаГрафика
	|		ПО ТаблицаГрафика.Ссылка = ДанныеДокумента.Ссылка
	|			И ТаблицаГрафика.Дата = ДанныеДокумента.ДатаВыкупа
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ТаблицаГрафика.Ссылка ЕСТЬ NULL
	|	И &СуммаВыкупаПредметовАренды <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикНачисленияПроцентовПоАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                    КАК ДатаИзменения,
	|	&Договор                   КАК Договор,
	|	&Ссылка                    КАК АктуальныеУсловияДоговора,
	|	ТаблицаГрафика.Дата        КАК Дата,
	|	ТаблицаГрафика.Проценты    КАК Проценты
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияПроцентов КАК ТаблицаГрафика
	|ГДЕ
	|	ТаблицаГрафика.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыПоФинансовымИнструментам";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос);

	ТекстЗапросаТаблицаВтУвеличениеСтоимости(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтУменьшениеСтоимости(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтКорректировкаРасчетов(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтИзлишнеНачисленныеПроценты(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область УвеличениеСтоимостиОС // Дт <01.03> - Кт <76.07.1>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УвеличениеСтоимости.Период                         КАК Период,
	|	УвеличениеСтоимости.ВидДвижения                    КАК ВидДвижения,
	|	УвеличениеСтоимости.АналитикаУчетаПоПартнерам      КАК АналитикаУчетаПоПартнерам,
	|	УвеличениеСтоимости.Договор                        КАК Договор,
	|	УвеличениеСтоимости.ТипСуммы                       КАК ТипСуммы,
	|	УвеличениеСтоимости.РасчетныйДокумент              КАК РасчетныйДокумент,
	|	УвеличениеСтоимости.Валюта                         КАК Валюта,
	|	СУММА(УвеличениеСтоимости.Сумма)                   КАК Сумма,
	|	СУММА(УвеличениеСтоимости.СуммаУпр)                КАК СуммаУпр,
	|	СУММА(УвеличениеСтоимости.СуммаРегл)               КАК СуммаРегл,
	|	УвеличениеСтоимости.ТипГрафика                     КАК ТипГрафика,
	|	УвеличениеСтоимости.СтатьяАналитики                КАК СтатьяАналитики,
	|	УвеличениеСтоимости.ИдентификаторСтроки            КАК ИдентификаторСтроки,
	|	УвеличениеСтоимости.ИдентификаторФинЗаписи         КАК ИдентификаторФинЗаписи,
	|	УвеличениеСтоимости.ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
	|	УвеличениеСтоимости.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	(ВЫБРАТЬ
	|		&Период КАК Период,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|		ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО) КАК АналитикаУчетаПоПартнерам,
	|		&Договор КАК Договор,
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства) КАК ТипСуммы,
	|		НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент,
	|		&ВалютаВзаиморасчетов КАК Валюта,
	|
	|		ВЫБОР
	|			КОГДА УвеличениеСтоимости.СтоимостьРегл <> 0
	|				ТОГДА УвеличениеСтоимости.СтоимостьРегл * ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|						ТОГДА 1
	|					ИНАЧЕ &КоэффициентПересчетаИзВалютыРегл
	|				КОНЕЦ
	|			КОГДА УвеличениеСтоимости.Стоимость <> 0
	|				ТОГДА УвеличениеСтоимости.Стоимость * ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|						ТОГДА 1
	|					ИНАЧЕ &КоэффициентПересчетаИзВалютыУпр
	|				КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Сумма,
	|
	|		УвеличениеСтоимости.Стоимость КАК СуммаУпр,
	|		УвеличениеСтоимости.СтоимостьРегл КАК СуммаРегл,
	|		НЕОПРЕДЕЛЕНО КАК ТипГрафика,
	|		НЕОПРЕДЕЛЕНО КАК СтатьяАналитики,
	|		УвеличениеСтоимости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		УвеличениеСтоимости.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|		&ХО_УвеличениеСтоимостиАрендованныхОС КАК ХозяйственнаяОперация,
	|		&НастройкаХО_УвеличениеСтоимостиАрендованныхОС КАК НастройкаХозяйственнойОперации
	|	ИЗ
	|		втУвеличениеСтоимости КАК УвеличениеСтоимости
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|			ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|	ГДЕ
	|		(УвеличениеСтоимости.Стоимость <> 0
	|		ИЛИ УвеличениеСтоимости.СтоимостьРегл <> 0)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		&Период КАК Период,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|		ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО) КАК АналитикаУчетаПоПартнерам,
	|		&Договор КАК Договор,
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства) КАК ТипСуммы,
	|		НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент,
	|		&ВалютаВзаиморасчетов КАК Валюта,
	|		СУММА(ВтНачисленияВосстановления.СуммаПроцентов) КАК ИзменениеСтоимостиПроценты,
	|		СУММА(ВтНачисленияВосстановления.СуммаПроцентовУпр) КАК ИзменениеСтоимостиПроцентыУпр,
	|		СУММА(ВтНачисленияВосстановления.СуммаПроцентовРегл) КАК ИзменениеСтоимостиПроцентыРегл,
	|		НЕОПРЕДЕЛЕНО КАК ТипГрафика,
	|		НЕОПРЕДЕЛЕНО КАК СтатьяАналитики,
	|		ВтНачисленияВосстановления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		ВтНачисленияВосстановления.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|		&ХО_УвеличениеСтоимостиАрендованныхОС КАК ХозяйственнаяОперация,
	|		&НастройкаХО_УвеличениеСтоимостиАрендованныхОС КАК НастройкаХозяйственнойОперации
	|	ИЗ
	|		ВтНачисленияВосстановления КАК ВтНачисленияВосстановления
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|			ПО СведенияОбУчете.ОсновноеСредство = ВтНачисленияВосстановления.ОсновноеСредство
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|			ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|	ГДЕ
	|		ВтНачисленияВосстановления.СуммаПроцентов < 0
	|	СГРУППИРОВАТЬ ПО
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства),
	|		ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО),
	|		ВтНачисленияВосстановления.ИдентификаторСтроки) КАК УвеличениеСтоимости
	|
	|СГРУППИРОВАТЬ ПО
	|	УвеличениеСтоимости.Период,
	|	УвеличениеСтоимости.ВидДвижения,
	|	УвеличениеСтоимости.АналитикаУчетаПоПартнерам,
	|	УвеличениеСтоимости.Договор,
	|	УвеличениеСтоимости.ТипСуммы,
	|	УвеличениеСтоимости.РасчетныйДокумент,
	|	УвеличениеСтоимости.Валюта,
	|	УвеличениеСтоимости.ТипГрафика,
	|	УвеличениеСтоимости.СтатьяАналитики,
	|	УвеличениеСтоимости.ИдентификаторСтроки,
	|	УвеличениеСтоимости.ИдентификаторФинЗаписи,
	|	УвеличениеСтоимости.ХозяйственнаяОперация,
	|	УвеличениеСтоимости.НастройкаХозяйственнойОперации";

	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УменьшениеСтоимостиОС // Дт <76.07.1> - Кт <01.03>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УменьшениеСтоимости.Период                          КАК Период,
	|	УменьшениеСтоимости.ВидДвижения                     КАК ВидДвижения,
	|	УменьшениеСтоимости.АналитикаУчетаПоПартнерам       КАК АналитикаУчетаПоПартнерам,
	|	УменьшениеСтоимости.Договор                         КАК Договор,
	|	УменьшениеСтоимости.ТипСуммы                        КАК ТипСуммы,
	|	УменьшениеСтоимости.РасчетныйДокумент               КАК РасчетныйДокумент,
	|	УменьшениеСтоимости.Валюта                          КАК Валюта,
	|	СУММА(УменьшениеСтоимости.Сумма)                    КАК Сумма,
	|	СУММА(УменьшениеСтоимости.СуммаУпр)                 КАК СуммаУпр,
	|	СУММА(УменьшениеСтоимости.СуммаРегл)                КАК СуммаРегл,
	|	УменьшениеСтоимости.ТипГрафика                      КАК ТипГрафика,
	|	УменьшениеСтоимости.СтатьяАналитики                 КАК СтатьяАналитики,
	|	УменьшениеСтоимости.ИдентификаторСтроки             КАК ИдентификаторСтроки,
	|	УменьшениеСтоимости.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	УменьшениеСтоимости.ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	УменьшениеСтоимости.НастройкаХозяйственнойОперации  КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	(ВЫБРАТЬ
	|		&Период КАК Период,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|		ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО) КАК АналитикаУчетаПоПартнерам,
	|		&Договор КАК Договор,
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства) КАК ТипСуммы,
	|		НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент,
	|		&ВалютаВзаиморасчетов КАК Валюта,
	|
	|		ВЫБОР
	|			КОГДА УменьшениеСтоимости.СтоимостьРегл <> 0
	|				ТОГДА УменьшениеСтоимости.СтоимостьРегл * ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|						ТОГДА 1
	|					ИНАЧЕ &КоэффициентПересчетаИзВалютыРегл
	|				КОНЕЦ
	|			КОГДА УменьшениеСтоимости.Стоимость <> 0
	|				ТОГДА УменьшениеСтоимости.Стоимость * ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|						ТОГДА 1
	|					ИНАЧЕ &КоэффициентПересчетаИзВалютыУпр
	|				КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Сумма,
	|
	|		УменьшениеСтоимости.Стоимость КАК СуммаУпр,
	|		УменьшениеСтоимости.СтоимостьРегл КАК СуммаРегл,
	|		НЕОПРЕДЕЛЕНО КАК ТипГрафика,
	|		НЕОПРЕДЕЛЕНО КАК СтатьяАналитики,
	|		УменьшениеСтоимости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		УменьшениеСтоимости.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|		&ХО_УменьшениеСтоимостиАрендованныхОС КАК ХозяйственнаяОперация,
	|		&НастройкаХО_УменьшениеСтоимостиАрендованныхОС КАК НастройкаХозяйственнойОперации
	|	ИЗ
	|		втУменьшениеСтоимости КАК УменьшениеСтоимости
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|			ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|	ГДЕ
	|		(УменьшениеСтоимости.Стоимость <> 0
	|		ИЛИ УменьшениеСтоимости.СтоимостьРегл <> 0)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		&Период КАК Период,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|		ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО) КАК АналитикаУчетаПоПартнерам,
	|		&Договор КАК Договор,
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства) КАК ТипСуммы,
	|		НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент,
	|		&ВалютаВзаиморасчетов КАК Валюта,
	|		СУММА(ВтНачисленияВосстановления.СуммаПроцентов) КАК ИзменениеСтоимостиПроценты,
	|		СУММА(ВтНачисленияВосстановления.СуммаПроцентовУпр) КАК ИзменениеСтоимостиПроцентыУпр,
	|		СУММА(ВтНачисленияВосстановления.СуммаПроцентовРегл) КАК ИзменениеСтоимостиПроцентыРегл,
	|		НЕОПРЕДЕЛЕНО КАК ТипГрафика,
	|		НЕОПРЕДЕЛЕНО КАК СтатьяАналитики,
	|		ВтНачисленияВосстановления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		ВтНачисленияВосстановления.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|		&ХО_УменьшениеСтоимостиАрендованныхОС КАК ХозяйственнаяОперация,
	|		&НастройкаХО_УменьшениеСтоимостиАрендованныхОС КАК НастройкаХозяйственнойОперации
	|	ИЗ
	|		ВтНачисленияВосстановления КАК ВтНачисленияВосстановления
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|			ПО СведенияОбУчете.ОсновноеСредство = ВтНачисленияВосстановления.ОсновноеСредство
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|			ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|	ГДЕ
	|		ВтНачисленияВосстановления.СуммаПроцентов > 0
	|	СГРУППИРОВАТЬ ПО
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства),
	|		ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО),
	|		ВтНачисленияВосстановления.ИдентификаторСтроки) КАК УменьшениеСтоимости
	|
	|СГРУППИРОВАТЬ ПО
	|	УменьшениеСтоимости.Период,
	|	УменьшениеСтоимости.ВидДвижения,
	|	УменьшениеСтоимости.АналитикаУчетаПоПартнерам,
	|	УменьшениеСтоимости.Договор,
	|	УменьшениеСтоимости.ТипСуммы,
	|	УменьшениеСтоимости.РасчетныйДокумент,
	|	УменьшениеСтоимости.Валюта,
	|	УменьшениеСтоимости.ТипГрафика,
	|	УменьшениеСтоимости.СтатьяАналитики,
	|	УменьшениеСтоимости.ИдентификаторСтроки,
	|	УменьшениеСтоимости.ИдентификаторФинЗаписи,
	|	УменьшениеСтоимости.ХозяйственнаяОперация,
	|	УменьшениеСтоимости.НастройкаХозяйственнойОперации";
	

	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеНДС // Дт <76.07.9> - Кт <76.07.1>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)                         КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.СуммаНДС                                                        КАК Сумма,
	|	Расчеты.СуммаНДСУпр                                                     КАК СуммаУпр,
	|	Расчеты.СуммаНДСРегл                                                    КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеНДСПоАренде                                               КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеНДСПоАренде                                      КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаНДС > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.СуммаНДС                                                        КАК Сумма,
	|	Расчеты.СуммаНДСУпр                                                     КАК СуммаУпр,
	|	Расчеты.СуммаНДСРегл                                                    КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеНДСПоАренде                                               КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеНДСПоАренде                                      КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаНДС > 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеНДС // Дт <76.07.1> - Кт <76.07.9>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	-Расчеты.СуммаНДС                                                       КАК Сумма,
	|	-Расчеты.СуммаНДСУпр                                                    КАК СуммаУпр,
	|	-Расчеты.СуммаНДСРегл                                                   КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеНДСПоАренде                                               КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеНДСПоАренде                                      КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаНДС < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)                         КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	-Расчеты.СуммаНДС                                                       КАК Сумма,
	|	-Расчеты.СуммаНДСУпр                                                    КАК СуммаУпр,
	|	-Расчеты.СуммаНДСРегл                                                   КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеНДСПоАренде                                               КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеНДСПоАренде                                      КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаНДС < 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеПроцентов // Дт <76.07.5> - Кт <76.07.1>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)                    КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.СуммаПроцентов                                                  КАК Сумма,
	|	Расчеты.СуммаПроцентовУпр                                               КАК СуммаУпр,
	|	Расчеты.СуммаПроцентовРегл                                              КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеПроцентовПоАренде                                         КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеПроцентовПоАренде                                КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаПроцентов > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.СуммаПроцентов                                                  КАК Сумма,
	|	Расчеты.СуммаПроцентовУпр                                               КАК СуммаУпр,
	|	Расчеты.СуммаПроцентовРегл                                              КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеПроцентовПоАренде                                         КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеПроцентовПоАренде                                КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаПроцентов > 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеПроцентов // Дт <76.07.1> - Кт <76.07.5>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	-Расчеты.СуммаПроцентов                                                 КАК Сумма,
	|	-Расчеты.СуммаПроцентовУпр                                              КАК СуммаУпр,
	|	-Расчеты.СуммаПроцентовРегл                                             КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеПроцентовПоАренде                                         КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеПроцентовПоАренде                                КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаПроцентов < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)                    КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	-Расчеты.СуммаПроцентов                                                 КАК Сумма,
	|	-Расчеты.СуммаПроцентовУпр                                              КАК СуммаУпр,
	|	-Расчеты.СуммаПроцентовРегл                                             КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеПроцентовПоАренде                                         КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеПроцентовПоАренде                                КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаПроцентов < 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ИзлишнеНачисленныеПроценты // Дт <76.07.5> - Кт <91.01>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)                    КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.Сумма                                                           КАК Сумма,
	|	Расчеты.СуммаУпр                                                        КАК СуммаУпр,
	|	Расчеты.СуммаРегл                                                       КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзлишнеНачисленныеПроцентыПоАренде                                  КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзлишнеНачисленныеПроцентыПоАренде                         КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втИзлишнеНачисленныеПроценты КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	&СозданДляПерехода";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область НедоначисленныеПроценты // Дт <91.02> - Кт <76.07.5>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)                    КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.Сумма                                                           КАК Сумма,
	|	Расчеты.СуммаУпр                                                        КАК СуммаУпр,
	|	Расчеты.СуммаРегл                                                       КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_НедоначисленныеПроцентыПоАренде                                     КАК ХозяйственнаяОперация,
	|	&НастройкаХО_НедоначисленныеПроцентыПоАренде                            КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втНедоначисленныеПроценты КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	&СозданДляПерехода";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПереоценки(ТекстыЗапроса, Запрос, "ОС", "Документ.ИзменениеУсловийДоговораАренды");
	ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&Организация                      КАК Организация,
	|	&ПодразделениеПроценты            КАК Подразделение,
	|	&НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                   КАК Статья,
	|	&АналитикаАктивовПассивовРасходы  КАК Аналитика,
	|	&НастройкаСчетовУчетаРасходы      КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииБУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|
	|	0 КАК ПостояннаяРазница,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииБУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВременнаяРазница,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	ТаблицаПереоценки.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УменьшениеНакопленнойАмортизации     КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ТаблицаПереоценки КАК ТаблицаПереоценки
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = ТаблицаПереоценки.ОсновноеСредство
	|ГДЕ
	|	(ТаблицаПереоценки.СуммаУценкиАмортизацииУУ > 0
	|		ИЛИ ТаблицаПереоценки.СуммаУценкиАмортизацииБУ > 0)
	|	И &СозданДляПерехода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&Организация                      КАК Организация,
	|	&ПодразделениеПроценты            КАК Подразделение,
	|	&НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                   КАК Статья,
	|	&АналитикаАктивовПассивовРасходы  КАК Аналитика,
	|	&НастройкаСчетовУчетаРасходы      КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ТаблицаРасходы.СуммаРегл КАК СуммаРегл,
	|
	|	0 КАК ПостояннаяРазница,
	|
	|	ТаблицаРасходы.СуммаРегл КАК ВременнаяРазница,
	|
	|	ТаблицаРасходы.СуммаУпр КАК СуммаУпр,
	|	ТаблицаРасходы.СуммаУпр КАК СуммаСНДС,
	|	ТаблицаРасходы.СуммаУпр КАК СуммаБезНДС,
	|
	|	&ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_НедоначисленныеПроцентыПоАренде КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втНедоначисленныеПроценты КАК ТаблицаРасходы
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И &СозданДляПерехода";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПрочиеДоходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;

	ТекстЗапросаТаблицаВтИзлишнеНачисленныеПроценты(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область ИзлишнеНачисленныеПроценты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)            КАК ВидДвижения,
	|	&Период                                           КАК Период,
	|	&Организация                                      КАК Организация,
	|	&Подразделение                                    КАК Подразделение,
	|	&НаправлениеДеятельности                          КАК НаправлениеДеятельности,
	|	&СтатьяДоходов                                    КАК СтатьяДоходов,
	|	&АналитикаДоходов                                 КАК АналитикаДоходов,
	|
	|	ТаблицаДоходы.Сумма                               КАК Сумма,
	|	ТаблицаДоходы.СуммаУпр                            КАК СуммаУпр,
	|	ТаблицаДоходы.СуммаРегл                           КАК СуммаРегл,
	|
	|	&ИдентификаторДокумента                           КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзлишнеНачисленныеПроцентыПоАренде            КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзлишнеНачисленныеПроцентыПоАренде   КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втИзлишнеНачисленныеПроценты КАК ТаблицаДоходы
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяДоходов) = ТИП(ПланВидовХарактеристик.СтатьиДоходов)
	|	И &СозданДляПерехода";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	
	#Область ПревышениеУменьшенияППАНадБалансовойСтоимостью
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                КАК ВидДвижения,
	|	&Период                                               КАК Период,
	|	&Организация                                          КАК Организация,
	|	&Подразделение                                        КАК Подразделение,
	|	&НаправлениеДеятельности                              КАК НаправлениеДеятельности,
	|	&СтатьяДоходов                                        КАК СтатьяДоходов,
	|	&АналитикаДоходов                                     КАК АналитикаДоходов,
	|
	|	ВЫБОР
	|		КОГДА УменьшениеСтоимости.Стоимость > БалансоваяСтоимость.БалансоваяСтоимостьУпр ТОГДА
	|			УменьшениеСтоимости.Стоимость - БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|		ИНАЧЕ 0
	|		КОНЕЦ                                             КАК Сумма,
	|
	|	ВЫБОР
	|		КОГДА УменьшениеСтоимости.Стоимость > БалансоваяСтоимость.БалансоваяСтоимостьУпр ТОГДА
	|			УменьшениеСтоимости.Стоимость - БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|		ИНАЧЕ 0
	|		КОНЕЦ                                             КАК СуммаУпр,
	|	
	|	ВЫБОР
	|		КОГДА УменьшениеСтоимости.СтоимостьРегл > БалансоваяСтоимость.БалансоваяСтоимостьРегл ТОГДА
	|			УменьшениеСтоимости.СтоимостьРегл - БалансоваяСтоимость.БалансоваяСтоимостьРегл
	|		ИНАЧЕ 0
	|		КОНЕЦ                                             КАК СуммаРегл,
	|	
	|	УменьшениеСтоимости.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеСтоимостиАрендованныхОС                 КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеСтоимостиАрендованныхОС        КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втУменьшениеСтоимости КАК УменьшениеСтоимости
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтБалансоваяСтоимостьОС КАК БалансоваяСтоимость
	|		ПО УменьшениеСтоимости.ОсновноеСредство = БалансоваяСтоимость.ОсновноеСредство
	|ГДЕ
	|	ВЫБОР
	|		КОГДА УменьшениеСтоимости.Стоимость > БалансоваяСтоимость.БалансоваяСтоимостьУпр ТОГДА
	|			УменьшениеСтоимости.Стоимость - БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|		ИНАЧЕ 0
	|		КОНЕЦ > 0
	|
	|	ИЛИ
	|
	|	ВЫБОР
	|		КОГДА УменьшениеСтоимости.СтоимостьРегл > БалансоваяСтоимость.БалансоваяСтоимостьРегл ТОГДА
	|			УменьшениеСтоимости.СтоимостьРегл - БалансоваяСтоимость.БалансоваяСтоимостьРегл
	|		ИНАЧЕ 0
	|		КОНЕЦ > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                КАК ВидДвижения,
	|	&Период                                               КАК Период,
	|	&Организация                                          КАК Организация,
	|	&Подразделение                                        КАК Подразделение,
	|	&НаправлениеДеятельности                              КАК НаправлениеДеятельности,
	|	&СтатьяДоходов                                        КАК СтатьяДоходов,
	|	&АналитикаДоходов                                     КАК АналитикаДоходов, 
	|
	|   ВЫБОР
	|		КОГДА ВтНачисленияВосстановления.СуммаПроцентовУпр > БалансоваяСтоимость.БалансоваяСтоимостьУпр ТОГДА
	|			ВтНачисленияВосстановления.СуммаПроцентовУпр - БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|		ИНАЧЕ 0
	|		КОНЕЦ                                             КАК Сумма,
	|
	|	ВЫБОР
	|		КОГДА ВтНачисленияВосстановления.СуммаПроцентовУпр > БалансоваяСтоимость.БалансоваяСтоимостьУпр ТОГДА
	|			ВтНачисленияВосстановления.СуммаПроцентовУпр - БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|		ИНАЧЕ 0
	|		КОНЕЦ                                             КАК СуммаУпр,
	|
	|	ВЫБОР
	|		КОГДА ВтНачисленияВосстановления.СуммаПроцентовРегл > БалансоваяСтоимость.БалансоваяСтоимостьРегл ТОГДА
	|			ВтНачисленияВосстановления.СуммаПроцентовРегл - БалансоваяСтоимость.БалансоваяСтоимостьРегл
	|		ИНАЧЕ 0
	|		КОНЕЦ                                             КАК СуммаРегл,
	|	
	|	ВтНачисленияВосстановления.ИдентификаторСтроки        КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеСтоимостиАрендованныхОС                 КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеСтоимостиАрендованныхОС        КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ВтНачисленияВосстановления КАК ВтНачисленияВосстановления
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|			ПО СведенияОбУчете.ОсновноеСредство = ВтНачисленияВосстановления.ОсновноеСредство
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтБалансоваяСтоимостьОС КАК БалансоваяСтоимость
	|			ПО ВтНачисленияВосстановления.ОсновноеСредство = БалансоваяСтоимость.ОсновноеСредство
	|ГДЕ
	|	ВЫБОР
	|		КОГДА ВтНачисленияВосстановления.СуммаПроцентовУпр > БалансоваяСтоимость.БалансоваяСтоимостьУпр ТОГДА
	|			ВтНачисленияВосстановления.СуммаПроцентовУпр - БалансоваяСтоимость.БалансоваяСтоимостьУпр
	|		ИНАЧЕ 0
	|		КОНЕЦ > 0 
	|
	|	ИЛИ
	|
	|	ВЫБОР
	|		КОГДА ВтНачисленияВосстановления.СуммаПроцентовРегл > БалансоваяСтоимость.БалансоваяСтоимостьРегл ТОГДА
	|			ВтНачисленияВосстановления.СуммаПроцентовРегл - БалансоваяСтоимость.БалансоваяСтоимостьРегл
	|		ИНАЧЕ 0
	|		КОНЕЦ > 0";
	
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область УменьшениеНакопленнойАмортизацииПриПереходеНаФСБУ25
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)   КАК ВидДвижения,
	|	&Период                                  КАК Период,
	|	&Организация                             КАК Организация,
	|	втСведенияОбУчете.Местонахождение        КАК Подразделение,
	|	&НаправлениеДеятельности                 КАК НаправлениеДеятельности,
	|	&СтатьяДоходов                           КАК СтатьяДоходов,
	|	&АналитикаДоходов                        КАК АналитикаДоходов,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВУпрУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_СтоимостьВНА.Стоимость ЕСТЬ НЕ NULL 
	|						ТОГДА (1 - (ВТ_СтоимостьВНА.Стоимость - Проценты.СуммаПроцентовУпр) / ВТ_СтоимостьВНА.Стоимость) 
	|								* ВТ_АмортизацияВНА.Амортизация
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВУпрУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_СтоимостьВНА.Стоимость ЕСТЬ НЕ NULL 
	|						ТОГДА (1 - (ВТ_СтоимостьВНА.Стоимость - Проценты.СуммаПроцентовУпр) / ВТ_СтоимостьВНА.Стоимость) 
	|								* ВТ_АмортизацияВНА.Амортизация
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР
	|		КОГДА втСведенияОбУчете.ОтражатьВРеглУчете
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_АмортизацияВНА.НачислятьИзнос
	|						ТОГДА 0
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ВТ_СтоимостьВНА.СтоимостьРегл ЕСТЬ НЕ NULL 
	|								ТОГДА (1 - (ВТ_СтоимостьВНА.СтоимостьРегл - Проценты.СуммаПроцентовРегл) / ВТ_СтоимостьВНА.СтоимостьРегл) 
	|										* ВТ_АмортизацияВНА.АмортизацияРегл
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|
	|	Проценты.ИдентификаторСтроки                  КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеНакопленнойАмортизации          КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеНакопленнойАмортизации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтНачисленияВосстановления КАК Проценты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО Проценты.ОсновноеСредство = ВТ_СтоимостьВНА.ОбъектУчета
	|			И (ВТ_СтоимостьВНА.Ссылка = &Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК ВТ_АмортизацияВНА
	|		ПО Проценты.ОсновноеСредство = ВТ_АмортизацияВНА.ОбъектУчета
	|			И (ВТ_АмортизацияВНА.Ссылка = &Ссылка)
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК втСведенияОбУчете
	|		ПО Проценты.ОсновноеСредство = втСведенияОбУчете.ОсновноеСредство
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяДоходов) = ТИП(ПланВидовХарактеристик.СтатьиДоходов)
	|	И &СозданДляПерехода";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаТоварыКОформлениюДокументовИмпорта(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыКОформлениюДокументовИмпорта";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);  
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ТоварыКОформлениюДокументовИмпортаОстаткиИОбороты.Период, МЕСЯЦ) КАК Период,
	|	ТоварыКОформлениюДокументовИмпортаОстаткиИОбороты.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
	|ПОМЕСТИТЬ ВтТоварыКОформлениюДокументовИмпортаОстатки
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюДокументовИмпорта.ОстаткиИОбороты(,, Месяц,,
	|		ДокументПоступления = &ДокументЗаключенияДоговораАренды
	|	И ТипДокументаИмпорта = &ИдентификаторЗаявлениеОВвозеТоваров) КАК ТоварыКОформлениюДокументовИмпортаОстаткиИОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ПредыдущийГрафикОплатыУслугПоАренде.Дата, МЕСЯЦ) КАК Период,
	|	СУММА(ПредыдущийГрафикОплатыУслугПоАренде.УслугаПоАренде) КАК УслугаПоАренде,
	|	СУММА(ПредыдущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж) КАК ОбеспечительныйПлатеж,
	|	СУММА(ПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость) КАК ВыкупнаяСтоимость,
	|	СУММА(ПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимостьАванс) КАК ВыкупнаяСтоимостьАванс
	|ПОМЕСТИТЬ ВтПредыдущийГрафикОплатыУслугПоАренде
	|ИЗ
	|	РегистрСведений.ГрафикОплатУслугПоАренде КАК ПредыдущийГрафикОплатыУслугПоАренде
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПредыдущиеУсловияДоговора КАК ВтПредыдущиеУсловияДоговора
	|		ПО ПредыдущийГрафикОплатыУслугПоАренде.АктуальныеУсловияДоговора = ВтПредыдущиеУсловияДоговора.АктуальныеУсловияДоговора
	|ГДЕ
	|	&ТребуетсяОформлениеЗаявленийОВвозе
	|СГРУППИРОВАТЬ ПО
	|	КОНЕЦПЕРИОДА(ПредыдущийГрафикОплатыУслугПоАренде.Дата, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ТекущийГрафикОплатыУслугПоАренде.Дата, МЕСЯЦ) КАК Период,
	|	СУММА(ТекущийГрафикОплатыУслугПоАренде.УслугаПоАренде) КАК УслугаПоАренде,
	|	СУММА(ТекущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж) КАК ОбеспечительныйПлатеж,
	|	СУММА(ТекущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость) КАК ВыкупнаяСтоимость
	|ПОМЕСТИТЬ ВтТекущийГрафикОплатыУслугПоАренде
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикОплатУслуг КАК ТекущийГрафикОплатыУслугПоАренде
	|ГДЕ
	|	ТекущийГрафикОплатыУслугПоАренде.Ссылка = &Ссылка
	|	И &ТребуетсяОформлениеЗаявленийОВвозе
	|СГРУППИРОВАТЬ ПО
	|	КОНЕЦПЕРИОДА(ТекущийГрафикОплатыУслугПоАренде.Дата, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВтТекущийГрафикОплатыУслугПоАренде.Период, ВтПредыдущийГрафикОплатыУслугПоАренде.Период) КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Ссылка КАК Регистратор,
	|	&Организация КАК Организация,
	|	&Партнер КАК Поставщик,
	|	&ДокументЗаключенияДоговораАренды КАК ДокументПоступления,
	|	&ИдентификаторЗаявлениеОВвозеТоваров КАК ТипДокументаИмпорта,
	|	0 КАК Количество,
	|	0 КАК КоличествоПоРНПТ,
	|	-(ВтПредыдущийГрафикОплатыУслугПоАренде.УслугаПоАренде + ВтПредыдущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж +
	|		ВтПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость +
	|		ВтПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимостьАванс) КАК Сумма,
	|	ИСТИНА КАК Первичное
	|ИЗ
	|	ВтПредыдущийГрафикОплатыУслугПоАренде КАК ВтПредыдущийГрафикОплатыУслугПоАренде
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТекущийГрафикОплатыУслугПоАренде КАК ВтТекущийГрафикОплатыУслугПоАренде
	|		ПО ВтПредыдущийГрафикОплатыУслугПоАренде.Период = ВтТекущийГрафикОплатыУслугПоАренде.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыКОформлениюДокументовИмпортаОстатки КАК ВтТоварыКОформлениюДокументовИмпортаОстатки
	|		ПО ВтПредыдущийГрафикОплатыУслугПоАренде.Период = ВтТоварыКОформлениюДокументовИмпортаОстатки.Период
	|ГДЕ
	|	(ЕСТЬNULL(ВтТоварыКОформлениюДокументовИмпортаОстатки.СуммаКонечныйОстаток, 0) <> 0
	|	ИЛИ ВтТоварыКОформлениюДокументовИмпортаОстатки.СуммаКонечныйОстаток ЕСТЬ NULL)
	|	И ВтПредыдущийГрафикОплатыУслугПоАренде.УслугаПоАренде + ВтПредыдущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж
	|		+ ВтПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость +
	|		ВтПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимостьАванс <> ЕСТЬNULL(ВтТекущийГрафикОплатыУслугПоАренде.УслугаПоАренде
	|		+ ВтТекущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж + ВтТекущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость,
	|		0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтТекущийГрафикОплатыУслугПоАренде.Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Ссылка,
	|	&Организация,
	|	&Партнер,
	|	&ДокументЗаключенияДоговораАренды,
	|	&ИдентификаторЗаявлениеОВвозеТоваров,
	|	0,
	|	0,
	|	ВтТекущийГрафикОплатыУслугПоАренде.УслугаПоАренде + ВтТекущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж +
	|		ВтТекущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость,
	|	ИСТИНА
	|ИЗ
	|	ВтТекущийГрафикОплатыУслугПоАренде КАК ВтТекущийГрафикОплатыУслугПоАренде
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыКОформлениюДокументовИмпортаОстатки КАК ВтТоварыКОформлениюДокументовИмпортаОстатки
	|		ПО ВтТекущийГрафикОплатыУслугПоАренде.Период = ВтТоварыКОформлениюДокументовИмпортаОстатки.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПредыдущийГрафикОплатыУслугПоАренде КАК ВтПредыдущийГрафикОплатыУслугПоАренде
	|		ПО ВтТекущийГрафикОплатыУслугПоАренде.Период = ВтПредыдущийГрафикОплатыУслугПоАренде.Период
	|ГДЕ
	|	(ЕСТЬNULL(ВтТоварыКОформлениюДокументовИмпортаОстатки.СуммаКонечныйОстаток, 0) <> 0
	|	ИЛИ ВтТоварыКОформлениюДокументовИмпортаОстатки.СуммаКонечныйОстаток ЕСТЬ NULL)
	|	И ВтТекущийГрафикОплатыУслугПоАренде.УслугаПоАренде + ВтТекущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж +
	|		ВтТекущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость <> ЕСТЬNULL(ВтПредыдущийГрафикОплатыУслугПоАренде.УслугаПоАренде
	|		+ ВтПредыдущийГрафикОплатыУслугПоАренде.ОбеспечительныйПлатеж +
	|		ВтПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимость +
	|		ВтПредыдущийГрафикОплатыУслугПоАренде.ВыкупнаяСтоимостьАванс, 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	&Партнер                                КАК Партнер,
	|	&Контрагент                             КАК Контрагент,
	|	&Договор                                КАК Договор,
	|	&Подразделение                          КАК Подразделение,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&СуммаДокумента                         КАК Сумма,
	|	&Валюта                                 КАК Валюта,
	|	&Период                                 КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)    КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Проведен                               КАК Проведен,
	|	ИСТИНА                                  КАК ОтражатьВРеглУчете,
	|	ИСТИНА                                  КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеУсловийДоговораАренды.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти

#Область ФормированиеВременныхТаблицыДляПроведения

Процедура ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "ВтПрочиеРасходы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "ВтИсходныеПрочиеРасходы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы());
	
	#Область НедоначисленныеПроценты // Дт <91.02> - Кт <76.07.5>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&ПодразделениеПроценты КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	ТаблицаРасходы.СуммаУпр КАК СуммаСНДС,
	|	ТаблицаРасходы.СуммаУпр КАК СуммаБезНДС,
	|	ТаблицаРасходы.СуммаУпр КАК СуммаБезНДСУпр,
	|	ТаблицаРасходы.СуммаРегл КАК СуммаСНДСРегл,
	|	ТаблицаРасходы.СуммаРегл КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	ТаблицаРасходы.СуммаРегл КАК ВременнаяРазница,
	|	
	|	&ХО_НедоначисленныеПроцентыПоАренде КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|
	|	&ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_НедоначисленныеПроцентыПоАренде КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втНедоначисленныеПроценты КАК ТаблицаРасходы
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И &СозданДляПерехода";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеНакопленнойАмортизации // Дт <91.02> - Кт <02.03>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	СведенияОбУчете.Местонахождение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСНДСРегл,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	0 КАК ПостояннаяРазница,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВременнаяРазница,
	|	
	|	&ХО_УвеличениеНакопленнойАмортизации КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|
	|	ТаблицаПереоценки.ИдентификаторФинЗаписи       КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УвеличениеНакопленнойАмортизации  КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ТаблицаПереоценки КАК ТаблицаПереоценки
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = ТаблицаПереоценки.ОсновноеСредство
	|ГДЕ
	|	(ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|		ИЛИ ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0)
	|	И ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "ВтПартииПрочихРасходов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "ВтИсходныеПартииПрочихРасходов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;

	ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПартииПрочихРасходов.ТекстОписаниеВтИсходныеПартииПрочихРасходов());

	#Область НедоначисленныеПроценты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                         КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Организация                                    КАК Организация,
	|	&ПодразделениеПроценты                          КАК Подразделение,
	|	&СтатьяРасходовПроценты                         КАК СтатьяРасходов,
	|	&АналитикаРасходовПроценты                      КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                    КАК АналитикаАктивовПассивов,
	|	&Ссылка                                         КАК ДокументПоступленияРасходов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	&НаправлениеДеятельности                        КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВидДеятельностиНДС,
	|
	|	ТаблицаРасходы.СуммаУпр                         КАК Стоимость,
	|	ТаблицаРасходы.СуммаУпр                         КАК СтоимостьБезНДС,
	|	0                                               КАК НДСУпр,
	|	ТаблицаРасходы.СуммаРегл                        КАК СтоимостьРегл,
	|	0                                               КАК НДСРегл,
	|	0                                               КАК ПостояннаяРазница,
	|	ТаблицаРасходы.СуммаРегл                        КАК ВременнаяРазница,
	|
	|	&ХО_НедоначисленныеПроцентыПоАренде             КАК ХозяйственнаяОперация,
	|	&ИдентификаторДокумента                         КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_НедоначисленныеПроцентыПоАренде    КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	втНедоначисленныеПроценты КАК ТаблицаРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК ПВХСтатьиРасходов
	|		ПО &СтатьяРасходовПроценты = ПВХСтатьиРасходов.Ссылка
	|ГДЕ
	|	ПВХСтатьиРасходов.ВариантРаспределенияРасходовРегл В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
	|	И &СозданДляПерехода";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтУвеличениеСтоимости(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "ВтУвеличениеСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКорректировкаСтоимости(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаСтоимости.ОсновноеСредство     КАК ОсновноеСредство,
	|	КорректировкаСтоимости.ИдентификаторСтроки  КАК ИдентификаторСтроки,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.Стоимость > 0
	|			ТОГДА КорректировкаСтоимости.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьНУ > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьНУ,
	|
	// ЗалоговаяСтоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.ЗалоговаяСтоимость > 0
	|			ТОГДА КорректировкаСтоимости.ЗалоговаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЗалоговаяСтоимость
	|
	|ПОМЕСТИТЬ ВтУвеличениеСтоимости
	|ИЗ
	|	ВтКорректировкаСтоимости КАК КорректировкаСтоимости
	|ГДЕ
	|	КорректировкаСтоимости.Стоимость > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ > 0
	|		ИЛИ КорректировкаСтоимости.ЗалоговаяСтоимость > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтУменьшениеСтоимости(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "ВтУменьшениеСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКорректировкаСтоимости(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаСтоимости.ОсновноеСредство     КАК ОсновноеСредство,
	|	КорректировкаСтоимости.ИдентификаторСтроки  КАК ИдентификаторСтроки,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.Стоимость < 0
	|			ТОГДА -КорректировкаСтоимости.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьНУ < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьНУ,
	|
	// ЗалоговаяСтоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.ЗалоговаяСтоимость < 0
	|			ТОГДА -КорректировкаСтоимости.ЗалоговаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК ЗалоговаяСтоимость
	|
	|ПОМЕСТИТЬ ВтУменьшениеСтоимости
	|ИЗ
	|	ВтКорректировкаСтоимости КАК КорректировкаСтоимости
	|ГДЕ
	|	КорректировкаСтоимости.Стоимость < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ < 0
	|		ИЛИ КорректировкаСтоимости.ЗалоговаяСтоимость < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКорректировкаСтоимости(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "втКорректировкаСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустыеТаблицыОтложенныхДвижений(ТекстыЗапроса, Запрос);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство    КАК ОсновноеСредство,
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА &ЕстьУчетУУ 
	|			ТОГДА ТаблицаОС.СтоимостьУУ - ЕСТЬNULL(СтоимостьОС.Стоимость, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА &ЕстьУчетБУ
	|			ТОГДА ТаблицаОС.СтоимостьБУ - ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА &ЕстьУчетНУ
	|			ТОГДА ТаблицаОС.СтоимостьНУ - ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьНУ,
	|
	// ЗалоговаяСтоимость
	|	ВЫБОР 
	|		КОГДА &Балансодержатель = ЗНАЧЕНИЕ(Перечисление.БалансодержательПредметовАренды.Арендодатель)
	|				И НЕ &ИспользуетсяУчетАрендыПоФСБУ25_2018
	|			ТОГДА ТаблицаОС.Стоимость - ЕСТЬNULL(СтоимостьОС.ЗалоговаяСтоимость, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЗалоговаяСтоимость
	|
	|ПОМЕСТИТЬ втКорректировкаСтоимости
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = &Ссылка
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И (ЕСТЬNULL(СтоимостьОС.Стоимость, 0) <> ТаблицаОС.СтоимостьУУ 
	|				И &ЕстьУчетУУ 
	|
	|		ИЛИ (ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)) <> ТаблицаОС.СтоимостьБУ 
	|				И &ЕстьУчетБУ 
	|
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) <> ТаблицаОС.СтоимостьНУ
	|
	|		ИЛИ ТаблицаОС.Стоимость <> ЕСТЬNULL(СтоимостьОС.ЗалоговаяСтоимость, 0)
	|				И &Балансодержатель = ЗНАЧЕНИЕ(Перечисление.БалансодержательПредметовАренды.Арендодатель)
	|				И НЕ &ИспользуетсяУчетАрендыПоФСБУ25_2018)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтИзлишнеНачисленныеПроценты(ТекстыЗапроса)
	
	ИмяТаблицы = "втИзлишнеНачисленныеПроценты";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКорректировкаНачисленныхПроцентов(ТекстыЗапроса);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	-Т.Сумма КАК Сумма,
	|	-Т.СуммаУпр КАК СуммаУпр,
	|	-Т.СуммаРегл КАК СуммаРегл
	|
	|ПОМЕСТИТЬ втИзлишнеНачисленныеПроценты
	|ИЗ
	|	втКорректировкаНачисленныхПроцентов КАК Т
	|ГДЕ
	|	Т.Сумма < 0
	|	И &СозданДляПерехода";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса)
	
	ИмяТаблицы = "втНедоначисленныеПроценты";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКорректировкаНачисленныхПроцентов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Т.Сумма КАК Сумма,
	|	Т.СуммаУпр КАК СуммаУпр,
	|	Т.СуммаРегл КАК СуммаРегл
	|
	|ПОМЕСТИТЬ втНедоначисленныеПроценты
	|ИЗ
	|	втКорректировкаНачисленныхПроцентов КАК Т
	|ГДЕ
	|	Т.Сумма > 0
	|	И &СозданДляПерехода";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКорректировкаНачисленныхПроцентов(ТекстыЗапроса)
	
	ИмяТаблицы = "втКорректировкаНачисленныхПроцентов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СУММА(Графики.Сумма) КАК Сумма,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетУУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА СУММА(Графики.Сумма)
	|		ИНАЧЕ СУММА(ВЫРАЗИТЬ(Графики.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)))
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетБУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА СУММА(Графики.Сумма)
	|		ИНАЧЕ СУММА(ВЫРАЗИТЬ(Графики.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)))
	|	КОНЕЦ КАК СуммаРегл
	|
	|ПОМЕСТИТЬ втКорректировкаНачисленныхПроцентов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТекущийГрафик.Проценты КАК Сумма
	|	ИЗ
	|		Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияПроцентов КАК ТекущийГрафик
	|	ГДЕ
	|		ТекущийГрафик.Ссылка = &Ссылка
	|		И (ТекущийГрафик.Дата < &НачалоМесяца
	|				ИЛИ &СозданДляПерехода И ТекущийГрафик.Дата <= КОНЕЦПЕРИОДА(&Период, ДЕНЬ))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		-ПредыдущийГрафик.Проценты
	|	ИЗ
	|		РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ПредыдущийГрафик
	|	ГДЕ
	|		ПредыдущийГрафик.Дата < &НачалоМесяца
	|		И ПредыдущийГрафик.Активность
	|		И ПредыдущийГрафик.Договор = &Договор
	|		И ПредыдущийГрафик.АктуальныеУсловияДоговора В (
	|			ВЫБРАТЬ
	|				Т.АктуальныеУсловияДоговора 
	|			ИЗ 
	|				втПредыдущиеУсловияДоговора КАК Т)
	|
	|	) КАК Графики";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКорректировкаРасчетов(ТекстыЗапроса)
	
	ИмяТаблицы = "втКорректировкаРасчетов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСуммаПроцентов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора,
	|	
	|	&СуммаНДС - УсловияДоговоровАренды.СуммаНДС КАК СуммаНДС,
	|	ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - УсловияДоговоровАренды.СуммаПроцентов КАК СуммаПроцентов,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА &СуммаНДС - УсловияДоговоровАренды.СуммаНДС
	|		ИНАЧЕ ВЫРАЗИТЬ((&СуммаНДС - УсловияДоговоровАренды.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА &СуммаНДС - УсловияДоговоровАренды.СуммаНДС
	|		ИНАЧЕ ВЫРАЗИТЬ((&СуммаНДС - УсловияДоговоровАренды.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетУУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - УсловияДоговоровАренды.СуммаПроцентов
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - УсловияДоговоровАренды.СуммаПроцентов) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаПроцентовУпр,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетБУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - УсловияДоговоровАренды.СуммаПроцентов
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - УсловияДоговоровАренды.СуммаПроцентов) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаПроцентовРегл
	|
	|ПОМЕСТИТЬ втКорректировкаРасчетов
	|ИЗ
	|	втПредыдущиеУсловияДоговора КАК УсловияДоговоровАренды
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСуммаПроцентов КАК втСуммаПроцентов
	|		ПО ИСТИНА";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса)
	
	ИмяТаблицы = "втПредыдущиеУсловияДоговора";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора,
	|	УсловияДоговоровАренды.СуммаНДС КАК СуммаНДС,
	|	УсловияДоговоровАренды.СуммаПроцентов КАК СуммаПроцентов
	|ПОМЕСТИТЬ втПредыдущиеУсловияДоговора
	|ИЗ
	|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(&Период, Договор = &Договор И Регистратор <> &Ссылка) КАК УсловияДоговоровАренды";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтСуммаПроцентов(ТекстыЗапроса)

	ИмяТаблицы = "втСуммаПроцентов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&СуммаПроцентов КАК Сумма
	|ПОМЕСТИТЬ втСуммаПроцентов
	|ГДЕ
	|	НЕ &СозданДляПерехода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(ТекущийГрафик.Проценты)
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияПроцентов КАК ТекущийГрафик
	|ГДЕ
	|	ТекущийГрафик.Ссылка = &Ссылка
	|	И &СозданДляПерехода";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

// Инициализировать вт корректировка стоимости ОС.
// 
// Параметры:
//  Запрос - Запрос
//  ТекстыЗапроса - СписокЗначений - Тексты запроса
Процедура ИнициализироватьВтКорректировкаСтоимостиОС(Запрос, ТекстыЗапроса)
	
	// Запрет повторной инициализации.
	Если Запрос.Параметры.Свойство("ВтКорректировкаСтоимостиОСИнициализирована") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросИнициализации = Новый Запрос();

	Для Каждого ПараметрЗапроса Из Запрос.Параметры Цикл
		ЗапросИнициализации.Параметры.Вставить(ПараметрЗапроса.Ключ,ПараметрЗапроса.Значение);
	КонецЦикла;
	
	ЗапросИнициализации.УстановитьПараметр("ОСПустаяСсылка", Справочники.ОбъектыЭксплуатации.ПустаяСсылка());
	
	
	// Получим доли каждого ОС в общей справедливой стоимости.
	ЗапросИнициализации.Текст = "
	|ВЫБРАТЬ
	|	СУММА(ИзменениеУсловийДоговораАрендыОС.СправедливаяСтоимость) КАК ОбщаяСправедливаяСтоимость,
	|	СУММА(ИзменениеУсловийДоговораАрендыОС.Стоимость) КАК ОбщаяСтоимость
	|ПОМЕСТИТЬ втОбщаяСправедливаяСтоимость
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ИзменениеУсловийДоговораАрендыОС
	|ГДЕ
	|	ИзменениеУсловийДоговораАрендыОС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОС.ОсновноеСредство      КАК ОсновноеСредство,
	|	ОС.СправедливаяСтоимость КАК СправедливаяСтоимость,
	|	СправедливаяСтоимостьОС.ОбщаяСправедливаяСтоимость,
	|	ОС.Стоимость КАК Стоимость,
	|	СправедливаяСтоимостьОС.ОбщаяСтоимость,
	|	ВЫБОР
	|		КОГДА СправедливаяСтоимостьОС.ОбщаяСправедливаяСтоимость <> 0
	|			ТОГДА ОС.СправедливаяСтоимость / СправедливаяСтоимостьОС.ОбщаяСправедливаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ДоляВСправедливойСтоимости,
	|	ВЫБОР
	|		КОГДА СправедливаяСтоимостьОС.ОбщаяСтоимость <> 0
	|			ТОГДА ОС.Стоимость / СправедливаяСтоимостьОС.ОбщаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ДоляВСтоимости,
	|	ОС.ИдентификаторСтроки   КАК ИдентификаторСтроки
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОбщаяСправедливаяСтоимость КАК СправедливаяСтоимостьОС
	|		ПО ИСТИНА
	|ГДЕ
	|	ОС.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДоляВСправедливойСтоимости УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втОбщаяСправедливаяСтоимость";
	
	РезультатРасчетаДолей = ЗапросИнициализации.Выполнить();
	ДолиВСправедливойСтоимости = РезультатРасчетаДолей.Выгрузить();
	
	// Получим общее изменение стоимости без разбивки по основным средствам.
	ТекстыЗапросаИнициализации = Новый СписокЗначений;
	ТекстЗапросаТаблицаВтКорректировкаРасчетов(ТекстыЗапросаИнициализации);
	
	ТекстЗапросаИзменений = "ВЫБРАТЬ
	|	втКорректировкаРасчетов.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	&ОСПустаяСсылка КАК ОсновноеСредство,
	|	втКорректировкаРасчетов.СуммаПроцентов КАК СуммаПроцентов,
	|	втКорректировкаРасчетов.СуммаПроцентовУпр КАК СуммаПроцентовУпр,
	|	втКорректировкаРасчетов.СуммаПроцентовРегл КАК СуммаПроцентовРегл,
	|	ВЫРАЗИТЬ("""" КАК Строка(36)) КАК ИдентификаторСтроки
	|ИЗ
	|	втКорректировкаРасчетов КАК втКорректировкаРасчетов";
	
	ТекстыЗапросаИнициализации.Добавить(ТекстЗапросаИзменений, "ТекстЗапросаИзменений");

	ТекстЗапросаИнициализации = СтрСоединить(ТекстыЗапросаИнициализации.ВыгрузитьЗначения(), Символы.ПС+";");
											 
	ЗапросИнициализации.Текст = ТекстЗапросаИнициализации;
	
	РезультатИзменений = ЗапросИнициализации.Выполнить();
	ТаблицаИзменений = РезультатИзменений.Выгрузить();
	
	// Пустая копия таблицы изменений.
	РаспределенныеИзменения = ТаблицаИзменений.Скопировать();
	РаспределенныеИзменения.Очистить();
	
	// Заполним таблицу распределенных изменений.
	Для Каждого СтрокаИзменений Из ТаблицаИзменений Цикл
		
		Для Каждого СтрокаДолей Из ДолиВСправедливойСтоимости Цикл
			
			Если СтрокаДолей.ОбщаяСправедливаяСтоимость <> 0 Тогда
				Доля = СтрокаДолей.ДоляВСправедливойСтоимости;
			Иначе
				Доля = СтрокаДолей.ДоляВСтоимости;
			КонецЕсли;
			
			СтрокаРаспределения = РаспределенныеИзменения.Добавить();
			
			СтрокаРаспределения.АктуальныеУсловияДоговора = СтрокаИзменений.АктуальныеУсловияДоговора;
			СтрокаРаспределения.ИдентификаторСтроки       = СтрокаДолей.ИдентификаторСтроки;
			СтрокаРаспределения.ОсновноеСредство          = СтрокаДолей.ОсновноеСредство;
			
			СтрокаРаспределения.СуммаПроцентов  = Окр(СтрокаИзменений.СуммаПроцентов * Доля, 2);
			СтрокаРаспределения.СуммаПроцентовУпр  = Окр(СтрокаИзменений.СуммаПроцентовУпр  * Доля, 2);
			СтрокаРаспределения.СуммаПроцентовРегл = Окр(СтрокаИзменений.СуммаПроцентовРегл * Доля, 2);
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Проверим результат распределения, и отнесем погрешность округления на первую строку (с максимальной долей).
	ПогрешностьСуммаПроцентов = РаспределенныеИзменения.Итог("СуммаПроцентов") - ТаблицаИзменений.Итог("СуммаПроцентов");
	ПогрешностьСуммаПроцентовУпр = РаспределенныеИзменения.Итог("СуммаПроцентовУпр") - ТаблицаИзменений.Итог("СуммаПроцентовУпр");
	ПогрешностьСуммаПроцентовРегл = РаспределенныеИзменения.Итог("СуммаПроцентовРегл") - ТаблицаИзменений.Итог("СуммаПроцентовРегл");
	
	Для Каждого СтрокаРаспределения Из РаспределенныеИзменения Цикл
		
		СтрокаРаспределения.СуммаПроцентов     = СтрокаРаспределения.СуммаПроцентов - ПогрешностьСуммаПроцентов;
		СтрокаРаспределения.СуммаПроцентовУпр  = СтрокаРаспределения.СуммаПроцентовУпр - ПогрешностьСуммаПроцентовУпр;
		СтрокаРаспределения.СуммаПроцентовРегл = СтрокаРаспределения.СуммаПроцентовРегл - ПогрешностьСуммаПроцентовРегл;

		Прервать;
	КонецЦикла;
	
	// Поместим РаспределенныеИзменения в параметры основного запроса, и добавим запрос на выборку во временную таблицу.
	Запрос.УстановитьПараметр("ТзРаспределенныеИзменения",РаспределенныеИзменения);
	
	ТекстВтНачисленияВосстановления = "ВЫБРАТЬ
	|	ТзКорректировки.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора,
	|	ТзКорректировки.ОсновноеСредство          КАК ОсновноеСредство,
	|	ТзКорректировки.СуммаПроцентов            КАК СуммаПроцентов,
	|	ТзКорректировки.СуммаПроцентовУпр         КАК СуммаПроцентовУпр,
	|	ТзКорректировки.СуммаПроцентовРегл        КАК СуммаПроцентовРегл,
	|	ТзКорректировки.ИдентификаторСтроки       КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВтНачисленияВосстановления
	|ИЗ
	|	&ТзРаспределенныеИзменения КАК ТзКорректировки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстВтНачисленияВосстановления, "ВтНачисленияВосстановления");
	
	// Признак успешной инициализации временной таблицы.
	Запрос.УстановитьПараметр("ВтНачисленияВосстановленияИнициализирована", Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтБалансоваяСтоимостьОС(Запрос, ТекстыЗапроса)
	ИмяТаблицы = "ВтБалансоваяСтоимостьОС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСтоимость.Ссылка                                                  КАК Ссылка,
	|	втСтоимость.ОбъектУчета                                             КАК ОсновноеСредство,
	|
	|	втСтоимость.Стоимость 
	|		- ЕСТЬNULL(втАмортизация.Амортизация, 0)
	|		- ЕСТЬNULL(втОбесценение.ОбесценениеУпр, 0)                     КАК БалансоваяСтоимостьУпр,
	|
	|	втСтоимость.СтоимостьРегл + втСтоимость.СтоимостьЦФ 
	|		- ЕСТЬNULL(втАмортизация.АмортизацияРегл, 0) - ЕСТЬNULL(втАмортизация.АмортизацияЦФ, 0) 
	|		- ЕСТЬNULL(втОбесценение.ОбесценениеРегл, 0)                    КАК БалансоваяСтоимостьРегл
	|
	|ПОМЕСТИТЬ ВтБалансоваяСтоимостьОС
	|
	|ИЗ
	|	ВТ_СтоимостьВНА КАК втСтоимость
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК втАмортизация
	|		ПО втСтоимость.ОбъектУчета = втАмортизация.ОбъектУчета
	|			И втАмортизация.Ссылка = &Ссылка
	|			И втСтоимость.Ссылка = &Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК втОбесценение
	|		ПО втСтоимость.ОбъектУчета = втОбесценение.ОбъектУчета";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ИзменениеУсловийДоговораАрендыЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат "";

КонецФункции

#КонецОбласти

#Область Заполнение

// Заполняет документ на основании договора аренды.
// 
// Параметры:
// 	Объект - ДокументОбъект.ИзменениеУсловийДоговораАренды - 
Процедура ЗаполнитьНаОснованииДоговораАренды(Объект) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект.Договор) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ОС.Очистить();
	Объект.ГрафикОплатУслуг.Очистить();
	Объект.ГрафикНачисленияУслуг.Очистить();
	Объект.ГрафикНачисленияПроцентов.Очистить();
	
	ТекстыПакетаЗапросов = Новый Массив;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	АрендованныеОС.Стоимость КАК Стоимость
	|ПОМЕСТИТЬ втАрендованныеОС
	|ИЗ
	|	РегистрСведений.АрендованныеОС.СрезПоследних(&Дата) КАК АрендованныеОС
	|ГДЕ
	|	АрендованныеОС.Договор = &Договор
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УсловияДоговоровАренды.Договор.Организация КАК Организация,
	|	УсловияДоговоровАренды.Договор.Партнер КАК Партнер,
	|	УсловияДоговоровАренды.Договор.Контрагент КАК Контрагент,
	|	УсловияДоговоровАренды.Договор.ВалютаВзаиморасчетов КАК Валюта,
	|	УсловияДоговоровАренды.Договор.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	УсловияДоговоровАренды.СтавкаНДС КАК СтавкаНДС,
	|	УсловияДоговоровАренды.СуммаОбеспечительногоПлатежа КАК СуммаОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.СуммаУслугПоАренде КАК СуммаУслугПоАренде,
	|	УсловияДоговоровАренды.СуммаВыкупаПредметовАренды КАК СуммаВыкупаПредметовАренды,
	|	УсловияДоговоровАренды.СуммаПроцентов КАК СуммаПроцентов,
	|	УсловияДоговоровАренды.СтоимостьПредметовАренды КАК СтоимостьПредметовАренды,
	|	УсловияДоговоровАренды.СуммаНДС КАК СуммаНДС,
	|	УсловияДоговоровАренды.СрокЛет КАК СрокЛет,
	|	УсловияДоговоровАренды.СрокМес КАК СрокМес,
	|	УсловияДоговоровАренды.СрокДней КАК СрокДней,
	|	УсловияДоговоровАренды.ДатаНачалаАренды КАК ДатаНачалаАренды,
	|	УсловияДоговоровАренды.ДатаПервогоПлатежа КАК ДатаПервогоПлатежа,
	|	УсловияДоговоровАренды.ДатаПервогоОбеспечительногоПлатежа КАК ДатаПервогоОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.ДатаВыкупа КАК ДатаВыкупа,
	|	УсловияДоговоровАренды.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	УсловияДоговоровАренды.ПериодичностьОплат КАК ПериодичностьОплат,
	|	УсловияДоговоровАренды.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	УсловияДоговоровАренды.ОплатаУслугиПоАренде КАК ОплатаУслугиПоАренде,
	|	УсловияДоговоровАренды.НачислениеУслугиПоАренде КАК НачислениеУслугиПоАренде,
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора
	|ПОМЕСТИТЬ втУсловияДоговоровАренды
	|ИЗ
	|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(
	|		&Дата, 
	|		Регистратор <> &Ссылка
	|			И Договор = &Договор) КАК УсловияДоговоровАренды
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УсловияДоговоровАренды.Организация КАК Организация,
	|	УсловияДоговоровАренды.Партнер КАК Партнер,
	|	УсловияДоговоровАренды.Контрагент КАК Контрагент,
	|	УсловияДоговоровАренды.Валюта КАК Валюта,
	|	УсловияДоговоровАренды.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	УсловияДоговоровАренды.СтавкаНДС КАК СтавкаНДС,
	|	УсловияДоговоровАренды.СуммаОбеспечительногоПлатежа КАК СуммаОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.СуммаУслугПоАренде КАК СуммаУслугПоАренде,
	|	УсловияДоговоровАренды.СуммаВыкупаПредметовАренды КАК СуммаВыкупаПредметовАренды,
	|	УсловияДоговоровАренды.СуммаПроцентов КАК СуммаПроцентов,
	|	УсловияДоговоровАренды.СтоимостьПредметовАренды КАК СтоимостьПредметовАренды,
	|	УсловияДоговоровАренды.СуммаНДС КАК СуммаНДС,
	|	УсловияДоговоровАренды.СрокЛет КАК СрокЛет,
	|	УсловияДоговоровАренды.СрокМес КАК СрокМес,
	|	УсловияДоговоровАренды.СрокДней КАК СрокДней,
	|	УсловияДоговоровАренды.ДатаНачалаАренды КАК ДатаНачалаАренды,
	|	УсловияДоговоровАренды.ДатаПервогоПлатежа КАК ДатаПервогоПлатежа,
	|	УсловияДоговоровАренды.ДатаПервогоОбеспечительногоПлатежа КАК ДатаПервогоОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.ДатаВыкупа КАК ДатаВыкупа,
	|	УсловияДоговоровАренды.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	УсловияДоговоровАренды.ПериодичностьОплат КАК ПериодичностьОплат,
	|	УсловияДоговоровАренды.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	УсловияДоговоровАренды.ОплатаУслугиПоАренде КАК ОплатаУслугиПоАренде,
	|	УсловияДоговоровАренды.НачислениеУслугиПоАренде КАК НачислениеУслугиПоАренде
	|ИЗ
	|	втУсловияДоговоровАренды КАК УсловияДоговоровАренды
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|	ДанныеГрафика.ВыкупнаяСтоимость + ДанныеГрафика.ВыкупнаяСтоимостьАванс КАК ВыкупнаяСтоимость,
	|	ДанныеГрафика.ВыкупнаяСтоимостьНДС КАК ВыкупнаяСтоимостьНДС
	|ИЗ
	|	РегистрСведений.ГрафикОплатУслугПоАренде КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.АктуальныеУсловияДоговора В (ВЫБРАТЬ Т.АктуальныеУсловияДоговора ИЗ втУсловияДоговоровАренды КАК Т)
	|	И ДанныеГрафика.Договор = &Договор
	|	И ДанныеГрафика.Активность
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.УслугаПоАрендеНДС КАК УслугаПоАрендеНДС,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияУслугПоАренде КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.АктуальныеУсловияДоговора В (ВЫБРАТЬ Т.АктуальныеУсловияДоговора ИЗ втУсловияДоговоровАренды КАК Т)
	|	И ДанныеГрафика.Договор = &Договор
	|	И ДанныеГрафика.Активность
	|	И (ДанныеГрафика.УслугаПоАренде <> 0 
	|		ИЛИ ДанныеГрафика.ОбеспечительныйПлатеж <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.Проценты КАК Проценты
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.АктуальныеУсловияДоговора В (ВЫБРАТЬ Т.АктуальныеУсловияДоговора ИЗ втУсловияДоговоровАренды КАК Т)
	|	И ДанныеГрафика.Договор = &Договор
	|	И ДанныеГрафика.Активность
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	ТекстыПакетаЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = ИзменениеУсловийДоговораАрендыЛокализация.ТекстЗапросаЗаполнениеТЧПоДоговору();
	
	Если ТекстЗапроса = Неопределено Тогда
		ТекстЗапроса =	
		"ВЫБРАТЬ
		|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьОстаток, 0) КАК СтоимостьУУ,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьРеглОстаток, 0) КАК СтоимостьБУ,
		|	ЕСТЬNULL(ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ, 0) КАК СрокИспользованияБУ,
		|	ЕСТЬNULL(ПараметрыАмортизацииОСУУ.СрокИспользования, 0) КАК СрокИспользованияУУ
		|ИЗ
		|	втАрендованныеОС КАК АрендованныеОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС.Остатки(
		|			&ПериодГраница, 
		|			Организация = &Организация
		|				И ОсновноеСредство В (
		|					ВЫБРАТЬ 
		|						втАрендованныеОС.ОсновноеСредство 
		|					ИЗ 
		|						втАрендованныеОС КАК втАрендованныеОС)) КАК СтоимостьОС
		|		ПО СтоимостьОС.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
		|			&Дата, 
		|			Регистратор <> &Ссылка
		|				И ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
		|				И ОсновноеСредство В (
		|					ВЫБРАТЬ 
		|						втАрендованныеОС.ОсновноеСредство 
		|					ИЗ 
		|						втАрендованныеОС КАК втАрендованныеОС)) КАК ПараметрыАмортизацииОСБУ
		|		ПО ПараметрыАмортизацииОСБУ.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
		|			&Дата, 
		|			Регистратор <> &Ссылка
		|				
		|				И ОсновноеСредство В (
		|					ВЫБРАТЬ 
		|						втАрендованныеОС.ОсновноеСредство 
		|					ИЗ 
		|						втАрендованныеОС КАК втАрендованныеОС)) КАК ПараметрыАмортизацииОСУУ
		|		ПО ПараметрыАмортизацииОСУУ.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
		|
		|УПОРЯДОЧИТЬ ПО
		|	АрендованныеОС.ОсновноеСредство.Наименование,
		|	АрендованныеОС.ОсновноеСредство.ИнвентарныйНомер";
		
	КонецЕсли;
	
	ТекстыПакетаЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ТекстыПакетаЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	ДатаДок = ?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Договор", Объект.Договор);
	Запрос.УстановитьПараметр("Дата", ДатаДок);
	Запрос.УстановитьПараметр("ПериодГраница", Новый Граница(Новый МоментВремени(ДатаДок, Объект.Ссылка), ВидГраницы.Исключая));
	
	Результат = Запрос.ВыполнитьПакет();
	Если Результат[0].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат[Результат.ВГраница()-4].Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(Объект, Выборка);
	
	Объект.ГрафикОплатУслуг.Загрузить(Результат[Результат.ВГраница()-3].Выгрузить());
	Объект.ГрафикНачисленияУслуг.Загрузить(Результат[Результат.ВГраница()-2].Выгрузить());
	Объект.ГрафикНачисленияПроцентов.Загрузить(Результат[Результат.ВГраница()-1].Выгрузить());
	Объект.ОС.Загрузить(Результат[Результат.ВГраница()].Выгрузить());
	
	ТекПроцентНДС = УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(Объект.СтавкаНДС);
	Объект.СуммаНДСВыкупаПредметовАренды = УчетНДСУПКлиентСервер.РассчитатьСуммуНДС(Объект.СуммаВыкупаПредметовАренды, ТекПроцентНДС, Истина);

	ВнеоборотныеАктивыКлиентСервер.ПересчитатьСуммуДокументаАренды(Объект);
	ВнеоборотныеАктивыКлиентСервер.ПересчитатьСуммуНДСВДокументеАренды(Объект);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = Справочники.ДоговорыАренды.ДобавитьКомандуРасшифровкиРасчетаПроцентов(КомандыПечати);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ГрафикНачисленияПроцентовВведенВручную", Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Параметры выбора статей и аналитик.
// 
// Параметры:
//  СозданДляПерехода - Булево - Создан для перехода.
// 
// Возвращаемое значение:
//  см. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики.
Функция ПараметрыВыбораСтатейИАналитик(СозданДляПерехода) Экспорт

	СписокПараметров = Новый Массив; // Структура
	
	#Область Расходы
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьиРасходы";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = СозданДляПерехода;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивовРасходы";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивовРасходы");
	
	СписокПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область Доходы
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";

	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходов");
	
	СписокПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти

	Возврат СписокПараметров;
	
КонецФункции

Функция ПараметрыНастройкиСчетовУчета() Экспорт
	
	СписокПараметров = Новый Массив;

	#Область Расходы
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.Представление = "ПредставлениеОтраженияВРеглУчетеРасходы";
	ПараметрыНастройки.ТипСтатьи = "ТипСтатьиРасходы";
	ПараметрыНастройки.НастройкаСчетовУчета = "НастройкаСчетовУчетаРасходы";
	
	ПараметрыНастройки.Организация = "Объект.Организация";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивовРасходы";

	ПараметрыНастройки.ЭлементыФормы.Добавить("ПредставлениеОтраженияВРеглУчетеРасходы");
	
	СписокПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	Возврат СписокПараметров;
	
КонецФункции

Процедура РассчитатьСтавкуСтоимостьПроценты(Объект, РеквизитыДоговора) Экспорт

	ПараметрыРасчета = ВнеоборотныеАктивы.ПараметрыРасчетаСтавкиСтоимостиПроцентов(Объект, РеквизитыДоговора);
	ПараметрыРасчета.ТаблицаОС = Объект.ОС;
	ПараметрыРасчета.ДатаНачалаАренды = Объект.ДатаНачалаАренды;
	
	ДанныеПоСтарымПроцентам = ПолучитьПроцентыПоСтарымУсловиям(Объект);
	
	ПараметрыРасчета.Вставить("ПроцентыПоСтарымУсловиям", ДанныеПоСтарымПроцентам.ПроцентыПоСтарымУсловиям);
	ПараметрыРасчета.Вставить("ГрафикСтарыхПроцентов", ДанныеПоСтарымПроцентам.ГрафикСтарыхПроцентов);
	ПараметрыРасчета.Вставить("СозданДляПерехода", ВнеоборотныеАктивы.СозданДляПереходаНаФСБУ25_2018(Объект.Дата, Объект.Организация));
	
	Если РеквизитыДоговора.ТипДоговора = ПредопределенноеЗначение("Перечисление.ТипыДоговоровАренды.Аренда")
		И РеквизитыДоговора.СпособОпределенияСтоимостиАктивов = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСтоимостиПредметовАренды.ПоФактическойСтоимости") Тогда
		ПараметрыРасчета.СтоимостьПредметовАренды = Объект.ОС.Итог("СправедливаяСтоимость") - Объект.ОС.Итог("НегарантированнаяСтоимость");
	Иначе
		ПараметрыРасчета.СтоимостьПредметовАренды = Объект.ОС.Итог("Стоимость");
	КонецЕсли;

	// Расчет производится по данным из табличных частей документа.
	// Заполнение табличных частей зависит от режима перехода на ФСБУ25.
	ВнеоборотныеАктивы.РассчитатьСтавкуСтоимостьПроценты(ПараметрыРасчета);
	
	Объект.ПроцентнаяСтавка = ПараметрыРасчета.ПроцентнаяСтавка;
	Объект.СуммаПроцентов = ПараметрыРасчета.СуммаПроцентов;
	Объект.ПриведеннаяСтоимость = ПараметрыРасчета.ПриведеннаяСтоимость;
	
КонецПроцедуры

Функция ПолучитьПроцентыПоСтарымУсловиям(Объект)
	
	МесяцИзмененияУсловий = НачалоМесяца(Объект.Дата);
	
	// Ищем диапазон графика начислений процентов, в который попадает изменение условий.
	// Вычисляем сумму процентов, рассчитанных по старым условиям, от начала диапазона графика до даты изменения условий.
	Запрос = Новый Запрос(); 
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК ПредыдущиеУсловияДоговора
	               |ПОМЕСТИТЬ ФильтрПредыдущиеУсловия
	               |ИЗ
	               |	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(
	               |			&ДатаИзмененияУсловий,
	               |			Договор = &Договор
	               |				И Регистратор <> &Ссылка) КАК УсловияДоговоровАренды
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РАЗНОСТЬДАТ(&КонецСтарогоПериода, ГрафикНачисленияПроцентовПоАренде.Дата, ДЕНЬ) КАК Сдвиг,
	               |	ГрафикНачисленияПроцентовПоАренде.Дата КАК ДатаСтарыхПроцентов,
	               |	ГрафикНачисленияПроцентовПоАренде.Проценты КАК Проценты
	               |ПОМЕСТИТЬ ПолныйГрафикНачисленийМесяцаИзменения
	               |ИЗ
	               |	РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ГрафикНачисленияПроцентовПоАренде
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФильтрПредыдущиеУсловия КАК ФильтрПредыдущиеУсловия
	               |		ПО ГрафикНачисленияПроцентовПоАренде.АктуальныеУсловияДоговора = ФильтрПредыдущиеУсловия.ПредыдущиеУсловияДоговора
	               |ГДЕ
	               |	НАЧАЛОПЕРИОДА(ГрафикНачисленияПроцентовПоАренде.Дата, МЕСЯЦ) = &МесяцИзмененияУсловий
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ДатаСтарыхПроцентов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ПолныйГрафикНачисленийМесяцаИзменения.Сдвиг КАК Сдвиг,
	               |	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ДатыПолногоГрафика.ДатаСтарыхПроцентов, ДЕНЬ, 1), &МесяцИзмененияУсловий) КАК НачалоПериода,
	               |	ПолныйГрафикНачисленийМесяцаИзменения.ДатаСтарыхПроцентов КАК ДатаСтарыхПроцентов,
	               |	ПолныйГрафикНачисленийМесяцаИзменения.Проценты КАК Проценты,
	               |	РАЗНОСТЬДАТ(ЕСТЬNULL(ДатыПолногоГрафика.ДатаСтарыхПроцентов, &МесяцИзмененияУсловий), ПолныйГрафикНачисленийМесяцаИзменения.ДатаСтарыхПроцентов, ДЕНЬ) КАК ДлительностьПериода
	               |ПОМЕСТИТЬ РасчетДиапазоновНачисления
	               |ИЗ
	               |	ПолныйГрафикНачисленийМесяцаИзменения КАК ПолныйГрафикНачисленийМесяцаИзменения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПолныйГрафикНачисленийМесяцаИзменения КАК ДатыПолногоГрафика
	               |		ПО ПолныйГрафикНачисленийМесяцаИзменения.ДатаСтарыхПроцентов > ДатыПолногоГрафика.ДатаСтарыхПроцентов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РасчетДиапазоновНачисления.ДатаСтарыхПроцентов КАК ДатаСтарыхПроцентов,
	               |	МИНИМУМ(РасчетДиапазоновНачисления.ДлительностьПериода) КАК ДлительностьПериода
	               |ПОМЕСТИТЬ ФильтрПериодов
	               |ИЗ
	               |	РасчетДиапазоновНачисления КАК РасчетДиапазоновНачисления
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	РасчетДиапазоновНачисления.ДатаСтарыхПроцентов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	РасчетДиапазоновНачисления.Проценты / 
	               |		(РАЗНОСТЬДАТ(РасчетДиапазоновНачисления.НачалоПериода, 
	               |					 РасчетДиапазоновНачисления.ДатаСтарыхПроцентов, ДЕНЬ) + 1) * 
	               |		(РАЗНОСТЬДАТ(РасчетДиапазоновНачисления.НачалоПериода, 
	               |					 &КонецСтарогоПериода, ДЕНЬ) + 1) КАК ПроцентыПоСтарымУсловиям,
	               |
	               |	&КонецСтарогоПериода КАК КонецСтарогоПериода
	               |
	               |ИЗ
	               |	РасчетДиапазоновНачисления КАК РасчетДиапазоновНачисления
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФильтрПериодов КАК ФильтрПериодов
	               |		ПО РасчетДиапазоновНачисления.ДатаСтарыхПроцентов = ФильтрПериодов.ДатаСтарыхПроцентов
	               |			И РасчетДиапазоновНачисления.ДлительностьПериода = ФильтрПериодов.ДлительностьПериода
	               |ГДЕ
	               |	РасчетДиапазоновНачисления.Сдвиг >= 0
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	РасчетДиапазоновНачисления.Сдвиг";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Договор", Объект.Договор);
	Запрос.УстановитьПараметр("МесяцИзмененияУсловий", МесяцИзмененияУсловий);
	Запрос.УстановитьПараметр("КонецСтарогоПериода", НачалоДня(НачалоДня(Объект.Дата) - 1));
	Запрос.УстановитьПараметр("ДатаИзмененияУсловий", НачалоДня(Объект.Дата));
	
	СтарыеПроценты = Запрос.Выполнить();
	
	ПроцентыПоСтарымУсловиям = 0;
	КонецСтарогоПериода = Неопределено;
	
	Если Не СтарыеПроценты.Пустой() Тогда
		Выборка = СтарыеПроценты.Выбрать();

		Пока Выборка.Следующий() Цикл
			ПроцентыПоСтарымУсловиям = Выборка.ПроцентыПоСтарымУсловиям;
			КонецСтарогоПериода = Выборка.КонецСтарогоПериода;
		КонецЦикла; 
		
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ГрафикНачисленияПроцентовПоАренде.Дата КАК Дата,
	               |	ГрафикНачисленияПроцентовПоАренде.Проценты КАК Проценты
	               |ИЗ
	               |	РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ГрафикНачисленияПроцентовПоАренде
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФильтрПредыдущиеУсловия КАК ФильтрПредыдущиеУсловия
	               |		ПО ГрафикНачисленияПроцентовПоАренде.АктуальныеУсловияДоговора = ФильтрПредыдущиеУсловия.ПредыдущиеУсловияДоговора
	               |ГДЕ
	               |	ГрафикНачисленияПроцентовПоАренде.Дата < &ДатаИзмененияУсловий"; 
	
	ГрафикСтарыхПроцентов = Запрос.Выполнить().Выгрузить();
	
	// Добавим в график старых процентов последнюю доп. строку с процентами по старым условиям до изменения.
	Если ПроцентыПоСтарымУсловиям <> 0 Тогда
		ПоследняяСтрока = ГрафикСтарыхПроцентов.Добавить();
		ПоследняяСтрока.Дата = КонецСтарогоПериода;
		ПоследняяСтрока.Проценты = ПроцентыПоСтарымУсловиям;
	КонецЕсли;
	
	Результат = Новый Структура("ПроцентыПоСтарымУсловиям, ГрафикСтарыхПроцентов",
								 ПроцентыПоСтарымУсловиям, ГрафикСтарыхПроцентов);
	
 	Возврат Результат;
КонецФункции


#КонецОбласти

#КонецОбласти

#КонецЕсли
