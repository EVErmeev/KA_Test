#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("Объекты");
	МассивНепроверяемыхРеквизитов.Добавить("Объекты.ВидОбъекта");
	МассивНепроверяемыхРеквизитов.Добавить("Адреса");
	МассивНепроверяемыхРеквизитов.Добавить("Адреса.АдресСтрокой");
	МассивНепроверяемыхРеквизитов.Добавить("ТранспортныеСредства");
	МассивНепроверяемыхРеквизитов.Добавить("ТранспортныеСредства.Номер");
	МассивНепроверяемыхРеквизитов.Добавить("ТранспортныеСредства.Марка");
	МассивНепроверяемыхРеквизитов.Добавить("ТранспортныеСредства.РегистрационныйЗнак");
	МассивНепроверяемыхРеквизитов.Добавить("ТранспортныеСредства.Грузоподъемность");
	МассивНепроверяемыхРеквизитов.Добавить("ТранспортныеСредства.Количество");
	
	Если СтавкаНалога = УчетУСНПСНКлиентСервер.НалоговаяСтавкаПоУмолчанию() Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НормаЗакона");
	КонецЕсли;
	
	ИспользуетсяСервисРегистрации = Ложь;
		
	Если Не ИспользуетсяСервисРегистрации И Лев(КодНалоговогоОргана, 2) <> КодРегиона Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Код инспекции не принадлежит региону регистрации'"),, "КодНалоговогоОргана", "Объект", Отказ);
	КонецЕсли;
	
	Классификатор =
		Обработки.ПомощникЗаполненияЗаявленияНаПолучениеПатента.КлассификаторВидовПредпринимательскойДеятельностиПСН(
			ДатаНачала);
	
	ВидДеятельности = Классификатор.Найти(КодВидаДеятельности, "Код");
	Если ВидДеятельности = Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Неправильно задан вид деятельности!'"),, "КодВидаДеятельности", "Объект", Отказ);
	КонецЕсли;
	
	Если ИспользуетсяСервисРегистрации Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ДатаНачала");
		
	ИначеЕсли ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) Тогда
		
		Если ДатаНачала > ДатаОкончания Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Дата окончания не может быть ранее даты начала'"),, "ДатаОкончания", "Объект", Отказ);
		ИначеЕсли Год(ДатаОкончания) <> Год(ДатаНачала) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Срок действия патента может быть только в пределах одного календарного года'"),,
					"ДатаОкончания", "Объект", Отказ);
		Иначе
			МинимальнаяДатаОкончания
				= Обработки.ПомощникЗаполненияЗаявленияНаПолучениеПатента.МинимальнаяДатаОкончанияПатента(ДатаНачала);
			Если ДатаОкончания < МинимальнаяДатаОкончания Тогда
				ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru = 'Срок действия патента должен быть не менее одного календарного месяца'"),,
						"ДатаОкончания", "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли