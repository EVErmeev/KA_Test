#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ФизическоеЛицо)
	|	И ЗначениеРазрешено(ГоловнаяОрганизация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// АПК:581-выкл. Методы могут вызываться из расширений.
// АПК:299-выкл. Методы могут вызываться из расширений.
// АПК:326-выкл. Методы поставляются комплектом и предназначены для совместного последовательного использования.
// АПК:325-выкл. Методы поставляются комплектом и предназначены для совместного последовательного использования.
// Транзакция открывается в методе НачатьЗаписьНабора, закрывается в ЗавершитьЗаписьНабора, отменяется в ОтменитьЗаписьНабора.

// Транзакционный вариант (с управляемой блокировкой) получения набора записей, соответствующего значениям измерений.
//
// Параметры:
//   ГоловнаяОрганизация - ОпределяемыйТип.Организация    - Значение отбора по соответствующему измерению.
//   ФизическоеЛицо     - СправочникСсылка.ФизическиеЛица - Значение отбора по соответствующему измерению.
//
// Возвращаемое значение:
//   РегистрСведенийНаборЗаписей.ОчередьОбработкиКадровыхДанныхФСС - Если удалось установить блокировку
//       и прочитать набор записей. При необходимости, открывает свою локальную транзакцию. Для закрытия транзакции
//       следует использовать одну из терминирующих процедур: ЗавершитьЗаписьНабора, либо ОтменитьЗаписьНабора.
//   Неопределено - Если не удалось установить блокировку и прочитать набор записей.
//       Вызывать процедуры ЗавершитьЗаписьНабора, ОтменитьЗаписьНабора не требуется,
//       поскольку если перед блокировкой функции потребовалось открыть локальную транзакцию,
//       то после неудачной блокировки локальная транзакция была отменена.
//
Функция НачатьЗаписьНабора(ГоловнаяОрганизация = Неопределено, ФизическоеЛицо = Неопределено) Экспорт
	ЕстьГоловнаяОрганизация = ЗначениеЗаполнено(ГоловнаяОрганизация);
	ЕстьФизическоеЛицо      = ЗначениеЗаполнено(ФизическоеЛицо);
	Если Не ЕстьГоловнаяОрганизация И Не ЕстьФизическоеЛицо Тогда
		Возврат Неопределено;
	КонецЕсли;
	ПолныеПраваИлиПривилегированныйРежим = Пользователи.ЭтоПолноправныйПользователь();
	Если Не ПолныеПраваИлиПривилегированныйРежим
		И Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ОчередьОбработкиКадровыхДанныхФСС) Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Недостаточно прав для изменения регистра ""%1"".'"),
			Метаданные.РегистрыСведений.ОчередьОбработкиКадровыхДанныхФСС.Представление());
	КонецЕсли;
	ЛокальнаяТранзакция = Не ТранзакцияАктивна();
	Если ЛокальнаяТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьОбработкиКадровыхДанныхФСС");
		Если ЕстьГоловнаяОрганизация Тогда
			ЭлементБлокировки.УстановитьЗначение("ГоловнаяОрганизация", ГоловнаяОрганизация);
		КонецЕсли;
		Если ЕстьФизическоеЛицо Тогда
			ЭлементБлокировки.УстановитьЗначение("ФизическоеЛицо", ФизическоеЛицо);
		КонецЕсли;
		Блокировка.Заблокировать();
		НаборЗаписей = СоздатьНаборЗаписей();
		Если ЕстьГоловнаяОрганизация Тогда
			НаборЗаписей.Отбор.ГоловнаяОрганизация.Установить(ГоловнаяОрганизация);
		КонецЕсли;
		Если ЕстьФизическоеЛицо Тогда
			НаборЗаписей.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицо);
		КонецЕсли;
		НаборЗаписей.Прочитать();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ЛокальнаяТранзакция", ЛокальнаяТранзакция);
	Исключение
		Если ЛокальнаяТранзакция Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось изменить %1 %2 о сотруднике %3 по причине: %4'"),
			Метаданные.РегистрыСведений.ОчередьОбработкиКадровыхДанныхФСС.Представление(),
			ГоловнаяОрганизация,
			ФизическоеЛицо,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.ОчередьОбработкиКадровыхДанныхФСС,
			ФизическоеЛицо,
			ТекстСообщения);
		НаборЗаписей = Неопределено;
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат НаборЗаписей;
КонецФункции

// Записывает набор и фиксирует локальную транзакцию, если она была открыта в функции НачатьЗаписьНабора.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.ОчередьОбработкиКадровыхДанныхФСС
//
Процедура ЗавершитьЗаписьНабора(НаборЗаписей) Экспорт
	НаборЗаписей.Записать(Истина);
	ЛокальнаяТранзакция = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НаборЗаписей.ДополнительныеСвойства, "ЛокальнаяТранзакция");
	Если ЛокальнаяТранзакция = Истина Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// Отменяет запись набора и отменяет локальную транзакцию, если она была открыта в функции НачатьЗаписьНабора.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.ОчередьОбработкиКадровыхДанныхФСС
//
Процедура ОтменитьЗаписьНабора(НаборЗаписей) Экспорт
	ЛокальнаяТранзакция = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НаборЗаписей.ДополнительныеСвойства, "ЛокальнаяТранзакция");
	Если ЛокальнаяТранзакция = Истина Тогда
		ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// АПК:326-вкл.
// АПК:325-вкл.
// АПК:299-вкл.
// АПК:581-вкл.

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПриЗаписиСправочникаФизическиеЛица(ФизическоеЛицоОбъект, Отказ) Экспорт
	ЗапланироватьОбновлениеПоФизическомуЛицу(ФизическоеЛицоОбъект.Ссылка);
КонецПроцедуры

Процедура ЗапланироватьОбновлениеПоФизическомуЛицу(ФизическоеЛицо, ГоловныеОрганизации = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина); // Обновляются все данные.
	
	Если ГоловныеОрганизации = Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СведенияОЗастрахованныхЛицахФСС.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОЗастрахованныхЛицахФСС.ГоловнаяОрганизация КАК ГоловнаяОрганизация
		|ИЗ
		|	РегистрСведений.СведенияОЗастрахованныхЛицахФСС КАК СведенияОЗастрахованныхЛицахФСС
		|ГДЕ
		|	СведенияОЗастрахованныхЛицахФСС.ФизическоеЛицо = &ФизическоеЛицо";
		Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
		ГоловныеОрганизации = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ГоловнаяОрганизация");
	КонецЕсли;
	
	Набор = НачатьЗаписьНабора(, ФизическоеЛицо);
	Если Набор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Таблица = Набор.Выгрузить();
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	Для Каждого ГоловнаяОрганизация Из ГоловныеОрганизации Цикл
		Если Не ЗначениеЗаполнено(ГоловнаяОрганизация) Тогда
			Продолжить;
		КонецЕсли;
		Запись = Таблица.Найти(ГоловнаяОрганизация, "ГоловнаяОрганизация");
		Если Запись = Неопределено Тогда
			Запись = Таблица.Добавить();
		КонецЕсли;
		ЗаполнитьЗапись(Запись, ГоловнаяОрганизация, ФизическоеЛицо);
	КонецЦикла;
	
	Набор.Загрузить(Таблица);
	ЗавершитьЗаписьНабора(Набор);
	
	ВключитьЗадание();
КонецПроцедуры

Процедура ЗапланироватьОбновлениеПоГоловнойОрганизации(ГоловнаяОрганизация, ФизическиеЛица) Экспорт
	УстановитьПривилегированныйРежим(Истина); // Обновляются все данные.
	
	Набор = НачатьЗаписьНабора(ГоловнаяОрганизация);
	Если Набор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Таблица = Набор.Выгрузить();
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	Для Каждого ФизическоеЛицо Из ФизическиеЛица Цикл
		Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
			Продолжить;
		КонецЕсли;
		Запись = Таблица.Найти(ФизическоеЛицо, "ФизическоеЛицо");
		Если Запись = Неопределено Тогда
			Запись = Таблица.Добавить();
		КонецЕсли;
		ЗаполнитьЗапись(Запись, ГоловнаяОрганизация, ФизическоеЛицо);
	КонецЦикла;
	
	Набор.Загрузить(Таблица);
	ЗавершитьЗаписьНабора(Набор);
	
	ВключитьЗадание();
КонецПроцедуры

Процедура ЗапланироватьОбновлениеПоТаблице(Таблица) Экспорт
	ФизическиеЛица      = КоллекцииБЗК.УникальныеЗначенияКолонки(Таблица, "ФизическоеЛицо");
	ГоловныеОрганизации = КоллекцииБЗК.УникальныеЗначенияКолонки(Таблица, "ГоловнаяОрганизация");
	
	// Выбирается наиболее оптимальный план записи - с наименьшим количеством наборов.
	Если ФизическиеЛица.Количество() <= ГоловныеОрганизации.Количество() Тогда
		Фильтр = Новый Структура("ФизическоеЛицо");
		Для Каждого ФизическоеЛицо Из ФизическиеЛица Цикл
			Фильтр.ФизическоеЛицо = ФизическоеЛицо;
			ГоловныеОрганизации = КоллекцииБЗК.УникальныеЗначенияКолонкиСФильтром(Таблица, Фильтр, "ГоловнаяОрганизация");
			ЗапланироватьОбновлениеПоФизическомуЛицу(ФизическоеЛицо, ГоловныеОрганизации);
		КонецЦикла;
	Иначе
		Фильтр = Новый Структура("ГоловнаяОрганизация");
		Для Каждого ГоловнаяОрганизация Из ГоловныеОрганизации Цикл
			Фильтр.ГоловнаяОрганизация = ГоловнаяОрганизация;
			ФизическиеЛица = КоллекцииБЗК.УникальныеЗначенияКолонкиСФильтром(Таблица, Фильтр, "ФизическоеЛицо");
			ЗапланироватьОбновлениеПоГоловнойОрганизации(ГоловнаяОрганизация, ФизическиеЛица);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьЗапись(Запись, ГоловнаяОрганизация, ФизическоеЛицо)
	Запись.ФизическоеЛицо          = ФизическоеЛицо;
	Запись.ГоловнаяОрганизация     = ГоловнаяОрганизация;
	Запись.ОбработкаНачата         = Ложь;
	Запись.ДатаНачалаОбработки     = '00010101';
	Запись.ДатаПоследнегоИзменения = ТекущаяДатаСеанса();
	Если Не ЗначениеЗаполнено(Запись.ДатаПервогоИзменения) Тогда
		Запись.ДатаПервогоИзменения = Запись.ДатаПоследнегоИзменения;
	КонецЕсли;
КонецПроцедуры

Процедура ВключитьЗадание()
	ДатаОчередногоЗапуска = Константы.ДатаОбработкиКадровыхДанныхФСС.Получить();
	Если ДатаОчередногоЗапуска + 3600*12 < ТекущаяДатаСеанса() Тогда
		// Если дата прошлого запуска пустая, значит задание отключено.
		// Если с даты прошлого запуска прошло более 12 часов, значит задание не смогло запуститься.
		Расписание = Новый РасписаниеРегламентногоЗадания;
		Расписание.ПериодПовтораДней = 1;
		Расписание.ВремяНачала = ТекущаяДата() + 5*60; // После завершения транзакции.
		Расписание.ПериодПовтораВТечениеДня = 3600;    // Каждый час вплоть до отключения.
		МетаданныеЗадания = Метаданные.РегламентныеЗадания.ОчередьОбработкиКадровыхДанныхФСС;
		ОбщегоНазначенияБЗК.ВключитьПредопределенноеЗадание(МетаданныеЗадания, Расписание);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработчикФоновогоЗадания() Экспорт
	УстановитьПривилегированныйРежим(Истина); // Обновляются все данные.
	
	СброситьДатуОбработкиНачалаСтарыхЗаданий();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Очередь.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Очередь.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Очередь.ОбработкаНачата КАК ОбработкаНачата,
	|	Очередь.ДатаПервогоИзменения КАК ДатаПервогоИзменения,
	|	Очередь.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
	|	Очередь.ДатаНачалаОбработки КАК ДатаНачалаОбработки
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1000
	|		Очередь.ФизическоеЛицо КАК ФизическоеЛицо,
	|		Очередь.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		Очередь.ОбработкаНачата КАК ОбработкаНачата,
	|		Очередь.ДатаПервогоИзменения КАК ДатаПервогоИзменения,
	|		Очередь.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
	|		Очередь.ДатаНачалаОбработки КАК ДатаНачалаОбработки
	|	ИЗ
	|		РегистрСведений.ОчередьОбработкиКадровыхДанныхФСС КАК Очередь
	|	ГДЕ
	|		Очередь.ОбработкаНачата = ЛОЖЬ
	|	
	|	УПОРЯДОЧИТЬ ПО
	|		ДатаПоследнегоИзменения) КАК Очередь
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизическоеЛицо";
	
	// Обработка должна начинаться после завершения транзакции изменения кадровых данных.
	ОжиданиеГотовностиКОбновлению = 60*5;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ГоловнаяОрганизация") Цикл
		ГоловнаяОрганизация = Выборка.ГоловнаяОрганизация;
		МассивФизлиц = Новый Массив;
		ТекущаяДатаСеанса = ТекущаяДатаСеанса();
		Пока Выборка.Следующий() Цикл
			ДатаГотовностиКОбновлению = Выборка.ДатаПоследнегоИзменения + ОжиданиеГотовностиКОбновлению;
			Если ДатаГотовностиКОбновлению <= ТекущаяДатаСеанса Тогда
				МассивФизлиц.Добавить(Выборка.ФизическоеЛицо);
			КонецЕсли;
		КонецЦикла;
		ОбновитьДанныеФизическихЛиц(ГоловнаяОрганизация, МассивФизлиц);
	КонецЦикла;
	
	Если Запрос.Выполнить().Пустой() Тогда
		ОбщегоНазначенияБЗК.ОтключитьПредопределенноеЗадание(
			Метаданные.РегламентныеЗадания.ОчередьОбработкиКадровыхДанныхФСС);
		Константы.ДатаОбработкиКадровыхДанныхФСС.Установить('00010101'); // Задание отключено.
	Иначе
		Константы.ДатаОбработкиКадровыхДанныхФСС.Установить(ТекущаяДатаСеанса());
	КонецЕсли;
	
КонецПроцедуры

Процедура СброситьДатуОбработкиНачалаСтарыхЗаданий()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ОчередьОбработкиКадровыхДанныхФСС.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОчередьОбработкиКадровыхДанныхФСС.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ИЗ
	|	РегистрСведений.ОчередьОбработкиКадровыхДанныхФСС КАК ОчередьОбработкиКадровыхДанныхФСС
	|ГДЕ
	|	ОчередьОбработкиКадровыхДанныхФСС.ОбработкаНачата = ИСТИНА
	|	И ОчередьОбработкиКадровыхДанныхФСС.ДатаНачалаОбработки > ДАТАВРЕМЯ(1, 1, 1)
	|	И ОчередьОбработкиКадровыхДанныхФСС.ДатаНачалаОбработки < &ДваЧасаНазад";
	Запрос.УстановитьПараметр("ДваЧасаНазад", ТекущаяДата() - 3600*2);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Набор = НачатьЗаписьНабора(Выборка.ГоловнаяОрганизация, Выборка.ФизическоеЛицо);
		Если Набор = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если Набор.Количество() = 0 Тогда
			ОтменитьЗаписьНабора(Набор);
			Продолжить;
		КонецЕсли;
		Запись = Набор[0];
		Запись.ОбработкаНачата     = Ложь;
		Запись.ДатаНачалаОбработки = '00010101';
		ЗавершитьЗаписьНабора(Набор);
	КонецЦикла;
КонецПроцедуры

Процедура ОбновитьДанныеФизическихЛиц(ГоловнаяОрганизация, ФизическиеЛица) Экспорт
	УстановитьПривилегированныйРежим(Истина); // Обновляются все данные.
	Если ФизическиеЛица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Истина);
	ТолькоРазрешенные = Ложь;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	ПараметрыПолучения = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолучения.ОтбиратьПоГоловнойОрганизации = Истина;
	ПараметрыПолучения.Организация         = ГоловнаяОрганизация;
	ПараметрыПолучения.СписокФизическихЛиц = ФизическиеЛица;
	ПараметрыПолучения.НачалоПериода       = ТекущаяДатаСеанса;
	ПараметрыПолучения.ОкончаниеПериода    = ТекущаяДатаСеанса;
	ПараметрыПолучения.КадровыеДанные      = Документы.СведенияОЗастрахованномЛицеФСС.ИменаПолейТребуемыхКадровыхДанных();
	
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(МенеджерВременныхТаблиц, ТолькоРазрешенные, ПараметрыПолучения);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ФизическиеЛица",      ФизическиеЛица);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СведенияОЗастрахованныхЛицахФСС.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОЗастрахованныхЛицахФСС.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СведенияОЗастрахованныхЛицахФСС.ПоследниеСведения КАК ПоследниеСведения,
	|	СведенияОЗастрахованныхЛицахФСС.ОтправленныеСведения КАК ОтправленныеСведения
	|ПОМЕСТИТЬ ВТСведения
	|ИЗ
	|	РегистрСведений.СведенияОЗастрахованныхЛицахФСС КАК СведенияОЗастрахованныхЛицахФСС
	|ГДЕ
	|	СведенияОЗастрахованныхЛицахФСС.ФизическоеЛицо В(&ФизическиеЛица)
	|	И СведенияОЗастрахованныхЛицахФСС.ГоловнаяОрганизация = &ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НЕ СотрудникиОрганизации.ФизическоеЛицо ЕСТЬ NULL КАК ЕстьКадровыеДанные,
	|	НЕ СведенияОЗастрахованныхЛицахФСС.ФизическоеЛицо ЕСТЬ NULL КАК ЕстьСведения,
	|	&ПоляКадровыхДанных КАК ПоляКадровыхДанных,
	|	ЕСТЬNULL(СведенияОЗастрахованныхЛицахФСС.ФизическоеЛицо, СотрудникиОрганизации.ФизическоеЛицо) КАК ФизическоеЛицо,
	|	ЕСТЬNULL(СведенияОЗастрахованныхЛицахФСС.ГоловнаяОрганизация, &ГоловнаяОрганизация) КАК ГоловнаяОрганизация,
	|	ЕСТЬNULL(СведенияОЗастрахованныхЛицахФСС.ПоследниеСведения, НЕОПРЕДЕЛЕНО) КАК ПоследниеСведения,
	|	ЕСТЬNULL(СведенияОЗастрахованныхЛицахФСС.ОтправленныеСведения, НЕОПРЕДЕЛЕНО) КАК ОтправленныеСведения
	|ИЗ
	|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТСведения КАК СведенияОЗастрахованныхЛицахФСС
	|		ПО СотрудникиОрганизации.ФизическоеЛицо = СведенияОЗастрахованныхЛицахФСС.ФизическоеЛицо";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляКадровыхДанных КАК ПоляКадровыхДанных", "СотрудникиОрганизации.*");
	
	Необработанные = КоллекцииБЗК.СкопироватьРекурсивно(ФизическиеЛица);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДатаПоследнегоИзменения = ТекущаяДатаСеанса;
		ОтметитьНачалоОбработки(Выборка.ГоловнаяОрганизация, Выборка.ФизическоеЛицо, ДатаПоследнегоИзменения);
		
		ЕстьСведения = Выборка.ЕстьСведения И (
			ЗначениеЗаполнено(Выборка.ПоследниеСведения) Или ЗначениеЗаполнено(Выборка.ОтправленныеСведения));
		Если ЕстьСведения Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(Необработанные, Выборка.ФизическоеЛицо);
			ПоляТребующиеАктуализации = СведенияКоторыеТребуетсяАктуализировать(Выборка);
			Если ПустаяСтрока(ПоляТребующиеАктуализации) Тогда
				РегистрыСведений.СведенияОЗастрахованныхЛицахФСС.ОтключитьФлажокТребуетсяАктуализировать(
					Выборка.ГоловнаяОрганизация,
					Выборка.ФизическоеЛицо);
			Иначе
				РегистрыСведений.СведенияОЗастрахованныхЛицахФСС.ВключитьФлажокТребуетсяАктуализировать(
					Выборка.ГоловнаяОрганизация,
					Выборка.ФизическоеЛицо,
					ПоляТребующиеАктуализации,
					ДатаПоследнегоИзменения);
			КонецЕсли;
		КонецЕсли;
		
		ОтметитьЗавершениеОбработки(Выборка.ГоловнаяОрганизация, Выборка.ФизическоеЛицо);
		
	КонецЦикла;
	
	Если Необработанные.Количество() > 0 Тогда
		РегистрыСведений.СведенияОЗастрахованныхЛицахФСС.ДобавитьСведенияОНовыхСотрудниках(
			ГоловнаяОрганизация,
			Необработанные);
	КонецЕсли;
	
КонецПроцедуры

// Включает флажок ОбработкаНачата.
Процедура ОтметитьНачалоОбработки(ГоловнаяОрганизация, ФизическоеЛицо, ДатаПоследнегоИзменения)
	Набор = НачатьЗаписьНабора(ГоловнаяОрганизация, ФизическоеЛицо);
	Если Набор <> Неопределено Тогда
		Если Набор.Количество() = 0 Тогда
			ОтменитьЗаписьНабора(Набор);
		Иначе
			Запись = Набор[0];
			Запись.ОбработкаНачата     = Истина;
			Запись.ДатаНачалаОбработки = ТекущаяДата();
			ДатаПоследнегоИзменения = Запись.ДатаПоследнегоИзменения;
			ЗавершитьЗаписьНабора(Набор);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Удаляет запись в регистре.
Процедура ОтметитьЗавершениеОбработки(ГоловнаяОрганизация, ФизическоеЛицо)
	Набор = НачатьЗаписьНабора(ГоловнаяОрганизация, ФизическоеЛицо);
	Если Набор <> Неопределено Тогда
		Если Набор.Количество() = 0 Тогда
			ОтменитьЗаписьНабора(Набор);
		Иначе
			Набор.Очистить();
			ЗавершитьЗаписьНабора(Набор);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция СведенияКоторыеТребуетсяАктуализировать(Выборка)
	Сведения = Выборка.ПоследниеСведения;
	Если Не ЗначениеЗаполнено(Сведения) Тогда
		Сведения = Выборка.ОтправленныеСведения;
		Если Не ЗначениеЗаполнено(Сведения) Тогда
			Возврат "";
		КонецЕсли;
	КонецЕсли;
	
	СведенияОбъект = Сведения.ПолучитьОбъект();
	Если Выборка.ЕстьКадровыеДанные Тогда
		СведенияОбъект.ДополнительныеСвойства.Вставить("КадровыеДанные", Выборка);
	КонецЕсли;
	
	// Очистка реквизитов которые отключают заполнение вторичных данных.
	СведенияОбъект.ДатаОтправки        = Неопределено;
	СведенияОбъект.РегистрацияСведений = Неопределено;
	
	// Отключение заполнения реквизитов которые не относятся к кадровым данным (не влияют на необходимость отправки).
	ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(СведенияОбъект, "СтраховательНаименование");
	ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(СведенияОбъект, "СтраховательЭлектроннаяПочта");
	ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(СведенияОбъект, "СтраховательТелефон");
	ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(СведенияОбъект, "КодПодчиненностиФСС");
	ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(СведенияОбъект, "УполномоченныйПредставитель");
	ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(СведенияОбъект, "УполномоченныйПредставительДолжность");
	ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(СведенияОбъект, "УполномоченныйПредставительОснованиеПодписи");
	ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(СведенияОбъект, "УполномоченныйПредставительФИО");
	ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(СведенияОбъект, "УполномоченныйПредставительТелефон");
	
	ИсправленияДоОбновления = СведенияОбъект.ФиксацияИзменений.Выгрузить();
	
	ПараметрыФиксации = Документы.СведенияОЗастрахованномЛицеФСС.ПараметрыФиксацииВторичныхДанных();
	ПараметрыФиксации.ФиксироватьОтличия = Истина;
	ПараметрыФиксации.ИзменятьДанныеПриФиксацииОтличий = Истина;
	Модифицирован = СведенияОбъект.ОбновитьВторичныеДанные(ПараметрыФиксации);
	Если Не Модифицирован Тогда
		Возврат "";
	КонецЕсли;
	
	Фильтр = Новый Структура("ИмяРеквизита, Путь, ИдентификаторСтроки");
	НовыеИсправления = СведенияОбъект.ФиксацияИзменений.Выгрузить();
	Для Каждого ИсправлениеДоОбновления Из ИсправленияДоОбновления Цикл
		ЗаполнитьЗначенияСвойств(Фильтр, ИсправлениеДоОбновления);
		Найденные = НовыеИсправления.НайтиСтроки(Фильтр);
		Для Каждого СтрокаТаблицы Из Найденные Цикл
			НовыеИсправления.Удалить(СтрокаТаблицы);
		КонецЦикла;
	КонецЦикла;
	
	Если НовыеИсправления.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ИзмененныеСмысловыеГруппы = Новый Массив;
	ИзмененныеРеквизиты = НовыеИсправления.ВыгрузитьКолонку("ИмяРеквизита");
	Для Каждого ИмяРеквизита Из ИзмененныеРеквизиты Цикл
		Если ИмяРеквизита = "АдресРегистрации" Или ИмяРеквизита = "АдресПроживания" Тогда
			Продолжить; // Значения адресов в формате JSON не контролируются, контролируются только их производные.
		ИначеЕсли СтрНачинаетсяС(ИмяРеквизита, "УдостоверениеЛичности")
			Или ИмяРеквизита = "ДатаРождения"
			Или ИмяРеквизита = "Пол" Тогда
			СмысловаяГруппа = НСтр("ru = 'Паспортные данные'");
		ИначеЕсли СтрНачинаетсяС(ИмяРеквизита, "АдресРегистрации") Тогда
			СмысловаяГруппа = НСтр("ru = 'Адрес регистрации'");
		ИначеЕсли СтрНачинаетсяС(ИмяРеквизита, "РазрешениеНаПроживание") Тогда
			СмысловаяГруппа = НСтр("ru = 'РВП/ВНЖ'");
		ИначеЕсли (СтрНачинаетсяС(ИмяРеквизита, "Банк") Или ИмяРеквизита = "НомерСчета")
			И СведенияОбъект.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ЧерезБанк Тогда
			СмысловаяГруппа = НСтр("ru = 'Способ выплаты'");
		ИначеЕсли ИмяРеквизита = "КартаМИР"
			И СведенияОбъект.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.НаКартуМИР Тогда
			СмысловаяГруппа = НСтр("ru = 'Способ выплаты'");
		ИначеЕсли СтрНачинаетсяС(ИмяРеквизита, "АдресПроживания")
			И СведенияОбъект.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ПочтовымПереводом Тогда
			СмысловаяГруппа = НСтр("ru = 'Адрес проживания'");
		ИначеЕсли ИмяРеквизита = "СНИЛС" Тогда
			СмысловаяГруппа = НСтр("ru = 'СНИЛС'");
		ИначеЕсли ИмяРеквизита = "СотрудникИНН" Тогда
			СмысловаяГруппа = НСтр("ru = 'ИНН'");
		ИначеЕсли ИмяРеквизита = "СотрудникФамилия" Тогда
			СмысловаяГруппа = НСтр("ru = 'Фамилия'");
		ИначеЕсли ИмяРеквизита = "СотрудникИмя" Тогда
			СмысловаяГруппа = НСтр("ru = 'Имя'");
		ИначеЕсли ИмяРеквизита = "СотрудникОтчество" Тогда
			СмысловаяГруппа = НСтр("ru = 'Отчество'");
		ИначеЕсли ИмяРеквизита = "СотрудникТелефон" Тогда
			СмысловаяГруппа = НСтр("ru = 'Телефон'");
		ИначеЕсли ИмяРеквизита = "ВидОсобойЗоны"
			Или ИмяРеквизита = "ОтношениеКОсобойЗоне"
			Или ИмяРеквизита = "ПричинаПредоставленияЛьготы" Тогда
			СмысловаяГруппа = НСтр("ru = 'Льготы'");
		ИначеЕсли СтрНачинаетсяС(ИмяРеквизита, "ТрудовойДоговор") Тогда
			СмысловаяГруппа = НСтр("ru = 'Трудовой договор'");
		ИначеЕсли ИмяРеквизита = "ДатаУвольнения" Тогда
			СмысловаяГруппа = НСтр("ru = 'Дата увольнения'");
		Иначе
			Продолжить; // Отслеживаются изменения только тех реквизитов, которые отправляются в электронном виде.
		КонецЕсли;
		ОбщегоНазначенияБЗК.ДобавитьЗначениеВМассив(ИзмененныеСмысловыеГруппы, СмысловаяГруппа);
	КонецЦикла;
	
	Возврат СтрСоединить(ИзмененныеСмысловыеГруппы, ", ");
КонецФункции

#КонецОбласти

#КонецЕсли