#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значения по умолчанию для ресурсов регистра.
// Имена ключей структуры должны строго соответствовать именам ресурсов регистра.
// 
// Возвращаемое значение:
// 	Структура - структура значений ресурсов регистра.
Функция ЗначенияПоУмолчанию() Экспорт
	
	СтруктураЗначений = Новый Структура;
	
	СтруктураЗначений.Вставить("МетодОценкиСтоимостиТоваров", Перечисления.МетодыОценкиСтоимостиТоваров.СредняяЗаМесяц);
	СтруктураЗначений.Вставить("УчетГотовойПродукцииПоПлановойСтоимости", Ложь);
	СтруктураЗначений.Вставить("ФормироватьРезервыПоСомнительнымДолгам", Ложь);
	СтруктураЗначений.Вставить("ПериодичностьРезервовПоСомнительнымДолгам", Перечисления.Периодичность.Месяц);
	СтруктураЗначений.Вставить("ПризнаватьРасходыПоИсследованиям", Ложь);
	СтруктураЗначений.Вставить("СтатьяРасходовПоИсследованиям", Неопределено);
	СтруктураЗначений.Вставить("АналитикаРасходовПоИсследованиям", Неопределено);
	СтруктураЗначений.Вставить("ИспользоватьВыделениеДолгосрочныхАктивовОбязательств", Ложь);
	СтруктураЗначений.Вставить("ДлительностьОперационногоЦикла", 12);
	СтруктураЗначений.Вставить("УчетАрендыПоФСБУ25_2018", Истина);
	СтруктураЗначений.Вставить("УчетДисконтированнойКредиторскойЗадолженностиПоставщикам", Ложь);
	СтруктураЗначений.Вставить("СтатьяСписанияПроцентныхРасходов", Неопределено);
	СтруктураЗначений.Вставить("АналитикаСписанияПроцентныхРасходов", Неопределено);
	СтруктураЗначений.Вставить("СрокДляПримененияДисконтирования", 0);
	СтруктураЗначений.Вставить("УчетнаяПолитикаСуществует", Ложь);
	СтруктураЗначений.Вставить("МесяцНачалаФинансовогоГода", 1);
	СтруктураЗначений.Вставить("ДетализироватьМатериальныеИПостатейныеЗатратыВСебестоимостиТоваров", Ложь);
	СтруктураЗначений.Вставить("ПорядокОценкиЗадолженности", Неопределено);
	
	//++ НЕ УТ
	СтруктураЗначений.Вставить("ПорядокУчетаВНА", Перечисления.ПорядокУчетаВНА.ПоСтандартамМУ);
	СтруктураЗначений.Вставить("ПорядокНачисленияАмортизацииОС", Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету);
	СтруктураЗначений.Вставить("ПорядокНачисленияАмортизацииНМА", Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету);
	СтруктураЗначений.Вставить("ПорядокНачисленияАмортизацииАренда", Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету);
	
	Если ПолучитьФункциональнуюОпцию("РегламентированныйУчетВНА") Тогда
		СтруктураЗначений.Вставить("ПорядокУчетаВНАРегл", Перечисления.ПорядокУчетаВНАРегл.Независимый);
		СтруктураЗначений.Вставить("ПорядокНачисленияАмортизацииОСРегл", Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету);
		СтруктураЗначений.Вставить("ПорядокНачисленияАмортизацииНМАРегл", Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету);
		СтруктураЗначений.Вставить("ПорядокНачисленияАмортизацииАрендаРегл", Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету);
	Иначе
		СтруктураЗначений.Вставить("ПорядокУчетаВНАРегл", Перечисления.ПорядокУчетаВНАРегл.СовпадаетСУпрУчетом);
		СтруктураЗначений.Вставить("ПорядокНачисленияАмортизацииОСРегл", Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету);
		СтруктураЗначений.Вставить("ПорядокНачисленияАмортизацииНМАРегл", Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету);
		СтруктураЗначений.Вставить("ПорядокНачисленияАмортизацииАрендаРегл", Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету);
	КонецЕсли;
	
	СтруктураЗначений.Вставить("ВариантПроводокПоОбесценениюВНА", Перечисления.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения);
	//-- НЕ УТ
	
	Возврат СтруктураЗначений
	
КонецФункции

// Возвращает представление начала финансового года
// 
// Параметры:
//  МесяцНачалаФинансовогоГода - Число - Порядковый номер месяца
// 
// Возвращаемое значение:
//  Строка - Представление месяца начала финансового года
Функция ПредставлениеНачалаФинансовогоГода(МесяцНачалаФинансовогоГода) Экспорт
	
	Если МесяцНачалаФинансовогоГода = 1 Тогда
		Возврат НСтр("ru='Январь'");
	ИначеЕсли МесяцНачалаФинансовогоГода = 2 Тогда
		Возврат НСтр("ru='Февраль'");
	ИначеЕсли МесяцНачалаФинансовогоГода = 3 Тогда
		Возврат НСтр("ru='Март'");
	ИначеЕсли МесяцНачалаФинансовогоГода = 4 Тогда
		Возврат НСтр("ru='Апрель'");
	ИначеЕсли МесяцНачалаФинансовогоГода = 5 Тогда
		Возврат НСтр("ru='Май'");
	ИначеЕсли МесяцНачалаФинансовогоГода = 6 Тогда
		Возврат НСтр("ru='Июнь'");
	ИначеЕсли МесяцНачалаФинансовогоГода = 7 Тогда
		Возврат НСтр("ru='Июль'");
	ИначеЕсли МесяцНачалаФинансовогоГода = 8 Тогда
		Возврат НСтр("ru='Август'");
	ИначеЕсли МесяцНачалаФинансовогоГода = 9 Тогда
		Возврат НСтр("ru='Сентябрь'");
	ИначеЕсли МесяцНачалаФинансовогоГода = 10 Тогда
		Возврат НСтр("ru='Октябрь'");
	ИначеЕсли МесяцНачалаФинансовогоГода = 11 Тогда
		Возврат НСтр("ru='Ноябрь'");
	ИначеЕсли МесяцНачалаФинансовогоГода = 12 Тогда
		Возврат НСтр("ru='Декабрь'");
	Иначе
		Возврат НСтр("ru='Неопределено'");
	КонецЕсли;
	
КонецФункции

// Возращает текст запроса по данным регистра.
// 
// Возвращаемое значение:
// 	Строка - Текст запроса.
Функция ТекстЗапросаДействующиеПараметрыНалоговУчетныхПолитик() Экспорт
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР КОГДА ТаблицаСрезПоследних.Период ЕСТЬ NULL Тогда
	|		ЛОЖЬ
	|	ИНАЧЕ
	|		ИСТИНА
	|	КОНЕЦ КАК УчетнаяПолитикаСуществует,
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ГоловныеОрганизации.ОбособленноеПодразделение КАК Организация,
	|	ТаблицаСрезПоследних.МетодОценкиСтоимостиТоваров КАК МетодОценкиСтоимостиТоваров,
	|	ТаблицаСрезПоследних.ФормироватьРезервыПоСомнительнымДолгам КАК ФормироватьРезервыПоСомнительнымДолгам,
	|	ТаблицаСрезПоследних.ПериодичностьРезервовПоСомнительнымДолгам КАК ПериодичностьРезервовПоСомнительнымДолгам,
	|	ТаблицаСрезПоследних.УчетГотовойПродукцииПоПлановойСтоимости КАК УчетГотовойПродукцииПоПлановойСтоимости,
	|	ТаблицаСрезПоследних.ПризнаватьРасходыПоИсследованиям КАК ПризнаватьРасходыПоИсследованиям,
	|	ТаблицаСрезПоследних.СтатьяРасходовПоИсследованиям КАК СтатьяРасходовПоИсследованиям,
	|	ТаблицаСрезПоследних.АналитикаРасходовПоИсследованиям КАК АналитикаРасходовПоИсследованиям,
	|	ТаблицаСрезПоследних.ИспользоватьВыделениеДолгосрочныхАктивовОбязательств,
	|	ТаблицаСрезПоследних.ДлительностьОперационногоЦикла,
	|	ТаблицаСрезПоследних.ПорядокУчетаВНА КАК ПорядокУчетаВНА,
	|	ТаблицаСрезПоследних.ПорядокНачисленияАмортизацииОС КАК ПорядокНачисленияАмортизацииОС,
	|	ТаблицаСрезПоследних.ПорядокНачисленияАмортизацииНМА КАК ПорядокНачисленияАмортизацииНМА,
	|	ТаблицаСрезПоследних.ПорядокНачисленияАмортизацииАренда КАК ПорядокНачисленияАмортизацииАренда,
	|	ТаблицаСрезПоследних.ПорядокУчетаВНАРегл КАК ПорядокУчетаВНАРегл,
	|	ТаблицаСрезПоследних.ПорядокНачисленияАмортизацииОСРегл КАК ПорядокНачисленияАмортизацииОСРегл,
	|	ТаблицаСрезПоследних.ПорядокНачисленияАмортизацииНМАРегл КАК ПорядокНачисленияАмортизацииНМАРегл,
	|	ТаблицаСрезПоследних.ПорядокНачисленияАмортизацииАрендаРегл КАК ПорядокНачисленияАмортизацииАрендаРегл,
	|	ТаблицаСрезПоследних.УчетАрендыПоФСБУ25_2018 КАК УчетАрендыПоФСБУ25_2018,
	|	ТаблицаСрезПоследних.ВариантПроводокПоОбесценениюВНА КАК ВариантПроводокПоОбесценениюВНА,
	|	ТаблицаСрезПоследних.МесяцНачалаФинансовогоГода КАК МесяцНачалаФинансовогоГода,
	|	ТаблицаСрезПоследних.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам КАК УчетДисконтированнойКредиторскойЗадолженностиПоставщикам,
	|	ТаблицаСрезПоследних.СтатьяСписанияПроцентныхРасходов КАК СтатьяСписанияПроцентныхРасходов,
	|	ТаблицаСрезПоследних.АналитикаСписанияПроцентныхРасходов КАК АналитикаСписанияПроцентныхРасходов,
	|	ТаблицаСрезПоследних.СрокДляПримененияДисконтирования КАК СрокДляПримененияДисконтирования,
	|	ТаблицаСрезПоследних.ДетализироватьМатериальныеИПостатейныеЗатратыВСебестоимостиТоваров,
	|	ТаблицаСрезПоследних.ПорядокОценкиЗадолженности КАК ПорядокОценкиЗадолженности
	|ИЗ
	|	ВтГоловныеОрганизации КАК ГоловныеОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК ТаблицаСрезПоследних
	|		ПО ГоловныеОрганизации.Организация = ТаблицаСрезПоследних.Организация
	|";
	Возврат ТекстЗапроса
	
КонецФункции

// Формирует текстовое описание установленных параметров.
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - ссылка на организацию.
// 	ДатаДействия - Дата - период действия настроек.
// 	ДействующиеНастройки - Структура - действующие параметры учетной политики.
// Возвращаемое значение:
// 	Строка - Описание действующих параметров строкой.
Функция ОписаниеДействующихПараметров(Организация, ДатаДействия = Неопределено, ДействующиеНастройки = Неопределено) Экспорт
	
	Если ДействующиеНастройки = Неопределено Тогда
		ДействующиеНастройки = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(
			"УчетнаяПолитикаФинансовогоУчета",
			Организация,
			ДатаДействия,
			Ложь);
	КонецЕсли;
	СтрокаШаблон = "%1: %2." + Символы.ПС;
	СтрокаШаблонБулево = "%1." + Символы.ПС;
	Если НЕ ЗначениеЗаполнено(ДействующиеНастройки) Тогда
		СтрокаОписанияНастроек = НСтр("ru='Не заданы параметры.'");
		Возврат СтрокаОписанияНастроек;
	КонецЕсли;
	
	СтрокаОписанияНастроек = СтрШаблон(СтрокаШаблон, 
		НСтр("ru='Начало финансового года'"),
		ПредставлениеНачалаФинансовогоГода(ДействующиеНастройки.МесяцНачалаФинансовогоГода));
	
	СтрокаОписанияНастроек = СтрокаОписанияНастроек 
		+ СтрШаблон(СтрокаШаблон, 
			НСтр("ru='Метод оценки стоимости товаров'"),
			ДействующиеНастройки.МетодОценкиСтоимостиТоваров);
	
	Если ДействующиеНастройки.УчетГотовойПродукцииПоПлановойСтоимости Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru='Готовая продукция учитывается по плановой стоимости'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ИспользоватьВыделениеДолгосрочныхАктивовОбязательств Тогда
		
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru='Используется выделение долгосрочных активов/обязательств'"));
		
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблон,
				НСтр("ru='Длительность операционного цикла в месяцах'"),
				ДействующиеНастройки.ДлительностьОперационногоЦикла);

	КонецЕсли;
	
	//++ НЕ УТ
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4") Тогда
		
		РегламентированныйУчетВНА = ПолучитьФункциональнуюОпцию("РегламентированныйУчетВНА");
				
		Если РегламентированныйУчетВНА Тогда
			
			Если ДействующиеНастройки.ПорядокУчетаВНА = Перечисления.ПорядокУчетаВНА.ПоСтандартамМУ Тогда
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru='Учет ОС и НМА в управленческом учете ведется по стандартам международного учета'"));
			Иначе
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru='Учет ОС и НМА в управленческом учете ведется по ПБУ 6/01 и 14/2007'"));
			КонецЕсли;
			
		КонецЕсли;
		
		Если ДействующиеНастройки.ПорядокНачисленияАмортизацииОС = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца
			И ДействующиеНастройки.ПорядокНачисленияАмортизацииНМА = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца
			И ДействующиеНастройки.ПорядокНачисленияАмортизацииАренда = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца Тогда
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru='Амортизация ОС и НМА в управленческом учете начисляется с первого числа месяца, следующего за месяцем принятия к учету'"));
			
		ИначеЕсли ДействующиеНастройки.ПорядокНачисленияАмортизацииОС = Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету
			И ДействующиеНастройки.ПорядокНачисленияАмортизацииНМА = Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету
			И ДействующиеНастройки.ПорядокНачисленияАмортизацииАренда = Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету Тогда
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru='Амортизация ОС и НМА в управленческом учете начисляется с даты принятия к учету'"));
					
		Иначе
			
			Если ДействующиеНастройки.ПорядокНачисленияАмортизацииОС = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца Тогда
			
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru='Амортизация собственных ОС в управленческом учете начисляется с первого числа месяца, следующего за месяцем принятия к учету'"));
			Иначе
				
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru='Амортизация собственных ОС в управленческом учете начисляется с даты принятия к учету'"));
						
			КонецЕсли;
					
			Если ДействующиеНастройки.ПорядокНачисленияАмортизацииАренда = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца Тогда
			
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru='Амортизация арендованных ОС в управленческом учете начисляется с первого числа месяца, следующего за месяцем принятия к учету'"));
			Иначе
				
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru='Амортизация арендованных ОС в управленческом учете начисляется с даты принятия к учету'"));
						
			КонецЕсли;
					
			Если ДействующиеНастройки.ПорядокНачисленияАмортизацииНМА = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца Тогда
			
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru='Амортизация НМА в управленческом учете начисляется с первого числа месяца, следующего за месяцем принятия к учету'"));
			Иначе
				
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru='Амортизация НМА в управленческом учете начисляется с даты принятия к учету'"));
						
			КонецЕсли;
					
		КонецЕсли;
		
		Если НЕ РегламентированныйУчетВНА Тогда
			
			Если ДействующиеНастройки.ПорядокУчетаВНАРегл = Перечисления.ПорядокУчетаВНАРегл.Независимый Тогда
			
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru='Учет ОС и НМА в регламентированном учете ведется независимо от управленческого учета'"));
						
				Если ДействующиеНастройки.ПорядокНачисленияАмортизацииОСРегл = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца
					И ДействующиеНастройки.ПорядокНачисленияАмортизацииНМАРегл = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца
					И ДействующиеНастройки.ПорядокНачисленияАмортизацииАрендаРегл = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца Тогда
					
					СтрокаОписанияНастроек = СтрокаОписанияНастроек
						+ СтрШаблон(СтрокаШаблонБулево,
							НСтр("ru='Амортизация ОС и НМА в регламентированном учете начисляется с первого числа месяца, следующего за месяцем принятия к учету'"));
					
				ИначеЕсли ДействующиеНастройки.ПорядокНачисленияАмортизацииОСРегл = Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету
					И ДействующиеНастройки.ПорядокНачисленияАмортизацииНМАРегл = Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету
					И ДействующиеНастройки.ПорядокНачисленияАмортизацииАрендаРегл = Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету Тогда
					
					СтрокаОписанияНастроек = СтрокаОписанияНастроек
						+ СтрШаблон(СтрокаШаблонБулево,
							НСтр("ru='Амортизация ОС и НМА в регламентированном учете начисляется с даты принятия к учету'"));
							
				Иначе
					
					Если ДействующиеНастройки.ПорядокНачисленияАмортизацииОСРегл = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца Тогда
					
						СтрокаОписанияНастроек = СтрокаОписанияНастроек
							+ СтрШаблон(СтрокаШаблонБулево,
								НСтр("ru='Амортизация собственных ОС в регламентированном учете начисляется с первого числа месяца, следующего за месяцем принятия к учету'"));
					Иначе
						
						СтрокаОписанияНастроек = СтрокаОписанияНастроек
							+ СтрШаблон(СтрокаШаблонБулево,
								НСтр("ru='Амортизация собственных ОС в регламентированном учете начисляется с даты принятия к учету'"));
								
					КонецЕсли;
							
					Если ДействующиеНастройки.ПорядокНачисленияАмортизацииАрендаРегл = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца Тогда
					
						СтрокаОписанияНастроек = СтрокаОписанияНастроек
							+ СтрШаблон(СтрокаШаблонБулево,
								НСтр("ru='Амортизация арендованных ОС в регламентированном учете начисляется с первого числа месяца, следующего за месяцем принятия к учету'"));
					Иначе
						
						СтрокаОписанияНастроек = СтрокаОписанияНастроек
							+ СтрШаблон(СтрокаШаблонБулево,
								НСтр("ru='Амортизация арендованных ОС в регламентированном учете начисляется с даты принятия к учету'"));
								
					КонецЕсли;
							
					Если ДействующиеНастройки.ПорядокНачисленияАмортизацииНМАРегл = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца Тогда
					
						СтрокаОписанияНастроек = СтрокаОписанияНастроек
							+ СтрШаблон(СтрокаШаблонБулево,
								НСтр("ru='Амортизация НМА в регламентированном учете начисляется с первого числа месяца, следующего за месяцем принятия к учету'"));
					Иначе
						
						СтрокаОписанияНастроек = СтрокаОписанияНастроек
							+ СтрШаблон(СтрокаШаблонБулево,
								НСтр("ru='Амортизация НМА в регламентированном учете начисляется с даты принятия к учету'"));
								
					КонецЕсли;
							
				КонецЕсли;
				
			Иначе
				
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru='Учет ОС и НМА в регламентированном учете совпадает с управленческим учетом'"));
						
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьОбесценениеВНА") Тогда
			Если ДействующиеНастройки.ВариантПроводокПоОбесценениюВНА
				= Перечисления.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения Тогда
				СтрокаОписанияНастроек = СтрокаОписанияНастроек + СтрШаблон(СтрокаШаблонБулево, НСтр(
					"ru='Амортизация ОС и НМА с обесценением начисляется на стоимость с учетом обесценения. Корректировка обесценения доначисляется'"));
			Иначе
				СтрокаОписанияНастроек = СтрокаОписанияНастроек + СтрШаблон(СтрокаШаблонБулево, НСтр(
					"ru='Амортизация ОС и НМА с обесценением начисляется на полную стоимость. Корректировка обесценения сторнируется'"));
			КонецЕсли;
		КонецЕсли;
		
		Если ДействующиеНастройки.ПризнаватьРасходыПоИсследованиям Тогда
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru='Расходы по исследованиям учитываются в текущем периоде'"));
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон,
					НСтр("ru='Статья расходов по исследованиям'"),
					ДействующиеНастройки.СтатьяРасходовПоИсследованиям);
			
	
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон,
					НСтр("ru='Аналитика расходов по исследованиям'"),
					ДействующиеНастройки.АналитикаРасходовПоИсследованиям);
	
		КонецЕсли;
		
	КонецЕсли;
	
	//-- НЕ УТ
	
	Если ДействующиеНастройки.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам 
		И ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru='Ведется учет дисконтированной кредиторской задолженности поставщикам'"));
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон,
					НСтр("ru='Статья списания процентных расходов дисконтирования'"),
					ДействующиеНастройки.СтатьяСписанияПроцентныхРасходов);
			
 			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон,
					НСтр("ru='Аналитика списания процентных расходов дисконтирования'"),
					ДействующиеНастройки.АналитикаСписанияПроцентныхРасходов);
			
	КонецЕсли;
	
	Если ДействующиеНастройки.ФормироватьРезервыПоСомнительнымДолгам Тогда
		
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru='Формируются резервы по сомнительным долгам'"));
		
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблон,
				НСтр("ru='Периодичность резервов по сомнительным долгам'"),
				ДействующиеНастройки.ПериодичностьРезервовПоСомнительнымДолгам);
		
	КонецЕсли;
	
	ОписаниеДействующихПараметровЛокализация(СтрокаОписанияНастроек, Организация, ДатаДействия, ДействующиеНастройки);
	
	Возврат СтрокаОписанияНастроек
	
КонецФункции

// Возвращает актуальные значения порядка оценки задолженности по организации на дату.
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - ссылка на организацию.
// 	Период - Дата - период действия настроек.
// 
// Возвращаемое значение:
//  ТаблицаЗначений, Неопределено - таблица оценки задолженности.
Функция ПорядокОценкиЗадолженности(Организация, Период) Экспорт
	
	ДействующиеНастройки = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(
		"УчетнаяПолитикаФинансовогоУчета", Организация, Период, Истина);
	
	Если ДействующиеНастройки <> Неопределено
		И ДействующиеНастройки.ПорядокОценкиЗадолженности <> Неопределено Тогда
			ХранилищеПорядокОценкиЗадолженности = ДействующиеНастройки.ПорядокОценкиЗадолженности; //ХранилищеЗначения
		Возврат ХранилищеПорядокОценкиЗадолженности.Получить();
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

// Возвращает параметры выбора статей и аналитик.
// 
// Параметры:
//  ПризнаватьРасходыПоИсследованиям - Булево.
//  УчетДисконтированнойКредиторскойЗадолженностиПоставщикам - Булево.
//  
// Возвращаемое значение:
//  Массив из см. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики
//
Функция ПараметрыВыбораСтатейИАналитик(ПризнаватьРасходыПоИсследованиям, УчетДисконтированнойКредиторскойЗадолженностиПоставщикам) Экспорт
	
	МассивПараметров = Новый Массив;
	
	#Область СтатьяРасходовПоИсследованиям
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Запись";
	ПараметрыВыбора.Статья      = "СтатьяРасходовПоИсследованиям";
	
	ПараметрыВыбора.ДоступностьПоОперации = ПризнаватьРасходыПоИсследованиям = Истина;
	ПараметрыВыбора.СкрыватьСтатьюНедоступнуюПоОперации = Ложь;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовПоИсследованиям";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовПоИсследованиям");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовПоИсследованиям");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область СтатьяСписанияПроцентныхРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Запись";
	ПараметрыВыбора.Статья      = "СтатьяСписанияПроцентныхРасходов";
	
	ПараметрыВыбора.ДоступностьПоОперации = 
		УчетДисконтированнойКредиторскойЗадолженностиПоставщикам 
		И ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов");
	ПараметрыВыбора.СкрыватьСтатьюНедоступнуюПоОперации = Ложь;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаСписанияПроцентныхРасходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяСписанияПроцентныхРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаСписанияПроцентныхРасходов");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти 
	
	Возврат МассивПараметров;
	
КонецФункции

Процедура ОписаниеДействующихПараметровЛокализация(СтрокаОписанияНастроек, Организация, ДатаДействия, ДействующиеНастройки)
	
	//++ Локализация
	
	СтрокаШаблон = "%1: %2." + Символы.ПС;
	СтрокаШаблонБулево = "%1." + Символы.ПС;

	//++ НЕ УТ
	
	Если ДействующиеНастройки.УчетАрендыПоФСБУ25_2018 Тогда
		
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru='Учет аренды ведется по ФСБУ 25/2018'"));
	КонецЕсли;

	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.12.23";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("4db80397-fd9c-450a-a10c-cb025337de54");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновляет регистр ""Учетная политика финансового учета"":
	|- заполняет новый реквизит ""Порядок учета внеоборотных активов (регл)""
	|- заполняет новый реквизит ""Порядок начисления амортизации арендованных ОС (упр)""
	|- заполняет новый реквизит ""Порядок начисления амортизации арендованных ОС (регл)""
	|- переносит значения настроек формирования резервов по сомнительным долгам из ""Учетная политика бухгалтерского учета""'");
	
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ПолноеИмя());
	
	//++ НЕ УТ
	
	//++ Локализация
		
	Читаемые.Добавить(Метаданные.РегистрыСведений.УчетнаяПолитикаБухУчета.ПолноеИмя());
	
	//-- Локализация
	
	//-- НЕ УТ
	
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ПолноеИмя());
	
	//++ НЕ УТ
	
	//++ Локализация
	
	Блокируемые.Добавить(Метаданные.РегистрыСведений.УчетнаяПолитикаБухУчета.ПолноеИмя());
	
	//-- Локализация
	
	//-- НЕ УТ
	
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.УчетнаяПолитикаФинансовогоУчета";
	
	Запрос = Новый Запрос;
	
	//++ НЕ УТ
	
	Запрос.Текст =
	"
	//++ Локализация
	|ВЫБРАТЬ
	|	УчетнаяПолитикаБухУчета.Период КАК Период,
	|	УчетнаяПолитикаБухУчета.Организация КАК Организация,
	|	УчетнаяПолитикаБухУчета.УдалитьФормироватьРезервыПоСомнительнымДолгамБУ КАК ФормироватьРезервыПоСомнительнымДолгамБУ
	|ПОМЕСТИТЬ ВТУчетнаяПолитикаБухУчета
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаБухУчета КАК УчетнаяПолитикаБухУчета
	|ГДЕ
	|	УчетнаяПолитикаБухУчета.УдалитьФормироватьРезервыПоСомнительнымДолгамБУ
	|	И ЛОЖЬ В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				УчетнаяПолитикаБухУчетаВложенный.УдалитьФормироватьРезервыПоСомнительнымДолгамБУ
	|			ИЗ
	|				РегистрСведений.УчетнаяПолитикаБухУчета КАК УчетнаяПолитикаБухУчетаВложенный
	|			ГДЕ
	|				УчетнаяПолитикаБухУчетаВложенный.Организация = УчетнаяПолитикаБухУчета.Организация
	|				И УчетнаяПолитикаБухУчетаВложенный.Период > УчетнаяПолитикаБухУчета.Период
	|			УПОРЯДОЧИТЬ ПО
	|				УчетнаяПолитикаБухУчетаВложенный.Период)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	NULL КАК ПериодВТ,
	|	УчетнаяПолитикаБухУчета.Период КАК Период,
	|	УчетнаяПолитикаБухУчета.Организация КАК Организация
	|ПОМЕСТИТЬ ВТРезервы
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаБухУчета КАК УчетнаяПолитикаБухУчета
	|ГДЕ
	|	УчетнаяПолитикаБухУчета.УдалитьФормироватьРезервыПоСомнительнымДолгамБУ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТУчетнаяПолитикаБухУчета.Период,
	|	МИНИМУМ(УчетнаяПолитикаБухУчета.Период),
	|	ВТУчетнаяПолитикаБухУчета.Организация
	|ИЗ
	|	ВТУчетнаяПолитикаБухУчета КАК ВТУчетнаяПолитикаБухУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаБухУчета КАК УчетнаяПолитикаБухУчета
	|		ПО ВТУчетнаяПолитикаБухУчета.Организация = УчетнаяПолитикаБухУчета.Организация
	|			И ВТУчетнаяПолитикаБухУчета.Период < УчетнаяПолитикаБухУчета.Период
	|			И (НЕ УчетнаяПолитикаБухУчета.УдалитьФормироватьРезервыПоСомнительнымДолгамБУ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТУчетнаяПолитикаБухУчета.Период,
	|	ВТУчетнаяПолитикаБухУчета.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРезервы.Период КАК Период,
	|	ВТРезервы.Организация КАК Организация
	|ИЗ
	|	ВТРезервы КАК ВТРезервы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО (ВТРезервы.Период = УчетнаяПолитикаФинансовогоУчета.Период)
	|			И (ВТРезервы.Организация = УчетнаяПолитикаФинансовогоУчета.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаБухУчета КАК УчетнаяПолитикаБухУчета
	|		ПО (ВТРезервы.Период = УчетнаяПолитикаБухУчета.Период
	|				И ВТРезервы.Организация = УчетнаяПолитикаБухУчета.Организация)
	|ГДЕ
	|	УчетнаяПолитикаФинансовогоУчета.ФормироватьРезервыПоСомнительнымДолгам ЕСТЬ NULL ИЛИ УчетнаяПолитикаФинансовогоУчета.ФормироватьРезервыПоСомнительнымДолгам <> УчетнаяПолитикаБухУчета.УдалитьФормироватьРезервыПоСомнительнымДолгамБУ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//-- Локализация
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ПорядокУчетаВНАРегл = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАРегл.ПустаяСсылка)
	|	ИЛИ ДанныеРегистра.ПорядокНачисленияАмортизацииАренда = ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.ПустаяСсылка)
	|	ИЛИ ДанныеРегистра.ПорядокНачисленияАмортизацииАрендаРегл = ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.ПустаяСсылка)
	|
	|	ИЛИ ДанныеРегистра.ПорядокНачисленияАмортизацииНМА = ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.ПустаяСсылка)
	|		И ДанныеРегистра.ПорядокНачисленияАмортизацииОС <> ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.ПустаяСсылка)
	|	ИЛИ ДанныеРегистра.ПорядокНачисленияАмортизацииНМАРегл = ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.ПустаяСсылка)
	|		И ДанныеРегистра.ПорядокНачисленияАмортизацииОСРегл <> ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.ПустаяСсылка)
	|";
	
	Данные = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
	
	//-- НЕ УТ
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.УчетнаяПолитикаФинансовогоУчета;
	ПолноеИмяОбъекта  = МетаданныеОбъекта.ПолноеИмя();

	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.ИмяВременнойТаблицы = "ВТДанныеДляОбработки";
	ДополнительныеПараметры.ВыбиратьПорциями = Ложь;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ЭтоМеждународнаяВерсия = НЕ ПолучитьФункциональнуюОпцию("ЛокализацияРФ");
	
	ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		ПолноеИмяОбъекта,
		МенеджерВременныхТаблиц,
		ДополнительныеПараметры);
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДляОбработки.Период КАК Период,
	|	ДанныеДляОбработки.Организация КАК Организация
	|ИЗ
	|	ВТДанныеДляОбработки КАК ДанныеДляОбработки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		//++ НЕ УТ
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;

			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Период", Выборка.Период);
			ЭлементБлокировки.УстановитьЗначение("Организация", Выборка.Организация);
			
			//++ Локализация
			
			ЭлементБлокировки = Блокировка.Добавить(Метаданные.РегистрыСведений.УчетнаяПолитикаБухУчета.ПолноеИмя());
			ЭлементБлокировки.УстановитьЗначение("Период", Выборка.Период);
			ЭлементБлокировки.УстановитьЗначение("Организация", Выборка.Организация);
			
			//-- Локализация
			
			Блокировка.Заблокировать();
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Прочитать();
			
			//++ Локализация
			
			ДанныеУчетнойПолитикиБухУчета = РегистрыСведений.УчетнаяПолитикаБухУчета.СоздатьНаборЗаписей();
			ДанныеУчетнойПолитикиБухУчета.Отбор.Период.Установить(Выборка.Период);
			ДанныеУчетнойПолитикиБухУчета.Отбор.Организация.Установить(Выборка.Организация);
			ДанныеУчетнойПолитикиБухУчета.Прочитать();

			Если ДанныеУчетнойПолитикиБухУчета.Количество() И Не НаборЗаписей.Количество() Тогда
				
				НоваяЗапись = НаборЗаписей.Добавить();

				ТекущаяУчетнаяПолитикаФинансовогоУчета = РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(Выборка.Период, Новый Структура("Организация", Выборка.Организация));
				Если ТекущаяУчетнаяПолитикаФинансовогоУчета.Количество() Тогда
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущаяУчетнаяПолитикаФинансовогоУчета[0]);
				Иначе
					ЗаполнитьЗначенияСвойств(НоваяЗапись, РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ЗначенияПоУмолчанию());
				КонецЕсли;
				
				НоваяЗапись.Период = Выборка.Период;
				НоваяЗапись.Организация = Выборка.Организация;
				НоваяЗапись.ФормироватьРезервыПоСомнительнымДолгам = ДанныеУчетнойПолитикиБухУчета[0].УдалитьФормироватьРезервыПоСомнительнымДолгамБУ;
				НоваяЗапись.ПериодичностьРезервовПоСомнительнымДолгам = ДанныеУчетнойПолитикиБухУчета[0].УдалитьПериодичностьРезервовПоСомнительнымДолгамБУ;
				НоваяЗапись.ПорядокОценкиЗадолженности = ДанныеУчетнойПолитикиБухУчета[0].УдалитьПорядокОценкиЗадолженности;
				
			Иначе
			//-- Локализация
			
				Для Каждого ЗаписьРегистра Из НаборЗаписей Цикл
				
					Если НЕ ЗначениеЗаполнено(ЗаписьРегистра.ПорядокУчетаВНАРегл) Тогда
						Если ЭтоМеждународнаяВерсия Тогда
							ЗаписьРегистра.ПорядокУчетаВНАРегл = Перечисления.ПорядокУчетаВНАРегл.СовпадаетСУпрУчетом;
							ЗаписьРегистра.ПорядокНачисленияАмортизацииОСРегл = ЗаписьРегистра.ПорядокНачисленияАмортизацииОС;
						Иначе
							ЗаписьРегистра.ПорядокУчетаВНАРегл = Перечисления.ПорядокУчетаВНАРегл.Независимый;
							ЗаписьРегистра.ПорядокНачисленияАмортизацииОСРегл = ЗаписьРегистра.ПорядокНачисленияАмортизацииОС;
						КонецЕсли;
					КонецЕсли;
					
					Если НЕ ЗначениеЗаполнено(ЗаписьРегистра.ПорядокНачисленияАмортизацииАренда) Тогда
						ЗаписьРегистра.ПорядокНачисленияАмортизацииАренда = ЗаписьРегистра.ПорядокНачисленияАмортизацииОС;
					КонецЕсли;
					Если НЕ ЗначениеЗаполнено(ЗаписьРегистра.ПорядокНачисленияАмортизацииАрендаРегл) Тогда
						ЗаписьРегистра.ПорядокНачисленияАмортизацииАрендаРегл = ЗаписьРегистра.ПорядокНачисленияАмортизацииОС;
					КонецЕсли;
					Если НЕ ЗначениеЗаполнено(ЗаписьРегистра.ПорядокНачисленияАмортизацииНМА) Тогда
						ЗаписьРегистра.ПорядокНачисленияАмортизацииНМА = ЗаписьРегистра.ПорядокНачисленияАмортизацииОС;
					КонецЕсли;
					Если НЕ ЗначениеЗаполнено(ЗаписьРегистра.ПорядокНачисленияАмортизацииНМАРегл) Тогда
						ЗаписьРегистра.ПорядокНачисленияАмортизацииНМАРегл = ЗаписьРегистра.ПорядокНачисленияАмортизацииОСРегл;
					КонецЕсли;
										
					//++ Локализация
					
					Если ДанныеУчетнойПолитикиБухУчета.Количество() Тогда
						ЗаписьРегистра.ФормироватьРезервыПоСомнительнымДолгам = ДанныеУчетнойПолитикиБухУчета[0].УдалитьФормироватьРезервыПоСомнительнымДолгамБУ;
						ЗаписьРегистра.ПериодичностьРезервовПоСомнительнымДолгам = ДанныеУчетнойПолитикиБухУчета[0].УдалитьПериодичностьРезервовПоСомнительнымДолгамБУ;
						ЗаписьРегистра.ПорядокОценкиЗадолженности = ДанныеУчетнойПолитикиБухУчета[0].УдалитьПорядокОценкиЗадолженности;
					КонецЕсли;
					
					//-- Локализация
					
				КонецЦикла;
				
			//++ Локализация
			
			КонецЕсли;
			
			//-- Локализация
						
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ТекстСообщения = НСтр("ru = 'Не удалось записать данные в регистр %ИмяРегистра% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПредставлениеОшибки);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", ПолноеИмяОбъекта);
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта, 
				Неопределено, 
				ТекстСообщения);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
		//-- НЕ УТ

	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
